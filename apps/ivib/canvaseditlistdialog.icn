#
# $Id$
#
# This file is in the public domain.
#
# Author: Robert Parlett (parlett@dial.pipex.com)
#

import gui

############################################################################
#
#
#
class CanvasEditListDialog(CanvasComponentDialog)
   public 
   selection_list,          #                      
   set_selection,           #                     
   selection,               #                 
   set_initial_text,        #                        
   initial_text,            #                    
   group,                   #             
   filter,                  #
   default_setting

   public validate_input()
      if (group.get_which_one() === set_selection) & not(integer(selection.get_contents())) then
         return alert_error(self, "Selection is non-integer")
      return cast(self,CanvasComponentDialog).validate_input()
   end

   public set_vals()
      cast(self,CanvasComponentDialog).set_vals()
      c.set_selection_list(selection_list.get_contents())
      case group.get_which_one() of {
         set_initial_text : c.set_contents(initial_text.get_contents())
         set_selection : c.set_selection(integer(selection.get_contents()))
         default_setting : c.set_selection(1) #c.set_contents(c.selection_list[1])
      }
      c.filter_str := filter.get_contents()
   end

   public on_set_initial_text()
      selection.set_is_shaded("t")
      initial_text.set_is_shaded("f") 
   end

   public on_set_selection()
      selection.set_is_shaded("f")
      initial_text.set_is_shaded("t") 
   end

   public on_default_setting()
      c.set_selection(1)
      selection.set_is_shaded("t")
      initial_text.set_is_shaded("t") 
   end

   public setup()
      local p, q, l, sn

      cast(self,CanvasComponentDialog).setup()

      p := TabItem()
      p.set_layout(GridLayout())
      add_tab_at_front(p)
      p.set_label("General")

      selection_list := EditableTextList()
      selection_list.set_size(150, 250)
      selection_list.set_contents(copy(c.selection_list))
      p.add(selection_list)

      q := Panel("c:x_weight=0")
      q.set_layout(GridLayout("doi=0","extra=borders"))

      group := CheckBoxGroup()

      set_selection := CheckBox()
      set_selection.connect(self.on_set_selection, Event.ACTION)
      set_selection.set_label("Set selection")
      q.add(set_selection)
      group.add(set_selection)

      selection := TextField("c:eol")
      selection.set_size(100)
      selection.set_contents(c.selection)
      q.add(selection)

      set_initial_text := CheckBox()
      set_initial_text.connect(self.on_set_initial_text, Event.ACTION)
      set_initial_text.set_label("Set initial_text")
      q.add(set_initial_text)
      group.add(set_initial_text)

      initial_text := TextField("c:eol")
      initial_text.set_size(100)
      initial_text.set_contents(c.get_contents())
      q.add(initial_text)

      default_setting := CheckBox("c:eol")
      default_setting.connect(self.on_default_setting, Event.ACTION)
      default_setting.set_label("Default")
      q.add(default_setting)
      group.add(default_setting)

      sn := \c.selection | 1
      if c.get_contents() ~== c.selection_list[sn] then {
         group.set_which_one(set_initial_text)
         selection.set_is_shaded("t")
      } else if sn ~= 1 then {
         group.set_which_one(set_selection)
         initial_text.set_is_shaded("t")
      } else {
         group.set_which_one(default_setting)
         selection.set_is_shaded("t")
         initial_text.set_is_shaded("t")
      }

      l := Label()
      l.set_label("Filter : ")
      q.add(l)
      
      filter := TextField("c:eol")
      filter.set_size(100)
      filter.set_align("l", "c")
      filter.set_contents(c.filter_str)
      q.add(filter)

      p.add(q)
   end
end


