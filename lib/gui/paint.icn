package gui

import 
   util,
   graphics(Window),
   ipl.graphics(GetShadowClone),
   ipl.strings(separate_lines)

# A {Paint} represents an arbitrary object which has a size and can be
# drawn.
#
abstract class Paint(Size)
   #
   # Initialize the instance.  This should set the size of the instance.
   # @param c The owning {Component}
   # @param accel The keyboard accelerator to be drawn
   public abstract initially(c, accel)

   # Draw the instance
   # @param W The window to draw into
   # @param x The x position
   # @param y The y position
   # @param w The available output width
   # @param h The available output height
   public abstract draw(W, x, y, w, h)

   # Cleanup any resources used by this instance.
   public finally()
   end

   # Convenient method to draw at a given point with size limit; just calls {draw}.
   # @param W The window to draw into
   # @param p The {Rect}, giving the point to draw at, and the
   # @        height/width limitation.
   public draw_rect(W, r)
      self.draw(W, r.x, r.y, r.w, r.h)
   end
end

procedure paint_for_string(s)
   if /s then
      return
   if match(".cell", s) then
      return GridPaint(s)
   else
      return TextPaint(s)
end

# A simple {Paint} instance which draws a string.
#
class TextPaint(Paint)
   public str, accel

   public initially(c, accel)
      self.accel := accel
      self.h := c.cbwin.get_line_height()
      self.w := c.cbwin.text_width(str)
   end

   public draw(W, x, y)
      draw_string_ex(W, x, y + W.get_line_ascent(), str, accel)
   end

   # Create a new instance.
   # @param s The string to draw
   public new(s)
      self.str := s
      return
   end
end

# A simple {Paint} instance which draws a string.  If the string is
# limited when output, then a truncated string ending in "..." is
# output.
#
class AbbreviatedTextPaint(Paint)
   public str, accel

   public initially(c, accel)
      self.accel := accel
      self.h := c.cbwin.get_line_height()
      self.w := c.cbwin.text_width(str)
   end

   public draw(W, x, y, w)
      draw_string_ex(W, x, y + W.get_line_ascent(), limit_text(W, str, w), accel)
   end

   # Create a new instance.
   # @param s The string to draw
   public new(s)
      self.str := s
      return
   end
end

# A simple {Paint} instance which draws a {Pixels} instance.
#
class PixelsPaint(Paint)
   public src, pix

   public initially()
      /pix | runerr("initially called out of sequence")
      pix := src.shared_copy()
      self.w := pix.get_width()
      self.h := pix.get_height()
   end

   public draw(W, x, y)
      W.draw_image(x, y, pix)
   end

   public finally()
      pix.close()
      pix := &null
   end

   # Create a new instance.
   # @param src The `Pixels` to draw
   public new(src)
      self.src := src
      return
   end
end

# A simple {Paint} instance which has zero size and draws nothing.
#
class NullPaint(Paint)
   public initially()
      self.w := self.h := 0
   end

   public draw(W, x, y)
   end
end

# A simple {Paint} instance which has the given size and draws nothing.
#
class EmptyPaint(Paint)
   public initially()
   end

   public draw(W, x, y)
   end

   public new(w, h)
      self.w := w
      self.h := h
      return
   end
end

#
# A {Paint} instance which paints one instance on top of another.  The
# width/height is taken as the maximum of the two inner {Paint}s.
#
class CompoundPaint(Paint)
   public
      top, bottom

   public initially(c, accel)
      top.initially(c, accel)
      bottom.initially(c, accel)
      w := max(top.w, bottom.w)
      h := max(top.h, bottom.h)
   end

   public finally()
      top.finally()
      bottom.finally()
   end

   public draw(W, x, y, w, h)
      bottom.draw(W, x, y, w, h)
      top.draw(W, x, y, w, h)
   end

   public set_top(top)
      self.top := top
      return self
   end

   public set_bottom(bottom)
      self.bottom := bottom
      return self
   end
end

# A {Paint} instance which draws its output using a {Grid}.  The input is a format
# string specifying the grid and its contents.
#
class GridPaint(Paint)
   public 
      fmt,          # Format string
      g,            # Grid object
      parent,       # Parent component
      win,          # Window buffer
      pass,         # Pass number
      curr_cell,    # Current cell being read
      x,            # Text 'cursor' location in cell
      y, 
      x_ialign,     # Alignment of lines in a cell
      y_ialign,
      llen,         # Current line len, height
      lheight, 
      lno,          # Line no. in cell 
      ii,           # Cell index counter.
      accel

   public draw(W, x, y)
      (\win).copy_to(,,,, W, x, y)
   end

   public initially(c, accel)
      local attribs

      /win | runerr("initially called out of sequence")
      parent := c
      attribs := copy(c.get_effective_wattribs()) | runerr("couldn't get effective wattribs in GridPaint", c)
      attribs[WAttrib.SIZE] := [1, 1]
      attribs[WAttrib.CANVAS] := "hidden"
      win := open_win_with(attribs) | runerr("Couldn't open window")
      g := Grid().set_extra(Grid.EXTRA_IGNORE)
      pass := 1
      x_ialign := y_ialign := Align.C
      lines(fmt)
      close_win()
      g.dimension()
      g.compute_preferred_col_widths()
      g.compute_preferred_row_heights()
      g.compute_actual_col_widths()
      g.compute_actual_col_positions(0)
      g.compute_actual_row_heights()
      g.compute_actual_row_positions(0)
      w := g.get_preferred_width()
      h := g.get_preferred_height()
      if (w > 0) & (h > 0) then {
         attribs["size"] := [w, h]
         win := open_win_with(attribs) | runerr("Couldn't open window")
         pass := 2
         curr_cell := &null
         ii := 0
         x_ialign := y_ialign := Align.C
         self.accel := accel
         lines(fmt)
      }
   end

   public lines(s)
      local t, cmd, args, got_cell
      every t := separate_lines(s) do t ? {
         if ="." then {
            if any('.') then {
               t := tab(0)
               if \got_cell then
                  str(t)
            } else {
               cmd := tab(upto(' ') | 0)
               args := []
               repeat {
                  tab(many(' '))
                  if pos(0) then
                     break
                  if any('\'') then {
                     move(1)
                     t := tab(upto('\'') | 0)
                     move(1)
                  } else
                     t := tab(upto(' ') | 0)
                  put(args, string(t))
               }
               if cmd == "cell" then {
                  do_cell(args)
                  got_cell := 1
               } else if \got_cell then case string(cmd) of {
                  "br" : do_brk(args)
                  "img" : do_img(args)
                  "imgl" : do_imgl(args)
                  "x_spc" : do_x_spc(args)
                  "y_spc" : do_y_spc(args)
                  "fg": do_fg(args)
                  "bg": do_bg(args)
                  "font": do_font(args)
                  "leading": do_leading(args)
                  "x_ialign" : do_x_ialign(args)
                  "y_ialign" : do_y_ialign(args)
               }
            }
         } else if \got_cell then
            str(t)
      }
   end

   public do_x_spc(args)
      local n
      n := integer(args[1]) | 0
      if pass = 1 then {
         llen +:= n
      } else {
         ready_to_output() | fail
         x +:= n
      }
   end

   public do_y_spc(args)
      local n
      n := integer(args[1]) | 0
      if pass = 1 then {
         lheight <:= n
      }
   end

   public do_cell(args)
      local t, key, val, a

      if pass = 1 then {
         t := table()
         every a := !args do {
            a ? {
               key := tab(upto('=') | 0)
               if ="=" then
                  val := tab(0)
               else
                  val := ""
               t[key] := case key of {
                  Grid.X_TAB | Grid.Y_TAB | Grid.X | Grid.Y | Grid.W | Grid.H | 
                     Grid.L_INSET | Grid.R_INSET | Grid.T_INSET | Grid.B_INSET:
                     integer(val)
                  Grid.X_ALIGN:
                     val == (Align.L | Align.R | Align.C)
                  Grid.Y_ALIGN:
                     val == (Align.T | Align.B | Align.C)
                  Grid.EOL | Grid.X_FILL | Grid.Y_FILL:
                     if *val = 0 then &yes else flag(val)
                  Grid.X_WEIGHT | Grid.Y_WEIGHT:
                     real(val)
               }
            }
         }
         curr_cell := g.create_cell(t)
         curr_cell.pw := curr_cell.ph := 0
         curr_cell.obj := []
         llen := lheight := 0
      } else {
         ii +:= 1
         curr_cell := g.info[ii]
         lno := 1
         x := &null
         y := curr_cell.ay
      }
   end

   public str(s)
      local y1
      if pass = 1 then {
         llen +:= win.text_width(s)
         lheight <:= win.get_line_height()
      } else {
         ready_to_output() | fail
         y1 := case y_ialign of {
            Align.T : y
            Align.C : y + (curr_cell.obj[lno].h - win.get_font_height()) / 2
            Align.B : y + curr_cell.obj[lno].h - win.get_font_height()
         }
         draw_string_ex(win, x, y1 + win.get_font_ascent(), s, accel)
         # Just print the accelerator once.
         if upto(\accel, map(s)) then
            accel := &null
         x +:= win.text_width(s)
      }
   end

   public do_brk(args)
      if pass = 1 then {
         curr_cell.pw <:= llen
         curr_cell.ph +:= lheight
         put(curr_cell.obj, Size(llen, lheight))
         llen := lheight := 0
      } else {
         x := &null
         y +:= curr_cell.obj[lno].h
         lno +:= 1
      }
   end

   public do_fg(args)
      win.set_fg(if *args = 0 then parent.cbwin.get_fg() else args[1])
   end

   public do_bg(args)
      win.set_bg(if *args = 0 then parent.cbwin.get_bg() else args[1])
   end

   public do_font(args)
      win.set_font(if *args = 0 then parent.cbwin.get_font() else args[1])
   end

   public do_leading(args)
      win.set_leading(if *args = 0 then parent.cbwin.get_leading() else real(args[1]))
   end

   public do_x_ialign(args)
      x_ialign := (args[1] == (Align.L | Align.R | Align.C))
   end

   public do_y_ialign(args)
      y_ialign := (args[1] == (Align.T | Align.B | Align.C))
   end

   private do_imgx_impl(W)
      local y1
      if pass = 1 then {
         llen +:= W.get_width()
         lheight <:= W.get_height()
      } else {
         ready_to_output() | fail
         y1 := case y_ialign of {
            Align.T : y
            Align.C : y + (curr_cell.obj[lno].h - W.get_height()) / 2
            Align.B : y + curr_cell.obj[lno].h - W.get_height()
         }
         W.copy_to(,,,, win, x, y1)
         x +:= W.get_width()
      }
      return
   end

   public do_img(args)
      do_imgx_impl(ImageCache.get(args[1], win))
   end

   public do_imgl(args)
      local W
      use {
         W := ImageCache.load(args[1], win),
         do_imgx_impl(W)
      }
   end

   public ready_to_output()
      if lno > *curr_cell.obj then
         fail
      /x :=  case x_ialign of {
         Align.L : curr_cell.ax
         Align.R : curr_cell.ax + curr_cell.aw - curr_cell.obj[lno].w
         Align.C : curr_cell.ax + (curr_cell.aw - curr_cell.obj[lno].w) / 2
      }
      return
   end

   public finally()
      close_win()
   end

   public close_win()
      (\win).close()
      win := &null
   end

   public draw_grid()
      local i, cw

      if /win then
         return

      use {
         cw := GetShadowClone(win).set_masked_pattern("gray"),
         {
            cw.draw_rectangle(0, 0, w, h)

            every i := !(\g).info do {
               cw.draw_rectangle(g.a_col_x[i.x],
                                 g.a_row_y[i.y],
                                 Grid.slice(i.x, i.w, g.a_col_w),
                                 Grid.slice(i.y, i.h, g.a_row_h),
                                 Gui.FINE_WIDTH)
            }
         }
      }
   end

   # Create a new instance.
   # @param s The format string
   public new(s)
      self.fmt := s
      return
   end
end

# A {Paint} instance which draws an image, which can come from a
# number of sources.  The image can also be scaled.
#
class ImagePaint(Paint)
   public 
      image_str,
      image_window,
      image_cache,
      win,
      fill_dimensions,
      max_w,
      max_h

   #
   # Set an image to use; the parameter is passed to {ImageCache.get}
   # to load the image.
   #
   public set_cache(x)
      self.image_cache := x
      return self
   end

   #
   # Set an image to use; the parameter is passed to {ImageCache.load}
   # to load the image.
   #
   public set_image(x)
      self.image_str := x
      return self
   end

   #
   # Set a window to use as the image source.  It will not be closed.
   #
   public set_window(x)
      self.image_window := x
      return self
   end

   #
   # Set the maximum allowed width; the image will be scaled if necessary.
   #
   public set_max_w(w)
      self.max_w := w
      return self
   end

   #
   # Set the maximum allowed height; the image will be scaled if necessary.
   #
   public set_max_h(h)
      self.max_h := h
      return self
   end

   #
   # Set the maximum allowed size; the image will be scaled if necessary.
   #
   public set_max_size(w, h)
      self.max_w := w
      self.max_h := h
      return self
   end
   
   # This setting controls how the image will be expanded if its size
   # is less than any specified maximum width and/or height (if none
   # is specified it has no effect).  If set to 0, then the image will
   # not be expanded; if 1 then the image will not be distorted, but
   # will be expanded to fill one of the dimensions depending on its
   # shape (if only one max dimension was specified, then the image
   # expands to this dimension).  If 2, and both maximum dimensions
   # were set, then the image will be distorted and expanded to fill
   # both dimensions.
   # 
   # If the image is bigger than the specified maximum size then it
   # will always be scaled down, but if this setting is 2, and a
   # maximum size is set, then again the image is distorted to fill
   # both those dimensions.
   #
   public set_fill_dimensions(n)
      self.fill_dimensions := n
      return self
   end

   public initially(c)
      /win | runerr("initially called out of sequence")
      load_image(c)
      zoom_image()
      if \win then {
         self.w := win.get_width()
         self.h := win.get_height()
      } else
         self.w := self.h := 0
   end

   public finally()
      close_image()
   end

   public close_image()
      (\win).close()
      win := &null
   end

   public load_image(c)
      win := if \self.image_str then
         ImageCache.load(self.image_str, c.cbwin)
      else if \self.image_cache then
         ImageCache.get(self.image_cache, c.cbwin).shared_copy()
      else if \self.image_window then
         self.image_window.shared_copy()
   end

   public zoom_image()
      local zoom_win, img_w, img_h,
         asp_r, asp_max, zoom_w, zoom_h

      if /win | (/max_w & /max_h) then
         fail

      img_w := win.get_width()
      img_h := win.get_height()
      if (fill_dimensions = 0) &
         (/max_w | img_w <= max_w)\1 & (/max_h | img_h <= max_h)\1 then
         fail

      asp_r := real(img_w) / img_h
      if \max_w then {
         if \max_h then {
            if fill_dimensions = 2 then { 
               zoom_w := max_w
               zoom_h := max_h
            } else {
               asp_max := real(max_w) / max_h
               if asp_r > asp_max then {
                  zoom_w := max_w
                  zoom_h := integer(max_w / asp_r)
               } else {
                  zoom_w := integer(max_h * asp_r)
                  zoom_h := max_h
               }
            }
         } else {
            # \max_w & /max_h
            zoom_w := max_w
            zoom_h := integer(max_w / asp_r)
         }
      } else {
         # /max_w & \max_h
         zoom_w := integer(max_h * asp_r)
         zoom_h := max_h
      }

      zoom_w <:= 1
      zoom_h <:= 1

      zoom_win := Window().set_size(zoom_w, zoom_h) | runerr("Couldn't open temp window")
      gui.zoom_win(win, zoom_win)

      # Now swap the zoomed window into win.
      close_image()
      win := zoom_win
   end

   public draw(W, x, y)
      (\win).copy_to(,,,, W, x, y)
   end

   public new()
      fill_dimensions := 0
      return
   end
end
