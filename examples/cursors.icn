import gui

procedure main()
   local d,s,l,i,b,cb
   d := Dialog().set_layout(GridLayout())
   i := 0
   every s := "X cursor"|"arrow"|"based arrow down"|"based arrow up"|"boat"|"bogosity"|
      "bottom left corner"|"bottom right corner"|"bottom side"|"bottom tee"|"box spiral"|
      "center ptr"|"circle"|"clock"|"coffee mug"|"cross"|"cross reverse"|"crosshair"|"diamond cross"|
      "dot"|"dotbox"|"double arrow"|"draft large"|"draft small"|"draped box"|"exchange"|"fleur"|
      "gobbler"|"gumby"|"hand1"|"hand2"|"heart"|"icon"|"iron cross"|"left ptr"|"left side"|"left tee"|
      "leftbutton"|"ll angle"|"lr angle"|"man"|"middlebutton"|"mouse"|"pencil"|"pirate"|"plus"|
      "question arrow"|"right ptr"|"right side"|"right tee"|"rightbutton"|"rtl logo"|"sailboat"|"sb down arrow"|
      "sb h double arrow"|"sb left arrow"|"sb right arrow"|"sb up arrow"|"sb v double arrow"|"shuttle"|
      "sizing"|"spider"|"spraycan"|"star"|"target"|"tcross"|"top left arrow"|"top left corner"|"top right corner"|
      "top side"|"top tee"|"trek"|"ul angle"|"umbrella"|"ur angle"|"watch"|"xterm"|"num glyphs" do 
   {
      l := Label().
         set_label(s).
         set_border(CompoundBorder().
                       set_outer(PlainBorder()).
                       set_inner(EmptyBorder())).
         set_pointer(s)
      d.add(l)
      i +:= 1
      if i % 5 = 0 then
         l.set_constraint("eol", &yes)
   }
   l.set_constraint("eol", &yes)
   b := TextButton().set_label("Quit").set_constraint("x_align", "l").set_bg("white")
   b.connect(d.dispose, Event.ACTION)
   d.add(b)

   cb := CheckBox().set_label("Light bg").set_constraint("x_align", "l").set_bg("white").set_is_checked(&yes)
   cb.connect(create { 
      if cb.is_checked() then
         d.set_bg("white")
      else
         d.set_bg("dark blue")
      d.reset()
   }, Event.ACTION)
   d.add(cb)

   d.connect(d.dispose, Event.WINDOW_CLOSE_BUTTON)
   d.show_modal()
end
