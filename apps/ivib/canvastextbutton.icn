#
# $Id$
#
# This file is in the public domain.
#
# Author: Robert Parlett (parlett@dial.pipex.com)
#

import gui


############################################################################
#
#
#
class CanvasTextButton(CanvasButton,TextButton)
   #
   # Generate code into Code object c.
   #
   public override gen_code(c)
      self.paint.gen_code(c)
      (\self.paint_down).gen_code(c)
      CanvasButton.gen_code(c)
      c.chain("set_paint(" || self.paint.name || ")")
      if \self.paint_down then
         c.chain("set_paint_down(" || self.paint_down.name || ")")
   end

   #
   # Return the I/O template.
   #
   public override get_template()
      suspend CanvasButton.get_template() |
          ["Paint", "paint"] |
          ["Paint down", "paint_down"]
   end

   # 
   # Duplicate object
   #
   public override dup()
      local c
      c := CanvasTextButton()
      CanvasButton.dup_fields(c)
      c.set_paint(self.paint.dup(c))
      c.set_paint_down((\self.paint_down).dup(c))
      return c
   end

   #
   # Open the configuration dialog box.
   #
   public override dialog()
      return CanvasTextButtonDialog(self)
   end

   public override gen_cos()
      suspend CanvasComponent.gen_cos() | self.paint | \self.paint_down
   end

   public override new()
      TextButton.new()
      CanvasButton.new()
      self.class_name := "TextButton"
      self.import_name := "gui"
      self.name := "text_button"
      return
   end
end

