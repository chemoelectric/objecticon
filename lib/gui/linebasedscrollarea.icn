#
# $Id$
#
# This file is in the public domain.
#
# Author: Robert Parlett (parlett@dial.pipex.com)
#

package gui

import io

class LineBasedScrollAreaRangeInfo()
   public
      first,
      last,
      first_whole,
      last_whole,
      offset
end


#
# This is a scroll area where the objects to be displayed are lines of
# a fixed height.
#
abstract class LineBasedScrollArea(DrawScrollArea)
   public 
      # Cached line height and count.
      line_height,
      cum_line_height,
      line_count

   #
   # Succeed if the line count is non-zero
   #
   public has_lines()
      succeed get_line_count() > 0
   end

   #
   # Get the line to go to on a page move.
   #
   public get_page_movement(curr, dir)
      local s, h, r, e
      if integer(h := get_line_height()) then
         return curr + dir * (view.h / h)
      else {
         s := 0
         r := curr
         if dir > 0 then {
            every e := h[curr + 1 to *h] do {
               if s + e >= view.h then
                  break
               r +:= 1
               s +:= e
            }
         } else {
            every e := h[curr - 1 to 1 by -1] do {
               if s + e >= view.h then
                  break
               r -:= 1
               s +:= e
            }
         }
         return r
      }
   end

   #
   # Get line info for the rectangle `r`, which defaults to the view.
   # If given, `r` must be within the view.  The result gives the
   # first and last lines in `r` and the first and last wholly visible
   # lines in `r`.  For the latter, if there are no such lines then
   # the first and last lines are returned.  Fails if there are no
   # data lines in `r`.
   #
   public get_line_info(r)
      local s, h, a, n, e, t, res, lc
      /r := view
      (r.h > 0) | fail
      a := r.y - get_subject_y()
      res := LineBasedScrollAreaRangeInfo()
      if integer(h := get_line_height()) then {
         lc := get_line_count()
         res.offset := a % h
         # Imagine r extended upwards to include the first partial bit...
         t := r.h + res.offset
         res.first := a / h + 1
         # This should only be possible if r is not view or lc = 0.
         if res.first > lc then
            fail
         res.last := res.first + t / h - 1
         if t % h > 0 then
            res.last +:= 1
         res.last >:= lc

         if res.offset > 0 & t / h > 1 then
            res.first_whole := min(res.first + 1, lc)
         else
            res.first_whole := res.first

         # The term in brackets gives the distance from the base of the partial
         # one below up to the top of r.
         if t % h > 0 & (r.h + h - t % h) / h > 1 then
            res.last_whole := min((a + r.h) / h, lc)
         else
            res.last_whole := res.last
      } else {
         s := 0
         n := 1
         every e := !h do {
            if /res.first & s + e > a then {
               res.first := n
               res.offset := a - s
            }
            if s >= a & s + e <= a + r.h then {
               /res.first_whole := n
               res.last_whole := n
            }
            if s < a + r.h then
               res.last := n
            else
               break
            n +:= 1
            s +:= e
         }
         if /res.first then
            fail
         /res.first_whole := res.first
         /res.last_whole := res.last
      }
      return res
   end

   #
   # Get the total line height upto but not including the line, which
   # may range from 1 ... line_count+1
   #
   public get_cum_line_height(line)
      local h, s
      if integer(h := get_line_height()) then
         return h * (line - 1)
      else {
         if /cum_line_height := [0] then {
            s := 0
            every put(cum_line_height, s +:= !h)
         }
         return cum_line_height[line]
      }
   end

   #
   # Goto line, horiz
   #
   public goto_pos(line, horiz)
      goto_first(\line)
      set_area_x(\horiz)
      return self
   end

   #
   # Make the given line the first in the view.
   #
   public goto_first(line)
      set_area_y(get_cum_line_height(line))
      return self
   end

   #
   # Make the given line the last in the view.
   #
   public goto_last(line)
      set_area_y(get_cum_line_height(line + 1) - view.h)
      return self
   end

   #
   # Ensure the given line is visible
   #
   public ensure_line_visible(line) 
      local i
      i := get_line_info() | fail
      # Use <= first rather than < first_whole: this is nicer if there are no
      # whole lines and line = first (it causes a scroll to top rather than not).
      if line <= i.first then
         goto_first(line)
      else if line >= i.last then
         goto_last(line)
      return self
   end

   #
   # Place the given line, so far as possible, in the centre of the
   # view area.
   #
   public center_line(line)
      set_area_y(get_cum_line_height(line) + get_height_for_line(line) / 2 - view.h / 2)
      return self
   end

   #
   # Invalidate the rectangle of the given line.
   #
   public invalidate_line(line, i)
      is_live() | fail
      /i := get_line_info()
      if \i & (i.first <= line <= i.last) then
         view.invalidate(get_line_rect(line))
      return self
   end

   #
   # Invalidate all the lines from line1 to line2
   #
   public invalidate_lines(line1, line2, i)
      local t
      is_live() | fail
      /i := get_line_info()
      if \i & t := range(line1, line2, i.first, i.last) then
         view.invalidate(get_line_rect(t.lo).union(get_line_rect(t.hi)))
      return self
   end

   # Return a `Rect` (in dialog-based coordinates) indicating the area
   # occupied by the line; note this may be outside of the view area.
   #
   public get_line_rect(line)
      return Rect(view.x, 
                  get_subject_y() + get_cum_line_height(line),
                  view.w,
                  get_height_for_line(line))
   end

   # Return a `Rect` (in area-based coordinates) indicating the area
   # occupied by the line.
   #
   public get_area_line_rect(line)
      return Rect(0, 
                  get_cum_line_height(line),
                  get_subject_width(),
                  get_height_for_line(line))
   end

   #
   # Convert the current y pointer offset to a line number.  The result is <=0 if the
   # pointer is above all lines, >get_line_count() if below.  The view window is not
   # taken into account, so the result may not actually be visible.
   #
   public pointer_to_line(e)
      local s, h, a, r, i
      a := e.y - get_subject_y()
      if integer(h := get_line_height()) then 
         return a / h + 1
      else {
         if a < 0 then
            return 0
         s := 0
         r := 1
         every i := !h do {
            if s <= a < s + i then
               break
            r +:= 1
            s +:= i
         }
         return r
      }
   end

   #
   # Return the line under the current x, y cursor, or fail if not over a line.
   #
   public get_line_under_pointer(e)
      local r, i
      (view.in_region(e) & has_lines()) | fail
      r := pointer_to_line(e)
      i := get_line_info() | fail
      if i.first <= r <= i.last then
         return r
   end

   #
   # Return the nearest line to the pointer; fails only if there are no lines.
   #
   public get_nearest_line_to_pointer(e)
      local r, i
      has_lines() | fail
      r := pointer_to_line(e)
      i := get_line_info() | fail
      r <:= i.first
      r >:= i.last
      return r
   end

   public override draw()
      local xp, yp, i, info, h, lh, r

      r := view.get_cbwin_clip()

      info := get_line_info(r) | fail
      h := get_line_height()

      #
      # Where to draw them
      #
      yp := r.y - info.offset

      #
      # Left offset
      #
      xp := get_subject_x()

      #
      # Write the lines
      #
      every i := info.first to info.last do {
         lh := integer(h) | h[i]
         draw_line(xp, yp, lh, i)
         yp +:= lh
      }
   end

   #
   # This method is overridden by the subclass to draw the given 
   # line at the given position, into the buffer window cbwin.
   # :Parameters :
   # :  `xp` - The x position it should be drawn at
   # :  `yp` - The y position it should be drawn at
   # :  `lh` - The line height allowed for this line
   # :  `i` - The line number to draw
   #
   public abstract draw_line(xp, yp, lh, i)

   #
   # Clear the cached value of line height (and subject height).
   #
   public invalidate_line_height()
      self.subject_height := self.line_height := self.cum_line_height := &null
      return self
   end

   #
   # This method is overridden by the subclass to work out the
   # line height.  Because this operation is potentially expensive, it
   # is called as late as possible and its value cached.
   #
   public abstract calculate_line_height()

   #
   # This method is overridden by the subclass to work out the
   # line count. Because this operation is potentially expensive, it
   # is called as late as possible and its value cached.
   #
   public abstract calculate_line_count()

   #
   # Return the line height
   #
   public get_line_height()
      /self.line_height := calculate_line_height()
      return self.line_height
   end

   #
   # Return the line count
   #
   public get_line_count()
      /self.line_count := calculate_line_count()
      return self.line_count
   end

   #
   # Clear the cached value of line count (and subject height).
   #
   public invalidate_line_count()
      self.subject_height := self.line_count := &null
      return self
   end

   public override calculate_subject_height()
      local h, s
      if integer(h := get_line_height()) then
         return get_line_count() * h
      else {
         s := 0
         every s +:= !h
         return s
      }
   end

   public get_height_for_line(i)
      local h
      if integer(h := get_line_height()) then
         return h
      else
         return h[i]
   end

   public override get_default_vertical_increment()
      local h
      if integer(h := get_line_height()) then
         return h
      else
         return scale(16)
   end

   public override get_default_horizontal_increment()
      return self.cbwin.text_width("m")
   end

   public dump()
      local i
      i := get_line_info()
      write("lines:", i.first, "-", i.last)
      write("whole lines:", i.first_whole, "-", i.last_whole)
      write("offset: ", i.offset)
      write("----------")
   end
end

