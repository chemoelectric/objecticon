import gui

class CanvasPaint(CanvasObject)
   public dialog()
      return PaintEdit(self)
   end

   private override pre_decode()
      new()
   end

   public new()
      self.name := "paint"
      self.import_name := "gui"
      return
   end
end


class CanvasTextPaint(CanvasPaint,TextPaint)
   public override dup()
      local c
      c := CanvasTextPaint()
      CanvasObject.dup_fields(c)
      c.str := self.str
      return c
   end

   public override get_template()
      suspend CanvasPaint.get_template() |  [ "Str", "str"]
   end

   public override gen_code(c)
      c.line(name || " := " || self.class_name || "(" || image(self.str) || ")")
      c.add_var(name, class_variable)
      c.add_import(self.import_name, self.class_name)
   end

   public override new(s)
      TextPaint.new(s)
      CanvasPaint.new()
      self.class_name := "TextPaint"
      return
   end
end


class CanvasImagePaint(CanvasPaint,ImagePaint)
   public override dialog()
      return ImagePaintEdit(self)
   end

   public override dup()
      local c
      c := CanvasImagePaint()
      CanvasObject.dup_fields(c)
      c.image_str := self.image_str
      c.image_cache := self.image_cache
      c.fill_dimensions := self.fill_dimensions
      c.max_w := self.max_w
      c.max_h := self.max_h
      return c
   end

   public override get_template()
      suspend CanvasPaint.get_template() |
         ["Image Str", "image_str"] |
         ["Image Cache", "image_cache"] |
         ["Fill Dimensions", "fill_dimensions"] |
         ["Max W", "max_w"] |
         ["Max H", "max_h"]

   end

   public override gen_code(c)
      c.line(name || " := " || self.class_name || "()")
      c.chain("set_image(" || image(\self.image_str) || ")")
      c.chain("set_cache(" || image(\self.image_cache) || ")")
      c.chain("set_max_w(" || \self.max_w || ")")
      c.chain("set_max_h(" || \self.max_h || ")")
      c.chain("set_fill_dimensions(" || (0 < self.fill_dimensions) || ")")
      c.add_var(name, class_variable)
      c.add_import(self.import_name, self.class_name)
   end

   public override new()
      ImagePaint.new()
      CanvasPaint.new()
      self.class_name := "ImagePaint"
      return
   end
end


class CanvasGridPaint(CanvasPaint,GridPaint)
   public override dialog()
      return GridPaintEdit(self)
   end

   public override dup()
      local c
      c := CanvasGridPaint()
      CanvasObject.dup_fields(c)
      c.fmt := self.fmt
      return c
   end

   public override get_template()
      suspend CanvasPaint.get_template() | [ "Fmt", "fmt"]
   end

   public override gen_code(c)
      c.line(name || " := " || self.class_name || "(" || image(self.fmt) || ")")
      c.add_var(name, class_variable)
      c.add_import(self.import_name, self.class_name)
   end

   public override new(s)
      GridPaint.new(s)
      CanvasPaint.new()
      self.class_name := "GridPaint"
      return
   end
end



