#
# $Id$
#

package mail

import ipl.pdco(String)

#
# This class represents a group, which is a phrase, plus
# zero or more mailboxes.
#
class Group(Address)
   private readable 
      mailboxes,
      phrase

   public get_phrase() 
      return .phrase
   end

   public set_phrase(x)
      phrase := x
      return self
   end

   public get_mailboxes() 
      return .mailboxes
   end

   public set_mailboxes(x)
      mailboxes := x
      return self
   end

   public add_mailbox(x)
      put(mailboxes, x)
      return self
   end

   public to_rfc822()
      local tmp
      tmp := String{ (!mailboxes).to_rfc822(), ", " }
      return if *phrase > 0 then
         phrase || ": " || tmp || " ;"
      else
         ": " || tmp || " ;"
   end

   public static parse(s)
      return RFC822Parser().parse_group(s)
   end   

   public gen_mailboxes()
      suspend !mailboxes
   end

   public new(mailboxes, phrase)
      /mailboxes := []
      /phrase := ""
      self.mailboxes := mailboxes
      self.phrase := phrase
      return
   end
end

