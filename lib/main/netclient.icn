#
# $Id$
#

package net

import 
   util, 
   io

#
# Common base class for a client class which holds a connection to a
# server and communicates using CRLF-terminated lines.
#
class NetClient(Connectable, SetFields)
   public static const 
      CONNECTING_EVENT,
      CONNECTED_EVENT,
      CLOSED_EVENT,
      SENDING_STRING_EVENT,
      SENDING_LINE_EVENT,
      READ_LINE_EVENT,
      READ_STRING_EVENT

   private static init()
      CONNECTING_EVENT := "Connecting"
      CONNECTED_EVENT := "Connected"
      CLOSED_EVENT := "Closed"
      SENDING_STRING_EVENT := "Sending string"
      SENDING_LINE_EVENT := "Sending line"
      READ_LINE_EVENT := "Read line"
      READ_STRING_EVENT := "Read string"
   end

   private readable
      path,
      server,
      port,
      timeout

   private
      connection,
      sbuff

   # 
   # Fail and set the error code, and close the connection.
   #
   protected error_and_close(a)
      error(a)
      close()
   end

   #
   # Set the timeout in ms to use.  If a figure of zero, or &null, is given (the default)
   # then no timeout will be used.
   #
   public set_timeout(timeout)
      return self.timeout := timeout
   end

   #
   # Open the connection.
   #
   protected open()
      local r, domain, addr

      if \server then {
         domain := ProtocolFormat.INET
         addr := "inet:" || server || ":" || port
      } else if \path then {
         domain := ProtocolFormat.UNIX
         addr := "unix:" || path
      } else
         runerr("Neither server/port nor path set")

      close() | fail
      connection := SocketStream.socket(domain)

      # If we are using a timeout, put the socket into non-blocking mode
      if \timeout > 0 then
         connection.flag(FileOpt.NONBLOCK) | fail

      fire(CONNECTING_EVENT)
      connection.connect(addr) | {
         if \timeout > 0 & errno() = Errno.EINPROGRESS then {
            r := DescStream.poll(connection, Poll.OUT, timeout)[1] | {
               close()
               return error("Couldn't connect to " || addr || ":" || &why)
            }
            if r ~= Poll.OUT then {
               close()
               return error("Couldn't connect to " || addr || " (socket error)")
            }
         } else {
            close()
            return error("Couldn't connect to " || addr || ":" || &why)
         }
      }
      sbuff := ""
      fire(CONNECTED_EVENT)
      return
   end

   #
   # Close the connection
   #
   protected close()
      if \connection then {
         connection.close() | return error("Failed to close connection:" || &why)
         fire(CLOSED_EVENT)
         connection := &null
      }
      return
   end

   #
   # Set the path to use (implies using PF_UNIX socket domain)
   #
   public set_path(s)
      path := s
   end

   #
   # Set the server to use (implies using PF_INET socket domain)
   #
   public set_server(s)
      server := s
   end

   #
   # Set the port
   #
   public set_port(n)
      port := n
   end

   #
   # Low-level write on the connection. 
   #
   private send(s)
      local i, r

      while *s > 0 do {
         if \timeout > 0 then {
            r := DescStream.poll(connection, Poll.OUT, timeout)[1] | fail
            if r ~= Poll.OUT then
               return error("Send failed (socket error)")
         }
         i := connection.out(s) | fail
         s := s[i+1 : 0]
      }
      return
   end

   #
   # Low-level read on the connection. 
   #
   private recv(len)
      local r

      if \timeout > 0 then {
         r := DescStream.poll(connection, Poll.IN, timeout)[1] | fail
         if r ~= Poll.IN then
            return error("Receive failed (socket error)")
      }
      return connection.in(len)
   end

   #
   # Write the given string to the connection.
   #
   protected write_str(s)
      fire(SENDING_STRING_EVENT, s)
      return send(s)
   end

   #
   # Write a single line to the connection, appending CRLF to the output stream.
   # 
   protected write_line(s)
      /s := ""
      fire(SENDING_LINE_EVENT, s)
      return send(s || "\r\n") 
   end

   #
   # Read a single line from the connection, and return it.
   #
   protected read_line()
      local line

      repeat {
         #
         # Look for a line and if found return it.
         #
         sbuff ? {
            if line := tab(find("\r\n")) then {
               move(2)
               sbuff := tab(0)
               fire(READ_LINE_EVENT, line)
               return line
            }
         }
         sbuff ||:= recv(1024) | fail
      }
   end

   #
   # Read up to len bytes on the connection.
   #
   protected read_str(len)
      local t
      #
      # Use the line buffer if it has anything in it.
      #
      if *sbuff > 0 then {
         sbuff ? {
            t := move(len) | tab(0)
            sbuff := tab(0)
         }
      } else {
         t := recv(len) | fail
      }
      fire(READ_STRING_EVENT, t)
      return t
   end

   protected is_open()
      if \self.connection then
         return
   end

   protected eof()
      return connection.eof()
   end

   protected set_one(f)
      case f.attr of {
         "timeout": set_timeout(f.int_val())
         "server": set_server(f.string_val())
         "path": set_path(f.string_val())
         "port": set_port(f.int_val())
         default : fail
      }
      return
   end

   protected new(a[])
      cast(self,Connectable).new()
      sbuff := ""
      set_fields(a)
      return
   end
end
