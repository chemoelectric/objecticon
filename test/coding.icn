import lang,io

record rec1()
record rec2(a,b,c)
record rec3(p1,p2,p3)

class One()
   public x,y,z
end

class Two(SelectiveObjectCodec)
   private a,b,c

   private override get_template()
      suspend ["Fred","a"]|["Jim","b"]|["Mary","c"]
   end

   public dood()
      write(a,",",b,",",c)
   end

end

class Three(ObjectCodec)
   public x,y,z

   protected override object_encode(e)
      e.encode(x)
      e.encode(y)
      e.encode(z)
   end

   protected override object_decode(e)
      x := e.decode()
      y := e.decode()
      z := e.decode()
   end
end

procedure test(obj)
   local s, x
   s := encode(obj)
   x := decode(s) | stop("Couldn't decode: ", s)
   equals(obj, x) | stop("Failed:",to_string(obj),"-v-",to_string(x))
end

procedure main()
   local t, u, l
   test(&null)
   test(100)
   test("abc")
   test(0)
   test(-100)
   test(-3.25)
   test('')
   test("")
   test('abc')
   test("abc")
   test([])
   test(list(3))
   test([11,2,3])
   test(["abc", [], "jim", &digits])
   test(&digits)
   test(&cset)
   test(&ascii)
   t := [1,2,3]
   put(t,t)
   test(t)
   u := [t]
   put(u,u)
   put(u,t)
   test(0)
   test(-100)
   test(-1)
   test(999999999999999)
   test(-999999999999999)
   test(0.1)
   test(0.0)
   test(-3e20)
   test("")
   test("xyz")
   test("|||\n\n\t")
   test(set())
   test(set([]))
   test(set([1,2,3]))
   t := set([1,2])
   insert(t,t)
   test(t)
   test(upto)
   test(main)
   t := table()
   test(t)
   t := table(100)
   test(t)
   t := table([99])
   test(t)
   insert(t,t,t)
   test(t)
   t := table(l := [1,2,3])
   put(l, l)
   insert(t,"abc",[1,2])
   insert(t,"himn",[3,1,2])
   insert(t, l, l)
   insert(t, t, &null)
   test(t)
   t := table()
   t[1] := "abc"
   t["def"] := 22
   t[3] := 4
   t[44] := &null
   t[&null] := 0
   test(t)
   t := rec1()
   test(t)
   t := rec2(1,3)
   test(t)
   t := rec2()
   test(t)
   t.a := t.b := t.c := t
   test(t)
   t := One()
   test(t)
   t.x := t.y := t
   test(t)
   t := One(1,2,3)
   test(t)
   test([ "abc\\is|here",1,2,3, main, upto, One(8,9,10), [], set([7,7,8]),t ,33.33, '1abcd88',
         &null, "fun", rec3(1,2,3), rec3()])
   t := Two(99,100,101)
   test(t)
   t := Three()
   test(t)
   t.x := t.y := t
   test(t)
   test(["hi","there"])

   t := [1,2]
   u := table(t)
   u[u] := u
   l := [t,u]
   test(l)
   test(rec1)
   test([upto,main,Prog.get_keyword("&yes"),Prog.get_operator("||",2),Prog.get_operator("*",1),
         Prog.get_operator("*",2)])
   write("Ok")
end
