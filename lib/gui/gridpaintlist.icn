#
# $Id$
#
# This file is in the public domain.
#
# Author: Robert Parlett (parlett@dial.pipex.com)
#

package gui

import ipl.pdco

#
# This class displays a list of objects using the GridPaint class.
#
class GridPaintList(SelectableScrollArea)
   public
      cache,
      cache_k,
      lim

   public get_draw(s)
      local d, k

      /cache := table()
      /cache_k := set()
      /lim := 10

      if d := \cache[s] then 
         return d

      if *cache > lim then {
         k := ?cache_k
         cache[k].finally()
         delete(cache, k)
         delete(cache_k, k)
      }

      d := GridPaint(s)
      d.initially(self)
      insert(cache, s, d)
      insert(cache_k, s)

      return d
   end

   public override finally()
      SelectableScrollArea.finally()
      every (!cache).finally()
      cache := cache_k := &null
   end

   public override calculate_line_height()
      return ipl.pdco.List{ get_draw(!self.contents).h }
   end

   public override calculate_subject_width()
      return MaxNN{ get_draw(!self.contents).w }
   end

   public override draw_line_data(xp, yp, lh, i)
      get_draw(contents[i]).draw(view.cbwin, xp, yp)
   end
end

