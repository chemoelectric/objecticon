############################################################################
#
#	File:     mirror.icn
#
#	Subject:  Procedure to mirror tile
#
#	Author:   Ralph E. Griswold
#
#	Date:     November 15, 1997
#
############################################################################
#
#  This file is in the public domain.
#
############################################################################
#
#  mirror(win)	mirrors win using p2mm symmetry and returns the result as a
#		hidden window.
#
############################################################################
#
#  Requires:  Version 9 graphics
#
############################################################################
#
#  Links:  wopen
#
############################################################################

package ipl.mirror

import
   graphics(Window)

# mirror with p2mm symmetry
procedure mirror(win, x, y, w, h)	
   local width, height, sym

   /x := 0
   /y := 0
   /w := win.get_width()
   /h := win.get_height()

   if w < 0 then {
      w := -w
      x -:= w
      }

   if h < 0 then {
      h := -h
      y -:= h
      }

   width := 2 * w
   height := 2 * h

   sym := Window().set_size(width, height) | fail

   win.copy_to(x, y, w, h, sym)

   every x := 0 to w - 1 do 
      sym.copy_to(x, 0, 1, h, sym, width - x - 1, 0)

   every y := 0 to h - 1 do
      sym.copy_to(0, y, width, 1, sym, 0, height - y - 1)

   return sym

end
