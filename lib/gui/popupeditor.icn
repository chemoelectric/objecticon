package gui

import graphics

#
# A class for wrapping a transient popup editor.
#
class PopupEditor(Component, MenuMode)
   private old_focus, editor, comp

   public popup(comp, ev)
      self.comp := comp
      # Copy attribs of the parent component
      set_wattrib_ancestor(comp)
      set_z(comp.parent_dialog.get_top_z())
      comp.parent_dialog.add(self)
      resize()
      old_focus := parent_dialog.get_focus()
      editor.grab_focus()
      parent_dialog.enter_menu_mode(self)
      invalidate()
      link
   end

   public override layout()
      editor.x := self.x
      editor.y := self.y
      editor.w := self.w
      editor.h := self.h
      editor.layout()
   end

   public override should_close(e)
      succeed member(Mouse.PRESS, e.code) & not(editor.in_region(e))
   end

   public override allow_nested(c)
      c := c.get_nesting_component()
      succeed c.gen_parents() === editor
   end

   public override get_nesting_component()
      return comp
   end

   public override gen_popup_components()
      return editor
   end

   protected forward_key(ev, src, type)
      # In menu mode key events are forwarded to this component,
      # rather than the editor, regardless of the focus.  So we
      # must forward them ourselves.
      if /ev.release & ev.code === "\e" then
         close_all()
      else
         parent_dialog.get_focus().fire(type, ev)
   end

   public override close_all()
      parent_dialog.set_focus(old_focus)
      invalidate()
      parent_dialog.exit_menu_mode(self)
      parent.remove(self)
      fire(Event.POPUP_CLOSED)
   end

   public override new(editor)
      Component.new()
      self.editor := editor
      editor.connect(close_all, Event.ACTION)
      self.add(editor)
      every connect(self.forward_key, Event.KEY_PRESS | Event.KEY_RELEASE)
      return
   end
end
