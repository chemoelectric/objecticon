#
# $Id$
#
# This file is in the public domain.
#
# Author: Robert Parlett (parlett@dial.pipex.com)
#

import gui


############################################################################
#
#
#
class CanvasSplitDialog(CanvasComponentDialog)
   public 
      show_inter,
      min_first,
      min_second,
      weight

   public override set_vals()
      if show_inter.is_checked() then
         c.sizer.set_show_inter(&yes)
      else
         c.sizer.set_show_inter(&no)
      c.set_min(integer(min_first.get_contents()),
                integer(min_second.get_contents()))
      c.set_weight(numeric(weight.get_contents()))
      CanvasComponentDialog.set_vals()
   end

   public override setup()
      local p
      CanvasComponentDialog.setup()
      
      p := TabItem()
      p.set_layout(GridLayout().set_extra("borders"))
      p.set_label("General")
      add_tab_at_front(p)

      show_inter := CheckBox().set_label("Show intermediate sizer bar").set_constraint("eol", &yes).set_constraint("w", 2)
      if \c.sizer.show_inter_flag then
         show_inter.set_is_checked(&yes)
      p.add(show_inter)

      p.add(Label().set_label("Min left/top size:"))
      min_first := TextField().set_size(scale(100)).set_filter(&digits).set_contents(c.min_first).set_constraint("eol", &yes)
      p.add(min_first)

      p.add(Label().set_label("Min right/bottom size:"))
      min_second := TextField().set_size(scale(100)).set_filter(&digits).set_contents(c.min_second).set_constraint("eol", &yes)
      p.add(min_second)

      p.add(Label().set_label("Weight:"))
      weight := TextField().set_size(scale(100)).set_filter(&digits ++ '.').set_contents(c.weight).set_constraint("eol", &yes)
      p.add(weight)
   end
end

