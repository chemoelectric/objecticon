#
# $Id$
#
# This file is in the public domain.
#
# Author: Robert Parlett (parlett@dial.pipex.com)
#

package gui

import graphics

#
# This class is just a superclass of {List} and {EditList}.
#
# @field selection=integer : see {set_selection}
# @field selection_list=t1,t2,t3,... : see {set_selection_list}.  This may
# @ also be given (as a list) as the first parameter to the constructor.
# @field direction=string : see {set_direction}
# @field max_height=integer : see {set_max_height}
# @field max_width=integer : see {set_max_width}
# @field see also {Component}
#
class DropDown(SelectionList,Component)
   public 
      b,                           
      view,
      tl,
      old_selection,
      direction,
      max_height,
      max_width

   public static const
      UP,
      DOWN,
      BEST

   private static init()
      UP := "up"
      DOWN := "down"
      BEST := "best"
   end

   #
   # Set the direction to pop-up in.  Options are {UP}, {DOWN} or
   # {BEST} (the default).
   #
   public set_direction(s)
      self.direction := s
   end

   public on_button_press(ev)
      if \tl then
         #
         # Button pressed whilst list open; just close
         #
         self.close_textlist()
      else
         #
         # Button pressed whilst no list; open list
         #
         self.open_textlist(ev)
   end

   public on_textlist_selection(ev)
      local tmp
      #
      # Selection in list - close textlist, amend label.
      #
      tmp := tl.get_selections()[1]
      self.close_textlist()
      go_to(\tmp, ev)
   end

   public start_change(e)
      self.old_selection := self.selection
   end

   public end_change(e)
      if self.old_selection ~=== self.selection then
         fire(Event.SELECTION_CHANGED, e)
   end

   #
   # Create the view component
   #
   public defer create_view()

   public create_popup()
      return TextList()
   end

   #
   # Set the maximum height of the dropdown list; by default it is as large as is necessary
   # and can be accommodated on the screen.
   #
   public set_max_height(n)
      self.max_height := n
   end

   #
   # Set the maximum width of the dropdown list; by default it is as wide as is necessary
   # and can be accommodated on the screen.
   #
   public set_max_width(n)
      self.max_width := n
   end

   public go_up(e)
      if *selection_list = 0 then
         fail
      start_change(e)
      if /self.selection | (self.selection = 1) then
         set_selection(*self.selection_list)
      else
         set_selection(self.selection - 1)
      end_change(e)
   end

   public go_down(e)
      if *selection_list = 0 then
         fail
      start_change(e)
      if /self.selection then
         set_selection(1)
      else
         set_selection(1 + self.selection % *self.selection_list)
      end_change(e)
   end

   public go_to(x, e)
      start_change(e)
      set_selection(x)
      end_change(e)
   end

   public open_textlist(ev)
      local max_h, max_w, spc_below, spc_above, spc_right, d, dim, win, win_spc_above, 
         win_spc_below, win_spc_right, use_popup_win, use_direction, rs, r

      win := parent_dialog.win
      dim := win.get_display_size()
      # Screen rectangle, in adjusted co-ordinates
      rs := Rect(-win.get_x() - win.get_dx(), -win.get_y() - win.get_dy(), dim[1], dim[2])
      r := parent_dialog.intersect(rs)

      win_spc_above := self.y - r.y
      win_spc_below := r.y + r.h - self.y - self.h
      win_spc_right := r.x + r.w - self.x

      if Gui.style.allow_popup(self) then {
         # Space available for popup limited by screen size
         spc_above := self.y - rs.y
         spc_below := rs.y + rs.h - self.y - self.h
         spc_right := rs.x + rs.w - self.x
      } else {
         # Space available limited by window size (and screen size, if window is off screen)
         spc_above := win_spc_above
         spc_below := win_spc_below
         spc_right := win_spc_right
      }

      max_h := case self.direction of {
         UP: spc_above
         DOWN: spc_below
         BEST: max(spc_above, spc_below)
         default: runerr("Unknown direction", self.direction)
      }
      max_w := spc_right
      max_w >:= \self.max_width
      max_h >:= \self.max_height

      self.tl := create_popup()
      self.tl.set_all_wattribs(self.get_effective_wattribs())
      self.tl.connect(self.on_textlist_selection, Event.SELECTION_CHANGED)
      self.tl.set_contents(self.selection_list)
      self.tl.set_draggable_cursor(&yes)
      self.tl.set_motion_cursor(&yes)
      self.tl.set_select_mode(Select.ONE)
      self.tl.set_selection_on_key_moves(&no)
      self.tl.set_z(self.parent_dialog.get_top_z())
      self.parent_dialog.add(tl)
      self.tl.initially()
      self.tl.set_ideal_size(self.w, 
                             max_w,
                             0, max_h)

      if tl.w_spec > win_spc_right then
         use_popup_win := 1
      case self.direction of {
         UP: {
            if tl.h_spec > win_spc_above then
               use_popup_win := 1
            use_direction := UP
         }
         DOWN: {
            if tl.h_spec > win_spc_below then
               use_popup_win := 1
            use_direction := DOWN
         }
         BEST: {
            if tl.h_spec > win_spc_below then
               if tl.h_spec <= spc_below then {
                  use_popup_win := 1
                  use_direction := DOWN
               } else if tl.h_spec > win_spc_above then {
                  use_popup_win := 1
                  use_direction := UP
               } else
                  use_direction := UP
            else
               use_direction := DOWN
         }
      }

      self.parent_dialog.enter_menu_mode(self)

      if Gui.style.allow_popup(self) & \use_popup_win then {
         # Remove tl from dialog and use add to popup window.
         self.tl.finally()
         self.parent_dialog.remove(tl)

         d := if use_direction == DOWN then 
            Dialog("canvas=popup", 
                   "w=", tl.w_spec, "h=",tl.h_spec,
                   "w:dx=", -self.x,
                   "w:dy=", -(self.y + self.h),
                   "w:x=", win.get_x() + win.get_dx() + self.x,
                   "w:y=", win.get_y() + win.get_dy() + self.y + self.h)
         else
            Dialog("canvas=popup", 
                   "w=", tl.w_spec, "h=",tl.h_spec,
                   "w:dx=", -self.x,
                   "w:dy=", -(self.y - tl.h_spec),
                   "w:x=", win.get_x() + win.get_dx() + self.x,
                   "w:y=", win.get_y() + win.get_dy() + self.y - tl.h_spec)

         d.add(tl)
         self.tl.set_pos(0, 0)
         self.parent_dialog.set_focus()
         self.parent_dialog.add_popup(d)
         d.show_modeless()
         d.set_focus(tl)
         d.held[ev.code] := [tl.view, tl, d]
      } else {
         if use_direction == DOWN then 
            self.tl.set_pos(self.x + win.get_dx(), self.y + self.h + win.get_dy())
         else
            self.tl.set_pos(self.x + win.get_dx(), self.y - self.tl.h_spec + win.get_dy())

         self.tl.resize()
         self.tl.invalidate()
         self.parent_dialog.hold(tl.view, ev.code)
         self.parent_dialog.set_focus(tl)
         # Since we are in menu mode, key events are forwarded to this
         # component, rather than the text list, regardless of the
         # focus.  So we must forward them on to the text list
         # ourselves.
         every self.parent_dialog.connect(self.forward_key_press, Event.KEY_PRESS | Event.KEY_RELEASE)
      } 

      self.parent_dialog.connect(self.dialog_event, Event.ICON)
      if \self.selection then {
         self.tl.set_cursor(self.selection)
         self.tl.center_line(self.selection)
      }
   end

   private forward_key_press(ev, src, type)
      tl.fire(type, ev)
   end

   public close_textlist()
      if /tl.parent_dialog.popup_manager then {
         if self.parent_dialog.focus === tl then
            self.parent_dialog.set_focus(view)
         self.tl.invalidate()
         self.tl.finally()
         self.parent_dialog.remove(tl)
      } else {
         if /self.parent_dialog.focus then
            self.parent_dialog.set_focus(view)
         self.parent_dialog.remove_popup(self.tl.parent_dialog)
         self.tl.parent_dialog.dispose()
      }
      self.tl := &null
      self.parent_dialog.exit_menu_mode()
      self.parent_dialog.disconnect_object(self)
   end

   public close_all()
      close_textlist()
   end

   public dialog_event(e)
      if (/e.release & (e.code === "\e")) | 
          (member(Mouse.PRESS, e.code) & 
                not(tl.in_region(e)) & not(b.in_region(e))) then 
      {
         #
         # Mouse click outside textlist.  Close.
         # 
         self.close_textlist()
      }
   end

   public initially()
      \self.selection_list | runerr("no selection list specified")
      cast(self,Component).initially()
   end

   public finally()
      #
      # Disposed with text list showing, just get rid of it
      #
      if \self.tl then 
         self.close_textlist()
      cast(self,Component).finally()
   end

   public get_default_height()
      return border.get_total_height() + view.get_default_height()
   end

   public display()
      border.draw_rect(self.cbwin, self)
      display_children()
   end

   public layout()
      local bw

      bw := b.get_preferred_width()
      #
      # Set button position and size
      #
      b.x := self.x + self.w - bw - border.get_r_inset()
      b.y := self.y + border.get_t_inset()
      b.w := bw
      view.x := self.x + border.get_l_inset()
      view.y := self.y + border.get_t_inset()
      view.w := self.w - border.get_total_width() - bw
      b.h := view.h := self.h - border.get_total_height()
      b.layout()
      view.layout()
   end

   protected set_one(f)
      case f.attr of {
         "selection_list" : set_selection_list(f.text_list_val())
         "selection" : set_selection(f.int_val())
         "direction" : set_direction(f.string_val())
         "max_height" : set_max_height(f.int_val())
         "max_width" : set_max_width(f.int_val())
         default: return cast(self,Component).set_one(f)
      }
      return
   end

   protected set_special(a)
      if type(a[1]) == "list" then
         set_selection_list(pop(a))
   end

   public new(a[])
      cast(self,Component).new()
      self.set_border(SunkenBorder())
      # Make the same width as the button in the scrollbar of the dropdown.
      self.b := IconButton("w=",ScrollArea.predict_scrollbar_size())
      self.b.set_parent(self)
      self.b.connect(self.on_button_press, Event.BUTTON_PRESS)
      self.b.set_paint(ImagePaint("cache=gui.ARROW_DOWN"))
      self.b.set_accepts_focus(&no)
      self.direction := BEST
      self.add(b)
      self.preferred_focus := self.view := create_view()
      self.add(view)
      self.set_constraint("x_fill", &yes)
      self.set_constraint("x_align", Align.L)
      self.set_constraint("x_weight", 1.0)
      connect(self.go_up, Event.MOUSE_WHEEL_UP)
      connect(self.go_down, Event.MOUSE_WHEEL_DOWN)
      set_fields(a)
      return
   end
end






