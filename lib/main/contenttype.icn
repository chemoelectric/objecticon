#
# $Id$
#

package mail

import datastruct(ClTable)

#
# This class represents a content type
#
class ContentType()
   private readable
      type,
      subtype,
      parameters

   #
   # Set type type
   #
   public set_type(t)
      self.type := t
      return self
   end

   #
   # Get the type
   #
   public get_type()
      return .type
   end

   #
   # Set subtype subtype
   #
   public set_subtype(s)
      self.subtype := s
      return self
   end

   #
   # Get the subtype
   #
   public get_subtype()
      return .subtype
   end

   #
   # Get the parameter for the given key.
   #
   # @param key the key
   public get_parameter(key)
      return parameters.member(key)
   end

   #
   # Set the parameter to the given value
   # @param key the key
   # @param value the value
   #
   public set_parameter(key, value)
      parameters.insert(key, value)
      return self
   end

   public static parse_rfc2616(s)
      return RFC2616Parser().parse_content_type(s)
   end   

   public static parse(s)
      return RFC822Parser().parse_content_type(s)
   end   

   public to_rfc1521()
      local s, e
      s := type || "/" || subtype
      every e := !parameters.sort() do
         s ||:= "; " || e[1] || "=" || RFC822Parser.escape_1521_value(e[2])
      return s
   end

   public new(type, subtype, parameters)
      /parameters := ClTable()
      self.type := type
      self.subtype := subtype
      self.parameters := parameters
      return
   end
end


