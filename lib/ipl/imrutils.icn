############################################################################
#
#	File:     imrutils.icn
#
#	Subject:  Procedures to deal with image records
#
#	Author:   Ralph E. Griswold
#
#	Date:     June 10, 2001
#
############################################################################
#
#  This file is in the public domain.
#
############################################################################
#
#  Procedures to manipulate image strings as records.  
#
#	imrcath(imr1, imr2)
#			concatenates imr1 and imr2 horizontally
#
#	imrcatv(imr1, imr2)
#			concatenates imr1 and imr2 vertically
#
#	imrcopy(imr)	create copy of imr
#
#	imrdraw(win, x, y, imr)
#			draws an image record
#
#	imrfliph(imr)	flips an image record horizontally
#
#	imrflipv(imr)	flips an image record vertically
#
#	imrnegative(imr)
#			produces "negative" of image; intended for
#			grayscale palettes
#
#	imropen(imr)	opens a hidden window with an image record
#
#	imrpshift(imr, ir)
#			shifts colors by mapping rotated palette
#
#	imrrot180(imr)
#			rotates an image record 180 degrees
#
#	imrrot90cw(imr)
#			rotates an image record 90 degrees clockwise
#
#	imrshifth(imr, i)
#			shifts an image record horizontally by i pixels
#			with wrap-around; positive i to the right,
#			negative to the left.
#
#	imrshiftv(imr, i)
#			shifts an image record vertically by i pixels
#			with wrap-around; positive i to the top,
#			negative to the bottom.
#
#	imstoimr(s)	converts an image string to an image record
#
#	imrtoims(imr)	converts an image record to an image string
#
#  Note:  All the procedures that produce image records modify their
#  argument records; they do not return modified copies.
#
############################################################################
#
#  Possible additions:
#
#	Make stripes from one (or more) rows/columns.
#
#	Convert from one palette to another.
#
############################################################################
#
#  Requires:  Version 9 graphics  
#
############################################################################
#
#  Links:  strings, wopen
#
############################################################################

package ipl.imrutils

import
   graphics(Window),
   ipl.strings(rotate)

record ImageRecord(width, palette, pixels)

# horizontally concatenate image records
procedure imrcath(imr1, imr2)	
   local imr, i, rows1, rows2

   if *imr1.pixels / imr1.width ~= *imr2.pixels / imr2.width then fail
   if imr1.palette ~== imr2.palette then fail

   imr := ImageRecord()
   imr.width := imr1.width + imr2.width
   imr.palette := imr1.palette

   rows1 := []

   imr1.pixels ? {
      while put(rows1, move(imr1.width))
      }

   rows2 := []

   imr2.pixels ? {
      while put(rows2, move(imr2.width))
      }

   imr.pixels := ""

   every i := 1 to *rows1 do
      imr.pixels ||:= rows1[i] || rows2[i]

   return imr

end

# vertically concatenate image records
procedure imrcatv(imr1, imr2)		
   local imr

   if imr1.width ~= imr2.width then fail
   if imr1.palette ~== imr2.palette then fail

   imr := ImageRecord()
   imr.width := imr1.width
   imr.palette := imr1.palette		# CHECK
   imr.pixels := imr1.pixels || imr2.pixels

   return imr

end

procedure imrcopy(imr)

   return copy(imr)

end

# draw image record
procedure imrdraw(win, x, y, imr)	


   /x := 0
   /y := 0

   return win.draw_image(x, y, imrtoims(imr))

end

# flip image record diagonally
procedure imrflipd(imr)			
   local height, columns, i, row

   height := *imr.pixels / imr.width
   columns := list(height, "")

   imr.pixels ? {
      while row := move(imr.width) do
         every i := 1 to imr.width do
            columns[i] ||:= row[i]
      }

   imr.pixels := ""

   every imr.pixels ||:= !columns

   imr.width := height

   return imr

end

# flip image record horizontally
procedure imrfliph(imr)			
   local pixels

   pixels := ""

   imr.pixels ? {
      while pixels ||:= reverse(move(imr.width))
      }

   imr.pixels := pixels

   return imr

end

# flip image record vertically
procedure imrflipv(imr)			
   local pixels

   pixels := ""

   imr.pixels ? {
      while pixels := move(imr.width) || pixels
      }

   imr.pixels := pixels

   return imr

end

# form negative of image record
procedure imrnegative(imr)		
   local chars

   chars := Window.palette_chars(imr.palette)

   imr.pixels := map(imr.pixels, chars, reverse(chars))

   return imr

end

# open window with image record
procedure imropen(imr)			
   local win

   win := Window().set_size(imr.width, *imr.pixels / imr.width)

   unless imrdraw(win, 0, 0, imr) then {
      win.close()
      fail
      }

   return win
   
end

# map shifted palette
procedure imrpshift(imr, i)		
   local chars

   chars := Window.palette_chars(imr.palette)

   imr.pixels := map(imr.pixels, chars, rotate(chars, i))

   return imr

end

# rotate image record 180 degrees
procedure imrrot180(imr)		

   imr.pixels := reverse(imr.pixels)

   return imr

end

# rotate image record 90 deg. clockwise
procedure imrrot90cw(imr)		
   local height, columns, i, row

   height := *imr.pixels / imr.width
   columns := list(imr.width, "")

   imr.pixels ? {
      while row := move(imr.width) do
         every i := 1 to imr.width do
            columns[i] := row[i] || columns[i]
      }

   imr.pixels := ""

   every imr.pixels ||:= !columns

   imr.width := height

   return imr

end

#  Note:  Since shifted out pixels enter in the top or bottom row, depending
#  on the direction of the shift, one full pass over the width raises the
#  image one pixel.

# shift image record horizontally
procedure imrshifth(imr, i)		

   imr.pixels := rotate(imr.pixels, i)

   return imr

end

#  See note on imrshifth()

# shift image record vertically
procedure imrshiftv(imr, i)		

   /i := 1

   imr.pixels := rotate(imr.pixels, i * imr.width)

   return imr

end

# convert image record to image string
procedure imrtoims(imr)			

   return imr.width || "," || imr.palette || "," || imr.pixels

end

# convert image string to image record
procedure imstoimr(s)			
   local imr

   imr := ImageRecord()

   s ? {
      imr.width := tab(upto(',')) | fail
      move(1)
      imr.palette := tab(upto(',')) | fail
      move(1)
      imr.pixels := tab(0)
      }

   return imr

end
