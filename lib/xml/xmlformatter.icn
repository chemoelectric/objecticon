#
# $Id$
#
# This file is in the public domain.
#
# Author: Robert Parlett (parlett@dial.pipex.com)
#

package xml

import util

#
# A formatter for XML documents.  The default formatter behaviour is to output the
# string content as held in the children array (this may or may not have had whitespace
# removed depending on whether validation against a DTD occurred), and without
# indentation.
#
class XmlFormatter(Formatter)
   public
      indent, 
      no_whitespace_flag, 
      text_trim_flag, 
      as_read_flag

   #
   # Ensure that each opening element appears with an indent of n chars.  Whitespace
   # will be inserted as appropriate.  By default, no indentation is done.
   #
   public set_indent(n)
      self.indent := /n | need_integer(n, 0)
      link
   end

   #
   # Configure whether whitespace-only string content will be discarded.  The default
   # is off, ie keep whitespace.
   #
   public set_no_whitespace(s)
      self.no_whitespace_flag := need_flag(s)
      link
   end

   #
   # Configure whether whitespace-only string content will be discarded,
   # and in addition any other string content will be trimmed at both ends.  The
   # default is off
   #
   public set_text_trim(s)
      self.text_trim_flag := need_flag(s)
      link
   end

   #
   # Configure the formatter so that the string content will be output as read from
   # the input; ie the formatter uses the whitespace_children list rather than the children
   # list.  The default is off.
   #
   public set_as_read(s)
      as_read_flag := need_flag(s)
      link
   end

   protected override format_document(n)
      local c
      every c := !n.children do {
         format_node(c)
         write()
      }
   end

   protected override format_content(s)
      writes1(s)
   end

   protected override format_doctype(n)
      local s, x
      s := "<!DOCTYPE "
      s ||:= \n.name
      x := n.external_id
      if \x then {
         if \x.public_id then
            s ||:= " PUBLIC \"" || x.public_id || "\" \"" || x.system_id || "\""
         else
            s ||:= " SYSTEM \"" || x.system_id || "\""
      }
      s ||:= ">"
      writes1(s)
   end

   protected override format_comment(n)
      writes("<!--", n.comment, "-->")
   end

   protected override format_pi(n)
      local s
      s := "<?" || n.target
      if \n.content then
         s ||:= " " || n.content 
      s ||:= "?>"
      writes1(s)
   end

   protected override format_element(n)
      local s, istr1, istr2, x, l, e

      if \indent then {
         istr1 := "\N" || repl(" ", indent * level)
         istr2 := "\N" || repl(" ", indent * (level - 1))
      }

      s := "<" || n.name
      if *n.attributes > 0 then {
         s ||:= " "
         every x := !sort(n.attributes) do {
            s ||:= x[1] || "=\"" || xml_escape(x[2], '&<>\"') || "\" "
         }
      }
      if *n.children = 0 then
         s ||:= "/>"
      else
         s ||:= ">"
      writes1(s)

      if *n.children > 0 then {
         l := get_children(n)
         if *l = 1 & text(l[1]) then {
            writes1(xml_escape(l[1], '&<>\"'))
         } else {
            every e := !l do {
               if \indent then
                  writes1(istr1)
               if text(e) then
                  writes1(xml_escape(e, '&<>\"'))
               else
                  format_node(e)
            }
            if \indent then
               writes1(istr2)
         }
         writes("</", n.name, ">")
      }
   end

   private get_children(el)
      if \as_read_flag then
         return \el.whitespace_children | el.children
      if \no_whitespace_flag then
         return el.get_children_no_whitespace()
      if \text_trim_flag then
         return el.get_trimmed_children()

      return el.children
   end

   protected override format_cdata(n)
      writes("<![CDATA[", n.content, "]]>")
   end

   protected override format_xmldecl(n)
      local s
      s := "<?xml "
      if \n.version then
         s ||:= "version='" || n.version || "' "
      if \n.encoding then
         s ||:= "encoding='" || n.encoding || "' "
      if \n.standalone then
         s ||:= "standalone='" || n.standalone || "' "
      s ||:= "?>"
      writes1(s)
   end
end

