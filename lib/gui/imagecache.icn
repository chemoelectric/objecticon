package gui

import 
   graphics, 
   util,
   cairo,
   io,
   lang,
   ipl.imagedetect

$if _PNG
$load CLOCK "images/gui.CLOCK_24.png"
$load CLOSE "images/gui.CLOSE_24.png"
$load FONT "images/gui.FONT_24.png"
$load FILESIZE "images/gui.FILESIZE_24.png"
$load CIRCULATE "images/gui.CIRCULATE_12.png"
$load FILE "images/gui.FILE_16.png"
$load OPEN_FOLDER "images/gui.OPEN_FOLDER_16.png"
$load CLOSED_FOLDER "images/gui.CLOSED_FOLDER_16.png"
$load FAVOURITES "images/gui.FAVOURITES_24.png"
$load UP "images/gui.UP_24.png"
$load PREV "images/gui.PREV_24.png"
$load NEXT "images/gui.NEXT_24.png"
$load REFRESH "images/gui.REFRESH_24.png"
$else
$load CLOCK "images/gui.CLOCK_24.gif"
$load CLOSE "images/gui.CLOSE_24.gif"
$load FONT "images/gui.FONT_24.gif"
$load FILESIZE "images/gui.FILESIZE_24.gif"
$load CIRCULATE "images/gui.CIRCULATE_12.gif"
$load FILE "images/gui.FILE_16.gif"
$load OPEN_FOLDER "images/gui.OPEN_FOLDER_16.gif"
$load CLOSED_FOLDER "images/gui.CLOSED_FOLDER_16.gif"
$load FAVOURITES "images/gui.FAVOURITES_24.gif"
$load UP "images/gui.UP_24.gif"
$load PREV "images/gui.PREV_24.gif"
$load NEXT "images/gui.NEXT_24.gif"
$load REFRESH "images/gui.REFRESH_24.gif"
$endif
$load HOME "images/gui.HOME_24.gif"
$load NEW_FOLDER "images/gui.NEW_FOLDER_24.gif"
$load HIDDENFILE "images/gui.HIDDENFILE_24.gif"
$load CONFIGURE "images/gui.CONFIGURE_24.gif"
$load WINDOW_CLOSE "images/gui.WINDOW_CLOSE_16.gif"
$load WINDOW_ICONIFY "images/gui.WINDOW_ICONIFY_16.gif"

$load CLOCK_SVG "images/gui.CLOCK_SVG.svgz"
$load FILESIZE_SVG "images/gui.FILESIZE_SVG.svgz"
$load FONT_SVG "images/gui.FONT_SVG.svgz"
$load REFRESH_SVG "images/gui.REFRESH.svgz"
$load UP_SVG "images/gui.UP.svgz"
$load PREV_SVG "images/gui.PREV.svgz"
$load NEXT_SVG "images/gui.NEXT.svgz"

final abstract class ImageCache()
   public static 
      imap,
      names

   private static init()
      local i, j
      names := table()
      imap := table()
      name_image("gui.HOME_24", HOME)
      name_image("gui.PREV_24", PREV)
      name_image("gui.NEXT_24", NEXT)
      name_image("gui.CLOSE_24", CLOSE)
      name_image("gui.NEW_FOLDER_24", NEW_FOLDER)
      name_image("gui.REFRESH_24", REFRESH)
      name_image("gui.UP_24", UP)
      name_image("gui.CLOCK_24", CLOCK)
      name_image("gui.FONT_24",FONT)
      name_image("gui.FILESIZE_24", FILESIZE)
      name_image("gui.HIDDENFILE_24", HIDDENFILE)
      name_image("gui.FAVOURITES_24", FAVOURITES)
      name_image("gui.CONFIGURE_24", CONFIGURE)
      name_image("gui.CLOSED_FOLDER_16", CLOSED_FOLDER)
      name_image("gui.OPEN_FOLDER_16", OPEN_FOLDER)
      name_image("gui.FILE_16", FILE)
      name_image("gui.WINDOW_CLOSE_16", WINDOW_CLOSE)
      name_image("gui.WINDOW_ICONIFY_16", WINDOW_ICONIFY)
      name_image("gui.CIRCULATE_12", CIRCULATE)

      name_image("gui.CLOCK_SVG", CLOCK_SVG)
      name_image("gui.FILESIZE_SVG", FILESIZE_SVG)
      name_image("gui.FONT_SVG", FONT_SVG)
      name_image("gui.REFRESH_SVG", REFRESH_SVG)
      name_image("gui.UP_SVG", UP_SVG)
      name_image("gui.NEXT_SVG", NEXT_SVG)
      name_image("gui.PREV_SVG", PREV_SVG)

      i := scale_odd(13)
      j := scale(2)
      name_image("gui.BOX_UP", mk_box_up(i, j))
      name_image("gui.BOX_DOWN",  mk_box_down(i, j))
      name_image("gui.DIAMOND_UP", mk_diamond_up(i, j))
      name_image("gui.DIAMOND_DOWN",  mk_diamond_down(i, j))

      i := scale_odd(11)
      name_image("gui.ARROW_UP", mk_arrow_up(i))
      name_image("gui.ARROW_DOWN", mk_arrow_down(i))

      i := scale_odd(9)
      name_image("gui.ARROW_LEFT", mk_arrow_left(i))
      name_image("gui.ARROW_RIGHT", mk_arrow_right(i))

      i := scale_odd(7)
      name_image("gui.TINY_ARROW_UP", mk_arrow_up(i))
      name_image("gui.TINY_ARROW_DOWN", mk_arrow_down(i))

      j := scale(1)
      i := (if j % 2 = 0 then scale_even else scale_odd)(9)
      name_image("gui.PLUS", mk_handle(i, j))
      name_image("gui.MINUS", mk_handle(i, j, &yes))

      i := scale_odd(5)
      j := scale(6)
      name_image("gui.VERT_GRID", mk_v_grid(i, j))
      name_image("gui.HORIZ_GRID", mk_h_grid(i, j))
   end

   #
   # Associate image data (eg gif file data), with a name.  The data
   # must be either a string, or a `Pixels` instance.  In the latter
   # case, the instance must never be closed.
   #
   # The name can then be passed to `get` or `load` to retrieve a
   # window with the corresponding image.  It is a runtime error if
   # the key already exists in the table.
   #
   public static name_image(name, data)
      name := need_string(name)
      if member(names, name) then
         runerr("ImageCache duplicate name key", name)
      is(data, Pixels) | (data := need_string(data))
      insert(names, name, data)
      link
   end

   #
   # Given an existing key, add an entry to the name-data table with
   # `new_key`, pointing to the same data.  Fails if `key` is not an
   # existing key into the data table.
   #
   public static copy_name(new_key, key)
      return name_image(new_key, get_named_image(key))
   end

   #
   # Return the data previously set with `name_image`.  Fails if the
   # name is unknown.
   #
   public static get_named_image(name)
      return member(names, need_string(name))
   end

   #
   # Place an entry into the window cache with the given key.  The
   # window should not be subsequently closed.  It is a runtime error
   # if the key already exists in the table.
   #
   public static cache_window(key, win)
      key := need_string(key)
      if member(imap, key) then
         runerr("ImageCache duplicate image key", key)
      insert(imap, key, need_Window(win))
      link
   end

   #
   # Given an existing key, add an entry to the window cache with
   # `new_key`, pointing to the same window.  Fails if `key` is not an
   # existing key into the image table.
   #
   public static copy_key(new_key, key)
      return cache_window(new_key, get_cached_window(key))
   end

   #
   # Return an entry in the window cache, or fail if the key is not
   # present.
   #
   public static get_cached_window(key)
      return member(imap, need_string(key))
   end

   #
   # Load an image, using the cache if possible.  The parameter `s`
   # should be either a file name, or the name of an image set by
   # `name_image()`.  On a successful load, the image is stored in the
   # cache for future use, and thus should not be closed after use.
   #
   # In this and subsequent methods, `win` is a `Window`.  This
   # parameter is only required if the image data in question is an
   # image string.  The window is then used to provide the foreground
   # and background palette colours.
   #
   public static get(s, win)
      return member(imap, get_key(s, win))
   end

   #
   # This is the same as `get`, but returns the key into the cached
   # window map, rather than the window.  If it succeeds, then the
   # returned key will be in the map, and the window can be retrieved
   # by `get_cached_window`.
   #
   public static get_key(s, win)
      local w, key, p, fl
      key := s := need_string(s)
      if s := member(names, key) then
         fl := &yes
      if is_image_string(s) then {
         need_Window(win)
         key ||:= "#With colour "|| Window.color_value(win.get_fg()) || "/" || Window.color_value(win.get_bg())
         if member(imap, key) then
            return key
         \fl | return error("Invalid key for ImageCache")
         w := use {
            p := image_string_to_pixels(s, win),
            open_image(p)
         } | fail
      } else {
         if member(imap, key) then
            return key
         \fl | Files.access(key) | return error("Invalid key for ImageCache")
         w := open_image(s) | fail
      }
      insert(imap, key, w)
      return key
   end

   #
   # Load an image without using the cache.  The parameter `s` should
   # be either a file name, or the name of an image set by
   # `name_image()`.  It may also be raw image data (unlike `get`).
   # The returned image should be closed after use.
   #
   public static load(s, win)
      local p
      return use {
         p := load_pixels(s, win),
         open_image(p)
      }
   end

   #
   # This is like `load`, but a `Pixels` is returned instead of a
   # `Window`.
   #
   public static load_pixels(s, win)
      s := need_string(s)
      s := member(names, s)
      return if is(s, Pixels) then
         s.shared_copy()
      else if is_image_string(s) then
         image_string_to_pixels(s, win)
      else
         Pixels(s)
   end

   #
   # Convenient method to combine `copy_key` and `get_key_with_resize`
   #
   public static copy_key_with_resize(new_key, s, width, height, win)
      return copy_key(new_key, get_key_with_resize(s, width, height, win))
   end

   #
   # This is like `get`, but the resulting cached window will be of
   # the given dimensions.
   #
   public static get_with_resize(s, width, height, win)
      return member(imap, get_key_with_resize(s, width, height, win))
   end

   #
   # Like `get_with_resize`, but returns the key rather than the
   # window.
   #
   public static get_key_with_resize(s, width, height, win)
      local w, key, key1, s1, p, fl
      key := s := need_string(s)
      width := need_integer(width, 1)
      height := need_integer(height, 1)
      s1 := s
      if s1 := member(names, key) then
         fl := &yes
      if is_image_string(s1) then {
         need_Window(win)
         \fl | return error("Invalid key for ImageCache")
         key ||:= "#With colour "|| Window.color_value(win.get_fg()) || "/" || Window.color_value(win.get_bg())
      } else {
         \fl | Files.access(key) | return error("Invalid key for ImageCache")
      }

      if w := member(imap, key) &
         w.get_width() = width & w.get_height() = height
      then
         return key

      key1 := key
      key ||:= "#Resized to " || width || "x" || height
      if member(imap, key) then
         return key
      use {
         p := load_pixels(s, win),
         {
            if p.get_width() = width & p.get_height() = height then
               key := key1
            w := zoom_pix(p, width, height)
         }
      } | fail

      insert(imap, key, w)
      return key
   end

   #
   # This function takes an SVG filename, or key to named SVG data set
   # by `name_image()` (ie, an xml document), `s`, scales the SVG to
   # the given size, and returns a cached window.
   #
   public static get_scaled_svg(s, width, height)
      return member(imap, get_scaled_svg_key(s, width, height))
   end

   #
   # Like `get_scaled_svg`, but returns the key rather than the window.
   #
   public static get_scaled_svg_key(s, width, height)
      local w, key, cc, svg
      s := need_string(s)
      width := need_integer(width, 1)
      height := need_integer(height, 1)
      key := s || "#Scaled to " || width || "x" || height
      if member(imap, key) then
         return key
      (s := member(names, s)) | Files.access(s) | return error("Invalid key for ImageCache")
      use {
         svg := SVG(s),
         {
            w := new_win()
            w.set_size(width, height).
              set_bg("transparent").
              erase_area()
            use {
               cc := Context(w).
                  scale(real(width) / svg.get_width(), real(height) / svg.get_height()),
               svg.render(cc)
            }
            insert(imap, key, w)
         }
      } | fail
      return key
   end

   #
   # A convenient function to try to get a scaled SVG from the given
   # data `s`, but if SVG capability is not available then to use the
   # alternative conventional image `alt_s`, resized in either case to
   # the given dimensions.
   #
   public static get_scaled_svg_key_alt(s, alt_s, width, height)
      return if \Context.LOADED then 
         get_scaled_svg_key(s, width, height) 
      else 
         get_key_with_resize(alt_s, width, height)
   end

   #
   # Useful debug function to print the contents of the cache.
   #
   public static show()
      local k, v, i
      write("Cache name table\n----------------")
      every k := key(names) do {
         v := names[k]
         if is(v, Pixels) then
            write("\t", k, " -> ", to_string(v))
         else {
            writes("\t", k, " -> ", *v, " bytes ")
            if i := ImageStringInfo(v) then
               write("image string ", i.width, "x", i.height)
            else if i := detect(v) then
               write(i.format, " ", i.width, "x", i.height)
            else
               write("data")
         }
      }
      write("\nCache image table\n-----------------")
      every k := key(imap) do {
         v := imap[k]
         write("\t", k, " -> ", to_string(v))
      }
   end
end
