############################################################################
#
#	File:     xformimg.icn
#
#	Subject:  Procedures to transform image
#
#	Author:   Ralph E. Griswold
#
#	Date:     February 4, 1995
#
############################################################################
#
#   This file is in the public domain.
#
############################################################################
#
#  These procedures perform reflections, rotations, and concatenations
#  of images.
#
#  Warning:  Some of these operations are slow.
#
############################################################################
#
#  Requires:  Version 9 graphics
#
############################################################################
#
#  Links:  numbers, wattrib, wopen
#
############################################################################

package ipl.xformimg

import
   graphics(Pixels, Window)

procedure wreflect(win1, dir)
   local win2, width, height, px1, px2, p

   /dir := "v"				# vertical reflection is the default

   height := win1.get_height()
   width := win1.get_width()

   win2 := Window().set_size(width, height) |
      syserr("*** cannot window for reflection")

   case dir of {
      "v":  {
         px1 := win1.get_pixels()
         px2 := Pixels(width, height)
         every p := px1.gen_rgba() do
            px2.set_rgba(p.x, height - 1 - p.y, p.red, p.green, p.blue)
         px1.close()
         win2.draw_image(,, px2)
         px2.close()
         }
      "h":  {
         px1 := win1.get_pixels()
         px2 := Pixels(width, height)
         every p := px1.gen_rgba() do
            px2.set_rgba(width - 1 - p.x, p.y, p.red, p.green, p.blue)
         px1.close()
         win2.draw_image(,, px2)
         px2.close()
         }
      default:  runerr("*** invalid specification for reflect()", dir)
      }

   return win2

end

procedure wrotate(win1, dir)
   local win2, width, height, px1, px2, p

   /dir := "90"				# 90-degree rotation is the default

   height := win1.get_height()
   width := win1.get_width()


   case integer(dir) of {
      90:  {
         win2 := Window().set_size(height, width) | syserr("*** cannot open target window")
         px1 := win1.get_pixels()
         px2 := Pixels(height, width)
         every p := px1.gen_rgba() do
            px2.set_rgba(height - 1 - p.y, p.x, p.red, p.green, p.blue)
         px1.close()
         win2.draw_image(,, px2)
         px2.close()
         }
      -90:  {
         win2 := Window().set_size(height, width) | syserr("*** cannot open target window")
         px1 := win1.get_pixels()
         px2 := Pixels(height, width)
         every p := px1.gen_rgba() do
            px2.set_rgba(p.y, width - 1 - p.x, p.red, p.green, p.blue)
         px1.close()
         win2.draw_image(,, px2)
         px2.close()
         }
      180:  {
         win2 := Window().set_size(width, height) | syserr("*** cannot open target window")
         px1 := win1.get_pixels()
         px2 := Pixels(width, height)
         every p := px1.gen_rgba() do
            px2.set_rgba(width - 1 - p.x, height - 1 - p.y, p.red, p.green, p.blue)
         px1.close()
         win2.draw_image(,, px2)
         px2.close()
         }
      default:  runerr("*** invalid specification for rotate()", dir)
      } | runerr("*** invalid specification for rotate()", dir)

   return win2

end

procedure wcatenate(win1, win2, dir)
   local width1, width2, height1, height2, win3

   /dir := "h"			# horizontal concatenation is the default

   width1 := win1.get_width()
   width2 := win2.get_width()
   height1 := win1.get_height()
   height2 := win2.get_height()

   case dir of {
      "h":  {
         win3 := Window().set_size(width1 + width2, max(height1, height2)) |
               syserr("*** cannot open window for concatenation")
         win1.copy_to(,,,, win3)
         win2.copy_to(0, 0, width2, height2, 
                      win3, width1, 0)
         }
      "v":  {
         win3 := Window().set_size(max(width1, width2), height1 + height2) |
               syserr("*** cannot open window for concatenation")
         win1.copy_to(,,,, win3)
         win2.copy_to(0, 0, width2, height2, 
                      win3, 0, height1)
         }
      default:  runerr("*** invalid specification for catenate()", dir)
      }

   return win3

end
