import gui

class CheckBoxImageTab(TabItem)
   public 
   c,
   browse_up,               #                     
   browse_down,             #                     
   icon_up,
   icon_down,
   cp_img

   public on_cp_img()
      self.icon_down.set_image(icon_up.image_str)
   end

   public on_browse_up()
      local fd, s
      fd := FileDialog()
      fd.set_directory(\Ivib.last_icon_dir)
      fd.show_modal(self.parent_dialog)
      Ivib.last_icon_dir := fd.get_directory()
      if s := util_read_icon(self.parent_dialog, fd.get_result()) then
         self.icon_up.set_image(s)
   end

   public on_browse_down()
      local fd, s
      fd := FileDialog()
      fd.set_directory(\Ivib.last_icon_dir)
      fd.show_modal(self.parent_dialog)
      Ivib.last_icon_dir := fd.get_directory()
      if s := util_read_icon(self.parent_dialog, fd.get_result()) then
         self.icon_down.set_image(s)
   end

   public validate_input()
      if image_width(icon_up.image_str) ~= image_width(icon_down.image_str) then
         return alert_error(parent_dialog, "Image widths differ")
      if image_height(icon_up.image_str) ~= image_height(icon_down.image_str) then
         return alert_error(parent_dialog, "Image heights differ")
      return
   end

   public set_vals()
      c.set_images(icon_up.image_str, icon_down.image_str)
   end

   public maybe_standardize(in_cbg)
      if in_cbg.is_checked() then {
         if self.icon_up.image_str == Icon.BOX_UP & self.icon_down.image_str == Icon.BOX_DOWN then {
            self.icon_up.set_image(Icon.DIAMOND_UP)
            self.icon_down.set_image(Icon.DIAMOND_DOWN)
         }
      } else {
         if self.icon_up.image_str == Icon.DIAMOND_UP & self.icon_down.image_str == Icon.DIAMOND_DOWN then {
            self.icon_up.set_image(Icon.BOX_UP)
            self.icon_down.set_image(Icon.BOX_DOWN)
         }
      }

   end

   public new(c)
      local panel_1, border_1, panel_2, border_2, label_3, label_4

      cast(self,TabItem).new()

      self.c := c

      set_label("Images")
      self.set_layout(GridLayout())

      border_2 := Border()
      panel_2 := Panel()
      border_2.set_content(panel_2)
      panel_2.set_layout(GridLayout())
      if \c.img_up then {
         icon_up := Image("c:eol")
         icon_up.set_size("75", "75")
         icon_up.set_image(c.img_up)
         panel_2.add(icon_up)
      }
      label_4 := Label()
      label_4.set_label("Up")
      border_2.set_title(label_4)

      browse_up := TextButton()
      browse_up.connect(self.on_browse_up, Event.ACTION)
      browse_up.set_label("Browse...")
      panel_2.add(browse_up)
      self.add(border_2)

      cp_img := TextButton()
      cp_img.connect(self.on_cp_img, Event.ACTION)
      cp_img.set_label("Copy ->")
      self.add(cp_img)

      border_1 := Border()
      panel_1 := Panel()
      border_1.set_content(panel_1)
      panel_1.set_layout(GridLayout())
      if \c.img_down then {
         icon_down := Image("c:eol")
         icon_down.set_size("75", "75")
         icon_down.set_border(RaisedBorder())
         icon_down.set_image(c.img_down)
         panel_1.add(icon_down)
      }
      label_3 := Label()
      label_3.set_label("Down")
      border_1.set_title(label_3)
      browse_down := TextButton()
      browse_down.connect(self.on_browse_down, Event.ACTION)
      browse_down.set_label("Browse...")
      panel_1.add(browse_down)
      self.add(border_1)

      return
   end
end




