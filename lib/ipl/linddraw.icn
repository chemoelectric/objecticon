############################################################################
#
#	File:     linddraw.icn
#
#	Subject:  Procedure to draw L-System strings
#
#	Author:   Ralph E. Griswold
#
#	Date:     May 2, 2001
#
############################################################################
#
#   This file is in the public domain.
#
############################################################################
#
#  This procedure draws strings of characters produced by
#  L-systems.
#
############################################################################
#
#  Links:  lindgen, turtle, graphics
#
############################################################################

package ipl.linddraw

import
   ipl.graphics(WDelay),
   ipl.lindgen(lindgen),
   ipl.turtle(TDraw, TGoto, TLeft, TReset,
              TRestore, TRight, TSave, TSkip)

#  The drawing is based on the axiom and the rewriting rules.  The other
#  parameters are the line length, the angle delta between lines, and
#  the number of generations.  Drawing starts at x,y.

# draw L-system
procedure linddraw(		
   x, y, axiom, rewrite, length, delta, gener, delay
   )
   local c

   /x :=  250
   /y :=  250
   /length := 5
   /delta := 90

   TReset()
   TGoto(x, y)

   every c := lindgen(!axiom, rewrite, gener) do {
      WDelay(delay)
      case c of {
         "F":   TDraw(length)			# draw forward
         "f":   TSkip(length)			# skip forward
         "+":   TRight(delta)			# turn right
         "-":   TLeft(delta)			# turn left
         "[":   TSave()				# save state
         "]":   TRestore()			# restore state
         }					# ignore other characters
      }

   WFlush()

   return

end
