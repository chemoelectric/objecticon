package gui

import xdg

class TextContextMenu(Menu)
   private static init()
      try_xdg("txt.cut", "edit-cut")
      try_xdg("txt.copy", "edit-copy")
      try_xdg("txt.paste", "edit-paste")
      try_xdg("txt.undo", "edit-undo")
      try_xdg("txt.redo", "edit-redo")
      try_xdg("txt.select-all", "edit-select-all")
   end

   private static try_xdg(key, icon)
      ImageCache.copy_key(key, IconTheme.find_icon_and_cache(icon, Gui.MENU_ICON_SIZE,, &yes))
   end

   public override new(parent)
      Menu.new()
      if parent.has_region() then {
         if parent.is_editable() then
            add(TextMenuItem().
                set_label("Cut").
                set_paint_left(ImagePaint().set_cache("txt.cut")).
                set_accel("t").
                set_label_right("^X").
                connect(parent.handle_cut, Event.ACTION))
         add(TextMenuItem().
             set_label("Copy").
             set_paint_left(ImagePaint().set_cache("txt.copy")).
             set_label_right("^C").
             set_accel("c").
             connect(parent.handle_copy, Event.ACTION))
      }
      if parent.is_editable() then {
         add(TextMenuItem().
             set_label("Paste").
             set_paint_left(ImagePaint().set_cache("txt.paste")).
             set_label_right("^V").
             set_accel("p").
             connect(parent.handle_paste, Event.ACTION))
         add(MenuSeparator())
         if parent.can_undo() then
            add(TextMenuItem().
                set_label("Undo").
                set_paint_left(ImagePaint().set_cache("txt.undo")).
                set_label_right("^Z").
                set_accel("u").
                connect(parent.handle_undo, Event.ACTION))
         if parent.can_redo() then
            add(TextMenuItem().
                set_label("Redo").
                set_paint_left(ImagePaint().set_cache("txt.redo")).
                set_label_right("^Y").
                set_accel("r").
                connect(parent.handle_redo, Event.ACTION))
      }
      if parent.has_contents() then {
         add(MenuSeparator())
         add(TextMenuItem().
             set_label("Select all").
             set_paint_left(ImagePaint().set_cache("txt.select-all")).
             set_label_right("^A").
             set_accel("a").
             connect(parent.handle_select_all, Event.ACTION))
      }
      tidy()
      return
   end
end
