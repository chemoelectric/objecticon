#SRC: JCON

import io, lang, ipl.test

record message(who,gap,what)

procedure main()
   local i, x, args2

   x := 1
   x +:= |1		# tickled optimizer bug.
   write(x)

   x := table(3)
   write(x[])

   x := "o"
   write("a" & "b")
   write("c" | "d")
   write(\ "e")
   write(!"f")
   write(\&null | "g")
   write(/&null & "h")
   write("i" || "jk")
   write(23 || "skidoo")
   write(x, .x, x := "b")

   every (write|"io.write")( (1|2)("hello", "mom"), "!")
   every (write|"io.write")! [ (1|2) ! ["hello", "mom"], "!"]
   write ! message("hello")
   write ! message("hello", " ", "pop")
   every i := -4 to 4 do
      write("i=", i, ": ", i("a","b","c") | "failed")

   every write(seq() \ 3)
   every write(seq(4) \ 3)
   every write(seq(,4) \ 3)
   every write(seq(10,20) \ 3)

   pairs { 1 to 100, 11 to 99 by 11 }

   write("repl: ", repl("",5), repl("x",3), repl("foo",0), repl("xyz",4))
   write("reverse: ", reverse(""), reverse("x"), reverse("ab"), reverse(12345));
   every i := 0 to 255 do
      if (ord(char(i)) ~= i) then write("char/ord oops ", i)
   writes("char: ") 
   every writes(char((64 to 126) | 10))

   write("proc: ")
   write("   ", image(proc("args")))
   write("   ", image(Proc.get_n_arguments))
   write("  >", args(main))
   args2 := Proc.get_n_arguments
   write("  >", args2(main))
   write("   ", image(proc("args")))
   write("   ", image(Proc.get_n_arguments))

   # the following should all be harmless, at least
   every collect(&null | (0 to 3), &null | 0 | 1 | 100 | 1000)

   evaluation("1234567890", "abcdefghi")

   every write(image(nullsuspend()))

   assignments()
   transmit_var()
   rvals()

   write("done")
   exit()
   write("oops!")
   dummy()
end

# test Icon's (odd) two-pass argument evaluation process.
procedure evaluation(a,b)
     local x,y

     write("argument evaluation test")
     write(x, x:=1)
     write(x:=2, x:=3)
     write(a, a := 3)
     write(b[2], b[2] := "q")
     write(b[2:3], b[1:4] := "qwerty")
     y := [1,2,3,4]
     write(y[1], y[1] := 3)
     x := 7
     write(x[2], y[2] := 3)
     y := table(0)
     write(y[3], y[3] := 7)
     x := y
     write(x[5], y[5] := 8)
end

procedure dummy()
   image(every 1) | 2	# this triggered a problem once upon a time.
end

procedure args(x[])	# later replaced by proc("args",0)
   local s
   s := ""
   every s ||:= image(!x) do
      s ||:= " "
   return s[1:-1] | ""
end

procedure pairs(e[])
   while write(@e[1], " ", @e[2])
end

procedure nullsuspend()
   suspend
   suspend
end

procedure f(x)
   return x
end

procedure ce()
   coact() := "z"
   coact()
end

procedure assignments()
   local x, s
   # Test some assignment/swaps that can fail
   &subject := "the quick brown fox"
   try{ "&pos := 100", &pos := 100 }
   try{ "&pos := funcs...", &pos := f(100) | f(1) | f(2) }
   x := 50
   try{ "&pos :=: x(50)", &pos :=: x }
   x := 5
   try{ "&pos :=: x(5)", &pos :=: x }

   # Test some same-substring swaps
   s := "amphibious"
   try{ "s[1] :=: s[-1]", s[1] :=: s[-1] }
   write("\ts=", image(s))
   s := "amphibious"
   try{ "s[1:8] :=: s[-8:0]", s[1:8] :=: s[-8:0] }
   write("\ts=", image(s))

   # Same, but list element var
   s := ["amphibious"]
   try{ "s[1,1] :=: s[1,-1]", s[1,1] :=: s[1,-1] }
   write("\ts[1]=", image(s[1]))
   s := ["amphibious"]
   try{ "s[1][1:8] :=: s[1][-8:0]", s[1][1:8] :=: s[1][-8:0] }
   write("\ts[1]=", image(s[1]))

   # Same, but table element var
   s := table(,1,"amphibious")
   try{ "s[1,1] :=: s[1,-1]", s[1,1] :=: s[1,-1] }
   write("\ts[1]=", image(s[1]))
   s := table(,1,"amphibious")
   try{ "s[1][1:8] :=: s[1][-8:0]", s[1][1:8] :=: s[1][-8:0] }
   write("\ts[1]=", image(s[1]))
end

procedure transmit_var()
   static g
   local e
   # Test transmission of variables to a coexpression.
   g := "amphibious"
   e := create ce()
   @e
   (g[3])@e
   write("g=", image(g))
end

procedure f2(x, y)
   return x||y
end

# Test of correct production of variables -vs- values
procedure rvals()
   local x, s

   x := 1
   s := (x + (x := 2))
   write("Simple: ", s)

   x := [1]
   s := (x[1] + (x[1] := 2))
   write("List element: ", s)

   x := table(1)
   x[1] := 1
   s := (x[1] + (x[1] := 2))
   write("Table subscript: ", s)

   s := (x[2] + (x[2] := 2))
   write("Table default: ", s)

   x := "quick brown fox"
   s := x[1:5] || (x := "lazy dog")
   write("Substring: ", s)

   x := "quick brown fox"
   s := "||"(x[1:5], (x := "lazy dog"))
   write("Invoke ||: ", s)

   x := "quick brown fox"
   s := f2(x[1:5], (x := "lazy dog"))
   write("Invoke proc: ", s)

   x := table(1)
   every write("To: ", x[2] to (x[2] := 5))

   x := "quick brown fox"
   s := x[1:5][(x := "lazy dog", 1)]
   write("Subscript: ", s)

   x := "quick brown fox"
   s := [ x[1:5], x := "lazy dog" ]
   write("List: ", s[1])
end
