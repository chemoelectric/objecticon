package exception

global throw_handler, thrown, thrower

procedure throw(x)
   \throw_handler | runerr("throw(): no handler set")
   thrown := x
   thrower := &current
   coact(, throw_handler,, 1)
   runerr("throw(): code co-expression reactivated unexpectedly")
end

procedure rethrow()
   \throw_handler | runerr("rethrow(): no handler set")
   coact(, throw_handler,, 1)
   runerr("rethrow(): code co-expression reactivated unexpectedly")
end

procedure clear()
   thrower := thrown := &null
end

procedure try(code)
   local v, t
   t := throw_handler
   throw_handler := &current
   clear()
   v := @code
   throw_handler := t
   if \thrower then
      &why := string(thrown) | ("Exception thrown:" || image(thrown))
   else
      return v
end

# Invoke co-expression e, saving the value of {throw_handler} before,
# and restoring it afterwards.  This is potentially useful when using
# co-operating scheduled tasks (see {io.Task}), which switch from
# within a try block.
#
procedure save_throw_handler(e)
   local sh, v
   sh := throw_handler
   v := @e
   throw_handler := sh
   return v
end

# Like {save_throw_handler}, but saves the value of {&handler}
# instead.
#
procedure save_handler(e)
   local sh, v
   sh := &handler
   v := @e
   &handler := sh
   return v
end
