import gui, graphics, io

class ImageViewTest(Dialog)
   public iv, img
   public override component_setup()
      self.setup()
      iv.set_image(img)
   end

   public override end_dialog()
      img.close()
   end

   public set_file(f)
      self.img := Window().set_image(f) | stop("Couldn't load image:", &why)
      (\iv).set_image(img)
   end

   public override init_dialog()
   end

   public on_quit(ev)
      dispose()
   end

   public override new()
      Dialog.new()
      return
   end

   private setup()
      local layout_1, quit, paint
      self.set_resize(&yes)
      iv := ImageView().
         set_size(scale(342), scale(284)).
         set_align(Align.C, Align.T).
         clear_constraints().
         set_constraint("eol", &yes).
         set_constraint("x_fill", &yes).
         set_constraint("x_weight", 1.0).
         set_constraint("y_fill", &yes).
         set_constraint("y_weight", 1.0)
      self.add(iv)
      paint := TextPaint("Quit")
      quit := TextButton().
         set_align(Align.C, Align.C).
         clear_constraints().
         set_accel("q").
         connect(self.on_quit, Event.ACTION).
         set_toggles(&no).
         set_paint(paint)
      self.add(quit)
      self.connect(self.dispose, Event.WINDOW_CLOSE_BUTTON)
      layout_1 := GridLayout().
         set_extra("cells")
      self.set_layout(layout_1)
   end
end


#
# An example of a custom scroll area which displays an image
# with scrollbars.
#

class ImageView(DrawScrollArea)
   public img

   public override draw(r)
      img.copy_to(get_area_x(), get_area_y(), view.w, view.h, 
                  view.cbwin, view.x, view.y)
   end

   public override calculate_subject_width()
      return img.get_width()
   end

   public override calculate_subject_height()
      return img.get_height()
   end

   public override get_default_vertical_increment()
      return 10
   end

   public override get_default_horizontal_increment()
      return 10
   end

   public set_image(img)
      self.img := img
      compute_and_invalidate()
   end

   public override new()
      DrawScrollArea.new()
      set_use_default_keys(&yes)
      return
   end
end



procedure main(a)
   local d, f
   f := a[1] | "babbage.gif"
   d := ImageViewTest()
   d.set_file(f)
   d.show_modal()
end

### Ivib-v2 layout: layouts/$.layout
