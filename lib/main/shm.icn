#
# $Id$
#
# This file is in the public domain.
#
# Author: Robert Parlett (parlett@dial.pipex.com)
#

$define LIB "objecticonipclib.so"

package ipc

import lang, io, util

#
# This class provides an inter process shared variable facility.  The implementation requires
# the accompanying C library objecticonipclib.so to be on the library path.
#
# Instances of this class should not be created directly, but rather using the static
# methods {open_public}, {create_public} and {create_private}
#
final class Shm(NoCopy)
   private 
      id

   public static const
      SUPPORTED

   #
   # Set the value to the given object.  The object may be an arbitrary
   # Icon structure, and will be encoded into a string by the {encode()}
   # procedure.  This method will fail if the given object is not
   # encodable, and set {&why} appropriately.
   #
   public set_value(o)
      return set_value_impl(encode(o, 1))
   end

   private native set_value_impl(s)

   #
   # Get the value of the object.
   #
   public get_value()
      return decode(get_value_impl())
   end

   private native get_value_impl()

   #
   # Clean up the resources used by the variable.  This should be called by
   # the parent process after the shared variable is no longer needed.
   #
   public native close()

   #
   # Return the underlying id of the shared variable.
   #
   public get_id()
      return id
   end

   private static native open_public_impl()
   private static native create_public_impl()
   private static native create_private_impl()

   private static init()
      if Class.load_library(Files.find_native_lib(LIB)) then
         SUPPORTED := &yes
   end

   private new(id)
      \SUPPORTED | return error("Shm ipc not supported")
      self.id := id
      return
   end

   #
   # Get an existing public shared variable with the given key, or fail
   # if no such shared variable exists.
   #
   public static open_public(key)
      return Shm(open_public_impl(key))
   end

   #
   # Create a new public shared variable with the given key and initial value
   #
   public static create_public(key, o)
      return Shm(create_public_impl(key, encode(o, 1)))
   end

   #
   # Create a new private shared variable with the given initial value
   #
   public static create_private(o)
      return Shm(create_private_impl(encode(o, 1)))
   end
end
