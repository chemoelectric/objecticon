import gui

class CodeTab(TabItem)
   public 
      c,
      name,                    #            
      class_name,              # Class name
      import_name,             # TextField - import name
      var_category0,           # Checkboxes for variable category
      var_category1,           #  
      var_category_cbg

   public set_vals()
      c.set_name(name.get_contents())
      c.set_class_name(self.class_name.get_contents())
      c.set_import_name(self.import_name.get_contents())
      c.class_variable := case var_category_cbg.get_which_one() of {
         var_category0 : 1
         var_category1 : &null
      }
   end

   #
   # Validate data
   #
   public validate_input()
      local s
      s := name.get_contents()
      valid_id(s) | return alert_error(parent_dialog, "Invalid identifier:" || s)

      if c.parent_Canvas.find_name(s) ~=== c then
         return alert_error(parent_dialog, "Identifier not unique:" || s)

      s := class_name.get_contents()
      if *s > 0 then
         valid_id(s) | return alert_error(parent_dialog, "Invalid identifier:" || s)

      s := import_name.get_contents()
      if *s > 0 then
         valid_dotted_id(s) | return alert_error(parent_dialog, "Invalid identifier:" || s)

      return
   end

   public override new(c)
      local l, p
      TabItem.new()
      set_label("Code")
      self.c := c

      set_layout(GridLayout().set_extra("borders"))

      p := Panel().set_constraint("eol", &yes).set_layout(GridLayout().set_doi(0))
      self.add(p)

      var_category_cbg := CheckBoxGroup()

      var_category0 := CheckBox()
      var_category0.set_label("Class variable")
      var_category_cbg.add(var_category0)
      p.add(var_category0)

      var_category1 := CheckBox().set_constraint("eol", &yes)
      var_category1.set_label("Local setup variable")
      var_category_cbg.add(var_category1)
      p.add(var_category1)

      if \c.class_variable then
         var_category_cbg.set_which_one(var_category0)
      else
         var_category_cbg.set_which_one(var_category1)

      p := Panel().set_layout(GridLayout().set_doi(0)).set_constraint("eol", &yes)
      self.add(p)
      l := Label()
      l.set_label("Import")
      p.add(l)

      self.import_name := TextField().set_constraint("eol", &yes)
      self.import_name.set_size(150)
      p.add(self.import_name)
      self.import_name.set_contents(c.import_name)

      l := Label()
      l.set_label("Object name")
      p.add(l)

      name := TextField().set_constraint("eol", &yes)
      name.set_size(150)
      p.add(name)

      name.set_contents(c.name)

      l := Label()
      l.set_label("Class name")
      p.add(l)

      self.class_name := TextField().set_constraint("eol", &yes)
      self.class_name.set_size(150)
      p.add(self.class_name)

      self.class_name.set_contents(c.class_name)
      return
   end
end

