#
# $Id$
#
# This file is in the public domain.
#
# Author: Robert Parlett (parlett@dial.pipex.com)
#


import gui

############################################################################
#
#
#
class CanvasCheckBoxMenuEdit(MenuComponentEdit)
   public 
      tab_basic,
      paint_field,
      paint_field_r,
      browse_custom,           #                     
      group_list,              #                  
      in_checkbox_group,       #                          
      initially_checked

   public on_in_checkbox_group()
      group_list.toggle_is_shaded()
   end
   
   public validate_input()
      \self.paint_field.get_contents() | return alert_error(self, "Label cannot be null")
      self.paint_field.validate_input() | fail
      self.paint_field_r.validate_input() | fail
      return MenuComponentEdit.validate_input()
   end

   public set_vals()
      MenuComponentEdit.set_vals()

      #
      # If in cbg and on, turn off, since may go out of cbg or into
      # a different one.
      #
      if (\c.parent_check_box_group).which_one === c then
         c.parent_check_box_group.set_which_one()

      if in_checkbox_group.is_checked() then
         #
         # Put in cbg.
         #
         c.set_parent_check_box_group(c.parent_component.parent_Canvas.checkbox_groups[group_list.get_selection()])
      else
         c.set_parent_check_box_group()

      #
      # Set checked flag
      #
      c.set_is_checked(initially_checked.get_status())
      c.set_paint_mid(paint_field.get_contents())
      c.set_paint_right(paint_field_r.get_contents())
   end

   public setup()
      local main_panel

      MenuComponentEdit.setup()

      main_panel := TabItem()
      main_panel.set_layout(GridLayout().set_extra("borders"))
      add_tab_at_front(main_panel)
      main_panel.set_label("General")

      paint_field := PaintField().set_title(Label().set_label("Label")).set_constraint("eol", &yes).set_constraint("w", 2)
      paint_field.set_contents(c.paint_mid)
      main_panel.add(paint_field)

      paint_field_r := PaintField().set_title(Label().set_label("Label(R)")).set_constraint("eol", &yes).set_constraint("w", 2)
      paint_field_r.set_contents(c.paint_right)
      main_panel.add(paint_field_r)

      in_checkbox_group := CheckBox()
      in_checkbox_group.connect(self.on_in_checkbox_group, Event.ACTION)
      in_checkbox_group.set_label("CheckBox Group")
      main_panel.add(in_checkbox_group)

      group_list := List().set_constraint("eol", &yes)
      group_list.set_size(scale(200))
      group_list.set_selection_list(cos_names(c.parent_component.parent_Canvas.checkbox_groups))
      main_panel.add(group_list)

      initially_checked := CheckBox().set_constraint("eol", &yes)
      initially_checked.set_label("Initially checked")
      main_panel.add(initially_checked)

      if *c.parent_component.parent_Canvas.checkbox_groups = 0 then {
         in_checkbox_group.set_is_shaded(&yes)
         group_list.set_is_shaded(&yes)
         if c.is_checked() then
            initially_checked.set_is_checked(&yes)
      } else {
         if \c.parent_check_box_group then {
            in_checkbox_group.set_is_checked(&yes)
            group_list.set_selection(keyof(c.parent_component.parent_Canvas.checkbox_groups,
                                           c.parent_check_box_group))
            if c === c.parent_check_box_group.which_one then
               initially_checked.set_is_checked(&yes)
         } else  {
            group_list.set_is_shaded(&yes)
            if c.is_checked() then
               initially_checked.set_is_checked(&yes)
         }
      }
   end
end


