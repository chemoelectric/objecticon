#
# $Id$
#
# This file is in the public domain.
#
# Author: Robert Parlett (parlett@dial.pipex.com)
#

package gui

import util

#
# This is a class for representing objects which can have an
# on-off state, in particular checkboxes, toggle buttons,
# and menu checkboxes.
#
abstract class Toggle()
   public 
      parent_check_box_group,
      is_checked_flag

   #
   # Set the parent `CheckBoxGroup`.
   # :Parameters :
   # :  `x` - The parent `CheckBoxGroup`.
   #
   public set_parent_check_box_group(x)
      self.parent_check_box_group := x
      synch_cbg()
      return self
   end

   #
   # Succeed if the object is checked.
   #
   public is_checked()
      succeed \self.is_checked_flag
   end

   #
   # Succeed if the object is not checked.
   #
   public is_unchecked()
      succeed /self.is_checked_flag
   end

   #
   # Return the status of the object; `&yes` if the object is checked,
   # `&null` otherwise.
   #
   public get_status()
      return self.is_checked_flag
   end

   #
   # Toggle the status of the object.
   #
   public toggle_is_checked()
      return set_is_checked(toggle_flag(self.is_checked_flag))
   end

   #
   # Set the checked status according to the given flag.
   #
   public set_is_checked(s)
      self.is_checked_flag := need_flag(s)
      synch_cbg()
      return self
   end

   public toggle_action(e)
      local t
      if \parent_check_box_group then {
         t := parent_check_box_group.which_one
         if \parent_check_box_group.allow_none_checked_flag then
            toggle_is_checked()
         else
            set_is_checked(&yes)
         if t ~=== parent_check_box_group.which_one then
            parent_check_box_group.fire(Event.SELECTION_CHANGED, e)
      } else
         toggle_is_checked()
   end

   #
   # Ensure any parent CBG is consistent with our checked status.
   #
   private synch_cbg()
      if \self.parent_check_box_group then { 
         if \self.is_checked_flag & self.parent_check_box_group.which_one ~=== self then {
            (\self.parent_check_box_group.which_one).set_is_checked(&no)
            self.parent_check_box_group.which_one := self
         } else if /self.is_checked_flag & self.parent_check_box_group.which_one === self then {
            self.parent_check_box_group.which_one := &null
         }
      }
   end
end

