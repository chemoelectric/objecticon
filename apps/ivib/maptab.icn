#
# $Id$
#

import gui, util

class MapTab(TabItem)
   public 
      tbl,
      add_button,          # Buttons              
      delete,                                
      apply,                                
      contents,            # Table contents
      attrib_tf,
      value

   public update_grey()
      local f
      f := Flag{ *tbl.get_selections() = 0}
      every (attrib_tf | value | delete | apply).set_is_shaded(f)
   end

   public on_add()
      #
      # Add a new value to the attributes table.
      #
      put(contents, ["Edit me", u""])
      tbl.contents_changed()
      tbl.table_content.goto_first(*contents)
      tbl.set_selections([*contents])
      tbl.set_cursor(*contents)
      attrib_tf.set_contents(contents[-1][1])
      value.set_contents(ucs(contents[-1][2]))
      update_grey()
      attrib_tf.grab_focus()
      attrib_tf.handle_select_all()
   end

   public on_tbl()
      local i
      #
      # Line in the table clicked - copy the line to the edit area.
      #
      if i := tbl.get_selections()[1] then {
         attrib_tf.set_contents(contents[i][1])
         value.set_contents(ucs(contents[i][2]))
      }
      update_grey()
   end

   public on_delete()
      local i
      #
      # Delete a line from the table.
      #
      i := tbl.get_selections()[1]
      contents := contents[1 : i] ||| contents[i + 1 : 0]
      tbl.set_contents(contents)
      tbl.clear_selections()
      update_grey()
   end

   public on_apply()
      local i
      #
      # Copy the edited item back to the table. 
      #
      i := tbl.get_selections()[1]
      contents[i] := [attrib_tf.get_contents(), value.get_contents()]
      tbl.contents_changed()
      tbl.clear_selections()
      update_grey()
   end

   public set_table(t)
      self.contents := sort(t)
      tbl.set_contents(contents)
      return self
   end

   public get_table()
      local t, e
      t := table()
      every e := !self.contents do
         every insert(t, e[1], e[2])
      return t
   end

   public set_one(f)
      case f.attr of {
         "table": set_table(f.table_val())
         default: return TabItem.set_one(f)
      }
      return
   end

   public new()
      local c1, c2, p
      TabItem.new()

      set_layout(GridLayout())

      tbl := Table().set_constraint("eol", &yes)
      tbl.connect(self.on_tbl, Event.SELECTION_CHANGED)
      tbl.set_select_mode(Select.ONE)
      tbl.set_size(scale(200), scale(160))
      add(tbl)

      c1 := TableColumn()
      c1.set_column_width(scale(100))
      c1.set_label("Key")
      tbl.add_column(c1)

      c2 := TableColumn()
      c2.set_label("Value")
      tbl.add_column(c2)

      p := Panel().set_constraint("eol", &yes).set_constraint("y_weight", 0)
      p.set_layout(GridLayout().set_doi(0))
      attrib_tf := TextField().set_constraint("x_weight", 0)
      attrib_tf.set_size(scale(95))
      attrib_tf.connect(self.on_apply, Event.ACTION)
      p.add(attrib_tf)
      value := TextField().set_contents(u"")
      value.set_size(scale(175))
      value.connect(self.on_apply, Event.ACTION)
      p.add(value)
      add(p)

      p := Panel().set_constraint("eol", &yes).set_constraint("y_weight", 0)
      p.set_layout(GridLayout().set_doi(0))
      add_button := TextButton()
      add_button.connect(self.on_add, Event.ACTION)
      add_button.set_label("Add")
      p.add(add_button)

      delete := TextButton()
      delete.connect(self.on_delete, Event.ACTION)
      delete.set_label("Delete")
      p.add(delete)

      apply := TextButton()
      apply.connect(self.on_apply, Event.ACTION)
      apply.set_label("Apply")
      p.add(apply)
      add(p)

      update_grey()

      return
   end
end




