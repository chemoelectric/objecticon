############################################################################
#
#	File:     intpdco.icn
#
#	Subject:  Procedures for programmer-defined control operations
#
#	Authors:  Ralph E. Griswold and Robert J. Alexander
#
#	Date:     August 14, 1996
#
############################################################################
#
#   This file is in the public domain.
#
############################################################################
#  
#  These procedures use co-expressions to perform operations on
#  integer sequences.
#
#	Compl{e}	   produces the integers not in e
#
#	Delta{e, i}	   produces the difference of values in e by
#			   i places	  
#
#	Selfrepl{e, j}	   produces i * j copies of i, where i is from
#			   e1
#
#	Sumlimit{e, i, j}  produces values of e until their sum exceeds
#			   i.  Values less than j are discarded.
#  
############################################################################
#
#  Requires:  co-expressions
#
############################################################################

package ipl.intpdco

# PDCO to generate integers not in sequence
procedure Compl(e)	
   local i, j

   j := 1

   while i := @e do {
      i := integer(i) | runerr("*** invalid value in sequence to Compl{}", i)
      suspend j to i - 1
      j := i + 1
      }

   suspend seq(j)

end

# PDCO to generate differences in sequences 
procedure Delta(e1, e2)	
   local C, i

   C := ^e1
   every 1 to @e2 do @C

   while i := @C - @e1 do
     suspend i

end

# PDCO to produce multiple of values in sequence
procedure Selfrepl(e1, e2)	
   local i, j

   j := @\e2 | 1
   j := integer(j) | runerr("*** invalid second argument to Selfrepl{}", j)

   while i := @e1 do {
      i := integer(i) | runerr("*** invalid value in Selfrepl{}", i)
      suspend (1 to i * j) & i
      }

end

# PDCO to sum sequence to a limit
procedure Sumlimit(e1, e2, e3) 	
   local sum, min, limit, i

   limit := integer(@\e2) | 2 ^ 31
   min := integer(@\e3) | 0
   sum := 0

   while i := @e1 do {
      if i < min then next
      if (sum + i) > limit then fail
      sum +:= i
      suspend i
      }

end
