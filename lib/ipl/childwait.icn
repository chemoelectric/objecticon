package ipl.childwait

import gui, posix

record Waiting(pid, on_exit)

# This class is used to cleanup child processes when they exit, using
# the System.wait call.
#
final abstract class ChildWait()
   private static
      waiting,
      task

   private static init()
      waiting := set()
      task := Dispatcher.new_task{child_cleanup()}
      task.start()
   end

   # Add a pid to wait for.  The optional `on_exit` function is called
   # after the given pid exits.
   #
   public static add_pid(pid, on_exit)
      insert(waiting, Waiting(pid, on_exit))
      link
   end

   private static child_cleanup()
      local r, w
      repeat {
         task.sleep(1000)
         every w := !waiting do {
            if r := System.wait(w.pid, WaitOpt.WNOHANG) & r.pid > 0 then {
               (\w.on_exit)(r)
               delete(waiting, w)
            }
         }
      }
   end
end
