#
# $Id$
#
# This file is in the public domain.
#
# Author: Robert Parlett (parlett@dial.pipex.com)
#

import gui


############################################################################
#
# Class for representing a List on the canvas.
#
class CanvasList(CanvasComponent,List)
   #
   # Generate code into Code object c.
   #
   public override gen_code(c)
      CanvasComponent.gen_code(c)
      c.chain("set_constant_label(" || image(\self.constant_label) || ")")
      c.chain("set_selection_list(" || contents_string(self.selection_list) || ")")
      if \self.selection ~= 1 then
         c.chain("set_selection(" || self.selection || ")")
   end

   #
   # Duplicate object.
   #
   public override dup()
      local c
      c := CanvasList()
      CanvasComponent.dup_fields(c)
      c.constant_label := self.constant_label
      c.set_selection_list(copy(self.selection_list))
      c.set_selection(self.selection)
      return c
   end

   #
   # Return the I/O template.
   #
   public override get_template()
      suspend CanvasComponent.get_template() |
         ["Constant Label", "constant_label"] |
         ["Selection", "selection"] |
         ["Selection List", "selection_list"]
   end

   private override post_decode()
      CanvasComponent.post_decode()
      # Little trick to initialize the selection index nicely
      set_selection_list(self.selection_list)
   end

   #
   # Open the configuration dialog box.
   #
   public override dialog()
      return CanvasListDialog(self)
   end

   public override allows_null_height()
      return 1
   end

   public override allows_null_width()
      return 1
   end

   public override new()
      List.new()
      CanvasComponent.new()
      self.class_name := "List"
      self.import_name := "gui"
      self.name := "list"
      return
   end
end

