#
# $Id$
#
# This file is in the public domain.
#
# Author: Robert Parlett (parlett@dial.pipex.com)
#

package gui

import graphics

class SpinTextField(TextField)
   public keeps(e)
      return member(Key.CURSOR, e.code)
   end

   public on_key(e)
      case e.code of {
         Key.UP : parent.go_up(e)
         Key.DOWN : parent.go_down(e)
      }
   end

   public new(a[])
      cast(self,TextField).new()
      self.connect(self.on_key, Event.KEY_PRESS)
      self.set_border(EmptyBorder("l_inset=",Gui.TEXT_INSET,
                                  "r_inset=",Gui.TEXT_INSET))
      set_fields(a)
      return
   end
end


#
# This class is the base class for editable spin components which use a
# TextField to display/edit the current value.
#
# @field filter=cset : see {set_filter}
# @field see also {Component}
#
class EditSpin(Spin)
   public 
      old_contents

   public on_textfield(ev, src, type)
      # Fire the event with self as source.
      fire(type, ev)
   end

   #
   # Set the text field's filter (see {TextField} for details).
   # @param c  The filter
   #
   public set_filter(c)
      return self.view.set_filter(c)
   end

   #
   # Return the contents of the text field (which may have
   # been edited).
   #
   public get_contents()
      return view.get_contents()
   end

   public start_change(e)
      self.old_contents := get_contents()
   end

   public end_change(e)
      if self.old_contents ~=== get_contents() then
         fire(Event.CONTENT_CHANGED, e)
   end

   public create_view()
      local tf
      tf := SpinTextField()
      every tf.connect(self.on_textfield, Event.CONTENT_CHANGED|Event.ACTION)
      return tf
   end

   public set_one(f)
      case f.attr of {
         "filter" : set_filter(f.cset_val())
         default: return cast(self,Spin).set_one(f)
      }
      return
   end
end



