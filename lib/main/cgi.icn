package cgi

import io, net, mail, util(error), posix(System), ipl.tables(inserts)

class CgiParams()
   private readable
      stdin,
      stdin_limit,
      params,
      query_params

   private static env(k)
      return System.getenv(k) | error("Missing environment variable " || k)
   end

   public process()
      local rm, ct, msg, p, cd, key, multi, s

      stdin := FileStream.stdin.read_all() | fail
      if *stdin > \stdin_limit then
         return error("Too much data read from stdin")

      # Get the URL query parameters
      s := env("QUERY_STRING") | fail
      query_params := URL.make_cgi_table(s)

      rm := env("REQUEST_METHOD") | fail
      case rm of {
         "GET": {
            params := query_params
         }
         "POST": {
            p := RFC822Parser()
            s := env("CONTENT_TYPE") | fail
            ct := p.parse_content_type(s) | return error("Couldn't parse CONTENT_TYPE param: " || &why)
            if ct.type == "application" & ct.subtype == "x-www-form-urlencoded" then {
               params := URL.make_cgi_table(stdin)
            } else if ct.type == "multipart" & ct.subtype == "form-data" then {
               msg := Message().
                  set_header("Content-Type", s).
                  set_content(stdin)
               multi := msg.get_content_object() | return error("Couldn't decode multipart: " || &why)
               params := table()
               every p := !multi.parts do {
                  cd := p.get_content_disposition() | return error("No Content-Disposition")
                  key := cd.get_parameter("name")
                  inserts(params, key, p)
               }
            } else 
               params := table()
         }
         default:
            return error("Unknown request method: " || image(rm))
      }

      return
   end

   public get_params(key)
      return member(params, key)
   end

   public get_first_param(key)
      return get_params(key)[1]
   end

   public get_query_params(key)
      return member(query_params, key)
   end

   public get_first_query_param(key)
      return get_query_params(key)[1]
   end

   public set_stdin_limit(n)
      self.stdin_limit := n
      link
   end
end
