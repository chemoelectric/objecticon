#
# $Id$
#
# This file is in the public domain.
#
# Author: Robert Parlett (parlett@dial.pipex.com)
#

import gui

#############################################################################
#
# Dialog asking whether or not to save a file.  The actual saving is left up
# to the creator of the dialog.
#     
class SaveChangesDialog(Dialog)
   public res

   public result()
      return self.res
   end

   public on_yes()
      res := "yes"
      self.dispose()
   end

   public on_no()
      res := "no"
      self.dispose()
   end

   public on_cancel()
      res := "cancel"
      self.dispose()
   end

   public new()
      local l, p, yes, no, cancel
      Dialog.new()
      set_label("Save changes?")

      set_layout(GridLayout())

      l := Label().set_constraint("eol", &yes).set_constraint("y_weight", 1.0).set_constraint("x_align", "c")
      l.set_label(".cell\nThe canvas has been modified.\n.br\nDo you wish to save these changes?\n.br")
      add(l)
      add(Line().set_constraint("eol", &yes).set_size(scale(100)).set_constraint("x_fill", &yes))

      p := Panel().set_constraint("y_weight", 0.0)
      p.set_layout(GridLayout().set_doi(0))
      yes := TextButton().set_label("Yes").set_accel("y")
      yes.connect(self.on_yes, Event.ACTION)
      p.add(yes)

      no := TextButton().set_label("No").set_accel("n")
      no.connect(self.on_no, Event.ACTION)
      p.add(no)

      cancel := TextButton().set_label("Cancel").set_accel("c")
      cancel.connect(self.on_cancel, Event.ACTION)
      p.add(cancel)

      self.add(p)

      self.set_focus(yes)
      return
   end
end


