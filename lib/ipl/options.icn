############################################################################
#
#	File:     options.icn
#
#	Subject:  Procedure to get command-line options
#
#	Authors:  Robert J. Alexander and Gregg M. Townsend
#
#	Date:     May 5, 2000
#
############################################################################
#
#   This file is in the public domain.
#
############################################################################
#
#	options(arg, optstring,usage,errproc) removes command options
#	from the argument list of an Icon main procedure, returning a
#	table of option values.
#
############################################################################

package ipl.options

import
   io, lang, util, ipl.scan, ipl.lists, ipl.pdco, ipl.tables(inserts),
   ipl.functional

# The result of the `Opt.format` method.
package record OptFormat(names, help)

# This class encapsulates a single option.
class Opt()
   public const
      # A list of the names of this option.
      names,
      # The conversion procedure; this should be one of the built-in
      # conversion functions (`integer`, `string`, etc), `util.flag`,
      # or another procedure which converts the string parameter, and
      # sets `&why` on failure.  If this field is null, then the
      # option takes no value.
      convert,
      # The descriptive help string produced when "-?" is used.  This
      # may be split into two parts, separated by a `#` character; the
      # part to the left is the string used to describe the option
      # parameter in the help output.  For example :-
      # ~
      #    Opt("f", string, "FILE#Load values from FILE")
      # ~
      # Would produce a help string :-
      # ~
      #    -f FILE             Load values from FILE
      # ~
      help,
      # A flag value; if set then multiple uses of the option are
      # allowed.  The resulting entry in the options table will then
      # be a list.
      multiple_flag,
      # An optional error string produced on an invalid flag value.
      # If set, it overrides any value of `&why` set by the convert
      # procedure.
      param_err

   # A utility function to help format an option string.
   public format()
      local ns, h, ph
      ns := String{ "-" || !names, ", " }
      h := \help | ""
      if \convert then {
         h ? {
            if ph := tab(upto('#')) then
               h := (move(1), tab(0))
            else
               ph := case convert of {
                  real|integer|numeric|ucs|text|string|cset :
                     map(Proc.get_name(convert), &lcase, &ucase)
                  flag: "FLAG"
                  default: "VALUE"
               }
         }
         ns ||:= " " || ph
      }
      return OptFormat(ns, h)
   end

   package build_param_err()
      local s
      if s := \param_err then
         return s
      return case convert of {
         real|integer|numeric|ucs|text|string|cset :
            capitalize_cnv(convert) || " expected"
         flag:
            "Flag expected"
         default:
            &why
      }
   end

   # Create a new Opt instance.
   # :Parameters :
   # :  `s` - the name of the option; may be a list of several synonymous names 
   # :  `convert` - the conversion function to apply; see `Opt.convert`
   # :  `help` - a descriptive help string; see `Opt.help`
   # :  `multiple` - a flag value, set if multiple uses of the option are 
   #          allowed
   # :  `param_err` - a string describing the type of parameter (for error output)
   #
   public new(s, convert, help, multiple, param_err)
      names := if type(s) == "list" then
         s
      else
         [s]
      self.convert := convert
      self.help := help
      self.multiple_flag := need_flag(multiple)
      self.param_err := param_err
      return
   end
end

# Output help for a list of `Opt`s.  This procedure may be useful if a
# custom "usage" procedure is provided to `options`.
#
# :Parameters :
# :  `opts` - a list of `Opt`s
procedure options_help(opts)
   local o, t
   if *opts > 0 then {
      write("Options :-")
      every o := !opts do {
         if is(o, Opt) then {
            t := o.format()
            help_line(t.names, t.help)
         } else
            help_line(o)
      }
   }
end

# Output a formatted two column help line; this is also useful if a
# custom "usage" procedure is provided.
#
# :Parameters :
# :  `ns` - the left hand (option) column
# :  `h` - the descriptive help string.
procedure help_line(ns, h)
   static cols
   local i
   initial
      cols := LineEditorStream.get_console_width() | 80
   if /h then {
      repeat {
         if i := backto(' ', ns, 1, cols - 5) then {
            write(ns[1:i - 1])
            ns := ns[i:0]
         } else {
            write(ns)
            break
         }
      }
   } else {
      if *ns > 18 then {
         write(ns)
         writes(repl(" ", 20))
      } else
         writes(left(ns, 20))
      repeat {
         if i := backto(' ', h, 1, cols - 25) then {
            write(h[1:i - 1])
            writes(repl(" ", 20))
            h := h[i:0]
         } else {
            write(h)
            break
         }
      }
   }
end

#
# This is just a wrapper around stop(), which prints out the given
# message and a helpful line about the "-?" option.
#
procedure help_stop(a[])
   push(a, Files.get_program_name() || ": ")
   put(a, Files.EOL || "Use the -? option for more information")
   stop!a
end

#
#     This procedure separates and interprets command options included in 
#  the main program argument list.  Option names and values are removed
#  from the argument list and returned in a table.
#
#     On the command line, options are introduced by a `-` character.
#  An option name is either a single printable character, as in `-n`
#  or `-?`, or a string of letters, numbers, and underscores, as in
#  `-geometry`.  Single-character options which don't take a parameter
#  may appear in combination, for example as `-qtv`.  If an option
#  string is ambiguous, in that it may either be interpreted as a
#  single multi-character option, or as several single-character
#  options, then the former always has precedence.
#
#     Some options require values.  Generally, the option name is one
#  argument and the value appears as the next argument, for example
#  `-F file.txt`.  However, with a single-character argument name (as
#  in that example), the value may be concatenated: `-Ffile.txt` is
#  accepted as equivalent.
#
#     Options may be specified using an alternative format, starting
#  with `--`.  In this case, no combination of single character
#  options is recognised, and a parameter may follow an `=` sign, eg
#  `--width=100`.
#  
#     Options may be freely interspersed with non-option arguments.
#  An argument of `-` is treated as a non-option.  The special argument
#  `--` terminates option processing.  Non-option arguments are returned
#  in the original argument list for interpretation by the caller.
#
#     An argument of the form `@filename` (a `@` immediately followed
#  by a file name) causes options() to replace that argument with
#  arguments retrieved from the file `filename`.  Each line of the file
#  is taken as a separate argument, exactly as it appears in the file.
#  Arguments beginning with - are processed as options, and those
#  starting with `@` are processed as nested argument files.  An argument
#  of `--` causes all remaining arguments IN THAT FILE ONLY to be
#  treated as non-options (including `@filename` arguments).
#
#   :The parameters of `options` are:
#  
#   :   `arg` -     the argument list as passed to the main procedure.
#
#   :   `optstring` - a string specifying the allowable options.  This is
#                   a concatenation, with optional spaces between, of
#                   one or more option specs of the form `-name%`, where
#                    *   `-`     introduces the option
#                    *   `name`  is either a string of alphanumerics
#                               (any of `a-z`, `A-Z`, `0-9`, and `_`)
#                               or any single printable character
#                    *   `%`     is one of the following flag characters:  
#                               `!`       No value is required or allowed  
#                               `:`       A string value is required  
#                               `+`       An integer value is required  
#                               `.`       A real value is required
#
#                   If the string is followed by a `*` character, then
#                   the option may be specified multiple times.
#
#                   The leading `-` may be omitted for a single-character
#                   option.  The `!` flag may be omitted except when
#                   needed to terminate a multi-character name.
#                   Thus, the following optstrings are equivalent:
#                   ~~~
#                       -n+ -t -v -q -F: -geometry: -silent
#                       n+tvqF:-geometry:-silent
#                       -silent!n+tvqF:-geometry:
#                   ~~~
#
#   :   `usage`  -  a procedure which will be called, with the list of
#                   `Opt` objects (see below), if the special `-help`
#                   or `-?` option is given; by default the options
#                   are listed and `exit(0)` called.  A string may be
#                   given, which is printed before the options.
#
#   :   `errproc` - a procedure which will be called if an error is
#                   is detected in the command line options.  The
#                   procedure is called with one argument:  a string
#                   describing the error that occurred.  After `errproc()`
#                   is called, `options()` immediately returns the outcome
#                   of `errproc()`, without processing further arguments.
#                   Already processed arguments will have been removed
#                   from `arg`.  If `errproc` is omitted, stop() is
#                   called if an error is detected.
#  `
#     Instead of an option string, a list of `Opt` objects may be given
#  instead.  This allows a descriptive help string to be given for
#  each option.
#
#     Using an `Opt` also allows other conversion procedures to be
#  specified.  For example, an integer option might be specified as:
#  ~~~
#         Opt("t", integer, "Timeout in seconds")
#  ~~~
#  This would allow `-t 3`, `--t=4`, etc.
#
#     The list of `Opt`s may also contain strings; these are interspersed
#  with the formatted option output when help is produced with the `-help`
#  option (or by calling `options_help()`).
#
#     A table is returned containing the options that were specified.
#  The keys are the specified option names.  The assigned values are the
#  data values following the options converted to the specified type.  For
#  an option allowing multiple usages, a the assigned value is a list.
#  A value of `&yes` is stored for options that accept no values.
#  The table's default value is `&null`.
#
#     Upon return, the option arguments are removed from arg, leaving
#  only the non-option arguments.
#
procedure options(arg, opts, usage, errproc)
   local fList, fileArg, fn, ignore, optname, opttable, x,
      option, k, v, p, opt, opttype, convert
   #
   #  Initialize.
   #
   /errproc := help_stop
   option := table()
   fList := []
   opttable := table()

   if opts := text(opts) then {
      #
      #  Scan the option specification string.
      #
      opts ? {
         opts := []
         while optname := move(1) do {
            if optname == " " then next
            if optname == "-" then
               optname := tab(many(&letters ++ &digits ++ '_')) | move(1) | break
            opttype := tab(any('!:+.')) | "!"
            convert := case string(opttype) of {
               ":": string
               "+": integer
               ".": real
               default: &null
            }
            put(opts, Opt(optname, convert,, Yes{tab(any('*'))}))
         }
      }
   }

   every opt := !opts | Opt(["help", "?"]) do {
      if is(opt, Opt) then {
         every optname := string(!opt.names) do {
            if member(opttable, optname) then
               runerr("duplicate option", optname)
            insert(opttable, optname, opt)
         }
      }
   }

   #
   #  Iterate over program invocation argument words.
   #
   while x := get(arg) do {
      if /x then ignore := &null     # if end of args from file, stop ignoring
      else if \ignore then
         put(fList,x)
      else x ? {
         if ="-" & not pos(0) then {
            if ="-" then {
               if pos(0) then
                  ignore := &yes   # ignore following args if --
               else {
                  optname := tab(upto('=') | 0)
                  opt := member(opttable, string(optname)) |
                     return errproc("Unrecognized option: --" || optname)

                  if /opt.convert then {
                     pos(0) | return errproc("--" || optname || " doesn't accept a parameter")
                     v := &yes
                  } else {
                     p := if ="=" then
                        tab(0)
                     else if \arg[1] then
                        get(arg)
                     else
                        return errproc("No parameter following --" || optname)
                     v := opt.convert(p) | return errproc("--" || optname || ": " || opt.build_param_err())
                  }

                  k := opt.names[1]
                  if /opt.multiple_flag then
                     insert(option, k, v)
                  else
                     inserts(option, k, v)
               }
            } else {
               tab(0) ? until pos(0) do {
                  opt := member(opttable, string(optname := ((pos(1),tab(0)) | move(1)))) | 
                     return errproc("Unrecognized option: -" || optname)

                  if /opt.convert then
                     v := &yes
                  else {
                     pos(*optname + 1) | return errproc("An option taking a parameter cannot be combined: -" || optname)

                     p := if not pos(0) then
                        tab(0)
                     else if \arg[1] then
                        get(arg)
                     else
                        return errproc("No parameter following -" || optname)
                     v := opt.convert(p) | return errproc("-" || optname || ": " || opt.build_param_err())
                  }
                  
                  k := opt.names[1]
                  if /opt.multiple_flag then
                     insert(option, k, v)
                  else
                     inserts(option, k, v)
               }
            }
         }
         #
         #  If the argument begins with the character "@", fetch option
         #  words from lines of a text file.
         #
         else if ="@" & not pos(0) then {
            fn := tab(0)
            fileArg := file2list(fn) |
               return errproc("Can't open " || fn || ": " || &why)
            # push null to signal end of args from file, followed by each line.
            every push(arg, &null | back(fileArg))
         }
         else put(fList,x)
      }
   }
   every push(arg, back(fList))

   if member(option, "help" | "?") then {
      /usage := "Usage: " || Files.get_program_name() || " [OPTIONS...]"
      if type(usage) == "list" then
         usage := cat_lines(usage)
      if usage := text(usage) then {
         write(usage)
         options_help(opts)
         exit(0)
      } else {
         usage(opts)
         fail
      }
   }

   return option
end

# Return the given builtin converter function's name, with its first
# letter capitalized (eg integer->"Integer").
package procedure capitalize_cnv(f)
   local s
   s := Proc.get_name(f)
   s[1] := map(s[1], &lcase, &ucase)
   return s
end

package procedure range_impl(cnv, min, max, x)
   x := cnv(x) | return error(capitalize_cnv(cnv) || " expected")
   if x < \min | x > \max then
      return error(capitalize_cnv(cnv) || " value out of valid range " || range_string(min, max))
   return x
end

# Converter function for an integer which must lie within the given
# min-max range.
#
procedure integer_range(min, max)
   return lpartial(range_impl, integer, min, max)
end

# Converter function for a real which must lie within the given
# min-max range.
#
procedure real_range(min, max)
   return lpartial(range_impl, real, min, max)
end

package procedure text_with_impl(cnv, q, s)
   s := cnv(s) | return error(capitalize_cnv(cnv) || " expected")
   return if (type(q) == "cset" & upto(q, s)) | find(q, s) then
      s
   else
      error(capitalize_cnv(cnv) || " containing " || image(q) || " expected")
end

# Converter function for a string option which must contain a char in
# `q` (if q is a cset), or the substring `q`, otherwise.
procedure string_with(q)
   return lpartial(text_with_impl, string, q)
end

# Same as `string_with`, but returns a ucs.
procedure ucs_with(q)
   return lpartial(text_with_impl, ucs, q)
end
