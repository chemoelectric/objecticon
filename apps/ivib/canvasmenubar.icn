#
# $Id$
#
# This file is in the public domain.
#
# Author: Robert Parlett (parlett@dial.pipex.com)
#

import gui


############################################################################
#
# Class for representing a MenuBar on the canvas.
#
class CanvasMenuBar(CanvasComponent,MenuBar)
   public 
      string_rep

   #
   # Generate code into Code object c.
   #
   public override gen_code(c)
      every (!self.menus).gen_code(c)
      CanvasComponent.gen_code(c)
      every c.chain("add(" || (!self.menus).name || ")")
   end

   #
   # Duplicate object.
   #
   public override dup(pc)
      local c
      c := CanvasMenuBar()
      CanvasComponent.dup_fields(c)
      every c.add((!self.menus).dup())
      return c
   end

   #
   # Return the I/O template.
   #
   public override get_template()
      suspend CanvasComponent.get_template() | ["Menus", "menus"]
   end

   protected override post_decode()
      every (!self.menus).component_link := self
   end

   #
   # Set the menu bar's string representation.
   #
   public set_string_rep()
      local sub, i
      string_rep := []
      every sub := self.menus[i := 1 to *self.menus] do 
         sub.set_string_rep(string_rep, 1, self, i)
      put(string_rep, MenuTreeNode(1, self, *self.menus + 1))
   end

   public get_string_rep()
      return self.string_rep      
   end

   #
   # Open the configuration dialog box.
   #
   public override dialog()
      return CanvasMenuBarDialog(self)
   end

   public override allows_null_height()
      return
   end

   public override allows_null_width()
      return
   end

   public override gen_cos()
      suspend CanvasComponent.gen_cos() | !self.menus
   end

   public override new()
      MenuBar.new()
      CanvasComponent.new()
      self.class_name := "MenuBar"
      self.import_name := "gui"
      self.name := "menu_bar"
      return
   end
end

