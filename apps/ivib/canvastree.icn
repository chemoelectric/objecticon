#
# $Id$
#
# This file is in the public domain.
#
# Author: Robert Parlett (parlett@dial.pipex.com)
#

import gui


############################################################################
#
# Class for representing a Tree on the canvas.
#
class CanvasTree(CanvasComponent,Tree)
   #
   # Default minimum width for the object
   #
   public override min_width()
      return scale(65)
   end

   #
   # Default minimum height for the object
   #
   public override min_height()
      return scale(65)
   end

   #
   # Generate code into Code object c.
   #
   public override gen_code(c)
      CanvasComponent.gen_code(c)
      c.chain("set_select_mode(" || c.select_str(self.select_mode) || ")")
      c.add_import("gui", "Select")
      if /self.show_root_flag then
         c.chain("set_show_root(&no)")
      if /self.show_root_handles_flag then
         c.chain("set_show_root_handles(&no)")
   end

   public override post_decode()
      CanvasComponent.post_decode()
      tree_structure_changed()
   end

   #
   # Duplicate object.
   #
   public override dup()
      local c
      c := CanvasTree()
      CanvasComponent.dup_fields(c)
      c.select_mode := self.select_mode
      c.show_root_flag := self.show_root_flag
      c.show_root_handles_flag := self.show_root_handles_flag
      c.tree_structure_changed()
      return c
   end

   #
   # Return the I/O template.
   #
   public override get_template()
      suspend CanvasComponent.get_template() |
         ["Select Mode", "select_mode"] |
         ["Show Root Handles", "show_root_handles_flag"] |
         ["Show Root", "show_root_flag"]
   end

   #
   # Open the configuration dialog box.
   #
   public override dialog()
      return CanvasTreeDialog(self)
   end

   public override new()
      local m, n, p
      Tree.new()
      CanvasComponent.new()
      self.class_name := "Tree"
      self.import_name := "gui"
      self.name := "tree"

      n := TreeNode().set_label("Root")
      self.set_root_node(n)
      m := TreeNode().set_label("Level one 1")
      n.add(m)
      p := TreeNode().set_label("Level two 1")
      m.add(p)
      p := TreeNode().set_label("Level two 2")
      m.add(p)
      p := TreeNode().set_label("Level two 3")
      m.add(p)
      m := TreeNode().set_label("Level one 2")
      n.add(m)
      m := TreeNode().set_label("Level one 3")
      n.add(m)
      p := TreeNode().set_label("Level two 1")
      m.add(p)
      p := TreeNode().set_label("Level two 2")
      m.add(p)
      p := TreeNode().set_label("Level two 3")
      m.add(p)
      m := TreeNode().set_label("Level one 4")
      n.add(m)
      return
   end
end




