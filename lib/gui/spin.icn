#
# $Id$
#
# This file is in the public domain.
#
# Author: Robert Parlett (parlett@dial.pipex.com)
#

package gui

import graphics

#
# This class is the base class for all spin components.  It just provides the up/down
# buttons and two abstract methods which are invoked to implement those actions.
#
abstract class Spin(Component)
   public 
      up, 
      down,
      view

   #
   # This method must be overridden to do something to the component when the
   # up button is pressed.
   #
   public abstract do_increment(ev)

   #
   # This method must be overridden to do something to the component when the
   # down button is pressed.
   #
   public abstract do_decrement(ev)

   #
   # Create the view component
   #
   public abstract create_view()

   #
   # This may be overridden to monitor for changes to trigger events.  It is
   # invoked before the do_increment/do_decrement methods are called
   #
   public start_change(e)
   end

   #
   # This may be overridden to monitor for changes to trigger events.  It is
   # invoked after the do_increment/do_decrement methods are called
   #
   public end_change(e)
   end

   public on_up(ev)
      go_up(ev)
   end

   public on_down(ev)
      go_down(ev)
   end

   public display()
      border.draw_rect(self.cbwin, self)
      display_children()
   end

   public focus_changed(e) 
      view.invalidate()
   end

   public get_default_width()
      return border.get_total_width() + up.get_preferred_width() + view.get_preferred_width()
   end

   public get_default_height(dw)
      dw -:= border.get_total_width() + up.get_preferred_width()
      return border.get_total_height() + view.get_preferred_height(dw)
   end

   public layout()
      local bh, bw
      bw := up.get_preferred_width()
      bh := (self.h - border.get_total_height()) / 2
      down.x := up.x := self.x + self.w - border.get_r_inset() - bw
      view.y := up.y := self.y + border.get_t_inset()
      down.y := up.y + bh
      down.w := up.w := bw
      up.h := bh
      down.h := self.h - border.get_total_height() - up.h
      view.x := self.x + border.get_l_inset()
      view.w := self.w - border.get_total_width() - bw
      view.h := self.h - border.get_total_height()
      up.layout()
      down.layout()
      view.layout()
   end

   public go_up(e)
      start_change(e)
      do_increment(e)
      end_change(e)
   end

   public go_down(e)
      start_change(e)
      do_decrement(e)
      end_change(e)
   end

   public new()
      Component.new()
      self.set_border(SunkenBorder())
      self.up := IconButton().
         set_width(ScrollArea.SCROLLBAR_SIZE).  # Consistent with DropDown
         connect(self.on_up, Event.BUTTON_PRESS).
         connect(self.on_up, Event.BUTTON_HELD).
         set_paint(ImagePaint().set_cache("gui.TINY_ARROW_UP")).
         set_accepts_focus(&no)
      self.add(up)
      self.down := IconButton().
         set_width(ScrollArea.SCROLLBAR_SIZE).
         connect(self.on_down, Event.BUTTON_PRESS).
         connect(self.on_down, Event.BUTTON_HELD).
         set_paint(ImagePaint().set_cache("gui.TINY_ARROW_DOWN")).
         set_accepts_focus(&no)
      self.add(down)
      self.preferred_focus := self.view := create_view()
      self.add(view)
      self.set_constraint("x_fill", &yes)
      self.set_constraint("x_align", Align.L)
      self.set_constraint("x_weight", 1.0)
      connect(self.go_up, Event.MOUSE_WHEEL_UP)
      connect(self.go_down, Event.MOUSE_WHEEL_DOWN)
      return
   end
end


#
# Selection from a list
#
abstract class SelectionSpin(SelectionList, Spin)
   public convert_element(o)
      return text(o)
   end

   public do_increment(e)
      if *selection_list = 0 then
         fail
      if /self.selection | self.selection = *self.selection_list then
         self.set_selection(1, e)
      else
         self.set_selection(self.selection + 1, e)
   end

   public do_decrement(e)
      if *selection_list = 0 then
         fail
      if /self.selection | (self.selection = 1) then
         self.set_selection(*self.selection_list, e)
      else
         self.set_selection(self.selection - 1, e)
   end

   public go_to(x, e)
      start_change(e)
      set_selection(x, e)
      end_change(e)
   end

   public initially()
      \self.selection_list | runerr("No selection list specified")
      Spin.initially()
   end
end


class SpinTextField(TextField)
   public keeps(e)
      return member(Key.CURSOR, e.code)
   end

   public on_key(e)
      case e.code of {
         Key.UP : parent.go_up(e)
         Key.DOWN : parent.go_down(e)
      }
   end

   public new()
      TextField.new()
      self.connect(self.on_key, Event.KEY_PRESS)
      self.set_border(EmptyBorder().set_l_inset(Gui.TEXT_INSET).set_r_inset(Gui.TEXT_INSET))
      return
   end
end


class SpinLabel(Label)
   public keeps(e)
      return member(Key.CURSOR_V, e.code)
   end

   public on_key(e)
      case e.code of {
         Key.UP : parent.go_up(e)
         Key.DOWN : parent.go_down(e)
         Key.HOME : if is(parent, SelectionSpin) then
                       parent.go_to(1, e)
         Key.END : if is(parent, SelectionSpin) then
                      parent.go_to(*parent.selection_list, e)
      }
   end

   public new()
      Label.new()
      self.set_label("")
      self.connect(self.on_key, Event.KEY_PRESS)
      self.set_accepts_focus(&yes)
      # Align with the text in the textlist.
      self.set_border(EmptyBorder().
                      set_x_align(Align.L).
                      set_l_inset(Gui.TEXT_INSET).
                      set_r_inset(Gui.TEXT_INSET))
      return
   end
end
