package io

import undo, util, lang, net, ipl.strings, ipl.io(get_null)

package record Save(text, undo)

class LineEditorStream(LineBasedInputStream, HasTask)
   package
      subj,
      pos
   private
      complete_function,
      history_limit,
      history,
      history_start_no,
      hpos,
      hmod,
      prompt,
      undo_manager,
      line_no,
      nlines,
      ctrl_d_flag,
      tty,
      isatty,
      dim,
      ti,
      buff_flag,
      buff

   # Set the limit in lines for the command history.
   #
   public set_history_limit(n)
      self.history_limit := n
      limit_history()
      return self
   end

   # Set the history list.
   #
   public set_history(l)
      self.history := l
      self.history_start_no := 1
      limit_history()
      return self
   end

   # Set a function to generate completions when the tab key is
   # pressed.  The function is given subject and cursor position, and
   # should return a list of `gui.CompleteElement` instances.
   #
   public set_complete_function(f)
      self.complete_function := f
      return self
   end

   # Set the prompt to display.
   #
   public set_prompt(s)
      self.prompt := s
      return self
   end

   # Return the history list.
   #
   public get_history()
      return .history
   end

   # Return the given history number line.
   #
   public get_history_item(n)
      return .history[0 < n - history_start_no + 1]
   end

   # Get the history number of the first entry in the history.
   #
   public get_history_start_no()
      return .history_start_no
   end

   public close()
      return self
   end

   public get_mode()
      return READ
   end

   private goto_hist(n)
      local d
      (1 <= n <= *history + 1) | fail
      if undo_manager.get_last() then
         insert(hmod, hpos, Save(subj, undo_manager))
      hpos := n
      if d := member(hmod, hpos) then {
         subj := d.text
         undo_manager := d.undo
      } else {
         subj := history[hpos] | u""
         undo_manager := UndoManager()
      }
      pos := *subj + 1
   end

   private get_mod_hist(n)
      return member(hmod, n).text | history[n]
   end

   private move_hist(n)
      goto_hist(hpos + n)
   end

   private hist_search(dir)
      local i, s, c
      c := pos
      s := subj[1:c]
      every i := if dir > 0 then (hpos + 1 to *history) | (1 to hpos - 1)
                            else (hpos - 1 to 1 by -1) | (*history to hpos + 1 by -1) do {
         if match(s, subj ~== get_mod_hist(i)) then {
            goto_hist(i)
            pos := c
            return
         }
      }
   end

   private static dbg(a[])
      static f
      initial
         f := FileStream("/tmp/lineeditor_dbg.txt", ior(FileOpt.WRONLY, FileOpt.TRUNC, FileOpt.CREAT))
      (\f).write!a
      return
   end

   public static get_console_size()
      local w, h
      # Could also use tput cols and tput lines to do this.
      return run_stty("size") ? {
         if h := integer(tab(many(&digits))) &
            tab(upto(&digits)) &
            w := integer(tab(many(&digits))) then [w, h]
         else error("Couldn't interpret stty size output")
      }
   end

   public static get_console_width()
      return get_console_size()[1]
   end

   private static run_stty(params[])
      local f, ss                   
      ss := StringStream()          
      use {                         
         f := ProgInputStream("stty", params,,, get_null()),
         f.copy_to(ss)                                      
      } | fail                                              
      return if f.get_exit_info().succeeded() then
         ss.str()                                           
      else                                                  
         error("stty failed: " || &why)
   end                                                      

   private static read_terminfo_escape()
      local ch, v
      return case move(1) of {
         "^":  char(iand(ord(move(1)), 8r37))
         "\\": {
            if any('01234567') & (v := char(iand("8r" || move(3), 8r377))) then
               v
            else case ch := move(1) of {
               "0": "\200"
               "E": "\e"
               "e": "\e"
               "n": "\n"
               "l": "\l"
               "r": "\r"
               "t": "\t"
               "b": "\b"
               "f": "\f"
               "s": " "
               default: ch
            }
         }
      }
   end

   private static parse_terminfo(f)
      local desc, t, s, k, v
      t := table()
      every s := separate_lines(f) do {
         s ? {
            if any('#') then 
               next
            if /desc then {
               desc := s
               next
            }
            repeat {
               tab(many(' \t'))
               k := tab(upto(',=#')) | break
               if any(',') then
                  v := &yes
               else if any('#') then {
                  move(1)
                  v := integer(tab(many(&digits)))
               } else {
                  move(1)
                  v := ""
                  repeat {
                     v ||:= tab(upto('\\^,')) | break
                     if any(',') then
                        break
                     v ||:= read_terminfo_escape()
                  }
               }
               =","
               insert(t, k, v)
            }
         }
      }
      return t
   end

   public static read_terminfo(name)
      local f, params, out
      params := ["-L"]
      put(params, \name)
      out := StringStream()
      use { f := FilterOutputStream(out, "infocmp", params) } | fail
      f.succeeded() | fail
      return parse_terminfo(out.str())
   end

   protected read_line_impl()
      \isatty | return tty.read_line()
      if \ctrl_d_flag then
         return
      return use {
         run_stty("-icanon",  "-echo"),
         use {
            # Enter/exit keyboard-transmit mode, see 
            # http://www.mpimf-heidelberg.mpg.de/~rohm/computing/mpimf/notes/terminal.html
            opt_write("keypad_xmit"),
            read_line_impl2(),
            opt_write("keypad_local")
         },
         run_stty("icanon",  "echo")
      }
   end

   private opt_write(key, def)
      local s
      if s := \ti[key] | \def then
         return tty.writes(s)
      else
         return
   end

   private read1()
      local ch
      if /buff_flag & (ch := buff[1]) then {
         buff := buff[2:0]
         return ch
      }
      choose_poll([tty, Poll.IN]) | fail
      ch := tty.in(1) | fail
      return \ch | error("Unexpected eof on tty")
   end

   private limit_history()
      while *history > \history_limit do {
         pop(history)
         history_start_no +:= 1
      }
   end

   private got()
      if *subj > 0 then {
         put(history, subj)
         limit_history()
      }
      return string(subj)
   end

   private reset_term()
      subj := u""
      pos := 1
      hpos := *history + 1
      clear(hmod)
      undo_manager.clear()
   end

   public insert_string(s)
      local ed
      s := text(s) | runerr(129, s)
      if *s > 0 then {
         ed := LineEditorInsertEdit(self, s)
         ed.doit()
         undo_manager.add_edit(ed)
      }
      return self
   end

   public assign_contents(x, pos, coalesce)
      local ed
      /x := ""
      x := text(x) | runerr(129, x)
      /pos := *x + 1
      if subj == x then {
         self.pos := pos
      } else {
         ed := LineEditorAssignEdit(self, x, pos, coalesce)
         ed.doit()
         undo_manager.add_edit(ed)
      }
      return self
   end

   private get_insertion(l)
      local e, t
      t := []
      every e := !l do {
         (subj[1:pos] == e.str[1:pos] &
          subj[pos:0] == e.str[e.pos:0]) | fail
         put(t, e.str[pos:e.pos])
      }
      return "" ~== coprefix(t)
   end

   private do_complete()
      local l, e, fl, cmd, s
      Files.is_flowterm_tty(tty) | fail
      l := (\complete_function)(subj, pos) | fail
      if *l = 1 then
         assign_contents(l[1].str, l[1].pos)
      else if *l > 0 then {
         if s := get_insertion(l) then {
            insert_string(s)
            if \line_no then 
               show()
         }
         tty.writes("\e[!")
         every e := !l do {
            if \fl then
               tty.writes(";")
            cmd := "\e[\"" || URL.percent_encode(e.str, URL.UNRESERVED) || "\";\"" || e.pos || "\"Q"
            tty.writes("\"", 
                       URL.percent_encode(e.label, URL.UNRESERVED), "\";\"", 
                       URL.percent_encode(cmd, URL.UNRESERVED), "\"")
            fl := &yes
         }
         tty.writes("P")
      }
   end

   private read_line_impl2()
      local ch, uch, p, n, ed, un, cmd, s
      reset_term()
      buff_flag := line_no := nlines := &null
      dim := get_console_size() | fail
      # Request line_no
      tty.writes("\e[6n")
      repeat {
         if \line_no then 
            show()
         ch := read1() | fail
         # Read a utf8 sequence into ch.  On gibberish, just go round and try again.
         un := Text.get_utf8_seq_len(ch) | next
         every 2 to un do
            ch ||:= read1() | fail
         uch := ucs(ch) | next

         if ch == "\e" then {
            cmd := .ch || (ch := read1()) | fail
            p := []
            if ch == "O" then
               cmd ||:= read1() | fail
            else if ch == "[" then {
               repeat {
                  cmd ||:= (ch := read1()) | fail
                  if any('"', ch) then {
                     s := ""
                     repeat {
                        cmd ||:= (ch := read1()) | fail 
                        if any('"', ch) then
                           break
                        s ||:= ch
                     }
                     cmd ||:= (ch := read1()) | fail 
                     put(p, URL.percent_decode(s))
                  } else {
                     n := integer(ch) | break
                     repeat {
                        cmd ||:= (ch := read1()) | fail
                        n := 10 * n + integer(ch) | break
                     }
                     put(p, n)
                  }
                  ch == ";" | break
               }
            }
            # Line no request response
            if cmd[2] == "[" & cmd[-1] == "R" then {
               line_no := p[1]
               if \buff_flag then
                  return got()
            } else if \buff_flag then
               buff ||:= cmd
            else if cmd[2] == "[" & cmd[-1] == "Q" then
               assign_contents(ucs(p[1]), integer(p[2]))
            else case cmd of {
               ti["key_up"]: move_hist(-1)
               ti["key_down"]: move_hist(1)
               ti["key_right"]: pos := (*subj + 1 >= pos + 1)
               ti["key_left"]: pos := (1 <= pos - 1)
               ti["key_home"]: pos := 1
               ti["key_end"]: pos := *subj + 1
               ti["key_dc"]: {
                  if pos <= *subj then {
                     ed := LineEditorDeleteRightEdit(self)
                     ed.doit()
                     undo_manager.add_edit(ed)
                  }
               }
            }
         } else if \buff_flag then
            buff ||:= ch
         else case ch of {
            "\^_": {
               if undo_manager.can_undo() then
                  undo_manager.undo()
            }
            "\^y": {
               if undo_manager.can_redo() then
                  undo_manager.redo()
            }
            "\^d": {
               ctrl_d_flag := &yes
               if /line_no then
                  tty.write(prompt, subj)
               else {
                  # Since we may be on a multiline input, but not on
                  # the last line of it, move the cursor to the end of
                  # the subject before printing the new line.
                  pos := *subj + 1
                  show()
                  tty.write()
               }
               if *subj > 0 then
                  return got()
               else
                  return
            }
            "\^a": pos := 1
            "\^e": pos := *subj + 1
            "\^g": reset_term()
            "\^k": {
               if pos < *subj + 1 then {
                  ed := LineEditorDeleteToEndOfLineEdit(self)
                  ed.doit()
                  undo_manager.add_edit(ed)
               }
            }
            "\^p": goto_hist(integer(subj) - history_start_no + 1)
            "\^r": hist_search(-1)
            "\^t": hist_search(1)
            "\b" | "\d": {
               if pos > 1 then {
                  ed := LineEditorDeleteLeftEdit(self)
                  ed.doit()
                  undo_manager.add_edit(ed)
               }
            }
            "\t": do_complete()
            "\n": {
               if /line_no then {
                  tty.write(prompt, subj)
                  # Buffer input until got line no request response, which is now out-of-date.
                  buff_flag := &yes
               } else {
                  # See ^d above
                  pos := *subj + 1
                  show()
                  tty.write()
                  return got()
               }
            }
            default: {
               if any('\x20-\x7e\x80-\U10ffff', uch) then {
                  ed := LineEditorDefaultEdit(self, uch)
                  ed.doit()
                  undo_manager.add_edit(ed)
               }
            }
         }
      }
   end

   private show()
      local n, cursor_line, cursor_col
      if \nlines then {
         # Clear the lines written the previous time show() was called
         every tty.writes("\e[",
                          line_no to line_no + nlines - 1,
                          ";1H", "\e[2K")
      }

      # The number of lines needed to display this line.
      nlines := 1 + (*prompt + *subj) / dim[1]

      # Check if we need to scroll some lines
      n := nlines + line_no - dim[2] - 1

      if n > 0 then {
         # Scroll n lines
         tty.writes("\e[", n ,"S")
         line_no -:= n
      }
      # Calculate position of cursor
      cursor_line := line_no + (*prompt + pos - 1) / dim[1]
      cursor_col := (*prompt + pos - 1) % dim[1] + 1

      # First, goto line line_no, col 1, and write prompt, subj
      # Then put cursor at correct position.
      tty.writes("\e[", line_no, ";1H", prompt, subj,
                 "\e[", cursor_line, ";", cursor_col, "H")
   end

   public new()
      LineBasedInputStream.new()
      tty := FileStream.stdin
      if run_stty() then {
         isatty := &yes
         ti := read_terminfo() | fail
      }
      undo_manager := UndoManager()
      hmod := table()
      history := []
      history_start_no := 1
      prompt := ""
      buff := ""
      return
   end
end

abstract class LineEditorEdit(UndoableEdit)
   protected
      parent,
      pos

   public doit()
      save()
      self.redo_impl()
   end

   public redo()
      restore()
      self.redo_impl()
   end

   public undo()
      self.undo_impl()
      restore()
   end

   protected abstract redo_impl()
   protected abstract undo_impl()

   private save()
      self.pos := parent.pos
   end

   private restore()
      parent.pos := self.pos
   end

   public new(parent)
      self.parent := parent
      return
   end
end

class LineEditorDefaultEdit(LineEditorEdit)
   private s

   public add_edit(other)
      if is(other, LineEditorDefaultEdit) &
         (other.pos = self.pos + *s) then {
            s ||:= other.s
            return
      }
   end

   protected redo_impl()
      parent.subj := parent.subj[1:parent.pos] || s || parent.subj[parent.pos:0]
      parent.pos +:= *s
   end

   protected undo_impl()
      parent.subj[self.pos +: *s] := ""
   end

   public new(parent, e)
      LineEditorEdit.new(parent)
      s := e
      return
   end
end

class LineEditorDeleteLeftEdit(LineEditorEdit)
   private ch

   public add_edit(other)
      if is(other, LineEditorDeleteLeftEdit) &
         (pos = other.pos + *ch) then
      {
         ch := other.ch || ch
         return
      }
   end

   protected redo_impl()
      parent.subj[parent.pos -: *ch] := ""
      parent.pos -:= *ch
   end

   protected undo_impl()
      if pos - *ch > *parent.subj then
         parent.subj ||:= ch
      else
         parent.subj[pos - *ch] := ch || parent.subj[pos - *ch]
   end

   public new(parent)
      LineEditorEdit.new(parent)
      ch := parent.subj[parent.pos - 1]
      return
   end
end

class LineEditorDeleteRightEdit(LineEditorEdit)
   private ch

   public add_edit(other)
      if is(other, LineEditorDeleteRightEdit) &
         (pos = other.pos) then 
      {
         ch ||:= other.ch
         return
      }
   end

   protected redo_impl()
      parent.subj[parent.pos +: *ch] := ""
   end

   protected undo_impl()
      if pos > *parent.subj then
         parent.subj ||:= ch
      else
         parent.subj[pos] := ch || parent.subj[pos]
   end

   public new(parent)
      LineEditorEdit.new(parent)
      ch := parent.subj[parent.pos]
      return
   end
end

class LineEditorDeleteToEndOfLineEdit(LineEditorEdit)
   private del

   protected redo_impl()
      del := parent.subj[parent.pos:0]
      parent.subj[parent.pos:0] := ""
   end

   protected undo_impl()
      parent.subj[self.pos:0] := del
   end
end

class LineEditorAssignEdit(LineEditorEdit)
   private s, p, pre, coalesce

   public add_edit(other)
      if \coalesce & is(other, LineEditorAssignEdit) & \other.coalesce then {
         self.s := other.s
         self.p := other.p
         return
      }
   end

   public redo_impl()
      pre := parent.subj
      parent.subj := s
      parent.pos := p
   end

   public undo_impl()
      parent.subj := pre
   end

   public new(parent, s, p, coalesce)
      LineEditorEdit.new(parent)
      self.s := s
      self.p := p
      self.coalesce := coalesce
      return
   end
end

class LineEditorInsertEdit(LineEditorEdit)
   private s

   protected redo_impl()
      parent.subj := parent.subj[1:parent.pos] || s || parent.subj[parent.pos:0]
      parent.pos +:= *s
   end

   protected undo_impl()
      parent.subj[self.pos +: *s] := ""
   end

   public new(parent, e)
      LineEditorEdit.new(parent)
      s := e
      return
   end
end
