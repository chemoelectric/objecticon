############################################################################
#
#	File:     tile.icn
#
#	Subject:  Procedure to tile window
#
#	Author:   Ralph E. Griswold
#
#	Date:     September 29, 1997
#
############################################################################
#
#   This file is in the public domain.
#
############################################################################
#
#  This procedure tiles a portion of win1 over the specified portion
#  of win2, doubling to reduce the number of copies required.
#
############################################################################
#
#  Requires:  Version 9 graphics
#
############################################################################

package ipl.tile

# tile area with image
procedure tile(win1, win2, x1, y1, w1, h1)	
   local   wmax, hmax

   /x1 := 0
   /y1 := 0
   /w1 := win1.attrib("width")
   /h1 := win1.attrib("height")
   wmax := win2.attrib("width")
   hmax := win2.attrib("height")

   if (w1 | h1) = 0 then fail

   if w1 < 0 then {
      w1 := -w1
      x1 -:= w1
      }

   if h1 < 0 then {
      h1 := -h1
      y1 -:= h1
      }

   win1.copy_to(win2, x1, y1, w1, h1)		# initial copy 

   while w1 < wmax do {				# copy and double
      win2.copy_to(win2, 0, 0, w1, h1, w1, 0)
      w1 *:= 2
      }

   while h1 < hmax do {				# copy and double
      win2.copy_to(win2, 0, 0, w1, h1, 0, h1)
      h1 *:= 2
      }

   return

end
