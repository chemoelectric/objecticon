#
# $Id$
#
# This file is in the public domain.
#
# Author: Robert Parlett (parlett@dial.pipex.com)
#

import gui

#############################################################################
#
# A simple dialog to output a multi-line message.
#     
class InfoDialog(Dialog)
   private lines, 
           title,
           button_text,
           alignment

   public component_setup()
      local l, s, b
      /button_text := "Close"
      /title := "Information"
      /alignment := "l"
      attrib("label=" || title)
      set_layout(GridLayout())
      l := Label("c:eol", "c:y_weight=1")
      s := ".cell x_align=" || alignment || "\n"
      every s ||:= !lines || "\n.br\n"
      l.set_label(s)
      add(l)
      add(Line("c:eol", "size=100", "c:x_fill"))
      b := TextButton()
      b.set_label(button_text)
      b.connect(self.dispose, Event.ACTION)
      self.add(b)
      set_focus(b)
   end

   public new(lines, title, button_text, alignment)
      cast(self,Dialog).new()
      self.lines := lines
      self.title := title
      self.button_text := button_text
      self.alignment := alignment
      return
   end
end


