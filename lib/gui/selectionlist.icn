package gui

import ipl.numbers

abstract class SelectionList()
   public 
      selection,
      selection_list

   #
   # Set the list selection items to the list x.
   # :Parameters :
   # :  `x` - The list of items
   #
   public set_selection_list(x)
      self.selection_list := x
      /self.selection := 1
      constrain_selection()
      apply_selection()
      link
   end

   #
   # Set the selected item.
   # :Parameters :
   # :  `x` - An index into the list of selectable items.
   #
   public set_selection(x, ev)
      self.selection := x
      constrain_selection()
      apply_selection(ev)
      link
   end

   #
   # Keep the selection within the bounds of the contents.
   #
   public constrain_selection()
      if *self.selection_list = 0 then
         self.selection := &null
      else if \self.selection then {
         self.selection <:= 1
         self.selection >:= *self.selection_list
      }
      link
   end

   #
   # Return an integer corresponding to the item in the list
   # presently selected.
   #
   public get_selection()
      return \self.selection
   end

   #
   # Convenient method to get the item in the selection list currently selected.
   #
   public object_get_selection()
      return self.selection_list[\self.selection]
   end

   #
   # Convenient method to set the selection to the given object.  Fails
   # if the item is not in the list, returns the index selected otherwise.
   #
   public object_set_selection(o)
      local i
      if i := keyof(selection_list, o) then {
         set_selection(i)
         return i
      }
   end

   #
   # Set the selection list, and constrain the selection, but do not
   # synchronize by calling `apply_selection`.
   #
   # :Parameters :
   # :  `x` - The list of items
   #
   public set_selection_list_only(x)
      self.selection_list := x
      constrain_selection()
      link
   end

   #
   # Set the selected item, and constrain that selection, but do not
   # synchronize by calling `apply_selection`.
   #
   # :Parameters :
   # :  `x` - An index into the list of selectable items.
   #
   public set_selection_only(x)
      self.selection := x
      constrain_selection()
      link
   end

   #
   # Helper function to increment the selection.  If the selection
   # list is empty, it does nothing and fails; otherwise the selection
   # is incremented, wrapping around at the end of the selection list.
   #
   public increment_selection(ev)
      if *selection_list = 0 then
         fail
      set_selection(if /selection then 1
                    else mod1(selection + 1, *selection_list), ev)
      link
   end

   #
   # Helper function to decrement the selection.  If the selection
   # list is empty, it does nothing and fails; otherwise the selection
   # is decremented, wrapping around at the start of the selection list.
   #
   public decrement_selection(ev)
      if *selection_list = 0 then
         fail
      set_selection(if /selection then *selection_list
                    else mod1(selection - 1, *selection_list), ev)
      link
   end

   public abstract apply_selection(ev)
end
