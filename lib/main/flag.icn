package util

#
# Convert to flag value (&yes/&no)
#
procedure flag(s)
   # Flag values just return themselves
   return if s === (&yes | &no) then
      s
   else case  map(string(s)) of {
      "y" | "&yes" | "yes" | "t" | "true" | "on" | "1" : &yes
      "n" | "&no" | "no" | "f" | "false" | "off" | "0" : &no
   }
end

#
# Check that x is a flag (&yes or &no), or give a runtime error
# message.  Note that no conversion is done.
#
procedure need_flag(x)
   x === (&yes | &no) | runerr(171, x)
   return x
end

#
# Toggle the value of a flag.
#
procedure toggle_flag(f)
   return if /f then &yes else &no
end

#
# Return &yes if the given expression succeeds, otherwise &no.
#
procedure Yes(e)
   return if @e then &yes else &no
end

#
# Return &no if the given expression succeeds, otherwise &yes.
#
procedure No(e)
   return if @e then &no else &yes
end

