#
# $Id$
#

package mail

#
# This class represents an internet Address, which
# may be parsed in accordance with RFC822.
#
class Mailbox(Address)
   private readable
      local_part,
      domain,
      phrase,
      route

   public get_local_part() 
      return local_part
   end

   public set_local_part(x)
      local_part := x
      return self
   end

   public get_domain()
      return domain
   end
   
   public set_domain(x)
      domain := x
      return self
   end

   public get_phrase() 
      return phrase
   end

   public set_phrase(x)
      phrase := x
      return self
   end

   public get_route() 
      return route
   end

   public set_route(x)
      route := x
      return self
   end

   public to_rfc822()
      local res, addr

      addr := local_part || "@" || domain

      if *(phrase | route) > 0 then {
         if *phrase > 0 then
            res := phrase || " <"
         else
            res := "<"
         if *route > 0 then {
            every res ||:= "@" || !route
            res ||:= ": "
         }
         return res || addr || ">"
      } else
         return addr
   end

   public static parse(s)
      local p
      p := RFC822Parser()
      return p.parse_mailbox(s)
   end   

   public generate_mailboxes()
      return self
   end

   public static mailbox_list_to_string(l)
      local s, e
      s := ""
      every e := !l do {
         if *s > 0 then
            s ||:= ","
         s ||:= e.to_rfc822()
      }
      return s
   end

   public new(local_part, domain, phrase, route)
      /phrase := ""
      /route := []
      self.local_part := local_part
      self.domain := domain
      self.phrase := phrase
      self.route := route
      return
   end
end

