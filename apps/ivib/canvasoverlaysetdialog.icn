#
# $Id$
#
# This file is in the public domain.
#
# Author: Robert Parlett (parlett@dial.pipex.com)
#

import gui, util

############################################################################
#
#
#
class CanvasOverlaySetDialog(CanvasComponentDialog)
   public 
      add_col,   
      delete,    
      edit,      
      struct,    
      up,
      down,
      which_one

   public update_grey()
      local i
      i := struct.get_selections()[1]
      if \i <= *c.children then {
         if *c.children > 1 then
            delete.set_is_shaded(&no)
         edit.set_is_shaded(&no)
         which_one.set_is_shaded(&no)
         up.set_is_shaded(Flag{i <= 1})
         down.set_is_shaded(Flag{i >= *c.children})
      } else {
         edit.set_is_shaded(&yes)
         which_one.set_is_shaded(&yes)
         delete.set_is_shaded(&yes)
         up.set_is_shaded(&yes)
         down.set_is_shaded(&yes)
      }

      # Enable the default width/height if applicable; our children may change this
      # during editing.
      if c.allows_null_width() then
         set_w_default.set_is_shaded(&no)
      if c.allows_null_height() then 
         set_h_default.set_is_shaded(&no)
   end

   public on_add_col()
      local new, l, nl
      new := CanvasOverlayItem()
      new.change_canvas(c.parent_Canvas)
      new.rename()
      l := struct.get_selections()
      nl := l[1] | (*c.children + 1)
      c.add(new, nl)
      struct.set_contents(c.string_rep())      
      struct.ensure_line_visible(nl)
      struct.set_selections([nl])
      struct.set_cursor(nl)
      update_grey()
   end

   public on_edit()
      local obj, i, d, cp
      i := struct.get_selections()[1]
      # The child dialog needs to know if we have a width/height set or not - if not then it
      # mandates a layout manager in the child.
      c.w_spec := if w_spec_list.is_shaded() then &null else w_spec_list.get_contents()
      c.h_spec := if h_spec_list.is_shaded() then &null else h_spec_list.get_contents()
      obj := c.children[i]
      cp := obj.dup()
      d := obj.dialog()
      d.show_modal(self)
      if \d.get_okay_result() then {
         struct.set_contents(c.string_rep())
         update_grey()
      } else {
         c.remove_index(i)
         cp.change_canvas(c.parent_Canvas)
         c.add(cp, i)
         if c.which_one === obj then
            c.which_one := cp
      }
   end

   public on_delete()
      local i
      i := struct.get_selections()[1]
      if c.which_one === c.children[i] then
         c.which_one := c.children[i + 1 | i - 1]
      c.remove_index(i)
      struct.set_contents(c.string_rep())
      struct.clear_selections()
      update_grey()
   end

   public validate_input()
      # Check any null dimension is allowable - it won't be if we've added
      # a new item.
      #
      if w_spec_list.is_shaded() & not c.allows_null_width() then
         return alert_error(self, ["Can't have a null height (some items",
                                   "don't have a layout manager)."])

      if h_spec_list.is_shaded() & not c.allows_null_height() then
         return alert_error(self, ["Can't have a null width (some items",
                                   "don't have a layout manager)."])

      return CanvasComponentDialog.validate_input()
   end

   public on_up()
      local i, t
      i := struct.get_selections()[1]
      i > 1 | fail
      t := c.children[i]
      c.remove_index(i)
      c.add(t, i - 1)
      struct.set_contents(c.string_rep())
      struct.set_selections([i - 1])
      struct.set_cursor(i - 1)
      update_grey()
   end

   public on_down()
      local i, t
      i := struct.get_selections()[1]
      i < *c.children | fail
      t := c.children[i]
      c.remove_index(i)
      c.add(t, i + 1)
      struct.set_contents(c.string_rep())
      struct.set_selections([i + 1])
      struct.set_cursor(i + 1)
      update_grey()
   end

   public on_which_one()
      c.which_one := c.children[struct.get_selections()[1]]
      struct.set_contents(c.string_rep())
   end

   public setup()
      local p, q

      CanvasComponentDialog.setup()

      p := TabItem()
      p.set_layout(GridLayout())
      add_tab_at_front(p)
      p.set_label("General")

      struct := TextList()
      struct.connect(self.update_grey, Event.SELECTION_CHANGED)
      struct.set_size(scale(175), scale(100))
      struct.set_select_mode(Select.ONE)
      struct.set_contents(c.string_rep())
      p.add(struct)

      q := Panel().set_constraint("x_weight", 0)
      q.set_layout(GridLayout().set_doi(0))

      add_col := TextButton().set_constraint("eol", &yes)
      add_col.connect(self.on_add_col, Event.ACTION)
      add_col.set_label("Add")
      q.add(add_col)

      delete := TextButton().set_constraint("eol", &yes)
      delete.connect(self.on_delete, Event.ACTION)
      delete.set_label("Delete")
      q.add(delete)

      edit := TextButton().set_constraint("eol", &yes)
      edit.connect(self.on_edit, Event.ACTION)
      edit.set_label("Edit")
      q.add(edit)

      which_one := TextButton().set_label("Which").set_constraint("eol", &yes)
      which_one.connect(self.on_which_one, Event.ACTION)
      q.add(which_one)

      up := TextButton().set_label("Up").set_constraint("eol", &yes)
      up.connect(self.on_up, Event.ACTION)
      q.add(up)

      down := TextButton().set_label("Down")
      down.connect(self.on_down, Event.ACTION)
      q.add(down)

      p.add(q)
   end
end




