#
# $Id$
#

package mail

import datastruct(SortTable), util(error), lang(Text)

#
# This class represents a content disposition
#
class ContentDisposition()
   private readable
      type,
      parameters

   #
   # Set type type
   #
   public set_type(t)
      self.type := t
      link
   end

   #
   # Get the type
   #
   public get_type()
      return .type
   end

   #
   # Get the parameter for the given key.
   #
   # :Parameters :
   # :  `key` - the key
   public get_parameter(key)
      return parameters.member(key)
   end

   #
   # Set the parameter to the given value
   # :Parameters :
   # :  `key` - the key
   # :  `value` - the value
   public set_parameter(key, value)
      parameters.insert(key, value)
      link
   end

   private parameter_error(h, s)
      return error("Error parsing " || h || " parameter: " || image(s) || ": " || &why)
   end

   #
   # Get the "filename" parameter, or fail if absent
   #
   public get_filename()
      return get_parameter("filename") | error("Missing parameter: filename")
   end

   #
   # Set the "filename" parameter.
   #
   public set_filename(v)
      set_parameter("filename", v)
      link
   end

   #
   # Get the "creation-date" parameter as a `Time` object.
   # :Fails :
   # :  if the parameter is absent or cannot be parsed.
   # :Returns :
   # :  a `Time` instance
   #
   public get_creation_date()
      local s
      s := get_parameter("creation-date") | return error("Missing parameter: creation-date")
      return RFC822Parser().parse_date_time(s) | parameter_error("creation-date", s)
   end

   #
   # Set the "creation-date" parameter from the given `Time` object.
   # :Parameters :
   # :  `t` - the `Time` from which to set the date field.
   #
   public set_creation_date(t)
      return set_parameter("creation-date", t.to_rfc822())
   end

   #
   # Get the "modification-date" parameter as a `Time` object.
   # :Fails :
   # :  if the parameter is absent or cannot be parsed.
   # :Returns :
   # :  a `Time` instance
   #
   public get_modification_date()
      local s
      s := get_parameter("modification-date") | return error("Missing parameter: modification-date")
      return RFC822Parser().parse_date_time(s) | parameter_error("modification-date", s)
   end

   #
   # Set the "modification-date" parameter from the given `Time` object.
   # :Parameters :
   # :  `t` - the `Time` from which to set the date field.
   #
   public set_modification_date(t)
      return set_parameter("modification-date", t.to_rfc822())
   end

   #
   # Get the "read-date" parameter as a `Time` object.
   # :Fails :
   # :  if the parameter is absent or cannot be parsed.
   # :Returns :
   # :  a `Time` instance
   #
   public get_read_date()
      local s
      s := get_parameter("read-date") | return error("Missing parameter: read-date")
      return RFC822Parser().parse_date_time(s) | parameter_error("read-date", s)
   end

   #
   # Set the "read-date" parameter from the given `Time` object.
   # :Parameters :
   # :  `t` - the `Time` from which to set the date field.
   #
   public set_read_date(t)
      return set_parameter("read-date", t.to_rfc822())
   end

   #
   # Get the "size" parameter as an integer, or fail if it's absent or
   # not an integer.
   #
   public get_size()
      local s
      s := get_parameter("size") | return error("Missing parameter: size")
      return integer(s) | error("Parameter size: " || image(s) || ": Not an integer")
   end

   #
   # Set the "size" parameter from the given integer.
   #
   public set_size(n)
      return set_parameter("size", string(integer(n)))
   end

   public static parse(s)
      return RFC822Parser().parse_content_disposition(s)
   end   

   public static parse_rfc2616(s)
      return RFC2616Parser().parse_content_disposition(s)
   end   

   public to_rfc1521()
      local s, e
      s := type 
      every e := parameters.entries() do
         s ||:= "; " || e.key || "=" || RFC822Parser.escape_1521_value(e.val)
      return s
   end

   public to_rfc2616()
      local s, e
      s := type 
      every e := parameters.entries() do
         s ||:= "; " || e.key || "=" || RFC2616Parser.escape_value(e.val)
      return s
   end

   public new(type)
      self.type := type
      self.parameters := SortTable(, Text.caseless_compare)
      return
   end
end
