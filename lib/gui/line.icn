#
# $Id$
#
# This file is in the public domain.
#
# Author: Robert Parlett (parlett@dial.pipex.com)
#

package gui

#
# This class implements an etched Line, drawn within its region.
#
class Line(Component, HasOrientation)
   public
      line_width

   public override set_line_width(n)
      self.line_width := n
      return self
   end

   public override get_default_width()
      if /self.is_horizontal_flag then
         return line_width
   end

   public override get_default_height()
      if \self.is_horizontal_flag then
         return line_width
   end

   public override display()
      local i, x1, y1
      if \self.is_horizontal_flag then {
         y1 := self.y + (self.h - line_width) / 2 
         every i := 0 to line_width / 2 - 1 do
            Gui.style.fill_shadow_rectangle(self.cbwin,  self.x, y1 + i, self.w, 1)
         every i := line_width / 2 to line_width - 1 do
            Gui.style.fill_hilite_rectangle(self.cbwin,  self.x, y1 + i, self.w, 1)
      } else {
         x1 := self.x + (self.w - line_width) / 2
         every i := 0 to line_width / 2 - 1 do
            Gui.style.fill_shadow_rectangle(self.cbwin, x1 + i, self.y, 1, self.h)
         every i := line_width / 2 to line_width - 1 do
            Gui.style.fill_hilite_rectangle(self.cbwin, x1 + i, self.y, 1, self.h)
      }
   end

   public override new()
      Component.new()
      self.line_width := Gui.BORDER_WIDTH
      self.is_horizontal_flag := &yes
      return
   end
end



