# $Id$

import gui, io

class BorderField(Component)
   public 
   borders,
   border_tbl,
   edit_border_button

   public get_contents()
      local b, i

      i := borders.get_selection()
      if i = 1 then
         return &null
      b := border_tbl[i] 
      if /b then {
         b := case i of {
            2 : CanvasEmptyBorder()
            3 : CanvasRaisedBorder()
            4 : CanvasSunkenBorder()
            5 : CanvasBevelledBorder()
            6 : CanvasPlainBorder()
            default: stop()
         }
         b.change_canvas(parent_dialog.c.parent_Canvas)
         b.rename()
         insert(border_tbl, i, b)
      }
      return b
   end

   public on_edit_border()
      get_contents().dialog().show_modal(parent_dialog)
   end

   public on_border_change()
      if borders.get_selection() = 1 then
         edit_border_button.set_is_shaded("t")
      else
         edit_border_button.set_is_shaded("f")
   end

   public set_contents(c)
      border_tbl := table()
      if /c then
         edit_border_button.set_is_shaded("t")
      else {
         borders.set_selection(case classof(c) of {
            CanvasEmptyBorder : 2
            CanvasRaisedBorder : 3
            CanvasSunkenBorder : 4
            CanvasBevelledBorder : 5
            CanvasPlainBorder : 6
            default : stop("Unknown border type")
         })
         border_tbl[borders.get_selection()] := c
      }
   end

   public setup()
      self.set_layout(GridLayout("flush"))
      borders :=  List("selection_list=",
                       ["Default",
                        "EmptyBorder",
                        "RaisedBorder",
                        "SunkenBorder",
                        "BevelledBorder",
                        "PlainBorder"])
      borders.connect(self.on_border_change, Event.SELECTION_CHANGED)
      self.add(borders)
      edit_border_button := TextButton("label=Edit...", "c:x_align=l", "c:eol")
      edit_border_button.connect(self.on_edit_border, Event.ACTION)
      self.add(edit_border_button)
   end

   public new(a[])
      cast(self,Component).new()
      setup()
      self.set_constraint("x_fill", "y")
      self.set_constraint("x_weight", "1")
      set_fields(a)
      return
   end
end


