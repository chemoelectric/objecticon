#
# $Id$
#
# This file is in the public domain.
#
# Author: Robert Parlett (parlett@dial.pipex.com)
#

import gui, ipl.pdco


############################################################################
#
# Class to represent a ButtonGroup on the canvas.
#
class CanvasButtonGroup(CanvasObject,ButtonGroup)
   #
   # Generate code into Code object c.
   #
   public override gen_code(c)
      local e
      #
      # Extend superclass method
      #
      CanvasObject.gen_code(c)
      every e := !get_members() do
         c.chain("add(" || e.name || ")")
   end

   public override dup()
      local c
      c := CanvasButtonGroup()
      CanvasObject.dup_fields(c)
      return c
   end

   public get_members()
      local o, l
      l := []
      every o := Seq{self.parent_Canvas.gen_cos().traverse_tree()} do {
         if (is(o, gui.Button) &
             o.parent_button_group === self) then
            put(l, o)
      }
      return l
   end

   public override new()
      ButtonGroup.new()
      self.class_name := "ButtonGroup"
      self.import_name := "gui"
      self.name := "button_group"
      return
   end
end

