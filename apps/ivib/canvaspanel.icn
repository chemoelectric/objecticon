#
# $Id$
#
# This file is in the public domain.
#
# Author: Robert Parlett (parlett@dial.pipex.com)
#

import gui


############################################################################
#
# Class for representing a Panel on the canvas.
#
class CanvasPanel(CanvasComponent,Panel)
   #
   # Generate code into Code object c.
   #
   public gen_code(c)
      local i
      cast(self,CanvasComponent).gen_code(c)
      every i := !self.children do {
         i.gen_code(c)
         c.line(self.name || ".add(" || i.name || ")")
      }
   end

   #
   # Duplicate object.
   #
   public dup(pc)
      local c
      c := CanvasPanel()
      cast(self,CanvasComponent).dup_fields(c)
      every c.add((!self.children).dup())
      return c
   end

   #
   # Return the I/O template.
   #
   public static get_template()
      suspend CanvasComponent.get_template() | ["Children", "children"]
   end

   #
   # Overrides CanvasComponent.is_inside()
   #
   public is_inside(c)
      local e, o
      if c === self then
         fail
      every e := rbang(self.z_children) do
         if o := e.is_inside(c) then
            return o
      if (self.x <= c.mx < self.x + self.w) & (self.y <= c.my < self.y + self.h) then
         return self
   end

   #
   # Overrides CanvasComponent.which_cursor_over() 
   #
   public which_cursor_over(e)
      local t, o
      cast(self,CanvasComponent).which_cursor_over(e) | fail
      if in_resize_region(e) then
         return self
      every t := rbang(self.z_children) do
         if o := t.which_cursor_over(e) then
            return o
      return self
   end

   #
   # Open the configuration dialog box.
   #
   public dialog()
      return CanvasPanelDialog(self)
   end

   public display(r)
      if in_preview() then
         cast(self,Panel).display(r)
      else {
         center_string(self.cbwin, self.x + self.w / 2, self.y + 10, "<Panel>")
         display_children(r)
         rectangle(self.cbwin, self.x, self.y, self.w, self.h)
      }
   end

   public allows_add()
      return 1
   end

   public gen_cos()
      suspend cast(self,CanvasComponent).gen_cos() | !self.children
   end

   public new()
      cast(self,Panel).new()
      cast(self,CanvasComponent).new()
      self.set_show_layout(&yes)
      self.class_name := "Panel"
      self.import_name := "gui"
      self.name := "panel"
      return
   end
end

