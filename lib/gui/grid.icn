package gui

import 
   lang,
   ipl.sort,
   io,
   ipl.numbers

class CellInfo(Insets,XYAlign,ObjectOutput)
   public 
      obj,            # An arbitrary object 
      ax,             # Actual computed pixel location and size,
      ay,             # within insets, ie where to draw the thing
      aw,             # inside the cell.
      ah,
      vw,             # Available space for aw, ah; they are either this or the
      vh,             #    preferred size depending on fill
      s,              # Identifying string for print_layout
      x,              # Location, size in grid units.
      y, 
      w, 
      h, 
      pw,             # Preferred cell width excluding insets
      ph, 
      ph_calc_w,      # Width used to calc ph
      x_fill,         # Constraints: fill, align, weights, insets
      y_fill,
      x_weight,
      y_weight

   #
   # Output object
   #
   private override object_output(oo)
      local t
      t := s || ": "
      t ||:= " pos=" || self.x || "," || self.y || " dim=" || self.w || "," || self.h ||
         " component pref width=" || pw
      t ||:= "\n\theight=" || ph || " calculated from width " || image(ph_calc_w)
      t ||:= "\n\tcell desired size=" || pw + l_inset + r_inset || "," || ph + t_inset + b_inset
      t ||:= " located to=" || \ax || "," || \ay || " size set to " || \aw || "," || \ah

      t ||:= " \n\tfill=" || image(x_fill) || "," || image(y_fill) || 
         " weight=" || x_weight || "," || y_weight ||
         " align=" || image(x_align) || "," || image(y_align) ||
         " insets=l:" || l_inset || " t:" || t_inset || " r:" || r_inset || " b:" || b_inset
      oo.object_string(self, t)
   end
end

class Grid()
   public static const 
      CONSTRAINT_SET,
      EXTRA_CELLS,
      EXTRA_BORDERS,
      EXTRA_IGNORE,
      X_TAB,
      Y_TAB,
      EOL,
      X,
      Y,
      W,
      H,
      L_INSET,
      R_INSET,
      T_INSET,
      B_INSET,
      X_FILL,
      Y_FILL,
      X_ALIGN,
      Y_ALIGN,
      X_WEIGHT,
      Y_WEIGHT

   public 
      gw,           # Grid width, height in cells
      gh, 
      p_col_w,      # Preferred column widths/row heights
      p_row_h,
      a_col_w,      # Actual column widths/row heights after extra
      a_row_h,      # space has been allocated
      a_col_x,      # Actual row x and y values
      a_row_y,
      col_weights,  # Column/row weights.
      row_weights,
      info,         # List of CellInfo for each component being laid out
      info_by_cell_height,   # info sorted by cell height
      info_by_cell_width,    # and width
      dii,          # Default inner padding, ie not at edge of grid
      doi,          # Default outer padding
      extra,        # Flag indicating how to allocate extra space
      pad_w,        # Padding to the left of the grid
      pad_h,        # Padding above the grid
      id_seq,       # For generating CellInfo string ids for debug
      add_x,        # For auto-computing cell position and sizes.
      add_y,
      add_row_h

   public static validate_int(t, key, def)
      local val
      if val := member(t, key) then
         return integer(val) | runerr("Expected integer grid constraint", val)
      else
         return \def
   end

   public static validate_real(t, key, def)
      local val
      if val := member(t, key) then
         return real(val) | runerr("Expected real-valued grid constraint", val)
      else
         return \def
   end

   public static validate_flag(t, key)
      local val
      if val := member(t, key) then
         return (val === (&yes | &no)) | runerr("Expected flag-valued grid constraint", val)
      else
         return &no
   end

   public static validate_x_align(t, key)
      local val
      if val := member(t, key) then {
         (val === (Align.L | Align.R | Align.C)) | runerr("Expected x-alignment grid constraint", val)
         return val
      }
      else
         return Align.C
   end

   public static validate_y_align(t, key)
      local val
      if val := member(t, key) then {
         (val === (Align.T | Align.B | Align.C)) | runerr("Expected y-alignment grid constraint", val)
         return val
      }
      else
         return Align.C
   end

   public create_cell(t)
      local ci
      ci := CellInfo()
      ci.s := @id_seq
      self.add_x +:= validate_int(t, X_TAB)
      if self.add_y +:= validate_int(t, Y_TAB) then
         add_row_h := 0
      ci.x := validate_int(t, X, add_x)
      ci.y := validate_int(t, Y, add_y)
      ci.w := validate_int(t, W, 1)
      ci.h := validate_int(t, H, 1)
      add_row_h <:= ci.h
      if /validate_flag(t, EOL) then {
         add_x := ci.x + ci.w
         add_y := ci.y
      } else {
         add_x := 1
         add_y +:= add_row_h
         add_row_h := 0
      }
      ci.l_inset := validate_int(t, L_INSET)
      ci.t_inset := validate_int(t, T_INSET)
      ci.r_inset := validate_int(t, R_INSET)
      ci.b_inset := validate_int(t, B_INSET)
      ci.x_fill := validate_flag(t, X_FILL)
      ci.y_fill := validate_flag(t, Y_FILL)
      ci.x_align := validate_x_align(t, X_ALIGN)
      ci.y_align := validate_y_align(t, Y_ALIGN)
      ci.x_weight := validate_real(t, X_WEIGHT, 0.0)
      ci.y_weight := validate_real(t, Y_WEIGHT, 0.0)

      put(info, ci)

      return ci
   end

   public dimension()
      local ci

      # Work out grid width/height
      gw := gh := 0
      every ci := !info do {
         gw <:= ci.x + ci.w - 1
         gh <:= ci.y + ci.h - 1
      }

      # Fill in final defaults now we have the grid dimensions.
      every ci := !info do {
         /ci.l_inset := if ci.x = 1 then self.doi else self.dii
         /ci.t_inset := if ci.y = 1 then self.doi else self.dii
         /ci.r_inset := if ci.x + ci.w = gw + 1 then self.doi else self.dii
         /ci.b_inset := if ci.y + ci.h = gh + 1 then self.doi else self.dii
      }

      info_by_cell_width := isort(info, cell_width_sort)
      info_by_cell_height := isort(info, cell_height_sort)

      compute_col_weights()
      compute_row_weights()
   end
   
   public static slice(start, span, l)
      local n
      n := 0
      every n +:= l[start to start + span - 1]
      return n
   end

   public static total(l)
      local n
      n := 0
      every n +:= !l
      return n
   end

   public static dist_by_weight(start, span, extra, l, weights, tw)
      local left, i, a, j

      left := extra
      every i := start to start + span - 1 do {
         a := integer(extra * weights[i] / tw)
         l[i] +:= a
         left -:= a
         if weights[i] > 0 then
            j := i
      }
      l[j] +:= left
   end

   public static dist_equally(start, span, extra, l)
      local left, i, a
      if extra = 0 then
         fail
      a := extra / span
      left := extra
      every i := start to start + span - 1 do {
         l[i] +:= a
         left -:= a
      }
      l[i] +:= left
   end

   public static allocate_space(start, span, extra, l, weights)
      local tw
      tw := slice(start, span, weights)
      if tw = 0 then
         dist_equally(start, span, extra, l)
      else
         dist_by_weight(start, span, extra, l, weights, tw)
   end

   public compute_col_weights()
      local i, need
      col_weights := list(gw, 0.0)
      every i := !info_by_cell_width do {
         need := i.x_weight - slice(i.x, i.w, col_weights)
         if need > 0.0 then
            dist_equally(i.x, i.w, need, col_weights)
      }
   end

   public compute_preferred_col_widths()
      local i, need
      p_col_w := list(gw, 0)
      every i := !info_by_cell_width do {
         need := i.pw + i.l_inset + i.r_inset - slice(i.x, i.w, p_col_w)
         if need > 0 then
            allocate_space(i.x, i.w, need, p_col_w, col_weights)
      }
   end

   public compute_row_weights()
      local i, need
      row_weights := list(gh, 0.0)
      every i := !info_by_cell_height do {
         need := i.y_weight - slice(i.y, i.h, row_weights)
         if need > 0.0 then
            dist_equally(i.y, i.h, need, row_weights)
      }
   end

   public compute_preferred_row_heights()
      local i, need
      p_row_h := list(gh, 0)
      every i := !info_by_cell_height do {
         need := i.ph + i.t_inset + i.b_inset - slice(i.y, i.h, p_row_h)
         if need > 0 then 
            allocate_space(i.y, i.h, need, p_row_h, row_weights)
      }
   end

   private static cell_width_sort(i)
      return i.w
   end

   private static cell_height_sort(i)
      return i.h
   end

   public print_layout()
      local x, y, t, i, s, l, u

      write("Grid dimensions:", gw, ",",gh)
      l := *info[-1].s
      t := table()
      u := repl(" ", l)
      every t[(1 to gw)||","||(1 to gh)] := u

      every i := !info do
         every t[(i.x to i.x + i.w - 1)||","||(i.y to i.y + i.h - 1)] := right(i.s, l)

      u := "+" || repl("-", l)
      write(s := repl(u, gw) || "+")
      every y := 1 to gh do {
         every x := 1 to gw do
            writes("|", t[x||","||y])
         write("|\n",s)
      }

      write("----- Items ------")
      every write(to_string(!info))

      write("----- Data -------")
      write("p_col_w=", to_string(p_col_w))
      if \self.a_col_w then {
         write("a_col_w=", to_string(a_col_w))
         write("\textra width in a_col_w:", total(a_col_w)-total(p_col_w))
         write("\tleft pad border:", self.pad_w)
      }
      write("p_row_h=", to_string(p_row_h))
      if \self.a_row_h then {
         write("a_row_h=", to_string(a_row_h))
         write("\textra height in a_row_h:", total(a_row_h)-total(p_row_h))
         write("\ttop pad border:", self.pad_h)
      }

      write("col_weights=", to_string(col_weights))
      write("row_weights=", to_string(row_weights))
      write("\nPreferred size gives:", get_preferred_width(),",",get_preferred_height())
   end

   public get_preferred_width()
      return total(p_col_w)
   end

   public get_preferred_height()
      return total(p_row_h)
   end

   public compute_actual_col_widths(w1)
      local i, ew, tw

      a_col_w := copy(p_col_w)

      case self.extra of {
         EXTRA_CELLS : {
            # Extra width
            ew := w1 - total(p_col_w)
            tw := total(col_weights)
            if tw = 0 then
               # All weights zero - it goes on the border
               self.pad_w := ew / 2
            else {
               # Some weights, so no border and allocate by weight.
               self.pad_w := 0
               dist_by_weight(1, gw, ew, a_col_w, col_weights, tw)
            }
         }
         EXTRA_BORDERS : 
            self.pad_w := (w1 - total(p_col_w)) / 2
         EXTRA_IGNORE : 
            self.pad_w := 0
         default : runerr("Bad value for extra", self.extra)
      }

      every i := !info do {
         i.vw := slice(i.x, i.w, a_col_w) - i.l_inset - i.r_inset
         i.aw := if \i.x_fill then i.vw else i.pw
      }
   end

   public compute_actual_col_positions(x1)
      local i, x, w

      a_col_x := []
      x := x1 + self.pad_w
      every w := !a_col_w do {
         put(a_col_x, x)
         x +:= w
      }

      every i := !info do {
         x := a_col_x[i.x] + i.l_inset
         i.ax := case i.x_align of {
            Align.L : x
            Align.R : x + i.vw - i.aw
            Align.C : x + (i.vw - i.aw) / 2
         }
      }
   end

   public compute_actual_row_heights(h1)
      local i, eh, tw

      a_row_h := copy(p_row_h)

      case self.extra of {
         EXTRA_CELLS : {
            # Extra height.
            eh := h1 - total(p_row_h)
            tw := total(row_weights)
            if tw = 0 then
               self.pad_h := eh / 2
            else {
               self.pad_h := 0
               dist_by_weight(1, gh, eh, a_row_h, row_weights, tw)
            }
         }
         EXTRA_BORDERS : 
            self.pad_h := (h1 - total(p_row_h)) / 2
         EXTRA_IGNORE : 
            self.pad_h := 0
         default : runerr("Bad value for extra", self.extra)
      }

      every i := !info do {
         i.vh := slice(i.y, i.h, a_row_h) - i.t_inset - i.b_inset
         i.ah := if \i.y_fill then i.vh else i.ph
      }
   end

   public compute_actual_row_positions(y1)
      local i, y, h

      a_row_y := []
      y := y1 + self.pad_h
      every h := !a_row_h do {
         put(a_row_y, y)
         y +:= h
      }

      every i := !info do {
         y := a_row_y[i.y] + i.t_inset
         i.ay := case i.y_align of {
            Align.T : y
            Align.B : y + i.vh - i.ah
            Align.C : y + (i.vh - i.ah) / 2
         }
      }
   end

   #
   # Set the default inner inset
   #
   public set_dii(n)
      self.dii := n
      link
   end

   #
   # Set the default outer inset
   #
   public set_doi(n)
      self.doi := n
      link
   end

   #
   # Configure where to allocate any extra space.
   #
   # :Parameters :
   # :  `s` - one of `EXTRA_CELLS`, `EXTRA_BORDERS`, `EXTRA_IGNORE`
   #
   public set_extra(s)
      self.extra := s
      link
   end

   private static init()
      EXTRA_CELLS := "cells"
      EXTRA_BORDERS := "borders"
      EXTRA_IGNORE := "ignore"
      X_TAB := "x_tab"
      Y_TAB := "y_tab"
      EOL := "eol"
      X := "x"
      Y := "y"
      W := "w"
      H := "h"
      L_INSET := "l_inset"
      R_INSET := "r_inset"
      T_INSET := "t_inset"
      B_INSET := "b_inset" 
      X_FILL := "x_fill"
      Y_FILL := "y_fill"
      X_ALIGN := "x_align"
      Y_ALIGN := "y_align"
      X_WEIGHT := "x_weight"
      Y_WEIGHT := "y_weight"
      CONSTRAINT_SET := set(X_TAB, Y_TAB, EOL, X, Y, W, H, L_INSET, R_INSET, T_INSET, B_INSET, 
                            X_FILL, Y_FILL, X_ALIGN, Y_ALIGN, X_WEIGHT, Y_WEIGHT)
   end

   public static is_constraint(s)
      succeed member(CONSTRAINT_SET, s)
   end

   public new()
      self.dii := self.doi := 0
      self.extra := EXTRA_CELLS
      self.id_seq := create alphaseq(seq())
      self.add_y := self.add_x := 1
      self.add_row_h := 0
      self.info := []
      return
   end
end
