package gui

class Frame(Component)
   public
      held_flag,
      bar,
      content,
      drag_x_offset,
      drag_y_offset

   public static const
      RSW

   private static init()
      RSW := env_scale("OI_FRAME_RSW") | scale(12)
   end

   public override can_drag(e)
      return bar.can_drag(e)
   end

   public setup()
      self.bar := create_bar()
      self.add(bar)
   end

   public create_bar()
      return FrameBar()
   end

   public set_title(s)
      bar.set_title(s)
      return self
   end

   public in_resize_corner(e)
      return in_region(e) & 
         (((self.x + self.w - RSW <= e.x < self.x + self.w) & 
          (self.y + self.h - border.get_b_inset() <= e.y < self.y + self.h)) | 
          ((self.x + self.w - border.get_r_inset() <= e.x < self.x + self.w) & 
           (self.y + self.h - RSW <= e.y < self.y + self.h)))
   end

   public override display()
      border.draw_rect(self.cbwin, self)
      self.display_children()
   end

   public set_content(c)
      remove(\self.content)
      self.content := c
      add(c)
      return self
   end

   public override initially()
      \self.content | runerr("No content component specified")
      Component.initially()
   end

   public override layout()
      bar.x := self.x + border.get_l_inset()
      bar.y := self.y + border.get_t_inset()
      bar.w := self.w - border.get_total_width()
      bar.h := bar.get_preferred_height(bar.w)
      content.x := bar.x
      content.y := bar.y + bar.h
      content.w := bar.w
      content.h := self.h - border.get_total_height() - bar.h
      every (!children).layout()
   end

   public override get_default_width()
      return border.get_total_width() + max(bar.get_preferred_width(), content.get_preferred_width())
   end

   public override get_default_height(dw)
      dw -:= border.get_total_width()
      return border.get_total_height() + bar.get_preferred_height(dw) + content.get_preferred_height(dw)
   end

   public override get_pointer(e)
      if in_resize_corner(e) then
         return "bottom right corner"
   end

   public handle_lpress(e)
      if in_region(e) then
         to_top()
      if can_drag(e) then {
         self.held_flag := 1
         self.drag_x_offset := e.x - self.x
         self.drag_y_offset := e.y - self.y
      } else if in_resize_corner(e) then {
         self.held_flag := 2
         self.drag_x_offset := e.x - (self.x + self.w)
         self.drag_y_offset := e.y - (self.y + self.h)
      }
   end

   public handle_ldrag(e)
      \self.held_flag | fail
      self.invalidate()
      case self.held_flag of {
         1: {
            shift(e.x - drag_x_offset - self.x,
                  e.y - drag_y_offset - self.y)
            self.x_spec := self.x - parent.x
            self.y_spec := self.y - parent.y
         }
         2: {
            self.w_spec := e.x - drag_x_offset - self.x
            self.h_spec := e.y - drag_y_offset - self.y
            self.resize()
         }
      }
      self.invalidate()
   end

   public to_top()
      if self ~=== parent.z_children[-1] then {
         self.set_z(parent.get_top_z())
         parent.compute_z_order()
         self.invalidate()
      }
   end

   public to_bottom()
      if self ~=== parent.z_children[1] then {
         self.set_z(parent.get_bottom_z())
         parent.compute_z_order()
         self.invalidate()
      }
   end

   public close()
      self.invalidate()
      self.parent.remove(self)
      return self
   end

   public handle_lrelease(e)
      self.held_flag := &null
   end

   public override new()
      Component.new()
      set_border(RaisedBorder())
      setup()
      connect(self.handle_lpress, Event.MOUSE_LEFT_PRESS)
      connect(self.handle_ldrag, Event.MOUSE_LEFT_DRAG)
      connect(self.handle_lrelease, Event.MOUSE_LEFT_RELEASE)
      return
   end
end


class FrameBar(Component)
   public lab

   private static init()
      ImageCache.copy_key_with_resize("frame.WINDOW_CLOSE", "gui.WINDOW_CLOSE_16")
   end

   public on_close()
      self.parent.close()
   end

   public set_title(s)
      lab.set_label(s)
      return self
   end

   public override can_drag(e)
      return lab.in_region(e)
   end

   public setup()
      local b
      set_layout(GridLayout().set_doi(0).set_dii(0))
      lab := Label().
         set_constraint("x_weight", 1).
         set_constraint("x_fill", &yes).
         set_constraint("l_inset", scale(3)).
         set_label("Frame")
      add(lab)
      b := IconButton().
         set_accepts_focus(&no).
         set_constraint("r_inset", scale(3)).
         set_tooltip("Close")
      b.set_border(NullBorder())
      b.set_paint(ImagePaint().set_cache("frame.WINDOW_CLOSE"))
      b.connect(self.on_close, Event.ACTION)
      self.add(b)
   end

   public override new()
      Component.new()
      setup()
      return
   end
end
