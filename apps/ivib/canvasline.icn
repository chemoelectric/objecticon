#
# $Id$
#
# This file is in the public domain.
#
# Author: Robert Parlett (parlett@dial.pipex.com)
#

import gui


############################################################################
#
# Class for representing a Line on the canvas.
#
class CanvasLine(CanvasComponent,Line)
   #
   # Overrides CanvasComponent.min_height()
   #
   public override min_height()
      return scale(3)
   end

   #
   # Overrides CanvasComponent.min_width()
   #
   public override min_width()
      return scale(3)
   end

   #
   # Generate code into Code object c.
   #
   public override gen_code(c)
      CanvasComponent.gen_code(c)
      if /self.is_horizontal_flag then
         c.chain("set_is_horizontal(&no)")
   end

   #
   # Duplicate object.
   #
   public override dup()
      local c
      c := CanvasLine()
      CanvasComponent.dup_fields(c)
      c.is_horizontal_flag := self.is_horizontal_flag
      return c
   end

   #
   # Return the I/O template.
   #
   public override get_template()
      suspend CanvasComponent.get_template() | 
         ["Is horizontal flag", "is_horizontal_flag"]
   end

   #
   # Open the configuration dialog box.
   #
   public override dialog()
      return CanvasLineDialog(self)
   end

   public override allows_null_width()
      return /self.is_horizontal_flag
   end

   public override allows_null_height()
      return \self.is_horizontal_flag
   end

   public override new()
      Line.new()
      CanvasComponent.new()
      self.class_name := "Line"
      self.import_name := "gui"
      self.name := "line"
      return
   end
end

