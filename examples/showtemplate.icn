import ipl.webscraper, xml, lang, io, ipl.options

procedure main(a)
   local ws, p, s, d, k, v, opts

   opts := options(a, [Opt("u",, "Use ucs"),
                       Opt("e",, "Just show encoded template"),
                       Opt("d",, "Turn debug flag on")],
                       "Usage: showtemplate [OPTIONS] template-file [src-file]\n_
                        Utility to test an ipl.WebScraper template file")
   if *a = 0 then
      stop("template file argument required")

   s := Files.file_to_string(a[1]) | stop(&why)
   if \opts["u"] then
      s := ucs(s) | stop("Not ucs")

   ws := WebScraper(s) | stop(&why)
   if \opts["e"] then {
      writes(encode(ws))
      exit(0)
   }

   if \opts["d"] then
      ws.set_debug(&yes)

   write(to_string(ws, 3))

   if *a > 1 then {
      s := Files.file_to_string(a[2]) | stop(&why)
      if \opts["u"] then
         s := ucs(s) | stop("Not ucs")
      p := HtmlParser()
      d := p.parse(s) | stop(&why)
      every k := ws.get_ids() do {
         write("\nTesting path ", k)
         if v := ws.lookup(d, k) then
            write("Success: ", image(v))
         else
            write("Failed: ", &why)
      }
   }
end

