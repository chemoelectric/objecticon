############################################################################
#
#	File:     io.icn
#
#	Subject:  Procedures for input and output
#
#	Author:   Ralph E. Griswold
#
#	Date:     October 6, 2000
#
############################################################################
#
#   This file is in the public domain.
#
############################################################################
#
#  Contributors:  Paul Abrahams, Bob Alexander, Will Evans, David A. Gamey,
#		  Richard L. Goerwitz, Will Menagarini, Charles Sharstis,
#		  and Gregg Townsend.
#
############################################################################
#
#  They provide facilities for handling input, output, and files.
#
#  There are other modules in the Icon program library that deal with
#  input and output.  They are not included here because they conflict
#  with procedures here or each other.
#
############################################################################
#
#  Links:  random, strings
#
############################################################################
#
#  Reading and writing files:
#
#	filetext(f)	reads the lines of f into a list and returns that
#			list
#
#	readline(file)	assembles backslash-continued lines from the specified
#			file into a single line.  If the last line in a file
#			ends in a backslash, that character is included in the
#			last line read.
#
#	splitline(file, line, limit)
#			splits line into pieces at first blank after
#			the limit, appending a backslash to identify split
#			lines (if a line ends in a backslash already, that's
#			too bad). The pieces are written to the	specified file.
#
############################################################################
#
#  Buffered input and output:
#
#	 ClearOut()	remove contents of output buffer without writing
#	 Flush()	flush output buffer
#	 GetBack()	get back line writen
#	 LookAhead()	look ahead at next line
#	 PutBack(s)	put back a line
#	 Read()		read a line
#	 ReadAhead(n)	read ahead n lines
#	 Write(s)	write a line
#
############################################################################

package ipl.io

import
   io(Files, close, open, read,
      write)

global buffer_in, buffer_out, Eof

# remove contents of output buffer
procedure ClearOut()			

   buffer_out := []

end

# flush output buffer
procedure Flush()			

   while write(pull(buffer_out))

   return

end

# get back line written
procedure GetBack()			

   return get(buffer_out)

end

# look at next line
procedure LookAhead()			

   return buffer_in[1]

end

# put back line read
procedure PutBack(s)			

   push(buffer_in,s)

   return

end

# read a line in buffered mode
procedure Read()			

   initial{
      buffer_in := []
      }

   if *buffer_in = 0 then
      put(buffer_in,read()) | (Eof := 1)
   return get(buffer_in)

end

# read ahead
procedure ReadAhead(n)			

   while *buffer_in < n do
      unless put(buffer_in,read()) then {
         Eof := 1
         fail
         }

   return

end

# write in buffered mode
procedure Write(s)			

   initial buffer_out := []

   push(buffer_out,s)
 
   return s

end

# open file on DPATH
procedure dopen(s)
   return open(Files.path_find(s, "DPATH"))
end

# read file into list
procedure filetext(f)			
   local input, text

   input := open(f) | fail

   text := []

   while put(text,read(input))

   close(input)

   return text

end

# assemble backslash-continued lines
procedure readline(file)		
   local line

   line := read(file) | fail

   while line[-1] == "\\" do
      line := line[1:-1] || read(file) | break

   return line

end

# split line into pieces
procedure splitline(file,line,limit)	
   local i, j

   if *line = 0 then {			# don't fail to write empty line
      write(file,line)
      return
      }
   while *line > limit do {
      line ?:= {
         i := j := 0
         every i := find(" ") do {	# find a point to split
            if i >= limit then break
            else j := i
            }
         if j = 0 then {		# can't split
            write(file,line)
            return
            }
         write(file,tab(j + 1),"\\")
         tab(0)				# update line
         }
      }
   if *line > 0 then write(file,line)	# the rest

   return

end
