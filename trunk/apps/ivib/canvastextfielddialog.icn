#
# $Id$
#
# This file is in the public domain.
#
# Author: Robert Parlett (parlett@dial.pipex.com)
#

import gui


############################################################################
#
#
#
class CanvasTextFieldDialog(CanvasComponentDialog)
   public 
      contents, 
      filter,
      is_editable_cb,
      move_on_rpress_cb,
      ucs_cb

   public set_vals()
      cast(self,CanvasComponentDialog).set_vals()
      c.set_contents(contents.get_contents())
      c.filter_str := filter.get_contents()
      c.is_editable_flag := is_editable_cb.get_status()
      c.move_on_rpress_flag := move_on_rpress_cb.get_status()
   end

   private on_ucs_cb()
      if ucs_cb.is_checked() then {
         contents.set_contents(u"")
         filter.set_contents(u"")
      } else {
         contents.set_contents("")
         filter.set_contents("")
      }
   end

   public setup()
      local p, l

      cast(self,CanvasComponentDialog).setup()

      p := TabItem()
      p.set_layout(GridLayout("extra=cells"))
      p.set_label("General")
      add_tab_at_front(p)

      l := Label()
      l.set_label("Initial contents : ")
      p.add(l)
      
      contents := TextField("c:eol")
      contents.set_contents(c.contents)
      p.add(contents)

      l := Label()
      l.set_label("Filter : ")
      p.add(l)
      
      filter := TextField("c:eol")
      filter.set_contents(c.filter_str)
      p.add(filter)

      is_editable_cb := CheckBox("label=Is editable", "c:eol", "c:w=2")
      if \c.is_editable_flag then
         is_editable_cb.set_is_checked(&yes)
      p.add(is_editable_cb)

      move_on_rpress_cb := CheckBox("label=Move cursor on &rpress", "c:eol", "c:w=2")
      if \c.move_on_rpress_flag then
         move_on_rpress_cb.set_is_checked(&yes)
      p.add(move_on_rpress_cb)

      ucs_cb := CheckBox("label=Ucs content", "c:w=2")
      if type(c.contents) == "ucs" then
         ucs_cb.set_is_checked(&yes)
      ucs_cb.connect(self.on_ucs_cb, Event.ACTION)
      p.add(ucs_cb)
   end
end

