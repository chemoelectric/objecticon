#
# $Id$
#
# This file is in the public domain.
#
# Author: Robert Parlett (parlett@dial.pipex.com)
#

import gui

      
############################################################################
#
# Class for representing a ScrollBar on the canvas.Class for a ScrollBar on the canvas.
#
class CanvasScrollBar(CanvasComponent,ScrollBar)
   #
   # Generate code into Code object c.
   #
   public override gen_code(c)
      CanvasComponent.gen_code(c)
      c.chain("set_value(" || self.value || ")")
      c.chain("set_increment_size(" || self.increment_size || ")")
      if \self.is_range_flag then
         c.chain("set_range(" || self.lo || ", " || self.hi || ")")
      else {
         c.chain("set_total_size(" || self.total_size || ")")
         c.chain("set_page_size(" || self.page_size || ")")
      }
      if \self.is_horizontal_flag then
         c.chain("set_is_horizontal(&yes)")
   end

   #
   # Duplicate object.
   #
   public override dup()
      local c
      c := CanvasScrollBar()
      CanvasComponent.dup_fields(c)
      c.increment_size := self.increment_size
      c.value := self.value
      c.is_range_flag := self.is_range_flag
      c.is_horizontal_flag := self.is_horizontal_flag
      c.lo := self.lo
      c.hi := self.hi
      c.page_size := self.page_size
      c.total_size := self.total_size
      return c
   end

   #
   # Return the I/O template.
   #
   public override get_template()
      suspend CanvasComponent.get_template() |
          ["Increment Size", "increment_size"] |
          ["Value", "value"] |
          ["Is Range Flag", "is_range_flag"] |
          ["Is Horizontal Flag", "is_horizontal_flag"] |
          ["Lo", "lo"] |
          ["Hi", "hi"] |
          ["Page Size", "page_size"] |
          ["Total Size", "total_size"]
   end
                                                      
   #
   # Open the configuration dialog box.
   #
   public override dialog()
      return CanvasScrollBarDialog(self)
   end

   public override allows_null_width()
      return /self.is_horizontal_flag
   end

   public override allows_null_height()
      return \self.is_horizontal_flag
   end

   public override new()
      ScrollBar.new()
      CanvasComponent.new()
      self.class_name := "ScrollBar"
      self.import_name := "gui"
      self.name := "scroll_bar"
      return
   end
end

