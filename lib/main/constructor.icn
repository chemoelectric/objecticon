package lang

#
# This class contains various helpful methods to get information about
# a record constructor at runtime.
#
final abstract class Constructor()
   # Return the name of the constructor
   #
   public static native get_name(c)

   # Return the constructor of the given record
   #
   public static native get_constructor(c)

   # Return the program this constructor resides in.  The program's &main
   # co-expression is returned.
   #
   public static native get_program(c)

   # Return the name of the package in which this constructor resides; fails
   # if the constructor is not in a package.
   #
   public static get_package(c)
      return extract_package(get_name(c))
   end

   # Return the source location of the constructor
   # @return a {Location} instance
   #
   public static get_location(c)
      return Prog.get_global_location(get_name(c), get_program(c))
   end

   # Generate the field names of the constructor {c}.
   #
   public static get_field_names(c)
      suspend get_field_name(c, 1 to get_n_fields(c))
   end

   # Return the number of fields in the constructor {c}.
   #
   public static native get_n_fields(c)

   # Return the field index number of the given field in constructor{c}.
   # @param c the constructor
   # @param field either a field name or number
   # @return an integer, being the index
   # @fail if the field is unknown
   #
   public static native get_field_index(c, field)

   # Return the field name of the given field in constructor{c}.
   # @param c the constructor
   # @param field either a field name or number
   # @return the field name
   # @fail if the field is unknown
   #
   public static native get_field_name(c, field)

   # Return the source location of the given field in constructor{c}.
   # @param c the constructor
   # @param field either a field name or number
   # @return a {Location} instance
   # @fail if the field is unknown
   #
   public static get_field_location(c, field)
      return Location!get_field_location_impl(c, field)
   end

   private static native get_field_location_impl(c, field)

   # Lookup a constructor by name
   # 
   # @param s a constructor name
   # @param ce a coexpression, representing a loaded program, defaults
   # @ to the main program
   # @return a constructor
   # @fail if the constructor is unknown
   #
   public static for_name(s, ce)
      local t
      s := string(s) | runerr(103, s)
      if not(integer(s)) &
         t := Prog.get_named_global(s, ce) &
         type(t) == "constructor" then
         return t
   end
end
