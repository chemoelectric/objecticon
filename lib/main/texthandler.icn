#
# $Id$
#

package mail

import util

class TextHandler(TypeHandler)
   public can_handle(ct)
      return map(ct.get_type()) == "text"
   end
   
   public convert_to_object(m, data)
      local res

      #
      # Convert from canonical form (CRLF) to local form (\n)
      #
      res := ""
      data ? repeat {
         res ||:= tab(find("\r\n") | 0)
         move(2) | break
         res ||:= "\n"
      }

      if map(m.get_content_type().get_parameter("charset")) == "utf-8" then
         res := ucs(res) | return error("TextHandler: raw data not in UTF-8 format")

      return res
   end

   public convert_from_object(m, obj)
      local res

      obj := string(obj) | return error("TextHandler: Couldn't convert object to string: " || image(obj))

      #
      # Convert from local form to canonical form (CRLF)
      #
      res := ""
      obj ? repeat {
         res ||:= tab(upto('\n') | 0)
         move(1) | break
         res ||:= "\r\n"
      }
      return res
   end
end
