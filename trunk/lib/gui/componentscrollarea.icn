#
# $Id$
#
# This file is in the public domain.
#
# Author: Robert Parlett (parlett@dial.pipex.com)
#

package gui

class ComponentScrollAreaView(Component)
   public
      inner, 
      inner_w,
      inner_h

   public initially()
      \self.inner | runerr("no inner component specified")
      cast(self,Component).initially()
      # Set here so we can call set_ideal_size on the parent, in the dialog's 
      # initially method.
      inner_w := inner.get_preferred_width()
      inner_h := inner.get_preferred_height()
   end

   public layout()
      inner.x := self.x - parent.get_area_x()
      inner.y := self.y - parent.get_area_y()
      inner.w := inner_w
      inner.h := inner_h
      inner.layout()
   end

   public size_inner()
      inner_w := max(inner.get_preferred_width(), parent.predict_view_width(parent.w, parent.vsb))
      inner_h := max(inner.get_preferred_height(), parent.predict_view_height(parent.h, parent.hsb))
      parent.invalidate_subject_width()
      parent.invalidate_subject_height()
   end

   public set_inner(inner)
      remove(\self.inner)
      self.inner := inner
      add(inner)
      return self
   end
end


#
# This class allows an arbitrary Component to be placed within 
# the ScrollArea.  The component is set with the {set_inner()}
# method.
#
class ComponentScrollArea(ScrollArea)
   public set_inner(inner)
      view.set_inner(inner)
      return self
   end

   public layout()
      local i, t1, t2
      every i := 0 to 3 do {
         t1 := self.hsb
         t2 := self.vsb
         view.size_inner()
         cast(self,ScrollArea).layout()
         if i > 0 & ((self.hsb === t1 & self.vsb === t2) | (\self.hsb & \self.vsb)) then
            break
      }
   end

   public calculate_subject_width()
      return view.inner_w 
   end

   public calculate_subject_height()
      return view.inner_h
   end

   public get_default_vertical_increment()
      return 10
   end

   public get_default_horizontal_increment()
      return 10
   end

   public on_vsb(ev)
      self.refresh()
      view.layout()
   end

   public on_hsb(ev)
      self.refresh()
      view.layout()
   end

   public create_view()
      return ComponentScrollAreaView()
   end
end

