package gui

import 
   ipl.sort,
   ipl.filegui,
   io,
   util,
   lang

package class DirEditList(EditList)
   protected override open_popup(ev)
      set_selection_only()
      synch_set_selection_list(parent_dialog.get_favourites_directories())
      EditList.open_popup(ev)
   end

   public override create_popup()
      return ItemPaintList().
         set_item_paint(StringDirItemPaint()).
         set_item_border(EmptyBorder().
                            set_insets(Gui.TEXT_INSET, Gui.TEXT_INSET, 0, 0).
                            set_x_align(Align.L))
   end

   public override create_view()
      return EditListField().
         set_directories_only(&yes)
   end
end

package class FileTreeWithTooltip(ItemPaintTree)
   public override get_tooltip(e)
      local n, s
      if n := contents[get_line_under_pointer(e)] then {
         s := item_paint.get_size(view.cbwin, n.label)
         if n.depth * col_w + s.w + item_border.get_total_width() > view.w + get_area_x() then
            return n.label.name
      }
   end
end

#
#
# Directory dialog class.  This class provides a standard directory selection dialog.
#
# Example :-
# ~
#   d := DirectoryDialog()
#   d.show_modal()
#   write(d.get_result() | "cancelled")
# ~
class DirectoryDialog(Dialog)
   public
      show_hidden_menu_item,
      tools_menu,
      tools_menu_button,
      home_button, 
      up_button, 
      cancel_button, 
      okay_button,
      init_dirs,         # Directory choices
      res,               # Resulting file path          
      selected_path,     # FilePath of tree selection
      message_label, 
      tree,
      dir,
      existing_file_mode,
      warned

   public static const
      WARN, IGNORE, REQUIRE, DISALLOW
   
   private static init()
      WARN := "warn"
      IGNORE := "ignore"
      REQUIRE := "require"
      DISALLOW := "disallow"
   end

   public set_existing_file_mode(s)
      existing_file_mode := s
      link
   end

   # Return a `ListEntryWithIcon` for the given file f.
   #
   public static get_entry(f)
      local lst, st
      lst := Files.lstat(f) | fail
      st := if lst.mode_str[1] == "l" then
         Files.stat(f) | fail
      else
         lst
      return ListEntryWithIcon(f, lst, st)
   end

   public override component_setup()
      local root_node, d, l
      #
      # Defaults and extra dirs.
      #
      /init_dirs := []
      root_node := TreeNode()
      every d := Files.get_roots() do
         if l := get_entry(ucs(d)) then
            root_node.add(TreeNode().
                            set_label(l).
                            set_always_expandable(&yes))
      tree.set_root_node(root_node)
      res := &null
      dir.set_use_wheel(&no)
   end

   public override init_dialog()
      res := &null
      # Clear any cached directory list in the suggest field
      dir.view.refresh()
      dir.grab_focus()
      win.set_min_size(win.get_width(), win.get_height())
      # Do this here as we need the tree to be setup so that we
      # can scroll to the selected dir.
      goto_dir(gen_favourites_directories())
   end

   #
   # Given a TreeNode n, load its children with the sub-directories.
   #
   public load_subtree(n)
      local s, e, l, i
      s := get_full_path(n).str()
      if l := list_directory(s) then {
         i := 1
         every e := !l do {
            while Text.consistent_compare(n.children[i].label.name, e.name) < 0 do
               n.remove_index(i)
            if Text.consistent_compare(n.children[i].label.name, e.name) = 0 then
               n.children[i].set_label(e)
            else
               n.add(TreeNode().set_label(e).set_always_expandable(&yes), i)
            i +:= 1
         }
         while n.remove_index(i)
         link
      } else {
         n.clear_children()
         fail
      }
   end

   public list_directory(s)
      local e, dir_list, l, t
      l := Files.ucs_list(s, ListEntryWithIcon) | fail
      dir_list := []
      every e := !l do {
         t := e.name
         if e.stat.mode_str[1] == "d" &
            not(Files.is_relative_dir(t)) &
            (show_hidden_menu_item.is_checked() | 
               not(Files.is_hidden(t))) then
            put(dir_list, e)
      }
      return qsort(dir_list, Files.name_cmp)
   end

   #
   # Given a TreeNode n, return a `FilePath` path it represents by traversing
   # up the tree structure to the root and converting to a `FilePath`.  Each
   # element in the path is a ucs string, so the ucs() method on the path will
   # always succeed.
   #
   public get_full_path(n)
      local l, t
      l := []
      repeat {
         push(l, n.get_label().name)
         n := n.get_parent()
         if n === tree.get_root_node() then
            break
      }
      # A UNC needs special handling, since the first tree node
      # must match to a three-element FilePath.
      return if (t := FilePath(l[1])).is_unc() then
         t.append(l[2:0])
      else
         FilePath(l)
   end

   public on_tree_expansion()
      local n
      n := tree.get_last_expanded()
      #
      # Check whether it was an expansion or a contraction.  If
      # an expansion, load the subtree and refresh the tree.
      #
      if n.is_expanded() then {
         if load_subtree(n) then
            check_existing()
         else
            msg(&why)
         tree.tree_structure_changed()
      }
   end

   public on_tree_selection(ev)
      # This may fail if the user clears the selection; in this case
      # do nothing and leave selected_path as is.
      selected_path := get_full_path(tree.object_first_selection()) | fail
      dir.set_contents(selected_path.ucs())
      dir.view.set_cwd(selected_path)
      check_existing()
   end

   public on_dir_action(ev)
      goto_dir(dir.get_contents())
   end

   public on_dir_content_changed(ev)
      if dir.was_applied(ev) | dir.view.was_applied(ev) then
         goto_dir(dir.get_contents())
      else
         check_existing()
   end

   #
   # Get the result, (will fail if cancel was pressed).
   #
   public get_result()
      return (\self.res).str()
   end

   #
   # Get the result, as a `FilePath` (will fail if cancel was pressed).
   #
   public get_result_path()
      return \self.res
   end

   #
   # Set the initial directory.
   #
   public set_directory(s)
      self.init_dirs := [ need_text(s) ]
      link
   end

   #
   # Set the initial directories as a list; the first is the one
   # initially shown.
   #
   public set_directories(l)
      self.init_dirs := need_list(l)
      link
   end

   #
   # Keep the directory last selected by this dialog.
   #
   public keep_directory()
      return set_directory(get_dir_path().str())
   end

   public get_dir_path()
      return FilePath(dir.get_contents()).absolute(selected_path).canonical()
   end

   public goto_dir(d)
      local n, s, p, err, i, root, v, l
      p := need_FilePath(d).absolute(selected_path).canonical().ucs_path() | fail
      n := root := tree.get_root_node()

      # For a UNC file, the first three elements make up the
      # top-level tree node.
      if p.is_unc() &
         v := p.slice(1, 4).ucs()
      then {
         unless n := find_child(root, v) then {
            if l := get_entry(Files.standard_case(v)) then {
               n := TreeNode().
                  set_label(l).
                  set_always_expandable(&yes)
               root.add(n)
               if load_subtree(n) then
                  n.set_expanded(&yes)
               else
                  err := &why
            }
            else
               err := &why
         }
         if /err then {
            every s := p.slice(4).gen() do {
               n := find_child(n, s) | break
               unless load_subtree(n) then {
                  err := &why
                  break
               }
               n.set_expanded(&yes)
            }
         }
      } else {
         every s := p.gen() do {
            n := find_child(n, s) | break
            unless load_subtree(n) then {
               err := &why
               break
            }
            n.set_expanded(&yes)
         }
      }

      # n may still be at the root if (on Win32) we input an invalid
      # or non-existent path.
      if n === root then
         n := root.children[1]
      else
         tree.tree_structure_changed()

      i := tree.find_line(n)
      tree.
         set_selections([i]).
         set_cursor(i).
         center_line(i)

      selected_path := get_full_path(n)
      dir.set_contents(p.ucs())
      dir.view.set_cwd(selected_path)
      if \err then
         msg(err)
      else if Files.is_directory(p.str()) then
         check_existing()
      else
         msg("Not a directory: " || p.ucs())
      link
   end

   public find_child(n, s)
      local n1
      s := Files.standard_case(s)
      every n1 := !n.children do {
         if Files.standard_case(n1.label.name) == s then
            return n1
      }
   end

   public check_existing()
      local f
      msg()
      warned := &null
      f := get_dir_path().str()
      if existing_file_mode === (WARN | DISALLOW) & Files.is_directory(f) & Files.access(f) then {
         warned := &yes
         msg("Existing directory")
      }
   end

   public on_okay_button()
      local f, fs
      f := get_dir_path()
      fs := f.str()
      if Files.access(fs) & not Files.is_directory(fs) then {
         msg("Selected file is not a directory")
         fail
      }
      case existing_file_mode of {
         REQUIRE: {
            unless Files.access(fs) then {
               msg("Selected directory doesn't exist")
               fail
            }
         }
         DISALLOW: {
            if Files.access(fs) then {
               msg("Selected directory already exists")
               fail
            }
         }
      }
      self.res := f
      self.dispose()
   end

   public on_cancel_button()
      self.dispose()
   end

   public set_show_hidden_files(s)
      show_hidden_menu_item.set_is_checked(s)
      on_show_hidden_menu_item()
      link
   end

   public on_show_hidden_menu_item(ev)
      dir.view.set_show_hidden_files(show_hidden_menu_item.get_status())
      if is_live() then
         on_refresh_menu_item(ev)
   end

   public on_up_button(ev)
      if selected_path.is_prefix() then
         msg("At root")
      else
         goto_dir(selected_path.parent())
   end

   public on_home_button(ev)
      goto_dir(Files.get_home())
   end

   public refresh_node(n)
      if n.is_expanded() then {
         load_subtree(n)
         every refresh_node(!n.children)
      }
   end

   public on_refresh_menu_item(ev)
      every refresh_node(!tree.get_root_node().children)
      tree.tree_structure_changed()
   end

   public on_new_dir_menu_item(ev)
      local d
      d := NewDirectoryDialog(selected_path.ucs()).
         show_modal(self)
      goto_dir(\d.result)
   end

   public msg(s)
      message_label.set(s)
   end

   public override new()
      Dialog.new()
      self.setup()
      tools_menu.set_border_mid(NullBorder())
      # Try themed icon buttons
      theme_toolbar_button(home_button, "go-home")
      theme_toolbar_button(up_button, "go-up", "gui.UP_SVG")
      theme_toolbar_button(tools_menu_button, "applications-system")
      tree.
         set_fixed_col_w(IconFilesItemPaint.ICON_SIZE + 2 * Tree.COL_PAD).
         set_item_paint(IconFilesItemPaint()).
         set_item_border(EmptyBorder().
                            set_insets(Tree.COL_PAD, Gui.TEXT_INSET, 0, 0).
                            set_x_align(Align.L))
         
      existing_file_mode := IGNORE
      return
   end

   public override end_dialog()
   end

   public get_favourites_directories()
      return FileDialog.unique_ucs_list{ gen_favourites_directories() }
   end

   public gen_favourites_directories()
      suspend FilePath(!init_dirs).canonical().ucs() | FavouriteDirectoriesDialog.gen_directories_plus()
   end

   public on_favourite_dirs_menu_item(ev)
      FavouriteDirectoriesDialog().show_modal(self)
   end

   private setup()
      local paint_2, layout_7, paint_45, favourite_dirs_menu_item, layout_4, paint_11, paint_42, refresh_menu_item, panel_4, paint_47, paint_1, paint_9, layout_6, paint_44, paint_10, paint, paint_3, panel_3, paint_46, paint_43, new_dir_menu_item, paint_35
      self.set_label("Select directory")
      self.set_resize(&yes)
      paint_45 := ImagePaint().
         set_cache("gui.CONFIGURE_24")
      paint_42 := TextPaint("New directory...")
      new_dir_menu_item := TextMenuItem().
         connect(on_new_dir_menu_item, Event.ACTION).
         set_paint_mid(paint_42)
      paint_43 := TextPaint("Favourite dirs...")
      favourite_dirs_menu_item := TextMenuItem().
         connect(on_favourite_dirs_menu_item, Event.ACTION).
         set_paint_mid(paint_43)
      paint_44 := TextPaint("Show hidden files")
      show_hidden_menu_item := CheckBoxMenuItem().
         connect(on_show_hidden_menu_item, Event.ACTION).
         set_paint_mid(paint_44)
      paint_46 := TextPaint("Refresh")
      refresh_menu_item := TextMenuItem().
         connect(on_refresh_menu_item, Event.ACTION).
         set_paint_mid(paint_46)
      tools_menu := Menu().
         set_paint_mid(paint_45).
         add(new_dir_menu_item).
         add(favourite_dirs_menu_item).
         add(show_hidden_menu_item).
         add(refresh_menu_item)
      paint_47 := TextPaint("Tools")
      tools_menu_button := MenuButton().
         clear_constraints().
         set_tooltip(paint_47).
         set_menu(tools_menu)
      paint_1 := ImagePaint().
         set_cache("gui.HOME_24")
      paint := TextPaint("Home")
      home_button := IconButton().
         set_align(Align.L, Align.C).
         clear_constraints().
         set_tooltip(paint).
         connect(self.on_home_button, Event.ACTION).
         set_no_click_focus(&yes).
         set_toggles(&no).
         set_paint(paint_1)
      paint_3 := ImagePaint().
         set_cache("gui.UP_24")
      paint_2 := TextPaint("Up")
      up_button := IconButton().
         set_align(Align.L, Align.C).
         clear_constraints().
         set_tooltip(paint_2).
         connect(self.on_up_button, Event.ACTION).
         set_no_click_focus(&yes).
         set_toggles(&no).
         set_paint(paint_3)
      layout_6 := GridLayout().
         set_doi(scale(0)).
         set_dii(scale(2)).
         set_extra("cells")
      panel_4 := Panel().
         clear_constraints().
         set_constraint(Grid.EOL, &yes).
         set_constraint(Grid.X_ALIGN, Align.L).
         set_layout(layout_6).
         add(tools_menu_button).
         add(home_button).
         add(up_button)
      self.add(panel_4)
      tree := FileTreeWithTooltip().
         set_size(scale(520), scale(360)).
         clear_constraints().
         set_constraint(Grid.EOL, &yes).
         set_constraint(Grid.X_FILL, &yes).
         set_constraint(Grid.X_WEIGHT, 1.0).
         set_constraint(Grid.Y_FILL, &yes).
         set_constraint(Grid.Y_WEIGHT, 1.0).
         connect(self.on_tree_expansion, Event.TREE_NODE_EXPANSION).
         connect(self.on_tree_selection, Event.SELECTION_CHANGED).
         set_select_mode(Select.ONE).
         set_show_root(&no)
      self.add(tree)
      paint_35 := TextPaint("Directory")
      dir := DirEditList().
         set_size(scale(150)).
         set_align(Align.L, Align.B).
         clear_constraints().
         set_constraint(Grid.EOL, &yes).
         set_constraint(Grid.X_ALIGN, Align.L).
         set_constraint(Grid.X_FILL, &yes).
         set_constraint(Grid.X_WEIGHT, 1.0).
         set_tooltip(paint_35).
         connect(self.on_dir_action, Event.ACTION).
         connect(self.on_dir_content_changed, Event.CONTENT_CHANGED).
         set_selection_list([""])
      self.add(dir)
      paint_9 := TextPaint("Okay")
      okay_button := TextButton().
         set_align(Align.L, Align.B).
         clear_constraints().
         connect(self.on_okay_button, Event.ACTION).
         set_toggles(&no).
         set_paint(paint_9)
      paint_10 := TextPaint("Cancel")
      cancel_button := TextButton().
         set_align(Align.L, Align.B).
         clear_constraints().
         connect(self.on_cancel_button, Event.ACTION).
         set_toggles(&no).
         set_paint(paint_10)
      paint_11 := TextPaint("")
      message_label := MessageLabel().
         set_size(scale(130)).
         set_align(Align.L, Align.B).
         set_fg("red").
         clear_constraints().
         set_constraint(Grid.X_ALIGN, Align.L).
         set_constraint(Grid.X_FILL, &yes).
         set_constraint(Grid.X_WEIGHT, 1.0).
         set_paint(paint_11)
      layout_4 := GridLayout().
         set_doi(scale(0)).
         set_extra("cells")
      panel_3 := Panel().
         clear_constraints().
         set_constraint(Grid.X_FILL, &yes).
         set_constraint(Grid.X_WEIGHT, 1.0).
         set_layout(layout_4).
         add(okay_button).
         add(cancel_button).
         add(message_label)
      self.add(panel_3)
      layout_7 := GridLayout().
         set_extra("cells")
      self.set_layout(layout_7)
   end
end

### Ivib-v2 layout: layouts/$.layout
