package parser

import util, io

class ErrorDetail()
   public const src, line, msg

   # Does the same as abbreviate() in tmain.c
   private static abbreviate(name)
      local l
      l := FilePath(name).get(-1)
      return if Files.access(name, Access.R_OK) then {
         if FilePath(l).canonical().standard_case().str() == name then
            l
         else
            name
      } else
      l
   end

   # Produce a string version of the message in the same form as
   # produced by the compiler, including embedded filename and line
   # no.
   #
   public esc_str()
      local s
      if \src then
         s := Files.begin_link(src, line) || "File " || abbreviate(src) || "; "
      else
         s := ""
      s ||:= "Line " || line
      if \src then
         s ||:= Files.end_link()
      s ||:= " # " || msg
      return s
   end

   #
   # Produce a string version of the message in the same form
   # as produced by the compiler.
   #
   public str()
      local s
      if \src then
         s := "File " || abbreviate(src) || "; "
      else
         s := ""
      s ||:= "Line " || line || " # " || msg
      return s
   end

   package final new(src, line, msg)
      self.src := src
      self.line := line
      self.msg := msg
      return
   end
end

class Parser(Connectable)
   private
      first_err, pp, lex, yy

   public static const 
      PREPROCESSOR_ERROR_EVENT, 
      LEXER_ERROR_EVENT,
      PARSER_ERROR_EVENT

   private static init()
      PREPROCESSOR_ERROR_EVENT := "preprocessor error"
      LEXER_ERROR_EVENT := "lexer error"
      PARSER_ERROR_EVENT := "parser error"
   end

   public override new()
      Connectable.new()
      pp := Preprocessor().connect(pperror)
      lex := Lex().connect(lxerror)
      yy := YY().connect(yyerror)
      return
   end

   public parse(s, encoding)
      local yyval
      first_err := &null
      # Because we don't have the pp-generated # at the top of the file we start
      # at line 1 not line 0, and also in the given encoding.
      yyval := yy.yyparse(create lex.yylex(s,, encoding, 1))
      if \first_err then
         return error(first_err)
      return Prog(yyval)
   end

   #
   # Parse `s`, as an expression (actually, a compound expression).
   #
   public parse_expression(s, encoding)
      s := "procedure main() ; " || s || " end"
      return parse(s, encoding).get_body().get(1).get_body_node()
   end

   public parse_file(fname, predefined_syms)
      local s, yyval
      first_err := &null
      s := pp.preprocess(fname, predefined_syms)
      if \first_err then
         return error(first_err)
      yyval := yy.yyparse(create lex.yylex(s))
      if \first_err then
         return error(first_err)
      return Prog(yyval)
   end

   private yyerror(s)
      local ed, token
      # The last token sent by the lexer.
      token := yy.yylval
      ed := ErrorDetail(token.filename, token.line, image(token.s) || ": " || s)
      fire(PARSER_ERROR_EVENT, ed)
      /first_err := ed.str()
   end

   private lxerror(ed)
      fire(LEXER_ERROR_EVENT, ed)
      /first_err := ed.str()
   end

   private pperror(ed)
      fire(PREPROCESSOR_ERROR_EVENT, ed)
      /first_err := ed.str()
   end
end
