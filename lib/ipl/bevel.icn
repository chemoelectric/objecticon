############################################################################
#
#	File:     bevel.icn
#
#	Subject:  Procedures for drawing beveled objects
#
#	Author:   Gregg M. Townsend
#
#	Date:     April 1, 1997
#
############################################################################
#
#   This file is in the public domain.
#
############################################################################
#
#	These procedures draw objects having a raised or sunken appearance.
#
#	BevelReset(win)				sets/resets shading colors.
#
#	BevelCircle(win, x, y, r, bw)		draws a beveled circle.
#
#	BevelDiamond(win, x, y, r, bw)		draws a beveled diamond.
#
#	BevelTriangle(win, x, y, r, o, bw)	draws a beveled triangle.
#
#	BevelSquare(win, x, y, r, bw)		draws a beveled square.
#
#	FillSquare(win, x, y, r)		fills a square.
#
#	FillDiamond(win, x, y, r)		fills a diamond.
#
#	FillTriangle(win, x, y, r, o)		fills a triangle.
#
#	RidgeRectangle(win, x, y, w, h, bw)	draws a ridged rectangle.
#
#	GrooveRectangle(win, x, y, w, h, bw)	draws a grooved rectangle.
#
#	BevelRectangle(win, x, y, w, h, bw)	draws a beveled rectangle.
#
#	DrawRidge(win, x1, y1, x2, y2, w)	draws a ridged line.
#
#	DrawGroove(win, x1, y1, x2, y2, w)	draws a grooved line.
#
############################################################################
#
#  These procedures allow the drawing of buttons and other objects
#  with a three-dimensional appearance.  They are intended to be
#  used like other graphics primitives (DrawRectangle() etc.).
#  However, this abstraction fails if the background color changes
#  or if clipping is set, due to the use of cached graphics contexts.
#
#  BevelReset(win) -- set/reset colors for beveling
#	This procedure is called automatically by the others.
#	It can be called explicitly if the background color is changed.
#
#  BevelCircle(win, x, y, r, bw) -- draw beveled circle
#  BevelDiamond(win, x, y, r, bw) -- draw beveled diamond
#  BevelTriangle(win, x, y, r, o, bw) -- draw beveled triangle
#  BevelSquare(win, x, y, r, bw) -- draw beveled square
#	These procedures draw a figure centered at (x,y) and having
#	a "radius" of r.  bw is the bevel width, in pixels.
#       o is the triangle orientation: "n", "s", "e", or "w".
#
#  FillSquare(win, x, y, r) -- fill square centered at (x,y)
#  FillDiamond(win, x, y, r) -- fill diamond centered at (x,y)
#  FillTriangle(win, x, y, r, o) -- fill triangle centered at (x,y)
#	These procedures complement the beveled outline procedures
#	by filling a figure centered at (x,y).  Fillcircle is already
#	an Icon function and so is not included here.
#
#  RidgeRectangle(win, x, y, w, h, bw) -- draw ridged rectangle
#  GrooveRectangle(win, x, y, w, h, bw) -- draw grooved rectangle
#  BevelRectangle(win, x, y, w, h, bw) -- draw beveled rectangle
#	These procedures draw a rectangle with the given external
#	dimensions and border width.  Beveled rectangles are raised
#	if bw > 0 or sunken if bw < 0.
#
#  DrawRidge(win, x1, y1, x2, y2, w) -- draw a ridged line
#  DrawGroove(win, x1, y1, x2, y2, w) -- draw a grooved line
#	These procedures draw a groove or ridge of width 2 at any angle.
#	If w = 0, a groove or ridge is erased to the background color.
#
#  For BevelSquare() and FillSquare(), the width drawn is 2 * r + 1,
#  not just 2 * r.  This is necessary to keep the visual center at the
#  specified (x, y) and is consistent with the other centered procedures
#  and the built-in function FillCircle.
#
############################################################################
#
#  Includes:  vdefns
#
############################################################################
#
#  Links:  graphics
#
############################################################################
#
#  Requires:  Version 9 graphics
#
############################################################################


$include "vdefns.icn"

package ipl.graphics

import util(Math)

global bev_table
record bev_record(shadow, hilite)


procedure BevelDispose(win)
   local b
   if b := \ (\bev_table)[win] then {
      b.hilite.uncouple()
      b.shadow.uncouple()
      delete(bev_table, win)
   }
end

procedure BevelDisposeAll()
local b
    every b := !\bev_table do {
       b.hilite.uncouple()
       b.shadow.uncouple()
    }
    bev_table := &null
end

#  BevelReset(win) -- set/reset colors for beveling
#
#  Called automatically the first time a beveling procedure is called;
#  must also be called explicitly if the background color is changed.
#  (Pale, weak background colors work best with beveling.)

# set colors for beveled drawing
procedure BevelReset(win)	
   local h, l, s, hilite, shadow, lhilite, lshadow

   /bev_table := table()

   BevelDispose(win)

   if win.get_depth() >= 4 then {

      HLS(win.get_bg()) ? {
         h := integer(tab(many(&digits)))
         move(1)
         l := integer(tab(many(&digits)))
         move(1)
         s := integer(tab(0))
         }

      case l of {
          0 <= l < 10 & l:	  { lshadow := 25;	lhilite := 50 }
         10 <= l < 25 & l:	  { lshadow := 0;	lhilite := l + 25 }
         25 <= l < 50 & l:	  { lshadow := l - 25;	lhilite := l + 25 }
         50 <= l < 75 & l:	  { lshadow := 25;	lhilite := l + 25 }
         75 <= l < 90 & l:	  { lshadow := l - 50;	lhilite := 100 }
         default:		  { lshadow := 40;	lhilite := 75 }
         }
      s /:= 2

      shadow := win.clone().
         set_fg(HLSValue(h || ":" || lshadow || ":" || s)).
         set_line_width(1).
         set_line_style("solid").
         set_fill_style("solid").
         set_draw_op("copy")
      hilite := shadow.clone().
         set_fg(HLSValue(h || ":" || lhilite || ":" || s))
   } else {
      shadow := win.clone().
         set_line_width(1).
         set_line_style("solid").
         set_fill_style("solid").
         set_draw_op("copy")
      hilite := shadow.clone().
         set_fill_style("textured").
         set_pattern("gray")
   }

   bev_table[win] := bev_record(shadow, hilite)
   return win
end


#  bev_lookup(win) -- look up and return bev_record for a window.
#
#  (Internal procedure)

procedure bev_lookup(win)
   local b, t
   b := \ (\bev_table)[win] | bev_table[BevelReset(win)]
   
   # Ensure the shadow and hilite windows have the same values for
   # some selected attributes.
   t := win.get_dx()
   every (b.shadow | b.hilite).set_dx(t)

   t := win.get_dy()
   every (b.shadow | b.hilite).set_dy(t)

   if t := win.get_clip() then
      every (b.shadow | b.hilite).clip!t
   else
      every (b.shadow | b.hilite).unclip()

   return b
end


#  BevelCircle(win, x, y, r, bw) -- draw beveled circle

# draw beveled circle
procedure BevelCircle(win, x, y, r, bw)		
   local b, upper, lower, a

   b := bev_lookup(win)

   /r := 6
   /bw := 2
   if bw >= 0 then {
      upper := b.hilite
      lower := b.shadow
   }
   else {
      upper := b.shadow
      lower := b.hilite
      bw := -bw
   }

   a := -Math.PI / 8
   while (bw -:= 1) >= 0 do {
      lower.draw_circle(x, y, r, a, Math.PI)
      upper.draw_circle(x, y, r, a + Math.PI, Math.PI)
      r -:= 1
   }
   return win
end


#  BevelDiamond(win, x, y, r, bw) -- draw beveled diamond

# draw beveled diamond
procedure BevelDiamond(win, x, y, r, bw)	
   local b, upper, lower

   b := bev_lookup(win)

   /r := 6
   /bw := 3
   if bw >= 0 then {
      upper := b.hilite
      lower := b.shadow
   }
   else {
      upper := b.shadow
      lower := b.hilite
      bw := -bw
   }

   while (bw -:= 1) >= 0 do {
      lower.draw_line(x - r, y, x, y + r, x + r, y)
      upper.draw_line(x - r, y, x, y - r, x + r, y)
      r -:= 1
   }
   return win
end


#  BevelTriangle(win, x, y, r, o, bw) -- draw beveled triangle

procedure BevelTriangle(win, x, y, r, o, bw)
   local b, upper, lower

   b := bev_lookup(win)

   /r := 6
   /bw := 2
   if bw >= 0 then {
      upper := b.hilite
      lower := b.shadow
   }
   else {
      upper := b.shadow
      lower := b.hilite
      bw := -bw
   }

   while (bw -:= 1) >= 0 do {
      case o of {
         default: {  #"n"
            lower.draw_line(x - r, y + r, x + r, y + r, x, y - r)
            upper.draw_line(x - r, y + r, x, y - r)
         }
         "s": {
            lower.draw_line(x, y + r, x + r, y - r)
            upper.draw_line(x, y + r, x - r, y - r, x + r, y - r)
         }
         "e": {
            lower.draw_line(x - r, y + r, x + r, y)
            upper.draw_line(x - r, y + r, x - r, y - r, x + r, y)
         }
         "w": {
            lower.draw_line(x - r, y, x + r, y + r)
            lower.draw_line(x + r, y + r, x + r, y-r)
            upper.draw_line(x - r, y, x + r, y - r)
         }
      }
      r -:= 1
   }
   return win
end


#  BevelSquare(win, x, y, r, bw) -- draw beveled square

# draw beveled square
procedure BevelSquare(win, x, y, r, bw)		
   /r := 6
   return BevelRectangle(win, x - r, y - r, 2 * r + 1, 2 * r + 1, bw)
end


#  RidgeRectangle(win, x, y, w, h, bw) -- draw ridged rectangle

# draw ridged rectangle
procedure RidgeRectangle(win, x, y, w, h, bw)	
   /bw := 2
   return GrooveRectangle(win, x, y, w, h, -bw)
end


#  GrooveRectangle(win, x, y, w, h, bw) -- draw grooved rectangle

# draw grooved rectangle
procedure GrooveRectangle(win, x, y, w, h, bw)	
   local abw

   /x := -win.get_dx()
   /y := -win.get_dy()
   /w := win.get_width() - (x + win.get_dx())
   /h := win.get_height() - (y + win.get_dy())

   if w < 0 then
      x -:= (w := -w)
   if h < 0 then
      y -:= (h := -h)

   /bw := 2
   if bw >= 0 then
      bw := (bw + 1) / 2
   else
      bw := -((-bw + 1) / 2)
   abw := abs(bw)

   BevelRectangle(win, x, y, w, h, -bw)
   BevelRectangle(win, x + abw, y + abw, w - 2 * abw, h - 2 * abw, bw)
   return win
end


#  BevelRectangle(win, x, y, w, h, bw) -- draw beveled rectangle
#
#  bw is the border width (>0 for raised bevel, <0 for sunken bevel).
#  (x,y,w,h) bounds the entire beveled rectangle, not the usable area inside.

# draw beveled rectangle
procedure BevelRectangle(win, x, y, w, h, bw)	
   local b, upper, lower, xx, yy

   b := bev_lookup(win)

   /x := -win.get_dx()
   /y := -win.get_dy()
   /w := win.get_width() - (x + win.get_dx())
   /h := win.get_height() - (y + win.get_dy())

   if w < 0 then
      x -:= (w := -w)
   if h < 0 then
      y -:= (h := -h)

   /bw := 2
   if bw >= 0 then {
      upper := b.hilite
      lower := b.shadow
   }
   else {
      upper := b.shadow
      lower := b.hilite
      bw := -bw
   }

   xx := x + w
   yy := y + h
   lower.fill_rectangle(x, yy, w, -bw)
   lower.fill_rectangle(xx, y, -bw, h)

   while (bw -:= 1) >= 0 do {
      upper.draw_line(x, yy -:= 1, x, y, xx -:= 1, y)
      x +:= 1
      y +:= 1
   }

   return win
end


#  DrawRidge(win, x1, y1, x2, y2, w) -- draw a ridged line
#
#  If w is negative, a groove is drawn instead.

# draw ridged line
procedure DrawRidge(win, x1, y1, x2, y2, w)	
   /w := 2

   DrawGroove(win, x1, y1, x2, y2, -w)
   return win
end


#  DrawGroove(win, x1, y1, x2, y2, w) -- draw a grooved line
#
#  If w > 0, draw groove of width 2.
#  If w = 0, erase groove/ridge of width 2.
#  If w < 0, draw ridge of width 2.
#
#  Horizontal and vertical grooves fill the same pixels as lines drawn
#  line_width=2.  Angled grooves are not necessarily the same, though.

# draw grooved line
procedure DrawGroove(win, x1, y1, x2, y2, w)	
   local a, n, b, upper, lower, fg

   /w := 2
   x1 := integer(x1)
   y1 := integer(y1)
   x2 := integer(x2)
   y2 := integer(y2)

   if w ~= 0 then {			# if really drawing
      b := bev_lookup(win)
      upper := b.shadow
      lower := b.hilite
      }
   else {
      fg := win.get_fg()			# if erasing, draw in bg color
      win.set_fg(win.get_bg())
      upper := lower := win
      }

   a := Math.atan(y2 - y1, x2 - x1)
   if a < 0 then
      a +:= Math.PI
   n := integer(8 * a / Math.PI)

   if w < 0 then			# if groove/ridge swap
      upper :=: lower
   if n = 2 then			# if tricky illumination angle
      upper :=: lower

   if 2 <= n <= 5 then {		# approximately vertical
      upper.draw_line(x1 - 1, y1, x2 - 1, y2)
      lower.draw_line(x1, y1, x2, y2)
      }
   else {				# approximately horizontal
      upper.draw_line(x1, y1 - 1, x2, y2 - 1)
      lower.draw_line(x1, y1, x2, y2)
      }

   win.set_fg(\fg)				# restore foreground if changed
   return win
end


#  FillSquare(win, x, y, r) -- fill square centered at (x,y)

# draw filled square
procedure FillSquare(win, x, y, r)		
   return win.fill_rectangle( x - r, y - r, 2 * r + 1, 2 * r + 1)
end


#  FillDiamond(win, x, y, r) -- fill diamond centered at (x,y)

# draw filled diamond
procedure FillDiamond(win, x, y, r)		
   return win.fill_polygon( x - r, y, x, y + r + 1, x + r + 1, y, x, y - r - 1)
end


#  FillTriangle(win, x, y, r, o) -- fill triangle centered at (x,y)
#
#  r is "radius" (1/2 of side of enclosing square)
#  o is orientation ("n", "s", "e", "w")

# draw filled triangle
procedure FillTriangle(win, x, y, r, o)		
   return case o of {
      default:  #"n"
         win.fill_polygon( x - r - 1, y + r + 1, x, y - r, x + r + 1, y + r + 1)
      "s":
         win.fill_polygon( x - r, y - r, x, y + r, x + r, y - r)
      "e":
         win.fill_polygon( x - r, y - r, x + r, y, x - r, y + r)
      "w":
         win.fill_polygon( x + r + 1, y - r - 1, x - r, y, x + r + 1, y + r + 1)
      }
end

