import gui,io,ipl.options,lang

procedure main(a)
   local d,opts
   opts := options(a, [Opt("we",, "Warn on existing file"),
                       Opt("re",, "Require existing file"),
                       Opt("de",, "Disallow existing file"),
                       Opt("l",, "Loop repeatedly, re-showing the dialog"),
                       Opt("h",, "Initially show hidden files"),
                       Opt("k",, "Keep directory between uses")])
   d :=  DirectoryDialog().
      set_label("A Dir Dialog").
      set_directories([Files.get_temp(), Files.get_home()])

   if \opts["h"] then
      d.set_show_hidden_files(&yes)

   if \opts["we"] then
      d.set_existing_file_mode(FileDialog.WARN)

   if \opts["re"] then
      d.set_existing_file_mode(FileDialog.REQUIRE)

   if \opts["de"] then
      d.set_existing_file_mode(FileDialog.DISALLOW)
  
   d.set_directory(a[1])
   repeat {
      d.show_modal()
      write("d.get_result()=",image(d.get_result()) | "FAILED")
      write("d.get_result_path()=",to_string(d.get_result_path()) | "FAILED")
      if /opts["l"] then break
      if \opts["k"] then
         d.keep_directory()
   }
end

