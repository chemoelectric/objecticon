#
#
# This file is in the public domain.
#
# Author: Robert Parlett (parlett@dial.pipex.com)
#

import gui


############################################################################
#
# Class for representing a TextDisplay on the canvas.
#
class CanvasTextDisplay(CanvasComponent,TextDisplay)
   public override min_height()
      # need a smart calculation font height + borders + scrollbar
      # for now based on constants in gui.icn, guiconst.icn we go with
      # 14 + 8*2 + 22
      return 52
   end
 
   public override min_width()
      # need a smart calculation font height + borders + scrollbar
      # for now based on constants in gui.icn, guiconst.icn we go with
      # 14 + 5*2 + 22
      return 46
   end

   #
   # Generate code into Code object c.
   #
   public override gen_code(c)
      CanvasComponent.gen_code(c)
      c.chain("set_contents(" || contents_string(self.contents) || ")")
   end

   #
   # Duplicate object.
   #
   public override dup()
      local c
      c := CanvasTextDisplay()
      CanvasComponent.dup_fields(c)
      c.set_contents(copy(self.contents))
      return c
   end

   #
   # Return the I/O template.
   #
   public override get_template()
      suspend CanvasComponent.get_template() | ["Contents", "contents"]
   end

   #
   # Open the configuration dialog box.
   #
   public override dialog()
      return CanvasTextDisplayDialog(self)
   end

   public override allows_null_height()
      return
   end

   public override allows_null_width()
      return
   end

   public override new()
      TextDisplay.new()
      CanvasComponent.new()
      self.class_name := "TextDisplay"
      self.import_name := "gui"
      self.name := "text_display"
      return
   end
end

