#
# $Id$
#
# This file is in the public domain.
#
# Author: Robert Parlett (parlett@dial.pipex.com)
#

import gui

$define INSIDE_BOX 30

############################################################################
#
# Class for representing a Border on the canvas.
#
class CanvasBorder(CanvasComponent,Border)
   #
   # Generate code into Code object c.
   #
   public gen_code(c)
      cast(self,CanvasComponent).gen_code(c)
      if self.internal_align ~== Align.L then
         c.line(name || ".set_internal_align(" || image(self.internal_align) || ")")
      if \self.title then {
         self.title.gen_code(c)
         c.line(self.name || ".set_title(" || self.title.name || ")")
      }
      self.content.gen_code(c)
      c.line(self.name || ".set_content(" || self.content.name || ")")
   end

   public post_decode()
      local e, c
      if /self.content then {
         # Fix an old-style Border, where there is no content object.

         # Create a content object.
         c := CanvasBorderContent()
         c.set_parent_Canvas(self.parent_Canvas)
         c.rename()

         # Move everything but the title into the content.
         every e := !copy(self.children) do {
            if e ~=== self.title then {
               remove(e)
               c.add(e)
            }
         }

         # Add the content
         self.set_content(c)

         # Transfer any layout to the content.
         #self.content.set_layout(self.layout_delegate)
         #cast(self,Component).set_layout()
         #write("POST:children")
         #every c := !children do
         #   write("\t",image(c))
         #write("POST:content children")
         #every c := !content.children do
         #   write("\t",image(c))
      }
      cast(self,CanvasComponent).post_decode()
   end

   #
   # Duplicate object.
   #
   public dup()
      local c
      c := CanvasBorder()
      cast(self,CanvasComponent).dup_fields(c)
      c.set_content(self.content.dup())
      c.set_title((\self.title).dup())
      c.internal_align := self.internal_align
      return c
   end

   public remove(c)
      if c === self.title then
         self.title := &null
      cast(self,Component).remove(c)
   end

   public place_inside(c)
      if (self.x + self.w - INSIDE_BOX <= c.mx < self.x + self.w) & 
         (self.y + self.h - INSIDE_BOX <= c.my < self.y + self.h) & /title then 
      {
         set_title(c)
         c.reset()
      }
   end

   #
   # Return the I/O template.
   #
   public get_template()
      return cast(self,CanvasComponent).get_template() ||| 
         [["Internal Alignment", "internal_align"],
          ["Children", "children"],
          ["Title Obj", "title"],
          ["Content", "content"]
         ]
   end

   #
   # Open the configuration dialog box.
   #
   public dialog()
      return CanvasBorderDialog(self)
   end

   #
   # Overrides CanvasComponent.which_cursor_over() 
   #
   public which_cursor_over()
      local o
      cast(self,CanvasComponent).which_cursor_over() | fail
      if in_resize_region() then
         return self
      if o := (\self.title | self.content).which_cursor_over() then
         return o
      return self
   end

   #
   # Overrides Border.display()
   #
   public display(r)
      local cw

      cast(self,Border).display(r)
      if in_ivib() then {
         cw := Clone(self.cbwin, "linestyle=dashed")
         Rectangle(cw, self.x + self.w - INSIDE_BOX, self.y + self.h - INSIDE_BOX, INSIDE_BOX - Gui.BORDER_WIDTH, INSIDE_BOX- Gui.BORDER_WIDTH)
         Uncouple(cw)
      }
   end

   #
   # Overrides CanvasComponent.inside()
   #
   public is_inside(c)
      local o
      if c === self then
         fail
      if o := (\self.title).is_inside(c) then
         return o
      if ((self.x + self.w - INSIDE_BOX <= c.mx < self.x + self.w) & (self.y + self.h - INSIDE_BOX <= c.my < self.y + self.h) & /title) then
         return self
      if o := self.content.is_inside(c) then
         return o
   end

   public allows_null_width()
      return self.content.allows_null_width()
   end

   public allows_null_height()
      return self.content.allows_null_height()
   end

   public gen_cos()
      suspend cast(self,CanvasComponent).gen_cos() | self.content | \self.title
   end

   public new()
      cast(self,Border).new()
      cast(self,CanvasComponent).new()
      self.class_name := "Border"
      self.import_name := "gui"
      self.name := "border"
      return
   end
end

class CanvasBorderContent(CanvasComponent,Component)
   #
   # Generate code into Code object c.
   #
   public gen_code(c)
      local i
      cast(self,CanvasComponent).gen_code(c)
      every i := !self.children do {
         i.gen_code(c)
         c.line(self.name || ".add(" || i.name || ")")
      }
   end

   #
   # Return the I/O template.
   #
   public get_template()
      return cast(self,CanvasComponent).get_template() ||| 
         [["Children", "children"]
         ]
   end

   #
   # Duplicate object.
   #
   public dup()
      local c
      c := CanvasBorderContent()
      cast(self,CanvasComponent).dup_fields(c)
      every c.add((!self.children).dup())
      return c
   end

   #
   # Overrides CanvasComponent.is_inside()
   #
   public is_inside(c)
      local e, o
      every e := !self.reverse_z_children do
         if o := e.is_inside(c) then
            return o
      if (self.x <= c.mx < self.x + self.w) & (self.y <= c.my < self.y + self.h) then
         return self
   end

   #
   # Overrides CanvasComponent.which_cursor_over() 
   #
   public which_cursor_over()
      local e, o
      every e := !self.reverse_z_children do
         if o := e.which_cursor_over() then
            return o
   end

   public allows_selection()
   end

   public allows_positioning()
   end

   public allows_add()
      return 1
   end

   public allows_copy()
   end

   public allows_delete()
   end

   #
   # Open the configuration dialog box.
   #
   public dialog()
      return CanvasBorderContentDialog(self)
   end

   public gen_cos()
     suspend cast(self,CanvasComponent).gen_cos() | !self.children
   end

   public new()
      cast(self,Component).new()
      cast(self,CanvasComponent).new()
      self.set_show_layout("t")
      self.class_name := "Component"
      self.import_name := "gui"
      self.name := "border_content"
      return
   end
end

class CanvasBorderContentDialog(CanvasComponentDialog)
   public validate_input()
      #
      # We must have a default size if the parent has no absolute size.
      #
      if (/c.parent.w_spec | /c.parent.h_spec) & /c.layout_delegate then
         return alert_error(self, "The border content must have a layout manager set.")

      cast(self,CanvasComponentDialog).validate_input() | fail 
      return
   end
end



