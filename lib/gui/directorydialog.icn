package gui

import 
   ipl.pdco,
   ipl.sort,
   ipl.filegui,
   io

#
#
# Directory dialog class.  This class provides a standard directory selection dialog.
#
# @example
# @ d := DirectoryDialog()
# @ d.show_modal()
# @ write(d.get_result() | "cancelled")
#
class DirectoryDialog(Dialog)
   public
      show_hidden_menu_item,
      tools_menu,
      tools_menu_button,
      home_button, 
      up_button, 
      cancel_button, 
      okay_button,
      init_dirs,         # Directory choices
      res,               # Resulting file path          
      selected_path,     # FilePath of tree selection
      message_label, 
      tree,
      dir,
      existing_file_mode,
      warned

   public static const
      WARN, IGNORE, REQUIRE, DISALLOW
   
   private static init()
      WARN := "warn"
      IGNORE := "ignore"
      REQUIRE := "require"
      DISALLOW := "disallow"
   end

   public set_existing_file_mode(s)
      existing_file_mode := s
      return self
   end

   public component_setup()
      local root_node, d
      #
      # Defaults and extra dirs.
      #
      /init_dirs := []
      dir.set_selection_list(get_favourites_directories()).set_selection(1)
      root_node := TreeNode()
      every d := Files.get_roots() do
         root_node.add(TreeNode().set_label(ListEntryWithIcon(ucs(d), Files.lstat(d), Files.stat(d))).set_always_expandable(&yes))
      tree.set_root_node(root_node)
      res := &null
   end

   public init_dialog()
      res := &null
      dir.grab_focus()
      win.set_min_size(win.get_width(), win.get_height())
      # Do this here as we need the tree to be setup so that we
      # can scroll to the selected dir.
      goto_dir(dir.get_contents())
   end

   #
   # Given a TreeNode n, load its children with the sub-directories.
   #
   public load_subtree(n)
      local s, e, l, i
      s := get_full_path(n).str()
      if l := list_directory(s) then {
         i := 1
         every e := !l do {
            while n.children[i].label.name << e.name do
               n.remove_index(i)
            if n.children[i].label.name == e.name then
               n.children[i].set_label(e)
            else
               n.add(TreeNode().set_label(e).set_always_expandable(&yes), i)
            i +:= 1
         }
         while n.remove_index(i)
         return self
      } else {
         n.clear_children()
         fail
      }
   end

   public list_directory(s)
      local e, dir_list, l, t
      l := Files.ucs_list(s, ListEntryWithIcon) | fail
      dir_list := []
      every e := !l do {
         t := e.name
         if e.stat.mode_str[1] == "d" &
            not(Files.is_relative_dir(t)) &
            (show_hidden_menu_item.is_checked() | 
               not(Files.is_hidden(t))) then
            put(dir_list, e)
      }
      return qsort(dir_list, Files.name_cmp)
   end

   #
   # Given a TreeNode n, return a {FilePath} path it represents by traversing
   # up the tree structure to the root and converting to a {FilePath}.  Each
   # element in the path is a ucs string, so the ucs() method on the path will
   # always succeed.
   #
   public get_full_path(n)
      local l
      l := []
      repeat {
         push(l, n.get_label().name)
         n := n.get_parent()
         if n === tree.get_root_node() then
            break
      }
      return FilePath.from_list(l)
   end

   public on_tree_expansion()
      local n
      n := tree.get_last_expanded()
      #
      # Check whether it was an expansion or a contraction.  If
      # an expansion, load the subtree and refresh the tree.
      #
      if n.is_expanded() then {
         if load_subtree(n) then
            check_existing()
         else
            msg(&why)
         tree.tree_structure_changed()
      }
   end

   public on_tree_selection(ev)
      # This may fail if the user clears the selection; in this case
      # do nothing and leave selected_path as is.
      selected_path := get_full_path(tree.object_get_selections()[1]) | fail
      dir.set_contents(selected_path.ucs())
      dir.view.set_cwd(selected_path.ucs())
      check_existing()
   end

   public on_dir_action(ev)
      goto_dir(dir.get_contents())
   end

   public on_dir_content_changed(ev)
      if dir.was_applied(ev) | dir.view.was_applied(ev) then
         goto_dir(dir.get_contents())
      else
         check_existing()
   end

   #
   # Get the result, (will fail if cancel was pressed).
   #
   public get_result()
      return (\self.res).str()
   end

   #
   # Get the result, as a `FilePath` (will fail if cancel was pressed).
   #
   public get_result_path()
      return \self.res
   end

   #
   # Set the initial directory.  It should be in canonical form.
   #
   public set_directory(s)
      self.init_dirs := [s]
      return self
   end

   #
   # Set the initial directories as a list; the first is the one
   # initially shown.  Each entry should be in canonical form.
   #
   public set_directories(l)
      self.init_dirs := l
      return self
   end

   #
   # Keep the directory last selected by this dialog.
   #
   public keep_directory()
      return set_directory(get_dir_path().str())
   end

   public get_dir_path()
      return FilePath(dir.get_contents()).absolute(selected_path).canonical()
   end

   public goto_dir(d)
      local n, s, p, err, i, root
      p := FilePath(d).absolute(selected_path).canonical().ucs_path() | fail
      n := root := tree.get_root_node()
      every s := p.gen() do {
         n := find_child(n, s) | break
         load_subtree(n) | {
            err := &why
            break
         }
         n.set_expanded(&yes)
      }
      # n may still be at the root if (on Win32) we input a network
      # path (which isn't in the tree).
      if n === root then
         n := root.children[1]
      else
         tree.tree_structure_changed()

      i := tree.find_line(n)
      tree.
         set_selections([i]).
         set_cursor(i).
         center_line(i)

      selected_path := get_full_path(n)
      dir.set_contents(p.ucs())
      dir.view.set_cwd(selected_path.ucs())
      if \err then
         msg(err)
      else if Files.is_directory(p.str()) then
         check_existing()
      else
         msg("Not a directory: " || p.ucs())
      return self
   end

   public find_child(n, s)
      local n1
      every n1 := !n.children do {
$ifdef _MS_WIN32
         if map(n1.label.name) == map(s) then
            return n1
$else
         if n1.label.name == s then
            return n1
$endif
      }
   end

   public check_existing()
      local f
      msg()
      warned := &null
      f := get_dir_path().str()
      if existing_file_mode === (WARN | DISALLOW) & Files.is_directory(f) & Files.access(f) then {
         warned := &yes
         msg("Existing directory")
      }
   end

   public on_okay_button()
      local f, fs
      f := get_dir_path()
      fs := f.str()
      if Files.access(fs) & not Files.is_directory(fs) then {
         msg("Selected file is not a directory")
         fail
      }
      case existing_file_mode of {
         REQUIRE: {
            if not Files.access(fs) then {
               msg("Selected directory doesn't exist")
               fail
            }
         }
         DISALLOW: {
            if Files.access(fs) then {
               msg("Selected directory already exists")
               fail
            }
         }
      }
      self.res := f
      self.dispose()
   end

   public on_cancel_button()
      self.dispose()
   end

   public set_show_hidden_files(s)
      show_hidden_menu_item.set_is_checked(s)
      on_show_hidden_menu_item()
      return self
   end

   public on_show_hidden_menu_item(ev)
      dir.view.set_show_hidden_files(show_hidden_menu_item.get_status())
      if is_live() then
         on_refresh_menu_item(ev)
   end

   public on_up_button(ev)
      if selected_path.size() > 1 then
         goto_dir(selected_path.parent().str())
      else
         msg("At root")
   end

   public on_home_button(ev)
      goto_dir(Files.get_home())
   end

   public refresh_node(n)
      if n.is_expanded() then {
         load_subtree(n)
         every refresh_node(!n.children)
      }
   end

   public on_refresh_menu_item(ev)
      every refresh_node(!tree.get_root_node().children)
      tree.tree_structure_changed()
   end

   public on_new_dir_menu_item(ev)
      local d
      d := NewDirectoryDialog(selected_path.ucs()).
         show_modal(self)
      goto_dir(\d.result)
   end

   public msg(s)
      message_label.set(s)
   end

   public new()
      local p
      Dialog.new()
      self.setup()
      tools_menu.set_border_mid(NullBorder())
      # Try themed icon buttons
      theme_toolbar_button(home_button, "go-home")
      theme_toolbar_button(up_button, "go-up")
      theme_toolbar_button(tools_menu_button, "applications-system")
      p := scale(2)
      tree.
         set_fixed_col_w(IconFilesItemPaint.ICON_SIZE + 2 * p).
         set_item_paint(IconFilesItemPaint()).
         set_item_border(EmptyBorder().
                            set_insets(p, Gui.TEXT_INSET, 0, 0).
                            set_x_align(Align.L))
         
      existing_file_mode := IGNORE
      return
   end

   public end_dialog()
   end

   public get_favourites_directories()
      local t
      t := ipl.pdco.List{ FilePath(!init_dirs).canonical().str() |
                          FavouriteDirectoriesDialog.gen_directories_plus() }
      return FileDialog.unique_ucs_list(t)
   end

   public on_favourite_dirs_menu_item(ev)
      local d
      d := FavouriteDirectoriesDialog()
      d.show_modal(self)
      \d.okay | fail
      dir.set_selection_only().synch_set_selection_list(get_favourites_directories())
   end

   private setup()
      local paint_2, layout_7, paint_45, favourite_dirs_menu_item, layout_4, paint_11, paint_42, refresh_menu_item, panel_4, paint_47, paint_1, paint_9, layout_6, paint_44, paint_10, paint, paint_3, panel_3, paint_46, paint_43, new_dir_menu_item, paint_35
      self.set_label("Select directory")
      self.set_resize(&yes)
      paint_45 := ImagePaint().
         set_cache("gui.CONFIGURE_24")
      paint_42 := TextPaint("New directory...")
      new_dir_menu_item := TextMenuItem().
         connect(on_new_dir_menu_item, Event.ACTION).
         set_paint_mid(paint_42)
      paint_43 := TextPaint("Favourite dirs...")
      favourite_dirs_menu_item := TextMenuItem().
         connect(on_favourite_dirs_menu_item, Event.ACTION).
         set_paint_mid(paint_43)
      paint_44 := TextPaint("Show hidden files")
      show_hidden_menu_item := CheckBoxMenuItem().
         connect(on_show_hidden_menu_item, Event.ACTION).
         set_paint_mid(paint_44)
      paint_46 := TextPaint("Refresh")
      refresh_menu_item := TextMenuItem().
         connect(on_refresh_menu_item, Event.ACTION).
         set_paint_mid(paint_46)
      tools_menu := Menu().
         set_paint_mid(paint_45).
         add(new_dir_menu_item).
         add(favourite_dirs_menu_item).
         add(show_hidden_menu_item).
         add(refresh_menu_item)
      paint_47 := TextPaint("Tools")
      tools_menu_button := MenuButton().
         clear_constraints().
         set_tooltip(paint_47).
         set_menu(tools_menu)
      paint_1 := ImagePaint().
         set_cache("gui.HOME_24")
      paint := TextPaint("Home")
      home_button := IconButton().
         set_align(Align.L, Align.C).
         clear_constraints().
         set_tooltip(paint).
         connect(self.on_home_button, Event.ACTION).
         set_no_click_focus(&yes).
         set_toggles(&no).
         set_paint(paint_1)
      paint_3 := ImagePaint().
         set_cache("gui.UP_24")
      paint_2 := TextPaint("Up")
      up_button := IconButton().
         set_align(Align.L, Align.C).
         clear_constraints().
         set_tooltip(paint_2).
         connect(self.on_up_button, Event.ACTION).
         set_no_click_focus(&yes).
         set_toggles(&no).
         set_paint(paint_3)
      layout_6 := GridLayout().
         set_doi(scale(0)).
         set_dii(scale(2)).
         set_extra("cells")
      panel_4 := Panel().
         clear_constraints().
         set_constraint(Grid.EOL, &yes).
         set_constraint(Grid.X_ALIGN, Align.L).
         set_layout(layout_6).
         add(tools_menu_button).
         add(home_button).
         add(up_button)
      self.add(panel_4)
      tree := ItemPaintTree().
         set_size(scale(520), scale(360)).
         clear_constraints().
         set_constraint(Grid.EOL, &yes).
         set_constraint(Grid.X_FILL, &yes).
         set_constraint(Grid.X_WEIGHT, 1.0).
         set_constraint(Grid.Y_FILL, &yes).
         set_constraint(Grid.Y_WEIGHT, 1.0).
         connect(self.on_tree_expansion, Event.TREE_NODE_EXPANSION).
         connect(self.on_tree_selection, Event.SELECTION_CHANGED).
         set_select_mode(Select.ONE).
         set_show_root(&no)
      self.add(tree)
      paint_35 := TextPaint("Directory")
      dir := DirEditList().
         set_size(scale(150)).
         set_align(Align.L, Align.B).
         clear_constraints().
         set_constraint(Grid.EOL, &yes).
         set_constraint(Grid.X_ALIGN, Align.L).
         set_constraint(Grid.X_FILL, &yes).
         set_constraint(Grid.X_WEIGHT, 1.0).
         set_tooltip(paint_35).
         connect(self.on_dir_action, Event.ACTION).
         connect(self.on_dir_content_changed, Event.CONTENT_CHANGED).
         set_selection_list([""])
      self.add(dir)
      paint_9 := TextPaint("Okay")
      okay_button := TextButton().
         set_align(Align.L, Align.B).
         clear_constraints().
         connect(self.on_okay_button, Event.ACTION).
         set_toggles(&no).
         set_paint(paint_9)
      paint_10 := TextPaint("Cancel")
      cancel_button := TextButton().
         set_align(Align.L, Align.B).
         clear_constraints().
         connect(self.on_cancel_button, Event.ACTION).
         set_toggles(&no).
         set_paint(paint_10)
      paint_11 := TextPaint("")
      message_label := MessageLabel().
         set_size(scale(130)).
         set_align(Align.L, Align.B).
         set_fg("red").
         clear_constraints().
         set_constraint(Grid.X_ALIGN, Align.L).
         set_constraint(Grid.X_FILL, &yes).
         set_constraint(Grid.X_WEIGHT, 1.0).
         set_paint(paint_11)
      layout_4 := GridLayout().
         set_doi(scale(0)).
         set_extra("cells")
      panel_3 := Panel().
         clear_constraints().
         set_constraint(Grid.X_FILL, &yes).
         set_constraint(Grid.X_WEIGHT, 1.0).
         set_layout(layout_4).
         add(okay_button).
         add(cancel_button).
         add(message_label)
      self.add(panel_3)
      layout_7 := GridLayout().
         set_extra("cells")
      self.set_layout(layout_7)
   end
end

### Ivib-v2 layout: layouts/$.layout
