############################################################################
#
#	File:     pixelmap.icn
#
#	Subject:  Procedure to create image from pixel list
#
#	Author:   Ralph E. Griswold
#
#	Date:     January 23, 1995
#
############################################################################
#
#   This file is in the public domain.
#
############################################################################
#
#  pixelmap(name, p, args[]) reads the pixel list in file name and
#  constructs an image, applying p ! args to each pixel.  If p is
#  omitted or null, the pixels are used as-is.
#
############################################################################
#
#  Requires:  Version 9 graphics
#
############################################################################
#
#  Links:  wopen
#
############################################################################

package ipl.pixelmap

import
   graphics(Window),
   io(open, read),
   util(error)

procedure pixelmap(name, p, args[])
   local input, width, height, x, y, win

   /p := 1
   push(args)			# place holder

   input := open(name) | return error("*** cannot open pixel list")

   read(input) ? {
      ="width=" &
      width := tab(many(&digits)) &
      =" height=" &
      height := tab(many(&digits))
      } | {
         input.close()
         return error("*** invalid pixel list header")
      }

   win := Window().set_size(width, height).set_canvas("normal")

   every y := 0 to height - 1 do
      every x := 0 to width - 1 do {
         unless args[1] := read(input) then {
            input.close()
            return error("*** short data in pixel list")
         }
         win.set_fg(p ! args)
         win.draw_point(x, y)
         }

   input.close()
   return win

end
