package ipl.server9p

import io, posix, ipl.options, util

package procedure usage_impl(s, opts)
   ewrite("Usage: ", FilePath(&progname).get(-1), s)
   if /opts then
      exit(1)
   else {
      options_help(opts)
      exit(0)
   }
end

procedure usage_1(opts)
   usage_impl(" [OPTIONS] [mountpt]", opts)
end

procedure usage_2(opts)
   usage_impl(" [OPTIONS] [mountpt attach]", opts)
end

procedure get_optl()
   return [Opt("v",, "Verbose mode"),
           Opt("s",string, "FILE#Don't mount, instead post the pipe in /srv/FILE"),
           Opt("a",, "Use the MAFTER flag when mounting"),
           Opt("b",, "Use the MBEFORE flag when mounting"),
           Opt("c",, "Use the MCREATE flag when mounting"),
           Opt("C",, "Use the MCACHE flag when mounting")]
end

global opts

procedure server_main_1(a, sess, run, optl)
   local mountpt
   /optl := get_optl()
   opts := options(a, optl, usage_1)
   if /opts["s"] then {
      if *a = 0 then
         usage_1()
      mountpt := a[1]
   }
   server_main(mountpt, "", opts, sess, run)
end

procedure server_main_2(a, sess, run, optl)
   local mountpt, attach
   /optl := get_optl()
   opts := options(a, optl, usage_2)
   if /opts["s"] then {
      if *a < 2 then
         usage_2()
      mountpt := a[1]
      attach := a[2]
   }
   server_main(mountpt, attach, opts, sess, run)
end

procedure server_main(mountpt, attach, opts, sess, run)
   local p, pid, pname, fl, s
   pname := FilePath(&progname).get(-1)
   /run := sess.io
   p := FileStream.pipe() | stop(&why)
   pid := System.fork(ior(ForkOpt.RFPROC, ForkOpt.RFFDG, ForkOpt.RFNAMEG, ForkOpt.RFNOTEG)) | stop(&why)
   if pid = 0 then {
      p[1].close()
      if \opts["v"] then 
         sess.set_verbose(&yes)
      run(p[2])
      p[2].close()
      if \opts["v"] then {
         sess.dump()
         system("cat /proc/" || System.getpid() || "/fd")
      }
      ewrite(pname, ": exit")
   } else {
      sess := &null
      p[2].close()
      if s := \opts["s"] then {
         Files.string_to_file("/srv/" || s, p[1].get_fd()) | stop(&why)
         ewrite(pname, ": serving on /srv/", s)
      } else {
         fl := if \opts["a"] then
            MountOpt.MAFTER
         else if \opts["b"] then
            MountOpt.MBEFORE
         else
            MountOpt.MREPL
         if \opts["c"] then
            fl := ior(fl, MountOpt.MCREATE)
         if \opts["C"] then
            fl := ior(fl, MountOpt.MCACHE)
         Files.mount(p[1], &null, mountpt, fl, attach) | stop(&why)
         ewrite(pname, ": mounted on ", mountpt)
      }
      p[1].close()
   }
   exit()
end

procedure async_sched_loop(sess, sched, f)
   local nb
   use {
      nb := NonBlockStream(f).set_close_underlying(&no),
      {
         sess.async_io(sched, nb)
         # Main loop
         until sched.empty() do
            sched.work() | delay(50)
      }
   }
end
