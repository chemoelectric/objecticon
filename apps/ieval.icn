import io, lang, util, net, ipl.options, ipl.lists

procedure create_prog(main, imps, links)
   local tf, prog

   imps ++:= set("ipl.ieval")  # Avoid changing imps parameter
   prog := ""
   every prog ||:= "import " || !imps || "\n"
   prog ||:= "invocable all\n_
procedure " || main || "(imports, files, opts)\n_
    ipl.ieval.Ieval(imports, files, opts).run()\n_
end\n"
   return use {
      tf := Files.make_temp_filename("ieval", Files.PROG_SUFFIX),
      compile(prog, tf, links),
      save_why{Files.remove(tf)}      # save_why since oit may remove the output file on failure
   }
end

procedure esc(s)
$ifdef _UNIX
   return Format.shell_escape(s)
$else
   return s
$endif
end

procedure compile1(prog, pf, tf, links)
   local s, i
   Files.string_to_file(pf, prog) | fail
   s := "oit -s -l 2 -o " || esc(tf) || " " || esc(pf)
   every s ||:= " " || esc(!links)
   i := system(s)
   if i = 0 then
      return
   else
      error("oit failed, exit status " || i)
end

procedure compile(prog, tf, links)
   local pf
   use {
      pf := Files.make_temp_filename("ieval", ".icn"),
      compile1(prog, pf, tf, links),
      Files.remove(pf)
   } | fail
   return lang.Prog.load(tf)
end

procedure usage(opts)
   local u
   write("Usage: ieval [OPTIONS] [INPUT-FILES...]\n_
          Interactively evaluate expressions")
   options_help(opts)
   u := URL("http://objecticon.sourceforge.net/Ieval.html")
   writes("\nFor more details, visit ")
   if FileStream.stdout.is_flowterm_tty() then
      write(Files.begin_link(u), u.str(), Files.end_link())
   else
      write(u.str())
   exit(0)
end

procedure main(a)
   local opts, prog, imports, links, main
   opts := options(a, [Opt("i", string, "Import the given package(s).  Several packages may be comma-separated, or several -i options may be given",&yes),
                       Opt("l", string, "Link the given source file.  Several -l options may be given",&yes),
                       Opt("m",, "Expect a main procedure in one of the linked source files"),
                       Opt("wglob",, "If given, then assignments to global variables are allowed"),
                       Opt("s",, "Read commands from standard input rather than using a line editor"),
                       Opt("g",, "Use a gui interface rather than the console"),
                       Opt("a",, "In gui mode, actively show completions, rather than on the tab key"),
                       Opt("z",, "Don't read commands interactively, but exit after processing input files"),
                       Opt("hlim",integer,"Set the history limit for the line editor (default is 500)"),
                       Opt("elim",integer,"Set the expression limit for evaluation (default is 35)"),
                       Opt("llim",integer,"Set the gui interface's log area line limit (default is 600)")],
                       usage)
   main := if /opts["m"] then "main" else "ieval_main"
   imports := set()
   every insert(imports, !str2list(!\opts["i"]))
   links := \opts["l"] | []
   prog := create_prog(main, imports, links) | stop(&why)
   Prog.get_global(main, prog)(imports, a, opts)
end
