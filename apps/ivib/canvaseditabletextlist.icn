#
# $Id$
#
# This file is in the public domain.
#
# Author: Robert Parlett (parlett@dial.pipex.com)
#

import gui


############################################################################
#
# Class for representing a EditableTextList on the canvas.
#
class CanvasEditableTextList(CanvasComponent,EditableTextList)
   public override min_height()
      return scale(50)
   end
 
   public override min_width()
      return scale(50)
   end

   #
   # Generate code into Code object c.
   #
   public override gen_code(c)
      CanvasComponent.gen_code(c)
      c.chain("set_contents(" || contents_string(self.contents) || ")")
      if /self.is_editable_flag then
         c.chain("set_is_editable(&no)")
      if /self.move_on_rpress_flag then
         c.chain("set_move_on_rpress(&no)")
   end

   #
   # Duplicate object.
   #
   public override dup()
      local c
      c := CanvasEditableTextList()
      CanvasComponent.dup_fields(c)
      c.set_contents(copy(self.contents))
      c.is_editable_flag := self.is_editable_flag
      c.move_on_rpress_flag := self.move_on_rpress_flag
      return c
   end

   #
   # Return the I/O template.
   #
   public override get_template()
      suspend CanvasComponent.get_template() |
         ["Contents", "contents"] |
         ["Is editable flag", "is_editable_flag"] |
         ["Move on rpress flag", "move_on_rpress_flag"]
   end

   #
   # Open the configuration dialog box.
   #
   public override dialog()
      return CanvasEditableTextListDialog(self)
   end

   public override allows_null_height()
      return
   end

   public override allows_null_width()
      return
   end

   public override new()
      EditableTextList.new()
      CanvasComponent.new()
      self.class_name := "EditableTextList"
      self.import_name := "gui"
      self.name := "editable_text_list"
      return
   end
end

