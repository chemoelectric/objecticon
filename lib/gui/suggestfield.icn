package gui

import graphics, util, lang

class Conversion()
   public const
      str, pos

   public new(str, pos)
      self.str := str
      /pos := *str + 1
      self.pos := pos
      return
   end
end

# This class is a `TextField` which provides suggested completions in
# a popup list based on partial input.
#
abstract class SuggestField(TextField, MenuMode)
   public 
      popup,
      direction,
      max_height,
      max_width,
      select_using_enter_flag,
      popup_on_tab_flag,
      forward_home_end_flag     # Forward home/end key to list?

   protected
      req, 
      req_held,
      req_doing,
      last,
      mod,
      applied_event,
      list_data

   #
   # Set the direction to pop-up in.  Options are `Direction.UP`, `Direction.DOWN` or
   # `Direction.BEST` (the default).
   #
   public set_direction(s)
      self.direction := s
      return self
   end

   #
   # Forward home/end key to list?  Default is yes.
   #
   public set_forward_home_end(s)
      self.forward_home_end_flag := need_flag(s)
      return self
   end

   #
   # Only popup when the tab key is pressed.
   #
   public set_popup_on_tab(s)
      self.popup_on_tab_flag := need_flag(s)
      return self
   end

   #
   # If set, the enter key is used to select from the list, rather than items being selected
   # using the cursor keys.
   #
   public set_select_using_enter(s)
      self.select_using_enter_flag := need_flag(s)
      return self
   end

   public create_popup()
      return TextList()
   end

   #
   # Set the maximum height of the dropdown list; by default it is as large as is necessary
   # and can be accommodated on the screen.
   #
   public set_max_height(n)
      self.max_height := n
      return self
   end

   #
   # Set the maximum width of the dropdown list; by default it is as wide as is necessary
   # and can be accommodated on the screen.
   #
   public set_max_width(n)
      self.max_width := n
      return self
   end

   public on_popup_selection(e)
      synch_contents(e)
      member(Key.NAVIGATE, e.code) | close_popup()
   end

   public synch_contents(ev)
      apply_value(popup.object_get_selections()[1], ev, &yes)
   end

   public convert_element(o)
      return text(o)
   end

   private requests()
      local l
      repeat {
         until mod === 1 do {
            req_held := &yes
            req.sleep()
            req_held := &null
         }
         mod := &null
         req_doing := &yes
         l := do_request() | &null
         req_doing := &null
         # If finally() has been called, close any request resources,
         # and stop the task.
         if /cbwin then {
            close_request()
            req.revert()
         }
         if \l then {
            # If contents have changed, skip, and repeat request if
            # mod = 1, or go to sleep if mod = 2
            if \mod then
               next
            open_popup(l)
         }
      }
   end

   # This method is implemented to provide the list of suggestions, based on
   # the current contents of the textfield.  `fg` is a flag, indicating
   # whether we are called from the background task, or in the foreground,
   # (after the tab key is pressed).
   #
   protected abstract do_request(fg)

   # This method is called to abort a request in progress; do_request() should then
   # fail.
   #
   protected abort_request()
   end

   # This is invoked whilst `do_request` is not in progress to allow
   # any resources which remain open between `do_request` calls to be
   # closed.  Note that `do_request` may later be invoked again.
   #
   protected close_request()
   end

   public on_change(ev)
      if \popup_on_tab_flag then {
         if \select_using_enter_flag | not member(Key.NAVIGATE, ev.code) then
            close_all()
      } else if applied_event === ev then
         mod := 2
      else if *contents = 0 & ev.code ~=== "\t" then
         keep_closed()
      else {
         mod := 1
         # Wakeup req if waiting
         if \req_held then
            req.notify()
      }
   end

   protected open_popup(data)
      local max_h, max_w, spc_below, spc_above, spc_right, d, dim, win,
         use_direction, rs

      # If the list is already open with the same data, do nothing.
      # This avoids the flickering of opening and closing the popup.
      if equals(data, list_data) then
         return

      close_popup()

      # If no data, or the focus has moved away, leave closed.
      if (*data = 0) | (parent_dialog.get_focus() ~=== self) then
         return

      list_data := copy(data)
      win := parent_dialog.win
      dim := win.get_display_size()
      # Screen rectangle, in adjusted co-ordinates
      rs := Rect(-win.get_x() - win.get_dx(), -win.get_y() - win.get_dy(), dim.width, dim.height)

      # Space available for popup limited by screen size
      spc_above := self.y - rs.y
      spc_below := rs.y + rs.h - self.y - self.h
      spc_right := rs.x + rs.w - self.x

      max_h := case self.direction of {
         Direction.UP: spc_above
         Direction.DOWN: spc_below
         Direction.BEST: 
            if Gui.POPUP_LIST_DIRECTION_FACTOR * spc_below > spc_above then spc_below else spc_above
         default: runerr("Unknown direction", self.direction)
      }
      max_w := spc_right
      max_w >:= \self.max_width
      max_h >:= \self.max_height

      self.popup := create_popup().
         set_wattrib_ancestor(self).
         connect(on_popup_selection, Event.SELECTION_CHANGED).
         set_contents(data).
         set_accepts_focus(&no).
         set_draggable_cursor(&yes).
         set_motion_cursor(&yes).
         set_always_show_focus_cursor(&yes).
         set_select_mode(Select.ONE).
         set_z(self.parent_dialog.get_top_z())

      if /popup_on_tab_flag | /select_using_enter_flag then
         popup.set_cursor()

      if \select_using_enter_flag then
         popup.set_selection_on_key_moves(&no)

      self.parent_dialog.add(popup)
      self.popup.set_ideal_size(self.w, max_w,
                             scale(5), max_h)

      use_direction := if direction == Direction.BEST then {
         if spc_below >= (popup.h_spec | spc_above) then
            Direction.DOWN
         else
            Direction.UP
      } else
         direction

      self.parent_dialog.enter_menu_mode(self)

      popup.
         set_pos(win.get_dx() + x,
                 win.get_dy() + if use_direction == Direction.DOWN then 
                    y + h
                  else
                    y - popup.h_spec).
         resize()

      if Gui.allow_internal_popups() &
         parent_dialog.contains(popup) then {
         popup.invalidate()
      } else {
         # Remove popup from dialog and use add to popup window.
         parent_dialog.remove(popup)
         d := PureDialog().
            set_canvas(Canvas.POPUP).
            set_width(popup.w).
            set_height(popup.h).
            set_dx(-popup.x).
            set_dy(-popup.y).
            set_x(win.get_x() + win.get_dx() + popup.x).
            set_y(win.get_y() + win.get_dy() + popup.y).
            copy_wattrib(WAttrib.DISPLAY, parent_dialog).
            add(popup)

         popup.set_pos(0, 0)
         parent_dialog.add_popup(d)
         d.show()
      }
   end

   protected close_popup()
      \popup | fail
      if popup.parent_dialog === parent_dialog then {
         popup.invalidate()
         parent_dialog.remove(popup)
      } else {
         parent_dialog.remove_popup(popup.parent_dialog)
         popup.parent_dialog.dispose()
      }
      list_data := popup := &null
      parent_dialog.exit_menu_mode(self)
      fire(Event.POPUP_CLOSED)
   end

   public get_nesting_component()
      return self
   end

   public gen_popup_components()
      return popup
   end

   public allow_nested(c)
   end

   public close_all()
      keep_closed()
   end

   public should_close(e)
      succeed member(Mouse.PRESS, e.code) & not(popup.in_region(e))
   end

   # Close the textlist (if it is open), and stop any running
   # background request from re-opening it (until the next key
   # modification).
   #
   public keep_closed()
      close_popup()
      if \req_doing then
         abort_request()
      mod := &null
   end

   public initially()
      TextField.initially()
      mod := &null
      req.start()
   end

   public finally()
      if \req_doing then
         abort_request()
      else {
         close_request()
         req.stop()
      }
      close_popup()
      TextField.finally()
   end

   public keeps(e)
      succeed e.code === "\t" | member(Key.CURSOR_H, e.code)
   end

   public was_applied(ev)
      succeed ev === applied_event
   end

   public handle_return(e)
      if \select_using_enter_flag then
         (\popup).fire(Event.KEY_PRESS, e)
      TextField.handle_return(e)
      keep_closed()
   end

   public handle_key_up(e)
      (\popup).fire(Event.KEY_PRESS, e)
   end

   public handle_key_page_up(e)
      (\popup).fire(Event.KEY_PRESS, e)
   end

   public handle_key_page_down(e)
      (\popup).fire(Event.KEY_PRESS, e)
   end

   public handle_key_down(e)
      if \popup then {
         if popup.get_cursor() then
            popup.fire(Event.KEY_PRESS, e)
         else {
            popup.set_cursor(1)
            if /select_using_enter_flag then {
               popup.set_selections([1])
               synch_contents(e)
            }
         }
      }
   end

   public handle_key_home(e)
      if \popup & \forward_home_end_flag then
         popup.fire(Event.KEY_PRESS, e)
      else
         TextField.handle_key_home(e)
   end

   public handle_key_end(e)
      if \popup & \forward_home_end_flag then
         popup.fire(Event.KEY_PRESS, e)
      else
         TextField.handle_key_end(e)
   end

   # This method may be overridden to return a non-empty string to
   # insert before the popup is opened.  `l` is the list of
   # completions returned by `do_request`.
   #
   public get_insertion(l)
   end

   public handle_tab(ev)
      local l, o
      if \popup then {
         if \select_using_enter_flag & o := popup.object_get_cursor() then
            apply_value(o, ev)
         else {
            if o := get_insertion(list_data) then
               apply_value(o, ev)
         }
      } else {
         l := do_request(&yes) | fail
         if o := get_insertion(l) then {
            apply_value(o, ev)
            if *l > 1 then
               open_popup(l)
         } else
            open_popup(l)
      }
   end

   public apply_value(v, ev, apply)
      local p
      v := convert_element(v) | contents[1:1]
      if is(v, Conversion) then {
         p := v.pos
         v := v.str
      }
      if /apply then
         assign_contents(v, p, ev)
      else
         assign_contents(v, p, applied_event := ev, toggle_flag(select_using_enter_flag))
   end

   public handle_key(e)
      case e.code of {
         "\t" : handle_tab(e)
         "\e" : close_all()
         Key.UP : handle_key_up(e)
         Key.DOWN : handle_key_down(e)
         Key.PGUP : handle_key_page_up(e)
         Key.PGDN : handle_key_page_down(e)
         default:
            TextField.handle_key(e)
      }
   end

   public new()
      TextField.new()
      connect(on_change, Event.CONTENT_CHANGED)
      req := Dispatcher.new_task{requests()}
      self.direction := Direction.BEST
      self.forward_home_end_flag := &yes
      return
   end
end
