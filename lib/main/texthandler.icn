#
# $Id$
#

package mail

import util, io, ipl.strings

class TextHandler(TypeHandler)
   public can_handle(ct)
      succeed map(ct.get_type()) == "text"
   end
   
   public convert_to_object(m, data)
      local res

      #
      # Convert line endings to local form
      #
      res := ""
      every res ||:= separate_lines(data) || Files.EOL
      res[-*Files.EOL:0] := ""

      if map(m.get_content_type().get_parameter("charset")) == "utf-8" then
         res := ucs(res) | return error("TextHandler: raw data not in UTF-8 format")

      return res
   end

   public convert_from_object(m, obj)
      local res

      obj := string(obj) | return error("TextHandler: Couldn't convert object to string: " || image(obj))

      #
      # Convert line endings to canonical form (CRLF)
      #
      res := ""
      every res ||:= separate_lines(obj) || "\r\n"
      res[-2:0] := ""

      return res
   end
end
