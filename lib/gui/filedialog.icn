package gui

import 
   ipl.wildcard,
   ipl.sort,
   ipl.filegui,
   ipl.pdco,
   ipl.functional,
   util,
   io,
   graphics,
   ipl.strings

class FileDialogPos()
   public const
      cursor_name,
      area_y

   public new(cursor_name, area_y)
      self.cursor_name := cursor_name
      self.area_y := area_y
      return
   end
end

abstract class EditListField(FileSuggestField)
   public keeps(e)
      return
   end

   public on_key(e)
      if /tl then case e.code of {
         Key.UP : parent.go_up(e)
         Key.DOWN : parent.go_down(e)
      }
   end

   public on_change(ev)
      # Don't start a suggestion on navigation of the list.
      parent.was_applied(ev) | SuggestField.on_change(ev)
   end

   public new()
      FileSuggestField.new()
      connect(self.on_key, Event.KEY_PRESS)
      set_border(EmptyBorder().set_l_inset(Gui.TEXT_INSET).set_r_inset(Gui.TEXT_INSET))
      set_select_using_enter(&yes)
      return
   end
end

class DirEditListField(EditListField)
   public new()
      EditListField.new()
      set_directories_only(&yes)
      return
   end
end

class FileEditListField(EditListField)
   protected do_request()
      # Don't popup if an existing file warning is in place.
      # Returning [] rather than failing causes the popup to close,
      # rather than stay open.
      if \parent_dialog.warned then
         return []
      return FileSuggestField.do_request()
   end
end

class DirEditList(EditList)
   public create_popup()
      return ItemPaintList().
         set_item_paint(StringDirItemPaint()).
         set_item_border(EmptyBorder().
                            set_insets(Gui.TEXT_INSET, Gui.TEXT_INSET, 0, 0).
                            set_x_align(Align.L))
   end

   public create_view()
      return DirEditListField()
   end
end

class FileEditList(EditList)
   public layout()
      # Hide the button if the init_files list was empty.
      if *selection_list = 0 then {
         view.x := x + border.get_l_inset()
         view.y := y + border.get_t_inset()
         view.w := w - border.get_total_width()
         view.h := h - border.get_total_height()
         b.x := x
         b.y := y
         b.w := b.h := 0
         b.layout()
         view.layout()
      } else
         EditList.layout()
   end

   public create_view()
      return FileEditListField()
   end
end

class DirList(ButtonOnlyList)
   public create_button()
      local b
      b := IconButton().set_paint(ImagePaint().set_cache("gui.FAVOURITES_24"))
      theme_toolbar_button(b, "emblem-favorite")
      return b
   end

   public create_popup()
      return ItemPaintList().
         set_item_paint(StringDirItemPaint()).
         set_item_border(EmptyBorder().
                            set_insets(Gui.TEXT_INSET, Gui.TEXT_INSET, 0, 0).
                            set_x_align(Align.L))
   end
end

#
#
# File dialog class.  This class provides a standard file dialog.
#
# @example
# @ d := FileDialog()
# @ d.show_modal()
# @ write(d.get_result() | "cancelled")
#
class FileDialog(Dialog)
   public
      show_hidden_menu_item,
      tools_menu_button,
      tools_menu,
      date_button,
      name_button,
      size_button,
      home_button, 
      prev_button,
      next_button,
      favourites_list,
      trail,
      up_button, 
      centre_panel,
      #dir, 
      dlist, 
      flist, 
      ascending,
      sorter,
      history,
      history_pos,
      pos_table,
      loaded_dir_path,
      loaded_dir_list,
      cancel_button, 
      file, 
      filter, 
      okay_button,
      init_dirs,                # Initial directory name
      init_files,               # Initial file name
      init_filters,
      res,                      # Resulting file path          
      message_label, 
      existing_file_mode,
      multiple_files_flag,
      single_file_overlay_item,
      multiple_files_overlay_item,
      file_overlay,
      file_label,
      img,
      img_label,
      warned

   public static const
      WARN, IGNORE, REQUIRE, DISALLOW
   
   private static init()
      WARN := "warn"
      IGNORE := "ignore"
      REQUIRE := "require"
      DISALLOW := "disallow"
   end

   public set_existing_file_mode(s)
      existing_file_mode := s
      return self
   end

   public set_multiple_files(s)
      self.multiple_files_flag := check_flag(s)
      return self
   end

   public static ucs_list(l)
      return ipl.pdco.List{ucs(!l)}
   end

   # Helper method to take a list of files and filter non-ucs files,
   # and duplicates.  Duplicate and trailing slashes are also cleaned
   # up (through use of `FilePath`).
   #
   public static unique_ucs_list(l)
      local res, seen, e, k
      res := []
      seen := set()
      every e := FilePath(ucs(!l)) do {
         k := e.standard_case().str()
         member(seen, k) | {
            insert(seen, k)
            put(res, e.ucs())
         }
      }
      return res
   end

   public component_setup()
      #
      # Defaults and extra dirs.
      #
      /init_dirs := []
      /init_files := []
      /init_filters := [u"*"]
      favourites_list.set_selection_list(get_favourites_directories())
      filter.set_selection_list(ucs_list(init_filters)).set_selection(1)
      if /multiple_files_flag then {
         file.set_selection_list(ucs_list(init_files)).set_selection(1)
         flist.set_select_mode(Select.ONE)
         file_overlay.set_which_one(single_file_overlay_item)
      } else {
         flist.set_select_mode(Select.MANY)
         file_overlay.set_which_one(multiple_files_overlay_item)
      }
      pos_table := table()
      res := &null
   end

   public init_dialog()
      if /multiple_files_flag then
         file.grab_focus()
      win.set_min_size(win.get_width(), win.get_height())
      goto_dir(favourites_list.selection_list[1])
   end

   public check_existing()
      local f
      /multiple_files_flag | fail
      f := get_current_selection().str()
      warned := &null
      if \f then {
         if Files.is_directory(f) then
            msg("Directory")
         else if existing_file_mode === (WARN | DISALLOW) & Files.access(f) then {
            warned := &yes
            msg("Existing file")
         }
      }
      if \img then {
         if endswith(map(\f), ".gif" | ".jpg" | ".jpeg" | ".png") then
            img.set_image(f)
         else
            img.set_image()
         if \img.win then
            img_label.set_label(".cell\nHeight: " || img.win.get_height() || "\n.br\n" ||
                                "Width: " || img.win.get_width() || "\n.br\n")
         else
            img_label.set_label("")
      }
   end

   public on_file_content_changed(ev)
      msg()
      check_existing()
      synch_file()
   end

   public synch_file()
      local i
      if /multiple_files_flag then {
         if i := Positions{(!flist.contents).name, file.get_contents()} then {
            flist.
               set_cursor(i).
               set_selections([i]).
               center_line(i)
         } else
            flist.clear_selections()
      } else {
         flist.clear_selections()
         update_file_label()
      }
   end

   public end_dialog()
   end

   #
   # Get the directory part of the result.  In multiple files mode,
   # this returns the directory which is common to each of the
   # selected files.  The result is always a string, never ucs.
   #
   public get_directory()
      \self.res | fail
      return if /multiple_files_flag then
         res.parent().str()
      else
         loaded_dir_path.str()
   end

   #
   # Get the file part of the result; the result is always a string,
   # never ucs. Not applicable in multiple files mode.
   #
   public get_file()
      return string((\self.res).get(-1))
   end

   #
   # Get the result, (will fail if cancel was pressed).  This will
   # return a non-empty list in multiple files mode.
   #
   public get_result()
      \self.res | fail
      return if /multiple_files_flag then
         res.str()
      else
         ipl.pdco.List{(!res).str()}
   end

   #
   # Get the result, as a `FilePath` (will fail if cancel was
   # pressed).  This will return a non-empty list of `FilePath`s in
   # multiple files mode.
   #
   public get_result_path()
      return \self.res
   end

   #
   # Keep the directory last selected by this dialog.
   #
   public keep_directory()
      return set_directory(get_dir_path().str())
   end

   #
   # Set the initial directory.  It should be in canonical form.
   #
   public set_directory(s)
      self.init_dirs := [s]
      return self
   end

   #
   # Set the initial directories as a list; the first is the one
   # initially shown.  Each entry should be in canonical form.
   #
   public set_directories(l)
      self.init_dirs := l
      return self
   end

   #
   # Set the initial file
   #
   public set_file(s)
      self.init_files := [s]
      return self
   end

   #
   # Set the initial files
   #
   public set_files(l)
      self.init_files := l
      return self
   end

   #
   # Set the file filter pattern list.
   #
   public set_filters(l)
      self.init_filters := l
      return self
   end

   #
   # Set the initial file/directory from a whole path.
   # 
   public set_path(s)
      local l
      l := FilePath(s).canonical()
      self.init_dirs := [l.parent().str()]
      self.init_files := [l.get(-1)]
      return self
   end

   #
   # Get the current file selected based on the file and dir fields;
   # do not call in multiple file selection mode.
   #
   public get_current_selection()
      local s, f
      s := file.get_contents()
      if *s = 0 then
         fail
      f := FilePath(s).absolute(loaded_dir_path)
      return f.canonical()
   end

   public on_okay_button()
      local f, fs, sel
      if /multiple_files_flag then {
         f := get_current_selection() | {
            msg("Please enter a filename")
            fail
         }
         fs := f.str()
         if Files.is_directory(fs) then {
            msg("Selected file is a directory")
            fail
         }
         case existing_file_mode of {
            REQUIRE: {
               if not Files.access(fs) then {
                  msg("Selected file doesn't exist")
                  fail
               }
            }
            DISALLOW: {
               if Files.access(fs) then {
                  msg("Selected file already exists")
                  fail
               }
            }
         }
         self.res := f
      } else {
         sel := flist.object_get_selections()
         if *sel = 0 then {
            msg("Please select a file")
            fail
         }
         self.res := ipl.pdco.List{loaded_dir_path.child((!sel).name)}
      }
      self.dispose()
   end

   public get_dir_path()
      return loaded_dir_path
   end

   public on_cancel_button()
      self.dispose()
   end

   public on_file_action(ev)
      local f
      if file.view.was_applied(ev) then
         fail
      if Files.is_directory(f := get_current_selection().str()) then {
         file.set_contents(u"")
         goto_dir(f)
      }
   end

   public on_dlist()
      goto_dir(loaded_dir_path.child(dlist.object_get_selections()[1].name).str())
   end

   public update_file_label()
      local n
      file_label.set_label(case n := *flist.get_selections() of {
         0:  "No files selected"
         1:  "One file selected"
         default:  n || " files selected"
      })
   end

   public on_flist()
      #
      # Clicked in file list; set TextField
      #
      msg()
      if /multiple_files_flag then
         file.set_contents(flist.object_get_selections()[1].name)
      else
         update_file_label()
      check_existing()
   end

   public update_filter()
      refresh_dir()
      file.view.set_filter(filter.get_contents())
   end

   public on_filter_action(ev)
      update_filter()
   end

   public on_filter_content_changed(ev)
      if filter.was_applied(ev) then
         update_filter()
   end

   public on_show_hidden_menu_item(ev)
      pos_table := table()
      refresh_dir()
   end

   public on_up_button(ev)
      if loaded_dir_path.size() > 1 then
         goto_dir(loaded_dir_path.parent().str())
      else
         msg("At root")
   end

   public on_home_button(ev)
      goto_dir(Files.get_home())
   end

   public on_refresh_menu_item(ev)
      refresh_dir(&yes)
   end

   public on_prev_button(ev)
      local s
      (history_pos < *history) | fail
      s := history[history_pos +:= 1]
      update_grey()
      goto_dir(s, &yes)
   end
   
   public on_next_button(ev)
      local s
      (history_pos > 1) | fail
      s := history[history_pos -:= 1]
      update_grey()
      goto_dir(s, &yes)
   end

   public update_grey()
      prev_button.set_is_shaded(No{ history_pos < *history })
      next_button.set_is_shaded(No{ history_pos > 1 })
   end

   public on_new_dir_menu_item(ev)
      local d, s
      s := loaded_dir_path.ucs() | return msg("Path not UTF-8")
      d := NewDirectoryDialog(s).
         show_modal(self)
      goto_dir(\d.result)
   end

   public get_favourites_directories()
      local t
      t := ipl.pdco.List{ FilePath(!init_dirs).canonical().str() |
                          FavouriteDirectoriesDialog.gen_directories_plus() }
      return unique_ucs_list(t)
   end

   public on_favourite_dirs_menu_item(ev)
      local d
      d := FavouriteDirectoriesDialog()
      d.show_modal(self)
      \d.okay | fail
      favourites_list.set_selection_list(get_favourites_directories())
      synch_favourites_list()
   end

   private synch_favourites_list()
      favourites_list.object_set_selection(loaded_dir_path.ucs()) | favourites_list.set_selection()
   end

   public get_pats()
      local pat, pats
      pats := []
      filter.get_contents() ? repeat {
         pat := tab(upto(';') | 0)
         put(pats, Wildcard(pat))
         move(1) | break
      }
      return pats
   end

   public msg(s)
      message_label.set(s)
   end

   public refresh_dir(load)
      save_pos()
      msg()
      if \load then
         load_dir_list()
      annotate_dir_list()
      set_lists(sort_dir_list())
      restore_pos()
   end

   public save_pos()
      insert(pos_table,
             (\loaded_dir_path).str(),
             FileDialogPos(dlist.object_get_cursor().name, dlist.get_area_y()))
   end

   public restore_pos()
      local e, i
      if e := member(pos_table, loaded_dir_path.str()) then {
         if i := Positions{(!dlist.contents).name, e.cursor_name} then
            dlist.set_cursor(i)
         dlist.set_area_y(e.area_y)
      }
   end

   public goto_dir(d, nav)
      loaded_dir_path := FilePath(d).canonical().ucs_path() | fail
      save_pos()
      trail.set_path(loaded_dir_path.ucs())
      synch_favourites_list()
      msg()
      file.view.set_cwd(loaded_dir_path)
      load_dir_list()
      annotate_dir_list()
      set_lists(sort_dir_list())
      restore_pos()
      if /nav then {
         every 1 to history_pos - 1 do
            pop(history)
         push(history, loaded_dir_path.ucs())
         history_pos := 1
         update_grey()
      }
      return self
   end

   public set_lists(l)
      #
      # Update directory and file lists.
      #
      dlist.
         set_contents(l[1]).
         set_area_x(0).
         set_area_y(0).
         clear_selections().
         set_cursor(1)

      flist.
         set_contents(l[2]).
         set_area_x(0).
         set_area_y(0).
         clear_selections().
         set_cursor(1)

      synch_file()
      check_existing()
   end

   public load_dir_list()
      (loaded_dir_list := Files.ucs_list(loaded_dir_path.str(), AnnotatedListEntryWithIcon)) | {
         msg(&why)
         loaded_dir_list := []
      }
   end

   public annotate_dir_list()
      local e, now
      now := Time.get_system_seconds()
      every e := !loaded_dir_list do {
         if e.stat.mode_str[1] == "d" then
            e.annotate_name()
         else case sorter of {
            Files.name_cmp : e.annotate_name()
            Files.date_cmp : e.annotate_date(now)
            Files.size_cmp : e.annotate_size()
            default : syserr("Bad sorter_opt")
         }
      }
   end

   public sort_dir_list()
      local e, dir_list, file_list, pats, n
      pats := get_pats()
      dir_list := []
      file_list := []
      every e := !loaded_dir_list do {
         n := e.name
         if show_hidden_menu_item.is_checked() | not(Files.is_hidden(n)) then {
            if e.stat.mode_str[1] == "d" then
               Files.is_relative_dir(n) | put(dir_list, e)
            else if (!pats).match(n) = *n + 1 then
               put(file_list, e)
         }
      }
      qsort(file_list, if /ascending then sorter else flip(sorter))
      qsort(dir_list, Files.name_cmp)
      return [dir_list, file_list]
   end

   #
   # Add an image preview tab to the dialog.  This should only be
   # called before the dialog is shown.
   #
   public add_image_preview()
      local p, split, other, q
      if is_initialized() then
         fail
      if /img then {
         img := Image().
            set_constraint(Grid.EOL, &yes).
            set_border(NullBorder())
         img_label := Label().
            set_label("").
            set_constraint(Grid.X_FILL, &yes).
            set_height(scale(100)).
            set_border(NullBorder())
         p := Panel().
            set_layout(GridLayout()).
            add(img).
            add(img_label)
         q := Border().
            set_content(p).
            set_width(scale(175)).
            set_border(SunkenBorder())
         other := centre_panel.children[1]
         centre_panel.remove_index(1)
         split := Split().
            set_left(other).
            set_right(q).
            set_weight(1.0).
            set_min(scale(250), scale(130))
         centre_panel.add(split)
      }
      return self
   end

   #
   # Remove the image preview tab to the dialog.  This should only be
   # called before the dialog is shown.
   #
   public remove_image_preview()
      local split
      if is_initialized() then
         fail
      if \img then {
         split := centre_panel.children[1]
         centre_panel.remove_index(1).add(split.first)
         img := img_label := &null
      }
      return self
   end

   private on_trail(ev)
      goto_dir(ev)
   end

   public new()
      Dialog.new()
      self.setup()
      tools_menu.set_border_mid(NullBorder())
      # Try themed icon buttons
      theme_toolbar_button(prev_button, "go-previous")
      theme_toolbar_button(next_button, "go-next")
      theme_toolbar_button(home_button, "go-home")
      theme_toolbar_button(up_button, "go-up")
      theme_toolbar_button(tools_menu_button, "applications-system")

      trail.
         set_height().
         set_constraint("l_inset", scale(10)).
         connect(on_trail, FileTrail.DIR_CHANGED_EVENT)
      favourites_list.set_size()

      dlist.
         set_draggable_cursor(&yes).
         set_selection_on_key_moves(&no).
         set_item_paint(IconFilesItemPaint()).
         set_item_border(EmptyBorder().
                             set_insets(Gui.TEXT_INSET, Gui.TEXT_INSET, 0, 0).
                             set_x_align(Align.L))
      flist.
         set_draggable_cursor(&yes).
         set_item_paint(AnnotatedIconFilesItemPaint()).
         set_item_border(EmptyBorder().
                             set_insets(Gui.TEXT_INSET, Gui.TEXT_INSET, 0, 0).
                             set_x_align(Align.L))
      sorter := Files.name_cmp
      synch_paints()
      existing_file_mode := IGNORE
      history := []
      history_pos := 0
      update_grey()
      return
   end

   private synch_paints()
      local cp, np, sp, dp, sz
      sz := Gui.TOOLBAR_ICON_SIZE
      cp := CompoundPaint().set_top(ImagePaint().set_cache(if /ascending then "gui.TINY_ARROW_DOWN" else "gui.TINY_ARROW_UP"))
      np := ImagePaint().set_cache(ImageCache.get_scaled_svg_key_alt("gui.FONT_SVG", "gui.FONT_24", sz, sz))
      sp := ImagePaint().set_cache(ImageCache.get_scaled_svg_key_alt("gui.FILESIZE_SVG", "gui.FILESIZE_24", sz, sz))
      dp := ImagePaint().set_cache(ImageCache.get_scaled_svg_key_alt("gui.CLOCK_SVG", "gui.CLOCK_24", sz, sz))
      case sorter of {
         Files.name_cmp: {
            name_button.set_paint(cp.set_bottom(np))
            date_button.set_paint(dp)
            size_button.set_paint(sp)
         }
         Files.size_cmp: {
            name_button.set_paint(np)
            date_button.set_paint(dp)
            size_button.set_paint(cp.set_bottom(sp))
         }
         Files.date_cmp: {
            name_button.set_paint(np)
            date_button.set_paint(cp.set_bottom(dp))
            size_button.set_paint(sp)
         }
      }
   end

   private on_date_button(ev)
      if sorter === Files.date_cmp then
         ascending := toggle_flag(ascending)
      else
         sorter := Files.date_cmp
      synch_paints()
      refresh_dir()
   end

   private on_name_button(ev)
      if sorter === Files.name_cmp then
         ascending := toggle_flag(ascending)
      else
         sorter := Files.name_cmp
      synch_paints()
      refresh_dir()
   end

   private on_size_button(ev)
      if sorter === Files.size_cmp then
         ascending := toggle_flag(ascending)
      else
         sorter := Files.size_cmp
      synch_paints()
      refresh_dir()
   end

   private on_favourites_list(ev)
      goto_dir(favourites_list.object_get_selection())
   end

   private setup()
      local paint_2, layout_7, paint_14, paint_45, layout_11, layout_4, paint_11, paint_37, paint_42, refresh_menu_item, panel_4, paint_29, split_content_1, split_content_2, paint_9, split_1, paint_31, paint_49, paint_3, layout_8, paint_15, paint_46, layout_12, paint_12, paint_43, new_dir_menu_item, paint_35, panel_2, paint_27, paint_32, favourite_dirs_menu_item, layout_9, paint_47, paint_1, layout_6, paint_13, paint_44, layout_10, layout_3, paint_10, paint, panel_3, paint_28, paint_30, paint_48
      self.set_label("Select file")
      self.set_resize(&yes)
      paint_45 := ImagePaint().
         set_cache("gui.CONFIGURE_24")
      paint_42 := TextPaint("New directory...")
      new_dir_menu_item := TextMenuItem().
         connect(on_new_dir_menu_item, Event.ACTION).
         set_paint_mid(paint_42)
      paint_43 := TextPaint("Favourite dirs...")
      favourite_dirs_menu_item := TextMenuItem().
         connect(on_favourite_dirs_menu_item, Event.ACTION).
         set_paint_mid(paint_43)
      paint_44 := TextPaint("Show hidden files")
      show_hidden_menu_item := CheckBoxMenuItem().
         connect(on_show_hidden_menu_item, Event.ACTION).
         set_paint_mid(paint_44)
      paint_46 := TextPaint("Refresh")
      refresh_menu_item := TextMenuItem().
         connect(on_refresh_menu_item, Event.ACTION).
         set_paint_mid(paint_46)
      tools_menu := Menu().
         set_paint_mid(paint_45).
         add(new_dir_menu_item).
         add(favourite_dirs_menu_item).
         add(show_hidden_menu_item).
         add(refresh_menu_item)
      paint_47 := TextPaint("Tools")
      tools_menu_button := MenuButton().
         clear_constraints().
         set_tooltip(paint_47).
         set_menu(tools_menu)
      paint_1 := ImagePaint().
         set_cache("gui.HOME_24")
      paint := TextPaint("Home")
      home_button := IconButton().
         set_align(Align.L, Align.C).
         clear_constraints().
         set_tooltip(paint).
         connect(self.on_home_button, Event.ACTION).
         set_no_click_focus(&yes).
         set_toggles(&no).
         set_paint(paint_1)
      paint_3 := ImagePaint().
         set_cache("gui.UP_24")
      paint_2 := TextPaint("Up")
      up_button := IconButton().
         set_align(Align.L, Align.C).
         clear_constraints().
         set_tooltip(paint_2).
         connect(self.on_up_button, Event.ACTION).
         set_no_click_focus(&yes).
         set_toggles(&no).
         set_paint(paint_3)
      paint_12 := ImagePaint().
         set_cache("gui.PREV_24")
      paint_13 := TextPaint("Prev")
      prev_button := IconButton().
         set_align(Align.L, Align.C).
         clear_constraints().
         set_tooltip(paint_13).
         connect(self.on_prev_button, Event.ACTION).
         set_no_click_focus(&yes).
         set_toggles(&no).
         set_paint(paint_12)
      paint_14 := ImagePaint().
         set_cache("gui.NEXT_24")
      paint_15 := TextPaint("Next")
      next_button := IconButton().
         set_align(Align.L, Align.C).
         clear_constraints().
         set_tooltip(paint_15).
         connect(self.on_next_button, Event.ACTION).
         set_no_click_focus(&yes).
         set_toggles(&no).
         set_paint(paint_14)
      paint_28 := ImagePaint().
         set_cache("gui.FONT_24")
      paint_27 := TextPaint("Sort by name")
      name_button := IconButton().
         set_align(Align.L, Align.C).
         clear_constraints().
         set_tooltip(paint_27).
         connect(self.on_name_button, Event.ACTION).
         set_no_click_focus(&yes).
         set_toggles(&no).
         set_paint(paint_28)
      paint_30 := ImagePaint().
         set_cache("gui.CLOCK_24")
      paint_29 := TextPaint("Sort by date")
      date_button := IconButton().
         set_align(Align.L, Align.C).
         clear_constraints().
         set_tooltip(paint_29).
         connect(self.on_date_button, Event.ACTION).
         set_no_click_focus(&yes).
         set_toggles(&no).
         set_paint(paint_30)
      paint_32 := ImagePaint().
         set_cache("gui.FILESIZE_24")
      paint_31 := TextPaint("Sort by size")
      size_button := IconButton().
         set_align(Align.L, Align.C).
         clear_constraints().
         set_tooltip(paint_31).
         connect(self.on_size_button, Event.ACTION).
         set_no_click_focus(&yes).
         set_toggles(&no).
         set_paint(paint_32)
      paint_49 := TextPaint("Favourite directories")
      favourites_list := DirList().
         set_size(scale(37), scale(30)).
         clear_constraints().
         set_tooltip(paint_49).
         connect(self.on_favourites_list, Event.SELECTION_CHANGED)
      trail := FileTrail().
         set_size(scale(200), scale(20)).
         clear_constraints().
         set_constraint(Grid.X_ALIGN, Align.L).
         set_constraint(Grid.X_FILL, &yes).
         set_constraint(Grid.X_WEIGHT, 1.0).
         set_constraint(Grid.Y_FILL, &yes).
         set_constraint(Grid.Y_WEIGHT, 1.0)
      layout_6 := GridLayout().
         set_doi(scale(0)).
         set_dii(scale(2)).
         set_extra("cells")
      panel_4 := Panel().
         clear_constraints().
         set_constraint(Grid.EOL, &yes).
         set_constraint(Grid.X_FILL, &yes).
         set_constraint(Grid.X_WEIGHT, 1.0).
         set_layout(layout_6).
         add(tools_menu_button).
         add(home_button).
         add(up_button).
         add(prev_button).
         add(next_button).
         add(name_button).
         add(date_button).
         add(size_button).
         add(favourites_list).
         add(trail)
      self.add(panel_4)
      dlist := ItemPaintList().
         set_size(scale(210), scale(200)).
         clear_constraints().
         set_constraint(Grid.R_INSET, scale(0)).
         set_constraint(Grid.X_FILL, &yes).
         set_constraint(Grid.X_WEIGHT, 1.0).
         set_constraint(Grid.Y_FILL, &yes).
         set_constraint(Grid.Y_WEIGHT, 1.0).
         connect(self.on_dlist, Event.SELECTION_CHANGED).
         set_select_mode(Select.ONE).
         set_contents([])
      layout_8 := GridLayout().
         set_doi(scale(0)).
         set_extra("cells")
      split_content_1 := Component().
         clear_constraints().
         set_layout(layout_8).
         add(dlist)
      flist := ItemPaintList().
         set_size(scale(465), scale(365)).
         clear_constraints().
         set_constraint(Grid.L_INSET, scale(0)).
         set_constraint(Grid.X_FILL, &yes).
         set_constraint(Grid.X_WEIGHT, 1.0).
         set_constraint(Grid.Y_FILL, &yes).
         set_constraint(Grid.Y_WEIGHT, 1.0).
         connect(self.on_flist, Event.SELECTION_CHANGED).
         set_select_mode(Select.ONE).
         set_contents([])
      layout_9 := GridLayout().
         set_doi(scale(0)).
         set_extra("cells")
      split_content_2 := Component().
         clear_constraints().
         set_layout(layout_9).
         add(flist)
      split_1 := Split().
         clear_constraints().
         set_constraint(Grid.X_FILL, &yes).
         set_constraint(Grid.X_WEIGHT, 1.0).
         set_constraint(Grid.Y_FILL, &yes).
         set_constraint(Grid.Y_WEIGHT, 1.0).
         set_left(split_content_1).
         set_right(split_content_2).
         set_min(scale(100), scale(100))
      layout_12 := GridLayout().
         set_doi(scale(0)).
         set_extra("cells")
      centre_panel := Panel().
         clear_constraints().
         set_constraint(Grid.EOL, &yes).
         set_constraint(Grid.X_FILL, &yes).
         set_constraint(Grid.X_WEIGHT, 1.0).
         set_constraint(Grid.Y_FILL, &yes).
         set_constraint(Grid.Y_WEIGHT, 1.0).
         set_layout(layout_12).
         add(split_1)
      self.add(centre_panel)
      paint_35 := TextPaint("Filter")
      filter := EditList().
         set_size(scale(150)).
         set_align(Align.L, Align.B).
         clear_constraints().
         set_constraint(Grid.X_ALIGN, Align.L).
         set_constraint(Grid.X_FILL, &yes).
         set_tooltip(paint_35).
         connect(self.on_filter_action, Event.ACTION).
         connect(self.on_filter_content_changed, Event.CONTENT_CHANGED).
         set_selection_list([""])
      layout_10 := GridLayout().
         set_doi(scale(0)).
         set_extra("cells")
      paint_37 := TextPaint("File")
      file := FileEditList().
         set_size(scale(150)).
         set_align(Align.L, Align.B).
         clear_constraints().
         set_constraint(Grid.X_ALIGN, Align.L).
         set_constraint(Grid.X_FILL, &yes).
         set_constraint(Grid.X_WEIGHT, 1.0).
         set_tooltip(paint_37).
         connect(self.on_file_action, Event.ACTION).
         connect(self.on_file_content_changed, Event.CONTENT_CHANGED).
         set_selection_list([""])
      single_file_overlay_item := OverlayItem().
         set_layout(layout_10).
         add(file)
      layout_11 := GridLayout().
         set_doi(scale(0)).
         set_extra("cells")
      paint_48 := TextPaint("Label")
      file_label := Label().
         clear_constraints().
         set_constraint(Grid.X_ALIGN, Align.L).
         set_constraint(Grid.X_FILL, &yes).
         set_constraint(Grid.X_WEIGHT, 1.0).
         set_paint(paint_48)
      multiple_files_overlay_item := OverlayItem().
         set_layout(layout_11).
         add(file_label)
      file_overlay := OverlaySet().
         clear_constraints().
         set_constraint(Grid.EOL, &yes).
         set_constraint(Grid.X_FILL, &yes).
         set_constraint(Grid.X_WEIGHT, 1.0).
         set_constraint(Grid.Y_FILL, &yes).
         set_constraint(Grid.Y_WEIGHT, 1.0).
         add(single_file_overlay_item).
         add(multiple_files_overlay_item).
         set_which_one(single_file_overlay_item)
      layout_3 := GridLayout().
         set_doi(scale(0)).
         set_extra("cells")
      panel_2 := Panel().
         clear_constraints().
         set_constraint(Grid.EOL, &yes).
         set_constraint(Grid.X_FILL, &yes).
         set_constraint(Grid.X_WEIGHT, 1.0).
         set_layout(layout_3).
         add(filter).
         add(file_overlay)
      self.add(panel_2)
      paint_9 := TextPaint("Okay")
      okay_button := TextButton().
         set_align(Align.L, Align.B).
         clear_constraints().
         connect(self.on_okay_button, Event.ACTION).
         set_toggles(&no).
         set_paint(paint_9)
      paint_10 := TextPaint("Cancel")
      cancel_button := TextButton().
         set_align(Align.L, Align.B).
         clear_constraints().
         connect(self.on_cancel_button, Event.ACTION).
         set_toggles(&no).
         set_paint(paint_10)
      paint_11 := TextPaint("")
      message_label := MessageLabel().
         set_size(scale(130)).
         set_align(Align.L, Align.B).
         set_fg("red").
         clear_constraints().
         set_constraint(Grid.X_ALIGN, Align.L).
         set_constraint(Grid.X_FILL, &yes).
         set_constraint(Grid.X_WEIGHT, 1.0).
         set_paint(paint_11)
      layout_4 := GridLayout().
         set_doi(scale(0)).
         set_extra("cells")
      panel_3 := Panel().
         clear_constraints().
         set_constraint(Grid.X_FILL, &yes).
         set_constraint(Grid.X_WEIGHT, 1.0).
         set_layout(layout_4).
         add(okay_button).
         add(cancel_button).
         add(message_label)
      self.add(panel_3)
      layout_7 := GridLayout().
         set_extra("cells")
      self.set_layout(layout_7)
   end
end

### Ivib-v2 layout: layouts/$.layout
