############################################################################
#
#	File:     xformimg.icn
#
#	Subject:  Procedures to transform image
#
#	Author:   Ralph E. Griswold
#
#	Date:     February 4, 1995
#
############################################################################
#
#   This file is in the public domain.
#
############################################################################
#
#  These procedures perform reflections, rotations, and concatenations
#  of images.
#
#  Warning:  Some of these operations are slow.
#
############################################################################
#
#  Requires:  Version 9 graphics
#
############################################################################
#
#  Links:  numbers, wattrib, wopen
#
############################################################################

package ipl.xformimg

import
   graphics(Window),
   io(stop)

procedure wreflect(win1, dir)
   local win2,  x2,  y2, width, height

   /dir := "v"				# vertical reflection is the default

   height := win1.get_height()
   width := win1.get_width()

   win2 := Window.open().set_size(width, height) |
      stop("*** cannot window for reflection")

   case dir of {
      "h":  {
         x2 := 0
         y2 := height - 1
         every win2.set_fg(!win1.get_pixels()) do {
            win2.draw_point(x2, y2)
            if x2 = width - 1 then {
               x2 := 0
               y2 -:= 1
               }
            else x2 +:= 1
            }
         }
      "v":  {
         x2 := width - 1
         y2 := 0
         every win2.set_fg(!win1.get_pixels()) do {
            win2.draw_point(x2, y2)
            if x2 = 0 then {
               x2 := width - 1
               y2 +:= 1
               }
            else x2 -:= 1
            }
         }
      default:  runerr("*** invalid specification for reflect()", dir)
      }

   return win2

end

procedure wrotate(win1, dir)
   local win2,  x2,  y2, width, height

   /dir := "90"				# 90-degree rotation is the default

   height := win1.get_height()
   width := win1.get_width()


   case integer(dir) of {
      90:  {
         x2 := height - 1
         y2 := 0
         win2 := Window.open().set_size(height, width) | stop("*** cannot open target window")
         every win2.set_fg(!win1.get_pixels()) do {
            win2.draw_point(x2, y2)
            if y2 = width - 1 then {
               y2 := 0
               x2 -:= 1
               }
            else y2 +:= 1
            }
         }
      -90:  {
         win2 := Window.open().set_size(height, width) | stop("*** cannot open target window")
         x2 := 0
         y2 := width - 1
         every win2.set_fg(!win1.get_pixels()) do {
            win2.draw_point(x2, y2)
            if y2 = 0 then {
               y2 := width - 1
               x2 +:= 1
               }
            else y2 -:= 1
            }
         }
      180:  {
         win2 := Window.open().set_size(width, height) | stop("*** cannot open target window")
         x2 := width - 1
         y2 := height - 1
         every win2.set_fg(!win1.get_pixels()) do {
            win2.draw_point(x2, y2)
            if x2 = 0 then {
               x2 := width - 1
               y2 -:= 1
               }
            else x2 -:= 1
            }
         }
      default:  runerr("*** invalid specification for rotate()", dir)
      } | runerr("*** invalid specification for rotate()", dir)

   return win2

end

procedure wcatenate(win1, win2, dir)
   local width1, width2, height1, height2, win3

   /dir := "h"			# horizontal concatenation is the default

   width1 := win1.get_width()
   width2 := win2.get_width()
   height1 := win1.get_height()
   height2 := win2.get_height()

   case dir of {
      "h":  {
         win3 := Window.open().set_size(width1 + width2, max(height1, height2)) |
               stop("*** cannot open window for concatenation")
         win1.copy_to(win3)
         win2.copy_to(win3, 0, 0, width2, height2, width1, 0)
         }
      "v":  {
         win3 := Window.open().set_size(max(width1, width2), height1 + height2) |
               stop("*** cannot open window for concatenation")
         win1.copy_to(win3)
         win2.copy_to(win3, 0, 0, width2, height2, 0, height1)
         }
      default:  runerr("*** invalid specification for catenate()", dir)
      }

   return win3

end
