#
# $Id$
#
# This file is in the public domain.
#
# Author: Robert Parlett (parlett@dial.pipex.com)
#

package gui

#
# Selection from a list with edit
#
class EditListSpin(SelectionList,EditSpin)
   public old_selection

   public apply_selection(ev)
      local s 
      s := self.selection_list[\self.selection] | ""
      self.view.set_contents(s)
   end

   public on_textfield(ev, src, type)
      if type = Event.CONTENT_CHANGED then
         self.selection := &null
      cast(self,EditSpin).on_textfield(ev, src, type)
   end

   #
   # Set the contents of the text to the given string; the
   # selection is set to any matching item in the list, or
   # &null if there is no match.
   #
   public set_contents(x)
      if not object_set_selection(x) then {
         self.selection := &null
         self.view.set_contents(x)
      }
   end

   public start_change(e)
      cast(self,EditSpin).start_change(e)
      self.old_selection := self.selection
   end

   public end_change(e)
      cast(self,EditSpin).end_change(e)
      if self.old_selection ~=== self.selection then
         fire(Event.SELECTION_CHANGED, e)
   end

   public do_increment()
      if *selection_list = 0 then
         fail
      if /self.selection | self.selection = *self.selection_list then
         self.set_selection(1)
      else
         self.set_selection(self.selection + 1)
   end

   public do_decrement()
      if *selection_list = 0 then
         fail
      if /self.selection | (self.selection = 1) then
         self.set_selection(*self.selection_list)
      else
         self.set_selection(self.selection - 1)
   end

   public initially()
      \self.selection_list | runerr("no selection list specified")
      cast(self,EditSpin).initially()
   end

   public get_default_width()
      local t
      t := 0
      # Max string length + space for cursor (cf TextField's get_view() method)
      every t <:= self.cbwin.text_width((view.contents | !self.selection_list) || " ")
      return border.get_total_width() + up.get_preferred_width() + t + view.border.get_total_width()
   end

   public set_one(f)
      case f.attr of {
         "selection" : set_selection(f.int_val())
         "selection_list" : set_selection_list(f.text_list_val())
         "contents" : set_contents(f.text_val())
         default: return cast(self,EditSpin).set_one(f)
      }
      return
   end
end


