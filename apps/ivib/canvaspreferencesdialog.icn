import gui

#############################################################################
#
# Preferences for Canvas.
#     
class CanvasPreferencesDialog(Dialog)
   public 
      c,                       # The object being edited
      okay,                    # Okay button
      cancel,                  # Cancel button
      w_spec_tf,               # TextField
      h_spec_tf,               # TextField
      set_w_default,           # CheckBoxes                     
      set_h_default,           #                     
      scale_dimensions_cb,
      edit_scale_tf,
      okay_result,             # Flag indicating result
      name,                    # TextField - object name
      is_component_cb,         # Component or not
      superclass_name,         # TextField - superclass name
      import_name,             # TextField - import name
      layout_filename_cb,
      layout_filename,
      tabset,                  # TabSet
      pos_tab,                 # Size/position tab
      methods_tab,             # Code generation tab
      attrib_tab,              # Attrib tab
      gen_main,                # CheckBoxes for code generation flags
      gen_component_setup,
      gen_init_dialog,
      gen_constructor,
      gen_indent,
      layout_field,
      orig_layout,
      event_tab

   #
   # Get the result
   #  
   public get_okay_result()
      return self.okay_result
   end

   public on_cancel()
      self.dispose()
   end

   public on_layout_change()
      # Need to set here to update the settings below.
      c.set_layout(layout_field.get_contents())

      if /self.c.layout_delegate then {
         set_w_default.set_is_shaded(&yes)
         set_w_default.set_is_checked(&no)
         w_spec_tf.set_is_shaded(&no)
      } else
         set_w_default.set_is_shaded(&no)

      if /self.c.layout_delegate then {
         set_h_default.set_is_shaded(&yes)
         set_h_default.set_is_checked(&no)
         h_spec_tf.set_is_shaded(&no)
      } else
         set_h_default.set_is_shaded(&no)
   end

   public on_okay()
      local d
      # 
      # Validate data
      #
      integer(self.gen_indent.get_contents()) | return alert_error(self, "Indent not integer")
      if w_spec_tf.is_unshaded() then
         integer(w_spec_tf.get_contents()) | return alert_error(self, "Invalid width")
      if h_spec_tf.is_unshaded() then
         integer(h_spec_tf.get_contents()) | return alert_error(self, "Invalid height")
      util_check_attribs(self, attrib_tab.get_table(), Gui.WATTRIB_SET) | fail
      real(edit_scale_tf.get_contents()) | return alert_error(self, "Edit scale not real")

      #
      # Set the attribs list.
      #
      c.dialog_struct.wattribs := normalize_wattribs(attrib_tab.get_table())

      c.event_handlers := event_tab.get_contents()
      c.gen_main := gen_main.get_status()
      c.gen_component_setup := gen_component_setup.get_status()
      c.gen_init_dialog := gen_init_dialog.get_status()
      c.gen_constructor := gen_constructor.get_status()
      c.gen_indent := integer(self.gen_indent.get_contents()) 

      if c.layout_delegate ~=== orig_layout then {
         if \c.layout_delegate then {
            every d := !c.children do {
               d.layout_safe()
            }
         } else {
            every d := !c.children do {
               d.x_spec := d.x - c.x
               d.y_spec := d.y - c.y
            }
         }
      }

      c.set_name(name.get_contents())
      c.superclass_name := self.superclass_name.get_contents()
      c.is_component := is_component_cb.get_status()
      c.import_name := self.import_name.get_contents()
      c.layout_filename := if self.layout_filename_cb.is_checked() then
         self.layout_filename.get_contents() else &null

      c.dialog_iw := if w_spec_tf.is_shaded() then &null else w_spec_tf.get_contents()
      c.dialog_ih := if h_spec_tf.is_shaded() then &null else h_spec_tf.get_contents()
      c.scale_dimensions_flag := scale_dimensions_cb.get_status()
      c.edit_scale := real(edit_scale_tf.get_contents())

      self.okay_result := &yes
      self.dispose()
   end

   public on_is_component_cb(ev)
      local s
      s := self.superclass_name.get_contents()
      if self.is_component_cb.is_checked() then {
         if s == "Dialog" then
            self.superclass_name.set_contents("Component")
         self.gen_init_dialog.set_is_shaded(&yes)
         self.gen_main.set_is_shaded(&yes)
      } else {
         if s == "Component" then
            self.superclass_name.set_contents("Dialog")
         self.gen_init_dialog.set_is_shaded(&no)
         self.gen_main.set_is_shaded(&no)
      }
   end

   public on_set_w_default()
      w_spec_tf.toggle_is_shaded()
   end

   public on_set_h_default()
      h_spec_tf.toggle_is_shaded()
   end

   public on_layout_filename_cb()
      self.layout_filename.toggle_is_shaded()
   end

   #
   # Add the tab to the TabSet
   #
   public add_tab(t) 
      t.label_border.x_align := Align.L
      tabset.add_tab(t)
   end

   public new(c)
      local label, l, code_tab, oc
      Dialog.new()
      self.c := c
      label := "Dialog Preferences"
      self.set_label(label)

      set_layout(GridLayout())

      tabset := ListTabSet().set_constraint("eol", &yes)
      self.set_focus(tabset.dl)
      add(tabset)

      pos_tab := TabItem()
      pos_tab.set_layout(GridLayout().set_extra("borders"))
      pos_tab.set_label("Size")
      add_tab(pos_tab)

      l := Label()
      l.set_label("W : ")
      pos_tab.add(l)

      w_spec_tf := TextField()
      w_spec_tf.set_size(scale(130))
      pos_tab.add(w_spec_tf)

      set_w_default := CheckBox().set_constraint("eol", &yes)
      set_w_default.connect(self.on_set_w_default, Event.ACTION)
      set_w_default.set_label("Default")
      if /c.dialog_iw then {
         set_w_default.set_is_checked(&yes)
         w_spec_tf.set_is_shaded(&yes)
         w_spec_tf.set_contents(c.dialog_w)
      } else
         w_spec_tf.set_contents(c.dialog_iw)
      if /c.layout_delegate then
         set_w_default.set_is_shaded(&yes)
      pos_tab.add(set_w_default)

      l := Label()
      l.set_label("H : ")
      pos_tab.add(l)

      h_spec_tf := TextField()
      h_spec_tf.set_size(scale(130))
      pos_tab.add(h_spec_tf)

      set_h_default := CheckBox().set_constraint("eol", &yes)
      set_h_default.connect(self.on_set_h_default, Event.ACTION)
      set_h_default.set_label("Default")
      if /c.dialog_ih then {
         set_h_default.set_is_checked(&yes)
         h_spec_tf.set_is_shaded(&yes)
         h_spec_tf.set_contents(c.dialog_h)
      } else
         h_spec_tf.set_contents(c.dialog_ih)
      if /c.layout_delegate then
         set_h_default.set_is_shaded(&yes)
      pos_tab.add(set_h_default)

      pos_tab.add(Label().set_label("Layout"))
      layout_field := LayoutField().set_constraint("eol", &yes).set_constraint("w", 2)
      layout_field.set_contents(c.layout_delegate)
      layout_field.connect(self.on_layout_change, Event.SELECTION_CHANGED)
      pos_tab.add(layout_field)
      orig_layout := c.layout_delegate

      pos_tab.add(Label().set_label("Edit scale"))
      edit_scale_tf := TextField().
         set_contents(c.edit_scale).
         set_filter(&digits ++ '.').
         set_constraint("eol", &yes).
         set_size(scale(100))
      pos_tab.add(edit_scale_tf)

      scale_dimensions_cb := CheckBox().
         set_label("Scale output dimensions").
         set_constraint("eol", &yes).
         set_constraint("x_tab", 1).
         set_constraint("w", 2)
      pos_tab.add(scale_dimensions_cb)

      if \c.scale_dimensions_flag then
         scale_dimensions_cb.set_is_checked(&yes)

      self.attrib_tab := MapTab().set_label("WAttribs").set_table(stringify_wattribs(c.dialog_struct.wattribs))
      add_tab(self.attrib_tab)

      methods_tab := TabItem()
      methods_tab.set_layout(GridLayout().set_extra("borders"))
      methods_tab.set_label("Methods")
      add_tab(methods_tab)

      gen_main := CheckBox().set_constraint("eol", &yes).set_constraint("w", 2)
      gen_main.set_label("Generate main() procedure")
      methods_tab.add(gen_main)

      gen_component_setup := CheckBox().set_constraint("eol", &yes).set_constraint("w", 2)
      gen_component_setup.set_label("Generate component_setup() method")
      methods_tab.add(gen_component_setup)

      gen_init_dialog := CheckBox().set_constraint("eol", &yes).set_constraint("w", 2)
      gen_init_dialog.set_label("Generate init_dialog() & end_dialog() methods")
      methods_tab.add(gen_init_dialog)

      gen_constructor := CheckBox().set_constraint("eol", &yes).set_constraint("w", 2)
      gen_constructor.set_label("Generate constructor")
      methods_tab.add(gen_constructor)

      if \c.gen_main then
         gen_main.set_is_checked(&yes)

      if \c.gen_component_setup then
         gen_component_setup.set_is_checked(&yes)

      if \c.gen_init_dialog then
         gen_init_dialog.set_is_checked(&yes)

      if \c.gen_constructor then
         gen_constructor.set_is_checked(&yes)


      l := Label()
      l.set_label("Indent")
      methods_tab.add(l)

      gen_indent := TextField()
      gen_indent.set_size(scale(50))
      methods_tab.add(gen_indent)
      gen_indent.set_contents(c.gen_indent)
      
      code_tab := TabItem()
      code_tab.set_layout(GridLayout().set_extra("borders"))
      code_tab.set_label("Code")
      add_tab(code_tab)

      l := Label()
      l.set_label("Name")
      code_tab.add(l)

      name := TextField().set_constraint("eol", &yes)
      name.set_size(scale(200))
      code_tab.add(name)

      name.set_contents(c.name)

      l := Label()
      l.set_label("Superclass")
      code_tab.add(l)

      self.superclass_name := TextField().set_constraint("eol", &yes)
      self.superclass_name.set_size(scale(200))
      code_tab.add(self.superclass_name)
      self.superclass_name.set_contents(c.superclass_name)

      self.is_component_cb := CheckBox().set_label("Subclass of component").set_constraint("x_tab", 1).set_constraint("eol", &yes)
      if \c.is_component then {
         self.is_component_cb.set_is_checked(&yes)
         self.gen_init_dialog.set_is_shaded(&yes)
         self.gen_main.set_is_shaded(&yes)
      }
      self.is_component_cb.connect(self.on_is_component_cb, Event.ACTION)
      code_tab.add(self.is_component_cb)

      code_tab.add(Label().set_label("Import"))
      self.import_name := TextField().set_constraint("eol", &yes).set_size(scale(200))
      code_tab.add(self.import_name)
      self.import_name.set_contents(c.import_name)

      self.layout_filename_cb := CheckBox().set_label("Save layout in")
      code_tab.add(self.layout_filename_cb)
      self.layout_filename := TextField().set_constraint("eol", &yes).set_size(scale(200))
      code_tab.add(self.layout_filename)
      if \c.layout_filename then {
         self.layout_filename_cb.set_is_checked(&yes)
         self.layout_filename.set_contents(c.layout_filename)
      } else {
         self.layout_filename.set_is_shaded(&yes)
         self.layout_filename.set_contents("$.layout")
      }
      self.layout_filename_cb.connect(self.on_layout_filename_cb, Event.ACTION)

      event_tab := EventTab(c.event_handlers, name)
      add_tab(event_tab)

      oc := OkCancel()
      oc.listen(self)
      self.add(oc)
      return
   end
end



