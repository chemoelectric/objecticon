#
# $Id$
#

package gui

#
# A progress bar
#
class ProgressBar(Component)
   public p

   public override get_default_height()
      return self.cbwin.get_line_height() + border.get_total_height()
   end

   public override display()
      local s, r, y1
      border.draw_rect(self.cbwin, self)
      r := border.get_inner_rect(self)
      s := p || "%"
      y1 := r.y + (r.h - cbwin.get_font_height()) / 2
      cbwin.draw_string(r.x + (r.w - self.cbwin.text_width(s)) / 2,
                        y1 + cbwin.get_font_ascent(),
                        s)
      #
      # Draw the bar in 'down' mode
      #
      Gui.style.fill_down_rectangle(self.cbwin, r.x, r.y, r.w * p / 100.0, r.h)
   end

   #
   # Get the current percentage.
   #
   public get_percentage()
      return p
   end

   #
   # Set the percentage.
   #
   public set_percentage(p)
      p <:= 0
      p >:= 100
      self.p := p
      self.invalidate()
      link
   end

   public override new()
      Component.new()
      self.set_border(CompoundBorder().
                           set_outer(RaisedBorder()).
                           set_inner(EmptyBorder()))
      self.set_percentage(0)
      return
   end
end

