#
# $Id$
#
# This file is in the public domain.
#
# Author: Robert Parlett (parlett@dial.pipex.com)
#

package gui

#
# This is a class for popup menus.  It is an invisible placeholder
# component, with zero size, from which a Menu can be popped up
# using the `popup()` method.
#
# Note that instances should never be added or removed from a dialog;
# that is done automatically.
#
class PopupMenu(Component, MenuSystem)
   public 
      menu,
      comp

   public override go_right()
   end

   public override go_left()
   end

   public close_menu()
      self.menu.hide()
      self.menu.set_component_link(&null)         
      self.menu.finally()
      self.menu := &null
      self.parent_dialog.exit_menu_mode(self)
      self.parent.remove(self)
      fire(Event.POPUP_CLOSED)
   end

   public override close_all()
      close_menu()
   end

   public override make_partial()
      close_menu()
   end

   public override should_close(e)
      return
   end

   public override allow_nested(c)
      succeed is(c, SubMenuProxyComponent) & c.menu.parent_component === self
   end

   public override gen_popup_components()
   end

   public override get_nesting_component()
      return comp
   end

   #
   # Popup a menu.  The use of the alternate positions is described in
   # `MenuComponent.show()`.
   #
   # :Parameters :
   # :  `comp` - The component to temporarily add the menu to
   # :  `menu` - The menu to show
   # :  `x` - The x position to show
   # :  `y` - The y position to show
   # :  `xa` - The alternate x position to show; defaults to `x`.
   # :  `ya` - The alternate y position to show; defaults to `&null`).
   #
   public popup(comp, menu, x, y, a[])
      local xa, ya
      xa := a[1] | x
      ya := a[2]
      self.comp := comp
      set_wattrib_ancestor(comp.parent_dialog.popup_manager)
      comp.parent_dialog.add(self)
      self.resize()
      self.parent_dialog.enter_menu_mode(self)
      self.menu := menu
      self.menu.set_component_link(self)         
      self.menu.initially()
      self.menu.show(x, y, xa, ya)
      return self
   end

   public override new()
      Component.new()
      self.x_spec := self.y_spec := self.w_spec := self.h_spec := 0
      set_is_hidden(&yes)
      return
   end
end
