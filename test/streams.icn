import io, ipl.test

# A stream that reads and writes n chars at a time.
class NStream(Stream)
   private
      n,
      ch,
      inex,
      wrote

   public override in(n)
      local s
      s := ""
      n >:= self.n
      every 1 to n do
         s ||:= @inex
      return s
   end

   public override out(s)
      local t
      t := min(*s, n)
      wrote ||:= s[1+:t]
      return t
   end
   
   public override get_mode()
      return READ_WRITE
   end

   public override close()
   end

   public get_wrote()
      local t
      t := wrote
      wrote := ""
      return t
   end

   public new(n)
      self.n := n
      inex := create |(!&ucase | "\n")
      wrote := ""
      return
   end
end

procedure do_mode(f)
   print{"Mode",f.get_mode()}
   yes_no{"Can read",f.can(Stream.READ)}
   yes_no{"Can write",f.can(Stream.WRITE)}
   yes_no{"Can readwrite",f.can(Stream.READ_WRITE)}
   yes_no{"Can seek",f.can(Stream.SEEK)}
   yes_no{"Can truncate",f.can(Stream.TRUNCATE)}
   yes_no{"Can all",f.can(Stream.ALL)}
end

procedure test_modes()
   local f

   write("Writable")
   f := FileStream("test", ior(FileOpt.WRONLY, FileOpt.TRUNC, FileOpt.CREAT)) | stop(&why)
   do_mode(f)
   f.close()

   write("Read/write")
   f := FileStream("test", FileOpt.RDWR) | stop(&why)
   do_mode(f)
   f.close()

   write("Read only")
   f := FileStream("test", FileOpt.RDONLY) | stop(&why)
   do_mode(f)
   f.close()

   write("Dir Read")
   f := DirStream(".") | stop(&why)
   do_mode(f)
   f.close()

$if _UNIX
   write("Prog Read")
   f := ProgInputStream("echo", ["hello"]) | stop(&why)
   do_mode(f)
   while f.read()
   f.close()

   write("Prog write")
   f := ProgOutputStream("sh", ["-c", "cat >/dev/null"]) | stop(&why)
   do_mode(f)
   f.close()
$else
   write("Prog Read")
   write("Mode: 1")
   write("Can read: yes")
   write("Can write: no")
   write("Can readwrite: no")
   write("Can seek: no")
   write("Can truncate: no")
   write("Can all: no")
   write("Prog write")
   write("Mode: 2")
   write("Can read: no")
   write("Can write: yes")
   write("Can readwrite: no")
   write("Can seek: no")
   write("Can truncate: no")
   write("Can all: no")
$endif

$if _UNIX
   write("Socket")
   f := SocketStream() | stop(&why)
   do_mode(f)
   f.close()
$else
   write("Socket")
   write("Mode: 3")
   write("Can read: yes")
   write("Can write: yes")
   write("Can readwrite: yes")
   write("Can seek: no")
   write("Can truncate: no")
   write("Can all: no")
$endif

   write("Ram stream")
   f := RamStream("abc")
   do_mode(f)
   f.close()
end

procedure check_file(f, data)
   local i, s, s1
   f.writes(data) | stop(&why)
   comp_with(f,data)
   # Add to the end
   f.seek(0)
   f.writes(&digits)
   data ||:= &digits
   comp_with(f,data)
   # Seek beyond the end
   f.seek(f.tell() + 20)
   f.writes(&digits)
   data ||:= repl("\0",20) || &digits
   comp_with(f,data)
   # Write some data in the middle
   every i := 1 to *data by 5 do {
      f.seek(i)
      f.writes("X")
      data[i] := "X"
   }
   comp_with(f,data)
   # Write some more data in the middle
   every i := 1 to *data by 7 do {
      f.seek(i)
      f.writes("ABC")
      data[i:min(i+3,*data+1)] := "ABC"
   }
   comp_with(f,data)
   # Write some lines and read them back
   every i := 1 to *data by 200 do {
      f.seek(i)
      s := "The quick brown fox" || repl("*",i/50)
      s1 := s || Files.EOL
      f.write(s)
      data[i:min(i+*s1,*data+1)] := s1
      f.seek(i)
      yes_no{"Line read", f.read() == s}
   }
   comp_with(f,data)

   f.close()
end

procedure comp_with(f, data)
   local s, t
   f.seek(1) | stop(&why)
   s := ""
   repeat {
      t := f.in(100) | stop(&why)
      if /t then break
      s ||:= t
   }
   yes_no{"Comparing", s == data}
end

procedure test_stream(x)
   local s, i, buff
   buff := BufferStream(x, "buff_size=5")
   every i := 1 to 26 do {
      s := &ucase[1:i]
      buff.writes(s)
      yes_no{"Reading", buff.read() == &ucase}
      yes_no{"Comparing", s == x.get_wrote()}
      yes_no{"Reading", buff.reads(26) == &ucase}
      s := repl(&digits, 20)
      buff.writes(s)
      yes_no{"Reading", buff.reads(1) == "\n"}
      yes_no{"Comparing", s == x.get_wrote()}
   }
end

procedure test_dir()
   local s, f, t
   f := DirStream("testdir") | stop(&why)
   s := f.reads(1000)
   t := set()
   s ? while insert(t, tab(find(Files.EOL))) do move(*Files.EOL)
   pr_seq{"Dir1", !sort(t)}
   f.close()
   t := set()
   f := DirStream("testdir") | stop(&why)
   while insert(t, f.read())
   pr_seq{"Dir2", !sort(t)}
   f.close()
end

procedure main()
   test_modes()
   check_file(FileStream("test", ior(FileOpt.RDWR, FileOpt.TRUNC, FileOpt.CREAT)),
              repl(&ucase, 100))
   check_file(BufferStream(FileStream("test", ior(FileOpt.RDWR, 
                                                       FileOpt.TRUNC, 
                                                       FileOpt.CREAT))),
              repl(&ucase, 100))
   check_file(BufferStream(FileStream("test", ior(FileOpt.RDWR, 
                                                       FileOpt.TRUNC, 
                                                       FileOpt.CREAT)),
                                                       "buff_size=5"),
              repl(&ucase, 100))
   check_file(BufferStream(BufferStream(FileStream("test", ior(FileOpt.RDWR, 
                                                                    FileOpt.TRUNC, 
                                                                    FileOpt.CREAT))
                                        ),
                                        "buff_size=5"),
                                        repl(&ucase, 100))
   check_file(RamStream(), repl(&ucase, 100))
   check_file(BufferStream(RamStream()), repl(&ucase, 100))

   check_file(StringStream(), repl(&ucase, 100))
   check_file(BufferStream(StringStream()), repl(&ucase, 100))

   test_stream(NStream(1))
   test_stream(NStream(10))

   test_dir()

   Files.remove("test")
end
