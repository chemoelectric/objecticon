#
# $Id$
#
# This file is in the public domain.
#
# Author: Robert Parlett (parlett@dial.pipex.com)
#

package gui

#
# This is a Spin for numeric values, optionally within a fixed range.
#
class RangeSpin(EditSpin)
   public
      lo,
      hi,
      increment_size

   public do_increment(ev)
      set_value(get_value() + self.increment_size, ev)
   end

   public do_decrement(ev)
      set_value(get_value() - self.increment_size, ev)
   end

   #
   # Set the range of allowed values.  The values may
   # be integer or real.
   #
   # @param lo  The lower bound
   # @param hi  The upper bound
   #
   public set_range(lo, hi)
      self.lo := lo
      self.hi := hi
      return self
   end

   #
   # Set the value
   #
   public set_value(x, ev)
      x <:= \self.lo
      x >:= \self.hi
      if /ev then
         view.set_contents(x)
      else
         view.assign_contents(x, ev)
      return self
   end

   #
   # Set the increment, ie the amount moved up/down by the buttons.
   #
   public set_increment_size(x)
      self.increment_size := x
      return self
   end

   public on_textfield(ev, src, type)
      local x
      if x := get_value() then {
         # No point using assign_contents, since an invalid value is in the
         # undo list.
         if x < \self.lo then
            view.set_contents(self.lo)
         else if x > \self.hi then
            view.set_contents(self.hi)
      } 
      cast(self,EditSpin).on_textfield(ev, src, type)
   end

   #
   # Return the value, or fail if the value is not presently a valid
   # numeric value.
   #
   public get_value()
      return numeric(view.get_contents())
   end

   public get_default_width()
      local t
      t := 0
      # Bit difficult if we don't have a maximum...
      every t <:= self.cbwin.text_width((view.contents | \self.hi) || " ")
      return border.get_total_width() + up.get_preferred_width() + t + view.border.get_total_width()
   end

   public new()
      cast(self,EditSpin).new()
      view.set_filter(&digits ++ '.\-')
      self.increment_size := 1
      return
   end
end

