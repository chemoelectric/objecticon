import io, lang

procedure imagex(o)
   local t
   t := ""
   image(o) ? {
      repeat {
         t ||:= tab(find("#") | 0)
         if pos(0) then
            return t
         t ||:= move(1)
         if tab(many(&digits)) then
            t ||:= "*"
      }
   }
end

procedure to_stringx(o)
   local t
   t := ""
   to_string(o) ? {
      repeat {
         t ||:= tab(find("#") | 0)
         if pos(0) then
            return t
         t ||:= move(1)
         if tab(many(&digits)) then
            t ||:= "*"
      }
   }
end

procedure pr_seq(s, e)
   local t
   writes(@s, ": {")
   t := ""
   while writes(t, imagex(@e)) do
      t := ", "
   write("}")
end

procedure yes_no(s, e)
   writes(@s, ": ")
   if @e then write("yes") else write("no")
end

procedure try(s, e)
   local x
   writes(@s, ": ")
   &error := -1
   errorclear()
   x := @e
   if &errornumber then
      write("Error ", &errornumber)
   else
      write("No error, gave ", imagex(x))
   &error := 0
   return \x
end

procedure print(s, e)
   local o
   writes(@s, ": ")
   if o := @e then
      write(imagex(o))
   else
      write("Failed")
   return o
end

procedure pr_type(s, e)
   local o
   writes(@s, ": ")
   if o := @e then
      write(type(o))
   else
      write("Failed")
   return o
end

procedure pr_seq_type(s, e)
   local x, t
   writes(@s, ": {")
   x := set()
   while insert(x, type(@e))
   t := ""
   every writes(t, !sort(x)) do
      t := ", "
   write("}")
end
