import io, ipl.strings(words)

procedure main(a)
   local l, i, s, r, title, t, export, e, creates,
      nparam,ndynam,nclo,ntmp,nlab,nmark
   while r := read() do {
      if i := find("#", r) then
         r := r[1:i]
      r ? {
         if ="$start" then {
            tab(many(' '))
            title := tab(0)
            l := []
            t := table()
            export := set()
            creates := nparam := ndynam := nclo := ntmp := nlab := nmark := 0
            repeat {
               r := read() | stop("Unexpected eof")
               if i := find("#", r) then
                  r := r[1:i]
               if r == "$end" then
                  break
               r ? if ="$export " then {
                  insert(export, tab(0))
                  next
               }
               every s := words(r) do {
                  if s[-1] == ":" then
                     t[s[1:-1]] := *l
                  else s ? {
                     if ="c:" then {
                        put(l, "(word)" || tab(0))
                     } else if ="arg:" then {
                        i := get_integer()
                        put(l, i)
                        nparam <:= i+1
                     } else if ="tmp:" then {
                        i := get_integer()
                        put(l, i)
                        ntmp <:= i+1
                     } else if ="mark:" then {
                        i := get_integer()
                        put(l, i)
                        nmark <:= i+1
                     } else if ="clo:" then {
                        i := get_integer()
                        put(l, i)
                        nclo <:= i+1
                     } else if ="lab:" then {
                        i := get_integer()
                        put(l, i)
                        nlab <:= i+1
                     } else if upto(':') then
                        stop("Unexpected : in word")
                     else
                        put(l, s)
                     if s == "Op_Create" then
                        creates := 1
                  }
               }
            }
            # Resolve labels and output
            every i := 1 to *l do
               l[i] := "(word)&" || title || "_code[" || \t[l[i]] || "]"
            write("static word ", title, "_code[] = {")
            every write("   ", !l, ",")
            write("};")
            write("#passthru CustomProc(", 
                  title, ",",
                  title, "_code,",
                  creates, ",", 
                  nparam, ",", 
                  ndynam, ",", 
                  nclo, ",", 
                  ntmp, ",", 
                  nlab, ",", 
                  nmark, ",", 
                  "\"", title, "\")")
            every e := !export do {
               write("static word *", title, "_", e, " = &", title, "_code[", \t[e], "];")
            }
         }
      }
   }
end

procedure get_integer()
   return integer(tab(0)) | stop("Integer expected")
end
