import io, util

procedure main()
   local s, i, j, l, c, status, done
   l := 0

   done := set()
   write("/* auto generated by keysymtocodepoint, based on the list found at */")
   write("/* http://www.cl.cam.ac.uk/~mgk25/ucs/keysyms.txt */")
   write()
   write("static int convert_KeySym_to_code_point(KeySym k)")
   write("{")
   write("    switch(k) {")
   while s := read() do {
      l +:= 1
      s ? {
         if ="0x" then {
            i := format_string_to_int(move(4)) | stop("Bad format: line ", l)
            tab(upto('U'))
            move(1)
            j := format_string_to_int(move(4)) | stop("Bad format: line ", l)
            tab(many(' '))
            status := move(1) | stop("Bad format: line ", l)
            if status == "d" then
               next
            if tab(upto('#')) then {
               move(1)
               c := "\t" || commentate(tab(0))
            } else
               c := ""
            if member(done, i) then 
               next
            insert(done, i)
            write("        case 0x", format_int_to_string(i), ": return 0x", format_int_to_string(j), ";", c)
         }
      }
   }
   write("        default: return 0;")
   write("    }")
   write("}")
end

procedure commentate(c)
   local t
   c ? {
      t := tab(find("/*") | 0)
      move(2)
      t ||:= tab(find("*/"))
      move(2)
      t ||:= tab(0)
   }
   return "/* " || t || " */"
end

