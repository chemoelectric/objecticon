#
# $Id$
#
# This file is in the public domain.
#
# Author: Robert Parlett (parlett@dial.pipex.com)
#

package gui

import lang, graphics, util, ipl.pdco(Max)

#
# Component representing the slider area
#
class SliderArea(Component)
   public 
      inner_x, 
      inner_y,
      inner_w,
      inner_h, 
      slider_x,
      slider_y,
      slider_w,
      slider_h

   public override display()
      Gui.style.draw_sunken_rectangle(self.cbwin, 
                                  self.inner_x, self.inner_y, 
                                  self.inner_w, self.inner_h,
                                  Gui.BORDER_WIDTH)
      self.cbwin.erase_area(self.slider_x, self.slider_y, self.slider_w, self.slider_h)
      Gui.style.draw_raised_rectangle(self.cbwin, 
                                  self.slider_x, self.slider_y, 
                                  self.slider_w, self.slider_h,
                                  Gui.BORDER_WIDTH)
   end
end


#
# This component is a slider between two set values, with optional tick marks
# and labels.
#
# Example :-
# ~
#   s := Slider()
#   s.set_pos("85%", "25%")      
#   s.set_size(, "40%") # Width defaults for a vertical slider
#   s.set_range(0,5)
#   s.set_value(2)
#   s.set_ticks(1)
#   s.set_labels(1)
#   s.set_snaps(1)
#   self.add(s)
# ~
class Slider(Component, HasOrientation, HasUseWheel)
   public 
      value,                   
      old_value,
      slider_down,
      slider_down_offset,
      slider_area,
      slider_pos,
      slider_area_pos,
      slider_area_size,
      old_slider_area_pos,
      old_slider_area_size,
      discrete_vals,
      ticks,
      labels,
      snaps,
      ticks_pos,
      labels_pos,
      is_paging,
      increment_size,
      wheel_factor,
      hi,                                
      lo,
      repeat_ticker,
      repeat_delay


   # These values refer to a horizontal slider, the logic is backwards for a vertical one.
   public static const
      AREA_W, AREA_H, INNER_H, TICK_H, DEFAULT_WHEEL_FACTOR

   private static init()
      AREA_W := env_scale("OI_SLIDER_AREA_W") | scale(31)
      AREA_H := env_scale("OI_SLIDER_AREA_H") | scale(19)
      INNER_H := env_scale("OI_SLIDER_INNER_H") | scale(4)
      TICK_H := env_scale("OI_SLIDER_TICK_H") | scale(10)
      DEFAULT_WHEEL_FACTOR := env_int("OI_SLIDER_DEFAULT_WHEEL_FACTOR", 1) | 1
   end

   #
   # Configure so that on release after a drag, the value will snap to 
   # the nearest multiple of n.
   #
   public set_snaps(n)
      self.snaps := n
      link
   end

   #
   # Draw ticks at the given interval
   #
   public set_ticks(n)
      self.ticks := n
      link
   end

   #
   # Draw labels at the given interval
   #
   public set_labels(n)
      self.labels := n
      link
   end

   #
   # Set the increment_size to increment on a click in the slider area; default is 1.
   # :Parameters :
   # :  `x` - The increment_size
   #
   public set_increment_size(x)
      self.increment_size := x
      self.invalidate()
      link
   end

   #
   # Get the increment_size
   #
   public get_increment_size()
      return self.increment_size
   end

   #
   # Set the wheel factor, being the number of increments to
   # move on a wheel press; by default it is `DEFAULT_WHEEL_FACTOR`
   #
   public set_wheel_factor(n)
      self.wheel_factor := n
      link
   end

   #
   # Get the wheel_factor
   #
   public get_wheel_factor()
      return self.wheel_factor
   end

   #
   # Set the current value
   #
   public set_value(x)
      if value ~=== x then {
         if is_live() then {
            move_value(x)
            set_pos_from_value()
         } else 
            self.value := x
      }
      link
   end

   private move_value(x)
      self.value := x
      self.value <:= self.lo
      self.value >:= self.hi
   end

   #
   # Get the value.
   # :Returns :
   # :  The value
   #
   public get_value()
      return self.value
   end

   private set_pos_from_value()
      if self.hi ~= self.lo then
         move_slider_pos(self.slider_area_pos + integer(((self.value - self.lo) * 
                                                         self.slider_area_size / (self.hi - self.lo))))
      else
         move_slider_pos(self.slider_area_pos)
   end

   private set_value_from_pos()
      if self.slider_area_size ~= 0 then {
         # For discrete vals, an adjustment is made so that, for example
         # the ranges where the cursor set the position might be
         # 0     1     2     3
         # 0001111112222223333
         # Without this adjustment 0 would only be in the leftmost position, which for
         # small ranges is unattractive.  For non-discrete ranges this behaviour is
         # not appropriate.
         if \self.discrete_vals then
            move_value(self.lo + ((self.slider_area_size / 2) + (self.hi - self.lo) * 
                                  (self.slider_pos - self.slider_area_pos)) / self.slider_area_size)
         else
            move_value(self.lo + ((self.hi - self.lo) * (self.slider_pos - self.slider_area_pos)) / self.slider_area_size)
      } else
         move_value(self.lo)
   end

   private move_slider_pos(x)
      x <:= self.slider_area_pos
      x >:= self.slider_area_pos + self.slider_area_size
      (self.slider_pos ~=== x) | fail
      self.slider_pos := x
      if /self.is_horizontal_flag then
         self.slider_area.slider_y := self.slider_pos - AREA_W / 2
      else
         self.slider_area.slider_x := self.slider_pos - AREA_W / 2
      self.slider_area.invalidate()
   end

   #
   # Set the range of the slider.  The values may
   # be integer or real.
   #
   # :Parameters :
   # :  `lo` - The lower bound
   # :  `hi` - The upper bound
   #
   public set_range(lo, hi)
      if self.lo ~=== lo | self.hi ~=== hi then {
         if lo >= hi then
            runerr("Invalid range")
         self.lo := lo
         self.hi := hi
         discrete_vals := Yes{ type(self.hi) == type(self.lo) == "integer" }
         reconfigure()
      }
      link
   end

   public handle_lmpress(e)
      start_handle(e)
      if (self.slider_area.slider_x <= e.x < self.slider_area.slider_x + self.slider_area.slider_w) & 
         (self.slider_area.slider_y  <= e.y < self.slider_area.slider_y + self.slider_area.slider_h) then {
         #
         # Click on slider; set flag and save offset between top of slider and pointer position
         #
         self.slider_down := &yes
         if /self.is_horizontal_flag then
            self.slider_down_offset := e.y - self.slider_pos
         else
            self.slider_down_offset := e.x - self.slider_pos
      } else if (if /is_horizontal_flag then slider_area.y  <= e.y < slider_area.slider_y
                 else slider_area.x <= e.x < slider_area.slider_x) then {
         if \e.shift | e.code === Mouse.MIDDLE_PRESS  then {
            self.slider_down := &yes
            slider_down_offset := 0
            move_slider_pos(if /self.is_horizontal_flag then e.y else e.x)
            set_value_from_pos()
         } else {
            move_value(self.value - self.increment_size)
            set_pos_from_value()
            start_paging(-1)
         }
      } else if (if /is_horizontal_flag then slider_area.slider_y + slider_area.slider_h <= e.y < slider_area.y + slider_area.h
                 else slider_area.slider_x + slider_area.slider_w <= e.x < slider_area.x + slider_area.w) then {
         if \e.shift | e.code === Mouse.MIDDLE_PRESS then {
            self.slider_down := &yes
            slider_down_offset := 0
            move_slider_pos(if /self.is_horizontal_flag then e.y else e.x)
            set_value_from_pos()
         } else {
            move_value(self.value + self.increment_size)
            set_pos_from_value()
            start_paging(1)
         }
      }
      end_handle(e)
   end

   public handle_lmrelease(e)
      local n
      start_handle(e)
      if \self.slider_down then {
         #
         # Released; clear flag
         #
         self.slider_down := &no
         if \self.snaps then {
            n := value + snaps / 2
            move_value(n - (n % snaps))
            set_pos_from_value()
         }
      } 
      stop_paging()
      end_handle(e)
   end

   public handle_interrupted()
      if slider_area.is_held(Mouse.LEFT_PRESS) then
         handle_lmrelease()
   end

   public handle_key_home(e)
      start_handle(e)
      move_value(self.lo)
      set_pos_from_value()
      end_handle(e)
   end

   public handle_key_end(e)
      start_handle(e)
      move_value(self.hi)
      set_pos_from_value()
      end_handle(e)
   end

   public handle_key_right(e)
      start_handle(e)
      move_value(self.value + self.increment_size)
      set_pos_from_value()
      end_handle(e)
   end

   public handle_key_left(e)
      start_handle(e)
      move_value(self.value - self.increment_size)
      set_pos_from_value()
      end_handle(e)
   end

   public handle_key(e)
      case e.code of {
         Key.HOME : handle_key_home(e)
         Key.END : handle_key_end(e)
         Key.RIGHT | Key.DOWN : handle_key_right(e)
         Key.LEFT | Key.UP : handle_key_left(e)
      }
   end

   private repeat_tick()
      repeat {
         if Prog.get_runtime_millis() > self.repeat_delay then {
            start_handle()
            move_value(self.value  + self.is_paging * self.increment_size)
            set_pos_from_value()
            end_handle()
         }
         repeat_ticker.sleep(Gui.REPEAT_RATE)
      }
   end

   public start_paging(n)
      self.is_paging := n
      self.repeat_delay := Prog.get_runtime_millis() + Gui.REPEAT_DELAY
      repeat_ticker.start()
   end

   public stop_paging()
      self.is_paging := &null
      repeat_ticker.stop()
   end

   public handle_lmdrag(e)
      \self.slider_down | fail

      start_handle(e)
      #
      # Slider dragged; compute new position
      #
      if /self.is_horizontal_flag then
         move_slider_pos(e.y - self.slider_down_offset)
      else
         move_slider_pos(e.x - self.slider_down_offset)
      set_value_from_pos()
      end_handle(e)
   end

   public override handle_wheel_up(e)
      start_handle(e)
      move_value(self.value - self.wheel_factor * self.increment_size)
      set_pos_from_value()
      end_handle(e)
   end

   public override handle_wheel_down(e)
      start_handle(e)
      move_value(self.value + self.wheel_factor * self.increment_size)
      set_pos_from_value()
      end_handle(e)
   end

   public start_handle(ev)
      old_value := value
   end

   public end_handle(ev)
      if old_value ~=== value then
         fire(Event.VALUE_CHANGED, ev)
   end

   private reconfigure()
      # Don't do anything if we haven't been positioned yet.
      is_live() | fail
      # Ensure value in lo..hi range.
      move_value(self.value)
      set_pos_from_value()
      invalidate()
   end

   public override get_default_width()
      local w
      if /self.is_horizontal_flag then {
         w := AREA_H + border.get_total_width()
         if \self.labels then
            w +:= Max{ cbwin.text_width(hi | lo) } + Gui.X_INSET
         if \self.ticks then
            w +:= TICK_H + Gui.X_INSET
         return w
      }
   end

   public override get_default_height()
      local h
      if \self.is_horizontal_flag then {
         h := AREA_H + border.get_total_height()
         if \self.labels then
            h +:= self.cbwin.get_font_height() + Gui.Y_INSET
         if \self.ticks then
            h +:= TICK_H + Gui.Y_INSET
         return h
      }
   end

   public override layout()
      self.labels_pos := self.ticks_pos := &null
      if /self.is_horizontal_flag then {
         if \self.labels then
            labels_pos := self.x + border.get_l_inset() + AREA_H + Gui.X_INSET
         if \self.ticks then {
            ticks_pos := self.x + border.get_l_inset() + AREA_H + Gui.X_INSET
            \labels_pos +:= TICK_H + Gui.X_INSET
         }
         slider_area.x := self.x + border.get_l_inset()
         slider_area.y := self.y + border.get_t_inset()
         slider_area.w := AREA_H
         slider_area.h := self.h - border.get_total_height()
         slider_area.inner_x := slider_area.x + slider_area.w / 2 - INNER_H  / 2
         slider_area.inner_y := slider_area.y + AREA_W / 2
         slider_area.inner_w := INNER_H
         slider_area.inner_h := slider_area.h - AREA_W
         slider_area.slider_x := slider_area.x
         slider_area.slider_h := AREA_W
         slider_area.slider_w := AREA_H
         slider_area_pos := slider_area.inner_y
         slider_area_size := slider_area.inner_h
      } else {
         if \self.labels then
            labels_pos := self.y + border.get_t_inset() + AREA_H + 
                              Gui.Y_INSET + self.cbwin.get_font_height() / 2
         if \self.ticks then {
            ticks_pos := self.y + border.get_t_inset() + AREA_H + Gui.Y_INSET
            \labels_pos +:= TICK_H + Gui.Y_INSET
         }
         slider_area.x := self.x + border.get_l_inset()
         slider_area.y := self.y + border.get_t_inset()
         slider_area.w := self.w - border.get_total_width()
         slider_area.h := AREA_H
         slider_area.inner_x := slider_area.x + AREA_W  / 2
         slider_area.inner_y := slider_area.y + slider_area.h / 2 - INNER_H  / 2
         slider_area.inner_h := INNER_H
         slider_area.inner_w := slider_area.w - AREA_W
         slider_area.slider_y := slider_area.y
         slider_area.slider_w := AREA_W
         slider_area.slider_h := AREA_H
         slider_area_pos := slider_area.inner_x
         slider_area_size := slider_area.inner_w
      }

      # This test avoids a cosmetic problem whereby the slider would
      # move slightly as its position is recalculated from the value.
      # There is no need for this calculation unless the slider area
      # has changed size/pos.
      #
      if old_slider_area_pos ~=== slider_area_pos |
         old_slider_area_size ~=== slider_area_size then
      {
         set_pos_from_value()
         old_slider_area_pos := slider_area_pos
         old_slider_area_size := slider_area_size
      }
   end

   public override shift(dx, dy)
      Component.shift(dx, dy)
      if /self.is_horizontal_flag then {
         (\labels_pos) +:= dx
         (\ticks_pos) +:= dx
         slider_area_pos +:= dy
         old_slider_area_pos +:= dy
         slider_pos +:= dy
      } else {
         (\labels_pos) +:= dy
         (\ticks_pos) +:= dy
         slider_area_pos +:= dx
         old_slider_area_pos +:= dx
         slider_pos +:= dx
      }
      slider_area.inner_x +:= dx
      slider_area.inner_y +:= dy
      slider_area.slider_x +:= dx
      slider_area.slider_y +:= dy
   end

   public draw_ticks()
      local i, xp, yp
      if /self.is_horizontal_flag then {
         i := self.lo
         while i <= self.hi do {
            yp := self.slider_area_pos + integer(((i - self.lo) * (self.slider_area_size) / (self.hi - self.lo)))
            self.cbwin.fill_rectangle(ticks_pos, yp, TICK_H, 1)
            i +:= ticks
         }
      } else {
         i := self.lo
         while i <= self.hi do {
            xp := self.slider_area_pos + integer(((i - self.lo) * (self.slider_area_size) / (self.hi - self.lo)))
            self.cbwin.fill_rectangle(xp, ticks_pos, 1, TICK_H)
            i +:= ticks
         }
      }
   end

   public draw_labels()
      local i, xp, yp, fh
      fh := self.cbwin.get_font_height()
      if /self.is_horizontal_flag then {
         i := self.lo
         while i <= self.hi do {
            yp := self.slider_area_pos + integer(((i - self.lo) * (self.slider_area_size) / (self.hi - self.lo)))
            cbwin.draw_string(labels_pos, yp - fh / 2 + cbwin.get_font_ascent(), i)
            i +:= labels
         }
      } else {
         i := self.lo
         while i <= self.hi do {
            xp := self.slider_area_pos + integer(((i - self.lo) * (self.slider_area_size) / (self.hi - self.lo)))
            cbwin.draw_string(xp - self.cbwin.text_width(i) / 2, labels_pos - fh / 2 + cbwin.get_font_ascent(), i)
            i +:= labels
         }
      }
   end

   public override initially()
      \self.value | runerr("Value not set")
      if /(self.lo | self.hi) then
         runerr("Range not set")
      # Ensure value in lo..hi range.
      move_value(self.value)
      Component.initially()
   end

   public override display()
      border.draw_rect(self.cbwin, self)

      if \self.labels then
         draw_labels()

      if \self.ticks then
         draw_ticks()

      display_children()

      if should_draw_focus() then
         border.draw_focus_rect(self.cbwin, self)
   end

   public override finally()
      repeat_ticker.stop()
      old_slider_area_pos := old_slider_area_size := &null
      Component.finally()
   end

   public override keeps(e)
      succeed member(if /self.is_horizontal_flag then Key.CURSOR_V else Key.CURSOR_H, e.code)
   end

   public override new()
      Component.new()
      repeat_ticker := Dispatcher.new_task{repeat_tick()}
      slider_area := SliderArea()
      accepts_focus_flag := &yes
      increment_size := 1
      wheel_factor := DEFAULT_WHEEL_FACTOR
      set_use_wheel(&yes)
      set_border(NullBorder())
      add(slider_area)
      slider_area.
         connect(handle_lmpress, Event.MOUSE_LEFT_PRESS).
         connect(handle_lmdrag, Event.MOUSE_LEFT_DRAG).
         connect(handle_lmrelease, Event.MOUSE_LEFT_RELEASE).
         connect(handle_lmpress, Event.MOUSE_MIDDLE_PRESS).
         connect(handle_lmdrag, Event.MOUSE_MIDDLE_DRAG).
         connect(handle_lmrelease, Event.MOUSE_MIDDLE_RELEASE)
      connect(handle_key, Event.KEY_PRESS)
      connect(handle_interrupted, Event.INTERRUPTED)
      return
   end
end
