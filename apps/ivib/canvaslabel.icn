#
# $Id$
#
# This file is in the public domain.
#
# Author: Robert Parlett (parlett@dial.pipex.com)
#

import gui


############################################################################
#
# Class for representing a Label on the canvas.
#
class CanvasLabel(CanvasComponent,Label)
   #
   # Overrides CanvasComponent.min_height()
   #
   public min_height()
      return 13
   end

   #
   # Generate code into Code object c.
   #
   public gen_code(c)
      cast(self,CanvasComponent).gen_code(c)
      self.paint.gen_code(c)
      c.line(name || ".set_paint(" || self.paint.name || ")")
   end

   #
   # Duplicate object.
   #
   public dup()
      local c
      c := CanvasLabel()
      cast(self,CanvasComponent).dup_fields(c)
      c.set_paint(self.paint.dup(c))
      return c
   end

   #
   # Return the I/O template.
   #
   public get_template()
      return cast(self,CanvasComponent).get_template() ||| 
         [["Paint", "paint"]]
   end

   #
   # Open the configuration dialog box.
   #
   public dialog()
      return CanvasLabelDialog(self)
   end

   public allows_null_width()
      return 1
   end

   public allows_null_height()
      return 1
   end

   public gen_cos()
      suspend cast(self,CanvasComponent).gen_cos() | self.paint
   end

# WARN : parameterless initially method
   public new()
      cast(self,Label).new()
      cast(self,CanvasComponent).new()
      self.class_name := "Label"
      self.import_name := "gui"
      self.name := "label"
      return
   end
end

