#
# $Id$
#

import gui, ipl.numbers

class CanvasSplit(CanvasComponent,Split)
   public 
      sizer_is_horizontal_flag,     # These 2 flags are just used or save and restore.
      sizer_show_inter_flag

   #
   # Generate code into Code object c.
   #
   public override gen_code(c)
      self.first.gen_code(c)
      self.second.gen_code(c)
      CanvasComponent.gen_code(c)
      if /self.sizer.is_horizontal_flag then {
         c.chain("set_left(" || self.first.name || ")")
         c.chain("set_right(" || self.second.name || ")")
      } else {
         c.chain("set_top(" || self.first.name || ")")
         c.chain("set_bottom(" || self.second.name || ")")
      }
      if (self.min_first | self.min_second) ~= 0 then
         c.chain("set_min(" || c.scalable_dim(self.min_first) || ", " || c.scalable_dim(self.min_second) || ")")
      if self.weight ~= 0 then
         c.chain("set_weight(" || self.weight || ")")
      if \self.sizer.show_inter_flag then
         c.chain("set_show_inter(&yes)")
   end

   #
   # Duplicate object.
   #
   public override dup()
      local c
      c := CanvasSplit()
      CanvasComponent.dup_fields(c)
      if /self.sizer.is_horizontal_flag then {
         c.set_left(self.first.dup())
         c.set_right(self.second.dup())
      } else {
         c.set_top(self.first.dup())
         c.set_bottom(self.second.dup())
      }
      c.min_first := self.min_first
      c.min_second := self.min_second
      c.weight := self.weight
      c.sizer.show_inter_flag := self.sizer.show_inter_flag
      return c
   end

   public override rescale(f)
      self.min_first := round(min_first * f)
      self.min_second := round(min_second * f)
   end

   #
   # Return the I/O template.
   #
   public override get_template()
      suspend CanvasComponent.get_template() |
         ["Sizer is horizontal flag", "sizer_is_horizontal_flag"] |
         ["Sizer show inter flag", "sizer_show_inter_flag"] |
         ["Min first", "min_first"] |
         ["Min second", "min_second"] |
         ["Weight", "weight"] |
         ["First", "first"] |
         ["Second", "second"]
   end

   public override pre_encode()
      CanvasComponent.pre_encode()
      # Because we don't save the sizer...
      self.sizer_is_horizontal_flag := self.sizer.is_horizontal_flag
      self.sizer_show_inter_flag := self.sizer.show_inter_flag
   end

   public override post_decode()
      CanvasComponent.post_decode()
      if /self.sizer_is_horizontal_flag then {
         self.set_left(first)
         self.set_right(second)
      } else {
         self.set_top(first)
         self.set_bottom(second)
      }
      if \self.sizer_show_inter_flag then
         self.set_show_inter(&yes)
   end

   #
   # Overrides CanvasComponent.inside()
   #
   public override is_inside(c)
      if c === self then
         fail
      return first.is_inside(c) | second.is_inside(c)
   end

   #
   # Overrides CanvasComponent.which_cursor_over() 
   #
   public override which_cursor_over(e)
      local o
      CanvasComponent.which_cursor_over(e) | fail
      if in_resize_region(e) then
         return self
      if o := (self.first | self.second).which_cursor_over(e) then
         return o
      return self
   end

   public override display()
      if in_preview() then
         Split.display()
      else {
         # Fiddly to get the label "<Split>" above the sizer, but below the
         # contents of first and second.  (See also CanvasSplitContent.display() below).
         display_child(sizer)
         center_string(self.cbwin, self.x + self.w / 2, self.y + 10, "<Split>")
         display_child(first)
         display_child(second)
         self.cbwin.draw_rectangle(self.x, self.y, self.w, self.h)
      }
   end

   #
   # Open the configuration dialog box.
   #
   public override dialog()
      return CanvasSplitDialog(self)
   end

   public override allows_null_width()
      return 1
   end

   public override allows_null_height()
      return 1
   end

   public override gen_cos()
      suspend CanvasComponent.gen_cos() | self.first | self.second
   end

   public override new()
      Split.new()
      CanvasComponent.new()
      self.class_name := "Split"
      self.import_name := "gui"
      self.name := "split"
      return
   end
end


class CanvasSplitContent(CanvasComponent,Component)
   #
   # Generate code into Code object c.
   #
   public override gen_code(c)
      every (!self.children).gen_code(c)
      CanvasComponent.gen_code(c)
      every c.chain("add(" || (!self.children).name || ")")
   end

   #
   # Return the I/O template.
   #
   public override get_template()
      suspend CanvasComponent.get_template() | ["Children", "children"]
   end

   #
   # Duplicate object.
   #
   public override dup()
      local c
      c := CanvasSplitContent()
      CanvasComponent.dup_fields(c)
      every c.add((!self.children).dup())
      return c
   end

   #
   # Overrides CanvasComponent.is_inside()
   #
   public override is_inside(c)
      local e, o
      if c === self then
         fail
      every e := back(self.z_children) do
         if o := e.is_inside(c) then
            return o
      if (self.x <= c.mx < self.x + self.w) & (self.y <= c.my < self.y + self.h) then
         return self
   end

   #
   # Overrides CanvasComponent.which_cursor_over() 
   #
   public override which_cursor_over(e)
      local t, o
      every t := back(self.z_children) do
         if o := t.which_cursor_over(e) then
            return o
   end

   public override allows_selection()
   end

   public override allows_add()
      return 1
   end

   public override allows_delete()
   end

   public override allows_copy()
   end

   public override display()
      if in_preview() then
         Component.display()
      else {
         parent.cbwin.draw_rectangle(parent.x, parent.y, parent.w, parent.h)
         center_string(self.cbwin, parent.x + parent.w / 2, parent.y + 10, "<Split>")
         display_children()
      }
   end

   #
   # Open the configuration dialog box.
   #
   public override dialog()
      return CanvasComponentDialog(self)
   end

   public override gen_cos()
     suspend CanvasComponent.gen_cos() | !self.children
   end

   public override new()
      Component.new()
      CanvasComponent.new()
      self.set_show_layout(&yes)
      self.class_name := "Component"
      self.import_name := "gui"
      self.name := "split_content"
      return
   end
end

