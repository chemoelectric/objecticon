#
# $Id$
#

package mail

import net, util(error)

class PopClient(NetClient)
   private
      username,
      password

   #
   # Flag the given message for deletion
   # :Parameters :
   # :  `n` - the message number
   #
   public dele(n)
      send_command("DELE " || n) | fail
      return self
   end

   #
   # Set the username for the session
   #
   public set_username(s)
      username := s
      return self
   end

   #
   # Set the password for the session
   #
   public set_password(s)
      password := s
      return self
   end

   #
   # Open a connection; should be followed by `login()`
   #
   public open()
      open_connection() | fail
      read_response() | {
         close_connection()
         fail
      }
      return self
   end

   #
   # Authenticate the username, password combination
   #
   public login()
      send_command("USER " || username) | fail
      send_command("PASS " || password) | fail
      return self
   end

   #
   # Disconnect using the QUIT command
   #
   public override close()
      local f
      send_command("QUIT") | (f := &why)
      close_connection() | (/f := &why)
      return if /f then self else error(f)
   end

   #
   # Send the RSET command
   #
   public rset()
      send_command("RSET") | fail
      return self
   end

   #
   # Retrieve the given message; the result is a `Message` object
   #
   public retr(n)
      local l, m, s
      send_command("RETR " || n) | fail
      l := read_multi_lines() | fail

      s := ""
      every s ||:= !l || "\r\n"

      m := Message.parse(s) | fail

      return m
   end

   #
   # Return a table of integer to integer mappings, of message numbers
   # to sizes.
   #
   # :Parameters :
   # :  `n` - optionally return only this message number
   #
   public list(n)
      local l, r, num, size, s

      r := table()
      
      if /n then {
         send_command("LIST") | fail
         l := read_multi_lines() | fail
         every s := !l do {
            s ? {
               num := integer(tab(many(&digits))) | return error("Bad list: " || s)
               =" "
               size := integer(tab(many(&digits))) | return error("Bad list: " || s)
            }
            insert(r, num, size)
         }
      }
      else {
         s := send_command("LIST " || n) | fail
         s ? {
            num := integer(tab(many(&digits))) | return error("Bad list: " || s)
            =" "
            size := integer(tab(many(&digits))) | return error("Bad list: " || s)
         }
         insert(r, num, size)
      }

      return r
   end

   #
   # Return a table of integer to integer mappings, of message numbers
   # to unique ids, using the UIDL command.
   #
   # :Parameters :
   # :  `n` - optionally return only this message number
   #
   public uidl(n)
      local l, r, num, id, s

      r := table()
      
      if /n then {
         send_command("UIDL") | fail
         l := read_multi_lines() | fail
         every s := !l do {
            s ? {
               num := integer(tab(many(&digits))) | return error("Bad list: " || s)
               =" "
               id := tab(0)
            }
            insert(r, num, id)
         }
      }
      else {
         s := send_command("UIDL " || n) | fail
         s ? {
            num := integer(tab(many(&digits))) | return error("Bad list: " || s)
            =" "
            id := tab(0)
         }
         insert(r, num, id)
      }

      return r
   end

   #
   # Read a multi-line response, ended with a "."
   # 
   private read_multi_lines()
      local l, s
      l := []
      repeat {
         s := expect_line() | fail
         if s[1:3] == ".." then
            s[1:3] := "."
         if s == "." then
            break
         put(l, s)
      }
      return l
   end

   #
   # Send a single command
   # 
   private send_command(msg)
      write_line(msg) | fail
      return read_response()
   end

   #
   # Return a two-item list being the number of messages followed by the
   # total size, obtained using the STAT command.
   #
   public stat()
      local s, num_messages, total_size

      s := send_command("STAT") | fail
      s ? {
         num_messages := integer(tab(many(&digits))) | return error("Bad stat listing: " || s)
         =" "
         total_size := integer(tab(many(&digits))) | return error("Bad stat listing: " || s)
      }

      return [num_messages, total_size]
   end

   #
   # Read a response line (+OK or -ERR)
   # 
   private read_response()
      local s

      s := expect_line() | fail

      s ? {
         if ="+OK" then {
            =" "
            return tab(0)
         }

         if ="-ERR" then {
            =" "
            return error("POP error: " || tab(0))
         }
      }

      return error("POP unrecognized response: " || s)
   end

   public override new()
      NetClient.new()
      set_port(110)
      set_server("localhost")
      return
   end
end
