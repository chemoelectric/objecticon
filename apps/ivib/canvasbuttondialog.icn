#
# $Id$
#
# This file is in the public domain.
#
# Author: Robert Parlett (parlett@dial.pipex.com)
#

import gui

############################################################################
#
#
#
class CanvasButtonDialog(CanvasComponentDialog)
   public 
      paint_field,         
      paint_down_field,
      button_group_list,   
      in_checkbox_group,   
      keyboard,            
      no_click_focus,
      checkbox_group_list, 
      in_button_group,     
      button_tab_set,
      tab_basic,
      tab_groups,
      tab_images,
      toggles,
      initially_checked,
      has_label,
      has_label_down,
      has_images

   public validate_input()
      cast(self,CanvasComponentDialog).validate_input() | fail
      if \has_label then {
         \self.paint_field.get_contents() | return alert_error(self, "Label cannot be null")
         self.paint_field.validate_input() | fail
      }
      if \has_label_down then
         self.paint_down_field.validate_input() | fail
      if \has_images then
         tab_images.validate_input() | fail
      return
   end
      
   public set_vals()
      cast(self,CanvasComponentDialog).set_vals()

      if \has_images then
         tab_images.set_vals()

      if \has_label then
         c.set_paint(paint_field.get_contents())

      if \has_label_down then
         c.set_paint_down(paint_down_field.get_contents())

      #
      # If in cbg and on, turn off, since may go out of cbg or into
      # a different one.
      #
      if (\c.parent_check_box_group).which_one === c then
         c.parent_check_box_group.which_one := &null                 

      if in_checkbox_group.is_checked() then {
         #
         # Put in cbg.
         #
         c.set_parent_check_box_group(c.parent_Canvas.checkbox_groups[checkbox_group_list.get_selection()])
         if initially_checked.is_checked() then {
            #      
            # Turn off current one on, if any; turn self on.
            #
            (\c.parent_check_box_group.which_one).is_checked_flag := &null
            c.parent_check_box_group.which_one := c
         }
      } else
         c.parent_check_box_group := &null

      #
      # Set checked flag
      #
      c.is_checked_flag := initially_checked.get_status()
      c.toggles_flag := toggles.get_status()
      if keyboard.is_checked() then
         c.set_accepts_focus("f")
      else
         c.set_accepts_focus("t")
      c.no_click_focus_flag := no_click_focus.get_status()
      c.set_parent_button_group(
        if in_button_group.is_checked() then 
            c.parent_Canvas.button_groups[button_group_list.get_selection()]
        else 
            &null
        )
   end

   public on_in_button_group()
      button_group_list.toggle_is_shaded()
   end

   public on_in_checkbox_group()
      checkbox_group_list.toggle_is_shaded()
   end

   public setup()
      cast(self,CanvasComponentDialog).setup()

      tab_basic := TabItem("label=General")
      tab_basic.set_layout(GridLayout())
      if \self.has_label then {
         paint_field := PaintField("title=",Label("label=Label"), "c:eol")
         paint_field.set_contents(c.paint)
         tab_basic.add(paint_field)
      }

      if \has_label_down then {
         paint_down_field := PaintField("title=",Label("label=Label down"), "c:eol")
         paint_down_field.set_contents(c.paint_down)
         tab_basic.add(paint_down_field)
      }

      keyboard := CheckBox("c:eol")
      keyboard.set_label("Don't accept focus at all")
      if /c.accepts_focus_flag then
         keyboard.set_is_checked("t")
      tab_basic.add(keyboard)

      no_click_focus := CheckBox("c:eol")
      no_click_focus.set_label("Don't accept focus on mouse click")
      if \c.no_click_focus_flag then
         no_click_focus.set_is_checked("t")
      tab_basic.add(no_click_focus)

      initially_checked := CheckBox("c:eol")
      initially_checked.set_label("Initially checked")
      tab_basic.add(initially_checked)

      toggles := CheckBox("c:eol")
      toggles.set_align("l", "c")
      toggles.set_label("Toggles state")
      if \c.toggles_flag then
         toggles.set_is_checked("t")
      tab_basic.add(toggles)
      add_tab_at_front(tab_basic)

      tab_groups := TabItem("label=Groups")
      tab_groups.set_layout(GridLayout("extra=borders"))
      in_button_group := CheckBox()
      in_button_group.connect(self.on_in_button_group, Event.ACTION)
      in_button_group.set_label("Button Group")
      tab_groups.add(in_button_group)
      button_group_list := List("c:eol")
      button_group_list.set_size(175)
      button_group_list.set_selection_list(cos_names(c.parent_Canvas.button_groups))
      tab_groups.add(button_group_list)

      in_checkbox_group := CheckBox()
      in_checkbox_group.connect(self.on_in_checkbox_group, Event.ACTION)
      in_checkbox_group.set_label("CheckBox Group")
      tab_groups.add(in_checkbox_group)
      checkbox_group_list := List("c:eol")
      checkbox_group_list.set_size(175)
      checkbox_group_list.set_selection_list(cos_names(c.parent_Canvas.checkbox_groups))
      tab_groups.add(checkbox_group_list)
      add_tab(tab_groups)

      if \self.has_images then {
         tab_images := CheckBoxImageTab(c)
         add_tab(tab_images)
      }

      self.set_wattrib("label", label)

      if *c.parent_Canvas.checkbox_groups = 0 then {
         in_checkbox_group.set_is_shaded("t")
         checkbox_group_list.set_is_shaded("t")
         if c.is_checked() then
            initially_checked.set_is_checked("t")
      } else {
         if \c.parent_check_box_group then {
            in_checkbox_group.set_is_checked("t")
            checkbox_group_list.set_selection(keyof(c.parent_Canvas.checkbox_groups,
                                                         c.parent_check_box_group))
            if c === c.parent_check_box_group.which_one then
               initially_checked.set_is_checked("t")
         } else  {
            checkbox_group_list.set_is_shaded("t")
            if c.is_checked() then
               initially_checked.set_is_checked("t")
         }
      }

      if *c.parent_Canvas.button_groups = 0 then {
         in_button_group.set_is_shaded("t")
         button_group_list.set_is_shaded("t")
      } else {
         if \c.parent_button_group then {
            in_button_group.set_is_checked("t")
            button_group_list.set_selection(keyof(c.parent_Canvas.button_groups,
                                                       c.parent_button_group))
         } else 
            button_group_list.set_is_shaded("t")
      }
   end
end


