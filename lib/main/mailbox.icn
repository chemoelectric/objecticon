#
# $Id$
#

package mail

#
# This class represents an internet Address, which
# may be parsed in accordance with RFC822.
#
class Mailbox(Address)
   private readable
      local_part,
      domain,
      phrase,
      route

   public get_local_part() 
      return local_part
   end

   public set_local_part(x)
      local_part := x
   end

   public get_domain()
      return domain
   end
   
   public set_domain(x)
      domain := x
   end

   public get_phrase() 
      return phrase
   end

   public set_phrase(x)
      phrase := x
   end

   public get_route() 
      return route
   end

   public set_route(x)
      route := x
   end

   public to_rfc822()
      local res, addr

      addr := local_part || "@" || domain

      if *(phrase | route) > 0 then {
         if *phrase > 0 then
            res := phrase || " <"
         else
            res := "<"
         if *route > 0 then {
            every res ||:= "@" || !route
            res ||:= ": "
         }
         return res || addr || ">"
      } else
         return addr
   end

   #
   # Output object
   #
   private object_output(oo)
      return oo.writes(to_rfc822())
   end

   public parse(s)
      local p
      p := RFC822Parser()
      return p.parse_mailbox(s, self)
   end   

   public generate_mailboxes()
      return self
   end

   public static mailbox_list_to_string(l)
      local s, e
      s := ""
      every e := !l do {
         if *s > 0 then
            s ||:= ","
         s ||:= e.to_rfc822()
      }
      return s
   end

   public new(a[])
      if *a = 1 then
         parse(a[1]) | fail
      else if *a >= 2 then {
         local_part := a[1]
         domain := a[2]
         phrase := a[3] | ""
         route := a[4] | []
      }
      return
   end
end

