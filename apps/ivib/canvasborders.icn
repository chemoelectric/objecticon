import gui

class CanvasABorder(CanvasObject)
   public 
     empty_l_inset,
     empty_r_inset,
     empty_t_inset,
     empty_b_inset, 
     x_align_val,
     y_align_val

   public defer create_visual_border()

   #
   # Copy fields from self to c.
   #
   public dup_fields(c)
      CanvasObject.dup_fields(c)
      c.empty_l_inset := self.empty_l_inset
      c.empty_r_inset := self.empty_r_inset
      c.empty_t_inset := self.empty_t_inset
      c.empty_b_inset := self.empty_b_inset
      c.x_align_val := self.x_align_val
      c.y_align_val := self.y_align_val
   end

   public rescale(f)
      self.empty_l_inset := integer(\self.empty_l_inset * f)
      self.empty_r_inset := integer(\self.empty_r_inset * f)
      self.empty_t_inset := integer(\self.empty_t_inset * f)
      self.empty_b_inset := integer(\self.empty_b_inset * f)
   end

   #
   # Return the I/O template.
   #
   public get_template()
      suspend CanvasObject.get_template() |
         ["Left inset", "empty_l_inset"] |
         ["Right inset", "empty_r_inset"] |
         ["Top inset", "empty_t_inset"] |
         ["Bottom inset", "empty_b_inset"] |
         ["X align", "x_align_val"] |
         ["Y align", "y_align_val"]
   end

   public dialog()
      return BorderEdit(self)
   end

   public new()
      self.name := "border"
      self.import_name := "gui"
      return
   end
end

class CanvasEmptyBorder(CanvasABorder)
   public gen_code(c)
      c.line(name || " := " || self.class_name || "()")
      c.chain("set_l_inset(" || c.scalable_dim(\self.empty_l_inset) || ")")
      c.chain("set_r_inset(" || c.scalable_dim(\self.empty_r_inset) || ")")
      c.chain("set_t_inset(" || c.scalable_dim(\self.empty_t_inset) || ")")
      c.chain("set_b_inset(" || c.scalable_dim(\self.empty_b_inset) || ")")
      c.chain("set_x_align(" || c.align_str(\self.x_align_val) || ")")
      c.chain("set_y_align(" || c.align_str(\self.y_align_val) || ")")
      c.add_var(name, class_variable)
      c.add_import(self.import_name, self.class_name)
   end

   public create_visual_border()
      local b
      b := EmptyBorder()
      b.set_l_inset(\self.empty_l_inset)
      b.set_r_inset(\self.empty_r_inset)
      b.set_t_inset(\self.empty_t_inset)
      b.set_b_inset(\self.empty_b_inset)
      b.set_x_align(\self.x_align_val)
      b.set_y_align(\self.y_align_val)
      return b
   end

   public dup()
      local c
      c := CanvasEmptyBorder()
      self.dup_fields(c)
      return c
   end

   public new()
      CanvasABorder.new()
      self.class_name := "EmptyBorder"
      return
   end
end

class CanvasNullBorder(CanvasABorder)
   public gen_code(c)
      c.line(name || " := " || self.class_name || "()")
      c.chain("set_x_align(" || c.align_str(\self.x_align_val) || ")")
      c.chain("set_y_align(" || c.align_str(\self.y_align_val) || ")")
      c.add_var(name, class_variable)
      c.add_import(self.import_name, self.class_name)
   end

   public create_visual_border()
      local b
      b := NullBorder()
      b.set_x_align(\self.x_align_val)
      b.set_y_align(\self.y_align_val)
      return b
   end

   public dup()
      local c
      c := CanvasNullBorder()
      self.dup_fields(c)
      return c
   end

   public new()
      CanvasABorder.new()
      self.class_name := "NullBorder"
      return
   end
end

class CanvasLineBorder(CanvasABorder)
   public 
      line_width_val,
      has_empty

   public get_template()
      suspend CanvasABorder.get_template() |
         [ "Line width", "line_width_val"] |
         [ "Has empty", "has_empty"]
   end

   public rescale(f)
      CanvasABorder.rescale(f)
      self.line_width_val := integer(\self.line_width_val * f)
   end

   public gen_code(c)
      if \has_empty then {
         c.line(name || " := CompoundBorder()")
         c.chain("set_outer(" || class_name || "()")
         c.chain("   set_line_width(" || c.scalable_dim(\self.line_width_val) || ")")
         c.append_prev(")")
         c.chain("set_inner(EmptyBorder()")
         c.chain("   set_l_inset(" || c.scalable_dim(\self.empty_l_inset) || ")")
         c.chain("   set_r_inset(" || c.scalable_dim(\self.empty_r_inset) || ")")
         c.chain("   set_t_inset(" || c.scalable_dim(\self.empty_t_inset) || ")")
         c.chain("   set_b_inset(" || c.scalable_dim(\self.empty_b_inset) || ")")
         c.append_prev(")")
         c.add_import("gui", "CompoundBorder")
         c.add_import("gui", "EmptyBorder")
      } else {
         c.line(name || " := " || self.class_name || "()")
         c.chain("set_line_width(" || c.scalable_dim(\self.line_width_val) || ")")
      }
      c.chain("set_x_align(" || c.align_str(\self.x_align_val) || ")")
      c.chain("set_y_align(" || c.align_str(\self.y_align_val) || ")")
      c.add_var(name, class_variable)
      c.add_import(self.import_name, self.class_name)
   end

   public dup_fields(c)
      CanvasABorder.dup_fields(c)
      c.line_width_val := self.line_width_val
      c.has_empty := self.has_empty
   end

   public create_visual_border()
      local b
      if \has_empty then {
         b := CompoundBorder().set_outer(create_outer_visual_border()).set_inner(EmptyBorder())
         b.inner.set_l_inset(\self.empty_l_inset)
         b.inner.set_r_inset(\self.empty_r_inset)
         b.inner.set_t_inset(\self.empty_t_inset)
         b.inner.set_b_inset(\self.empty_b_inset)
         b.outer.set_line_width(\self.line_width_val)
      } else {
         b := create_outer_visual_border()
         b.set_line_width(\self.line_width_val)
      }
      b.set_x_align(\self.x_align_val)
      b.set_y_align(\self.y_align_val)
      return b
   end

   public defer create_outer_visual_border()
end

class CanvasPlainBorder(CanvasLineBorder)
   public dup()
      local c
      c := CanvasPlainBorder()
      CanvasLineBorder.dup_fields(c)
      return c
   end

   public create_outer_visual_border()
      return PlainBorder()
   end

   public new()
      CanvasLineBorder.new()
      self.class_name := "PlainBorder"
      return
   end
end

class CanvasBevelledBorder(CanvasLineBorder)
   public dup()
      local c
      c := CanvasBevelledBorder()
      CanvasLineBorder.dup_fields(c)
      return c
   end

   public create_outer_visual_border()
      return BevelledBorder()
   end

   public new()
      CanvasLineBorder.new()
      self.class_name := "BevelledBorder"
      return
   end
end

class CanvasRaisedBorder(CanvasLineBorder)
   public dup()
      local c
      c := CanvasRaisedBorder()
      CanvasLineBorder.dup_fields(c)
      return c
   end

   public create_outer_visual_border()
      return RaisedBorder()
   end

   public new()
      CanvasLineBorder.new()
      self.class_name := "RaisedBorder"
      return
   end
end

class CanvasSunkenBorder(CanvasLineBorder)
   public dup()
      local c
      c := CanvasSunkenBorder()
      CanvasLineBorder.dup_fields(c)
      return c
   end

   public create_outer_visual_border()
      return SunkenBorder()
   end

   public new()
      CanvasLineBorder.new()
      self.class_name := "SunkenBorder"
      return
   end
end

