#
# $Id$
#

package mail

import util, ipl.strings, ipl.pdco

class TextHandler(TypeHandler)
   public override can_handle(ct)
      succeed map(ct.get_type()) == "text"
   end
   
   public override convert_to_object(m, data)
      local res

      #
      # Convert line endings to local form
      #
      res := String{ separate_lines(data), "\N" }

      if map(m.get_content_type().get_parameter("charset")) == "utf-8" then
         res := ucs(res) | return error("TextHandler: raw data not in UTF-8 format")

      return res
   end

   public override convert_from_object(m, obj)
      local res

      obj := string(obj) | return error("TextHandler: Couldn't convert object to string: " || image(obj))

      #
      # Convert line endings to canonical form (CRLF)
      #
      res := String{ separate_lines(obj), "\r\n" }

      return res
   end
end
