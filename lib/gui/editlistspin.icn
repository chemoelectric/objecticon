#
# $Id$
#
# This file is in the public domain.
#
# Author: Robert Parlett (parlett@dial.pipex.com)
#

package gui

#
# Selection from a list with edit
#
class EditListSpin(SelectionSpin)
   public 
      cc_ev

   public apply_selection(ev)
      local s 
      s := extract_selection() | ""
      if /ev then
         self.view.set_contents(s)
      else {
         # Note the event so that we don't set selection to &null in on_textfield below.
         cc_ev := ev
         self.view.assign_contents(s, ev)
      }
   end

   public on_textfield(ev, src, type)
      if type = Event.CONTENT_CHANGED & ev ~=== cc_ev then
         self.selection := &null
      # Fire the event with self as source.
      fire(type, ev)
   end

   #
   # Set the text field's filter (see {TextField} for details).
   # @param c  The filter
   #
   public set_filter(c)
      self.view.set_filter(c)
      return self
   end

   #
   # Return the contents of the text field (which may have
   # been edited).
   #
   public get_contents()
      return view.get_contents()
   end

   public create_view()
      return SpinTextField()
   end

   #
   # Set the contents of the text to the given string; the
   # selection is set to any matching item in the list, or
   # &null if there is no match.
   #
   public set_contents(x)
      if not object_set_selection(x) then {
         self.selection := &null
         self.view.set_contents(x)
      }
      return self
   end

   public get_default_width()
      local t
      t := 0
      # Max string length + space for cursor (cf TextField's get_view() method)
      every t <:= self.cbwin.text_width((view.contents | !self.selection_list) || " ")
      return border.get_total_width() + up.get_preferred_width() + t + view.border.get_total_width()
   end

   public new()
      cast(self,SelectionSpin).new()
      every view.connect(self.on_textfield, Event.CONTENT_CHANGED | Event.ACTION)
      return
   end
end


