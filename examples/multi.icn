import gui

class ChildDialog(Dialog)
   public clock_label, ticker

   public handle_create_modeless(ev)
      local d
      d := ChildDialog().set_label("Modeless Dialog")
      d.show(self)
   end

   public handle_create_orphan_modeless(ev)
      local d
      d := ChildDialog().set_label("Orphan modeless Dialog")
      d.show()
   end

   public handle_create_modal(ev)
      local d
      d := ChildDialog().set_label("Modal Dialog")
      d.show_modal(self)
   end

   public handle_create_orphan_modal(ev)
      local d
      d := ChildDialog().set_label("Orphan modal Dialog")
      d.show_modal()
   end

   public override end_dialog()
      ticker.stop()
   end

   public override init_dialog()
      ticker := Dispatcher.new_task{tick()}
      ticker.start()
   end

   public tick()
      repeat {
         clock_label.set_label(&clock)
         ticker.sleep(1000)
      }
   end

   public override component_setup()
      local p
      self.set_resize(&yes)
      set_layout(GridLayout())
      add(Label().set_font("serif,20").set_constraint("eol", &yes).set_constraint("x_align", "c").set_label("Multiple dialogs"))

      add(clock_label := Label().set_label(&clock).set_constraint("eol", &yes).set_constraint("x_fill", &yes).set_border(PlainBorder().set_align("c","c")))

      add(TextButton().set_label("Create Child Modeless Dialog").set_constraint("eol", &yes).connect(handle_create_modeless, Event.ACTION))

      add(TextButton().set_label("Create Orphan Modeless Dialog").set_constraint("eol", &yes).connect(handle_create_orphan_modeless, Event.ACTION))

      add(TextButton().set_label("Create Child Modal Dialog").set_constraint("eol", &yes).connect(handle_create_modal, Event.ACTION))

      add(TextButton().set_label("Create Orphan Modal Dialog").set_constraint("eol", &yes).connect(handle_create_orphan_modal, Event.ACTION))

      p := Component().set_layout(GridLayout().set_doi(0))
      p.add(TextButton().set_label("Close").connect(dispose, Event.ACTION))
      p.add(TextButton().set_label("Quit").connect( create exit(), Event.ACTION))
      self.add(p)
   end
end


procedure main()
   local d
   d := ChildDialog().set_label("Dialog Modes")
   d.show()
   Dispatcher.message_loop()
end


