#
#
# This file is in the public domain.
#
# Author: Robert Parlett (parlett@dial.pipex.com)
#

import gui

############################################################################
#
#
#
class CanvasTextDisplayDialog(CanvasComponentDialog)
   public 
      contents,
      ucs_cb

   public set_vals()
      CanvasComponentDialog.set_vals()
      if *contents.get_contents_str() = 0 then
         c.set_contents([])
      else
         c.set_contents(contents.get_contents())
   end

   private on_ucs_cb()
      if ucs_cb.is_checked() then
         contents.set_contents([u""])
      else
         contents.set_contents([""])
   end

   public setup()
      local p

      CanvasComponentDialog.setup()

      p := TabItem()
      p.set_layout(GridLayout())
      add_tab_at_front(p)
      p.set_label("Contents")

      contents := EditableTextList().set_constraint("eol", &yes)
      contents.set_size(scale(200), scale(200))
      contents.set_contents(copy(c.get_contents()))
      p.add(contents)

      ucs_cb := CheckBox().set_label("Ucs content")
      if type(c.contents[1]) == "ucs" then
         ucs_cb.set_is_checked(&yes)
      ucs_cb.connect(self.on_ucs_cb, Event.ACTION)
      p.add(ucs_cb)
   end
end

