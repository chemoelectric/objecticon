import io, ipl.pdco

procedure dir_recurse1(f)
   local s, p, f2, l
   write("Opening ", f)
   s := DirStream(f) | {
      write("Failed to open ", f, ": ", &why)
      fail
   }

   coact(f)
   p := FilePath(f)
   repeat {
      l := s.read_line() | fail
      if /l then
         break
      if Files.is_relative_dir(l) then
         next
      f2 := p.child(l).str() 
      if Files.is_directory(f2) then
         dir_recurse1(f2)
      else
         coact(f2)
   }

   write("Closing ", f)
   s.close()
end

procedure dir_recurse(f)
   suspend Seq{ dir_recurse1(f) }
end

procedure main(a)
   local f, d, n

   d := a[1] | "/tmp"
   n := integer(a[2]) | 100

   every f := dir_recurse(d) \ n do
      write("   Got ", f)

   write("Exiting")
end
