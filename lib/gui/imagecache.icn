package gui

import util, graphics, io

class ImageCache()
   public static 
      map,
      path

   private static init()
      map := table()
      path := []
   end

   public static add_dir(d)
      d := Files.get_path(d).canonical()
      # Ensure it isn't in the list twice.
      if d.str() == (!path).str() then
         return
      push(path, d)
   end

   #
   # Get the full file path where the given window was found and loaded from.
   #
   public static get_source(w)
      return \map[w]
   end

   public static get(s, bg)
      local f, w, key
      /bg := "white"
      every f := get_paths(s) do {
         key := bg || "#" || f
         if w := \map[key] then
            return w
         if Files.stat(f).mode[1] == "-" then {
            w := Window.open("bg=",bg, "canvas=hidden")
            w.set_image(f) | return error("Couldn't load image from file " || f)
            insert(map, key, w)
            insert(map, w, f)
            return w
         }
      }
      return error("Couldn't find on path:" || s)
   end

   public static delete(s, bg)
      local f, w, key
      /bg := "white"
      every f := get_paths(s) do {
         key := bg || "#" || f
         if w := \map[key] then {
            w.close()
            default.delete(map, key)
            default.delete(map, w)
            return
         }
      }
   end

   public static get_paths(s)
      local p
      p := Files.get_path(s)
      if p.is_absolute() then
         return p.canonical().str()
      suspend (!path).append(p).str()
   end
   
   final private new()
   end
end

