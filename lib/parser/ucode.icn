package parser

import
   util(error),
   lang(UcodeOp)

class UFile()
   private readable
      pack,
      declend_offset,
      strinv,
      version

   private const
      imports,
      invocables,
      symbols

   package set_package(pack)
      self.pack := pack
   end

   package add_symbol(obj)
      put(symbols, obj)
   end

   public get_symbols()
      suspend .!symbols
   end

   package add_import(i)
      put(imports, i)
   end

   public get_imports()
      suspend .!imports
   end

   package add_invocable(s)
      put(invocables, s)
   end

   public get_invocables()
      suspend .!invocables
   end

   package set_declend_offset(p)
      self.declend_offset := p
   end

   package set_strinv(v)
      self.strinv := v
   end

   package set_version(s)
      self.version := s
   end

   package new()
      self.symbols := []
      self.invocables := []
      self.imports := []
      return
   end
end

class UObject()
   public const
      name,
      file,
      line

   package new(name, file, line)
      self.name := name
      self.file := file
      self.line := line
      return
   end
end

class UTopLevel(UObject)
   public const
      package_flag

   package new(name, package_flag, file, line)
      UObject.new(name, file, line)
      self.package_flag := package_flag
      return
   end
end

class UClass(UTopLevel)
   public const
      supers,
      fields,
      flags

   package add_field(o)
      put(fields, o)
   end

   public get_fields()
      suspend .!fields
   end

   package add_super(s)
      put(supers, s)
   end

   public get_supers()
      suspend .!supers
   end

   package new(name, package_flag, file, line, flags)
      UTopLevel.new(name, package_flag, file, line)
      self.flags := flags
      self.supers := []
      self.fields := []
      return
   end
end

class UVariable(UObject)
   public const
      flags

   package new(name, file, line, flags)
      UObject.new(name, file, line)
      self.flags := flags
      return
   end
end

class ULocal()
   public static const
      # variable declared local dynamic 
      Dynamic,
      # variable declared local static 
      Static,
      # variable is a formal parameter 
      Argument,
      # argument is a vararg
      Vararg

   public const
      flags,
      id

   private static init()
      Dynamic := 8r020
      Static := 8r040
      Argument := 8r01000
      Vararg := 8r0200
   end

   package new(id, flags)
      self.id := id
      self.flags := flags
      return
   end
end

class UConst()
   public const
      flags,
      val

   public static const
      # literal is an integer 
      IntLit,
      # literal is a real 
      RealLit,
      # literal is a string 
      StrLit,
      # literal is a cset 
      CsetLit,
      # literal is a ucs 
      UcsLit,
      # literal is a large int
      LrgintLit

   private static init()
      IntLit := 8r02000
      RealLit := 8r04000
      StrLit := 8r010000
      CsetLit := 8r020000
      UcsLit := 8r040000
      LrgintLit := 8r0100000
   end

   package new(val, flags)
      self.val := val
      self.flags := flags
      return
   end
end

class UFunction()
   private readable
      locals,
      consts

   package add_local(l)
      put(locals, l)
   end

   public get_locals()
      suspend .!locals
   end

   package add_const(l)
      put(consts, l)
   end

   public get_consts()
      suspend .!consts
   end

   package new()
      self.locals := []
      self.consts := []
      return
   end
end

class UMethod(UObject, UFunction)
   public const
      flags

   package new(name, file, line, flags)
      UObject.new(name, file, line)
      UFunction.new()
      self.flags := flags
      return
   end
end

class UProcedure(UTopLevel, UFunction)
   package new(name, package_flag, file, line)
      UTopLevel.new(name, package_flag, file, line)
      UFunction.new()
      self.package_flag := package_flag
      return
   end
end

class URecord(UTopLevel)
   private
      fields

   public get_fields()
      suspend .!fields
   end

   package add_field(s)
      put(fields, s)
   end

   package new(name, package_flag, file, line)
      UTopLevel.new(name, package_flag, file, line)
      self.fields := []
      return
   end
end

class UGlobal(UTopLevel)
   public const
      readable_flag

   package new(name, package_flag, readable_flag, file, line)
      UTopLevel.new(name, package_flag, file, line)
      self.readable_flag := readable_flag
      return
   end
end

class UReader()
   public scan_decls()
      local uf, file, line, uop, s, n, curr_import, curr_class, 
         curr_record, curr_func
      uf := UFile()
      uop := uin_expectop() | fail
      uop == UcodeOp.Version | return error("Missing version identification")
      s := uin_str() | fail
      uf.set_version(s)
      repeat {
         uop := uin_expectop() | fail
         case uop of {
            UcodeOp.Filen:
               file := uin_str() | fail

            UcodeOp.Line:
               line := uin_16() | fail

            UcodeOp.Declend: {
               uf.set_declend_offset(&pos)
               break
            }

            UcodeOp.Package: {
               s := uin_str() | fail
               uf.set_package(s)
            }

            UcodeOp.Import: {
               s := uin_str() | fail
               n := uin_16() | fail
               curr_import := ImportDec(s, n)
               uf.add_import(curr_import)
            }

            UcodeOp.Importsym: {
               s := uin_str() | fail
               curr_import.add(s)
            }

            UcodeOp.Class: {
               n := uin_32() | fail
               s := uin_str() | fail
               curr_class := UClass(s, &no, file, line, n)
               uf.add_symbol(curr_class)
            }

            UcodeOp.PkClass: {
               n := uin_32() | fail
               s := uin_str() | fail
               curr_class := UClass(s, &yes, file, line, n)
               uf.add_symbol(curr_class)
            }

            UcodeOp.Super: {
               s := uin_str() | fail
               curr_class.add_super(s)
            }

            UcodeOp.Classfield: {
               n := uin_32() | fail
               s := uin_str() | fail
               if iand(n, lang.Class.Method) ~= 0 then {
                  curr_func := UMethod(s, file, line, n)
                  curr_class.add_field(curr_func)
               } else
                  curr_class.add_field(UVariable(s, file, line, n))
            }

            UcodeOp.Recordfield: {
                s := uin_str() | fail
                curr_record.add_field(s)
             }

             UcodeOp.Record: {
                s := uin_str() | fail
                curr_record := URecord(s, &no, file, line)
                uf.add_symbol(curr_record)
             }

             UcodeOp.PkRecord: {
                s := uin_str() | fail
                curr_record := URecord(s, &yes, file, line)
                uf.add_symbol(curr_record)
             }

             UcodeOp.Procdecl: {
                s := uin_str() | fail
                curr_func := UProcedure(s, &no, file, line)
                uf.add_symbol(curr_func)
             }

             UcodeOp.PkProcdecl: {
                s := uin_str() | fail
                curr_func := UProcedure(s, &yes, file, line)
                uf.add_symbol(curr_func)
             }

             UcodeOp.Local: {
                n := uin_32() | fail
                s := uin_str() | fail
                curr_func.add_local(ULocal(s, n))
             }

             UcodeOp.Ldata: {
                n := uin_32() | fail
                s := uin_lbin() | fail
                curr_func.add_const(UConst(s, n))
             }

             UcodeOp.Sdata: {
                n := uin_32() | fail
                s := uin_sbin() | fail
                curr_func.add_const(UConst(s, n))
             }

             UcodeOp.Global: {
                s := uin_str() | fail
                uf.add_symbol(UGlobal(s, &no, &no, file, line))
             }
             UcodeOp.PkGlobal: {
                s := uin_str() | fail
                uf.add_symbol(UGlobal(s, &yes, &no, file, line))
             }
             UcodeOp.PkRdGlobal: {
                s := uin_str() | fail
                uf.add_symbol(UGlobal(s, &yes, &yes, file, line))
             }

             UcodeOp.Invocable: {
                s := uin_str() | fail
                if s == "0" then
                   uf.set_strinv(&yes)
                else
                   uf.add_invocable(s)
             }

            default:
               return error("ill-formed ufile"||uop)
         }
      }

      return uf
   end

   private uin_expectop()
      return uin_op() | error("Unexpected EOF")
   end

   private uin_op()
      return ord(move(1))
   end

   private uin_str()
      local s
      s := tab(upto('\0')) | return error("Unexpected EOF")
      move(1)
      return s
   end

   private uin_16()
      local i
      i := raw_convert(move(2)) | return error("Unexpected EOF")
      if i > 16r7fff then
         i -:= 16r10000
      return i
   end

   private uin_32()
      local i
      i := raw_convert(move(4)) | return error("Unexpected EOF")
      if i > 16r7fffffff then
         i -:= 16r100000000
      return i
   end

   private uin_sbin()
      return move(ord(move(1))) | error("Unexpected EOF")
   end

   private uin_lbin()
      return move(raw_convert(move(4))) | error("Unexpected EOF")
   end

   private static native raw_convert(s)
end
