#
# $Id$
#
# This file is in the public domain.
#
# Author: Robert Parlett (parlett@dial.pipex.com)
#

import xml, ipl.options, io

procedure main(a)
   local fmt, p, s, d, t, x, opt
   fmt := CanonicalXmlFormatter()
   p := XmlParser().set_preserve_insignificant_whitespace(&yes).set_do_namespaces(&no)

   opt := options(a, "-u!")
   *a > 0 | stop("usage: testcase source [output]")

   s := Files.file_to_string(a[1]) | stop("couldn't open" || a[1])
   if \opt["u"] then
      s := ucs(s) | stop("Invalid utf-8 in valid case ", a[1], ": ",  &why)

   if d := p.parse(s) then {
      if d.validity_errors > 0 then {
         write("Testcase : ", a[1], " unexpectedly gave validity errors")
         write(s)
         write("Failure")
         write("End===============================================================")
         exit(1)
      }

      if *a = 1 then 
         exit(0)

      t := Files.file_to_string(a[2]) | stop("couldnt open" || a[2])

      x := fmt.format_to_string(d)
      if x == t then
         exit(0)

      write("Testcase : ", a[1])
      write("Parsed:")
      write(image(x))
      write("Test:")
      write(image(t))
      write("Failure")
      write("End===============================================================")
      exit(1)
   } else {
      write("Couldn't parse case ", a[1], ":")
      write(s)
      write("Failure")
      write("End===============================================================")
      exit(1)
   }

end


procedure to_utf8(s)
   local t, c
write("fallback")
   if type(s) == "ucs" then
      return string(s)
   t := ""
   every c := !s do {
      if ord(c) > 127 then
         t ||:= string(uchar(ord(c)))
      else
         t ||:= c
   }
   return t
end

