#
# $Id$
#
# This file is in the public domain.
#
# Author: Robert Parlett (parlett@dial.pipex.com)
#

import gui


############################################################################
#
# Class for representing a OverlaySet on the canvas.
#
class CanvasOverlaySet(CanvasComponent,OverlaySet)
   #
   # Generate code into Code object c.
   #
   public override gen_code(c)
      every (!self.children).gen_code(c)
      CanvasComponent.gen_code(c)
      c.chain("add(" || (!self.children).name || ")")
      c.chain("set_which_one(" || self.which_one.name || ")")
   end

   #
   # Duplicate object.
   #
   public override dup()
      local c, e, e1
      c := CanvasOverlaySet()
      CanvasComponent.dup_fields(c)
      every e := !self.children do {
         e1 := e.dup()
         c.add(e1)
         if self.which_one === e then
            c.set_which_one(e1)
      }
      return c
   end
      
   #
   # Return the I/O template.
   #
   public override get_template()
      suspend CanvasComponent.get_template() |
         ["Children", "children"] |
         ["Which One", "which_one"]
   end

   #
   # Overrides CanvasComponent.is_inside()
   #
   public override is_inside(c)
      return (c ~=== self) & self.which_one.is_inside(c)
   end

   #
   # Overrides CanvasComponent.which_cursor_over() 
   #
   public override which_cursor_over(e)
      local o
      CanvasComponent.which_cursor_over(e) | fail
      if in_resize_region(e) then
         link
      if o := self.which_one.which_cursor_over(e) then
         return o
      link
   end

   #
   # Open the configuration dialog box.
   #
   public override dialog()
      return CanvasOverlaySetDialog(self)
   end

   public override allows_null_width()
      local c
      every c := !self.children do
         c.allows_null_width() | fail
      return
   end

   public override allows_null_height()
      local c
      every c := !self.children do
         c.allows_null_height() | fail
      return
   end

   public string_rep()
      local l, c, s
      l := []
      every c := !self.children do {
         s := c.name
         if c === self.which_one then
            s ||:= "*"
         put(l, s)
      }
      put(l, "")
      return l
   end

   public override gen_cos()
      suspend CanvasComponent.gen_cos() | !self.children
   end

   public override new()
      OverlaySet.new()
      CanvasComponent.new()
      self.class_name := "OverlaySet"
      self.import_name := "gui"
      self.name := "overlay_set"
      return
   end
end

