#
# $Id$
#

#
# Convert old Ivib layout lines to new ones
#

import lang, gui, util, io


class Loader()
   private readable
      fname,
      lname

   public load()
      local f, r
      f := open(fname) | return error("Couldn't open " || fname)
      r := load_impl(f)
      close(f)
      return \r
   end

   public load_impl(f)
      local s
      repeat {
         s := read(f) | return error("No Ivib layout in " || fname)
         if s == Ivib.V1_MARKER then
            return Source1(load_layout(f))
         if s == Ivib.V2_MARKER then
            return Source2(load_layout(f))
         s ? if =Ivib.V2i_MARKER then {
            lname := tab(0)
            return Source2(load_ilayout())
         }
      }
   end

   public load_ilayout()
      local t, g
      t := resolve_layout_filename(fname, lname)
      g := open(t) | return error("Couldn't open layout file:" || t)
      t := read(g)
      close(g)
      return t
   end

   public load_layout(f)
      local s, t, hash
      t := ""
      while s := read(f) do s ? {
         if /hash := tab(many('#')) then
            tab(1)
         if =hash then
            t ||:= tab(0)
         else
            break
      }
      if *t = 0 then
         return error("Misformed Ivib layout in " || fname)

      return t
   end

   public new(fname)
      self.fname := fname
      return
   end
end


class Source()
   public 
      version, 
      code

   public optional createconverterset()

   public migrate()
      local cs, d, o, e
      cs := createconverterset()
      if any(&lcase, code) then
         d := LegacyCustomDecode(cs)
      else
         d := CustomDecode(cs)
      o := (code ? d.decode()) | fail
      e := CustomEncode()
      e.encode(o)
      return e.get_string()
   end

end


class Source1(Source)
   #
   # ConverterSet for MenuComponents
   #
   public createcmc_converter(s)
      local c
      c := Converter(s)
      c.add_action("Var Category", MenuVarCategoryAction())
      c.add_extra(AddAction("Import Name", "gui"))
      return c
   end

   #
   # ConverterSet for Components
   #
   public createcc_converter(s)
      local c
      c := Converter(s)
      c.add_action("Tooltip", FixTooltipAction())
      c.add_action("Var Category", VarCategoryAction())
      c.add_extra(AddAction("Import Name", "gui"))
      c.add_extra(FixDefaultDimensionsAction())
      c.add_extra(FixAttribsAction())
      c.add_extra(AddAction("Z", 0))
      c.add_extra(AddAction("Tab ord", 0))
      c.add_extra(AddAction("Constraints", table()))
      return c
   end

   #
   # Create the ConverterSet to use.
   #
   public createconverterset()
      local cs, c
      cs := ConverterSet()

      c := Converter("CDialog")
      c.add_extra(FixAttribsAction())
      cs.add(c)

      c := Converter("Canvas")
      c.add_action("Gen Methods", RemoveAction())
      c.add_action("Items", ChildrenAction())
      c.add_action("Button Groups", FixGroupsAction())
      c.add_action("Checkbox Groups", FixGroupsAction())
      c.add_extra(AddAction("SuperClass Name", "Dialog"))
      c.add_extra(AddAction("Import Name", "gui"))
      c.add_extra(AddAction("Event Handlers", [["&null", "handle_v2_events"]]))
      c.add_extra(AddMissingKeyAction("Edit Scale", 1.0))
      cs.add(c)

      c := createcc_converter("CanvasBorder")
      c.add_action("Components", ChildrenAction())
      cs.add(c)

      c := Converter("CanvasButtonGroup")
      c.add_extra(AddAction("Import Name", "gui"))
      cs.add(c)

      c := createcc_converter("CanvasCheckBox")
      c.add_action("Label", StringToPaintAction("Paint"))
      cs.add(c)

      c := Converter("CanvasCheckBoxGroup")
      c.add_extra(AddAction("Import Name", "gui"))
      cs.add(c)

      c := createcc_converter("CanvasCustom")
      c.add_action("W Default", ChangeAction(&null))
      c.add_action("H Default", ChangeAction(&null))
      cs.add(c)

      c := createcc_converter("CanvasEditableTextList")
      c.add_action("Draw Border", ChangeAction(1))
      cs.add(c)

      c := createcc_converter("CanvasEditList")
      cs.add(c)

      c := createcc_converter("CanvasIcon")
      c.add_action("Img", ImgToPaintAction("Paint"))
      cs.add(c)

      c := createcc_converter("CanvasIconButton")
      c.add_action("Img Up", ImgToPaintAction("Paint"))
      c.add_action("Img Down", ImgToPaintAction("Paint down"))
      cs.add(c)

      c := createcc_converter("CanvasImage")
      cs.add(c)

      c := createcc_converter("CanvasLabel")
      c.add_action("Label", StringToPaintAction("Paint"))
      cs.add(c)

      c := createcc_converter("CanvasList")
      cs.add(c)

      c := createcc_converter("CanvasOverlayItem")
      c.add_action("Components", ChildrenAction())
      cs.add(c)

      c := createcc_converter("CanvasOverlaySet")
      c.add_action("Components", ChildrenAction())
      cs.add(c)

      c := createcc_converter("CanvasPanel")
      c.add_action("Components", ChildrenAction())
      cs.add(c)

      c := createcc_converter("CanvasScrollBar")
      cs.add(c)

      c := createcc_converter("CanvasTabItem")
      c.add_action("Components", ChildrenAction())
      c.add_action("Label", StringToPaintAction("Paint"))
      cs.add(c)

      c := createcc_converter("CanvasTable")
      c.add_extra(FixSelectionAction())
      cs.add(c)

      c := createcc_converter("CanvasTableColumn")
      c.add_action("Label", StringToPaintAction("Paint"))
      cs.add(c)

      c := createcc_converter("CanvasTabSet")
      c.add_action("Components", ChildrenAction())
      cs.add(c)

      c := createcc_converter("CanvasTextButton")
      c.add_action("Label", StringToPaintAction("Paint"))
      cs.add(c)

      c := createcc_converter("CanvasTextField")
      cs.add(c)

      c := createcc_converter("CanvasTextList")
      c.add_action("Draw Border", ChangeAction(1))
      c.add_extra(FixSelectionAction())
      cs.add(c)

      c := createcmc_converter("CanvasTextMenuItem")
      c.add_action("Label", StringToPaintAction("Paint mid"))
      c.add_action("Label Left", StringToPaintAction("Paint left"))
      c.add_action("Label Right", StringToPaintAction("Paint right"))
      c.add_action("Img Left", ImgToPaintAction("Paint left"))
      c.add_action("Img Right", ImgToPaintAction("Paint right"))
      cs.add(c)

      c := createcmc_converter("CanvasMenu")
      c.add_action("Menus", MenuChildrenAction("Children"))
      c.add_action("Label", StringToPaintAction("Paint mid"))
      c.add_action("Label Left", StringToPaintAction("Paint left"))
      c.add_action("Label Right", StringToPaintAction("Paint right"))
      c.add_action("Img Left", ImgToPaintAction("Paint left"))
      c.add_action("Img Right", ImgToPaintAction("Paint right"))
      cs.add(c)

      c := createcmc_converter("CanvasCheckBoxMenuItem")
      c.add_action("Label", StringToPaintAction("Paint mid"))
      cs.add(c)

      c := createcc_converter("CanvasMenuBar")
      cs.add(c)

      c := createcc_converter("CanvasMenuButton")
      cs.add(c)

      c := createcmc_converter("CanvasMenuSeparator")
      cs.add(c)

      c := createcc_converter("CanvasToolBar", "CanvasBorder")
      cs.add(c)

      return cs
   end

   public new(s)
      version := 1
      code := s
      return
   end
end


class Source2(Source)
   #
   # ConverterSet for MenuComponents
   #
   private createcmc_converter(s)
      local c
      c := Converter(s)
      return c
   end

   #
   # ConverterSet for Components
   #
   private createcc_converter(s, t)
      local c
      c := Converter(s, t)
      c.add_action("Tooltip", FixTooltipAction())
      c.add_extra(FixDefaultDimensionsAction())
      c.add_extra(FixAttribsAction())
      c.add_extra(FixConstraintsAction())
      c.add_extra(FixEventsAction())
      c.add_extra(AddMissingKeyAction("Z", 0))
      c.add_extra(AddMissingKeyAction("Tab ord", 0))
      return c
   end

   #
   # Create the ConverterSet to use.
   #
   public createconverterset()
      local cs, c
      cs := ConverterSet()

      c := Converter("CDialog")
      c.add_extra(FixAttribsAction())
      cs.add(c)

      c := Converter("Canvas")
      c.add_action("Items",RenameAction("Children"))
      c.add_action("Button Groups", FixGroupsAction())
      c.add_action("Checkbox Groups", FixGroupsAction())
      c.add_extra(FixEventsAction())
      c.add_extra(AddMissingKeyAction("Edit Scale", 1.0))
      cs.add(c)

      cs.add(createcc_converter("CanvasBorder"))
      cs.add(createcc_converter("CanvasCustom"))
      cs.add(createcc_converter("CanvasEditableTextList"))
      cs.add(createcc_converter("CanvasEditList"))
      cs.add(createcc_converter("CanvasImage"))
      cs.add(createcc_converter("CanvasList"))
      cs.add(createcc_converter("CanvasOverlayItem"))
      cs.add(createcc_converter("CanvasOverlaySet"))
      cs.add(createcc_converter("CanvasPanel"))
      cs.add(createcc_converter("CanvasScrollBar"))
      cs.add(createcc_converter("CanvasTabSet"))
      cs.add(createcc_converter("CanvasTextField"))
      cs.add(createcc_converter("CanvasMenuButton"))

      c := createcc_converter("CanvasMenuBar")
      cs.add(c)

      c := createcc_converter("CanvasTextButton")
      c.add_action("Label", StringToPaintAction("Paint"))
      cs.add(c)

      c := createcc_converter("CanvasIconButton")
      c.add_action("Img Up", ImgToPaintAction("Paint"))
      c.add_action("Img Down", ImgToPaintAction("Paint down"))
      cs.add(c)

      c := createcc_converter("CanvasCheckBox")
      c.add_action("Label", StringToPaintAction("Paint"))
      cs.add(c)

      c := createcc_converter("CanvasTabItem")
      c.add_action("Label", StringToPaintAction("Paint"))
      cs.add(c)

      c := createcmc_converter("CanvasTextMenuItem")
      c.add_action("Label", StringToPaintAction("Paint mid"))
      c.add_action("Label Left", StringToPaintAction("Paint left"))
      c.add_action("Label Right", StringToPaintAction("Paint right"))
      c.add_action("Img Left", ImgToPaintAction("Paint left"))
      c.add_action("Img Right", ImgToPaintAction("Paint right"))
      cs.add(c)

      c := createcmc_converter("CanvasMenu")
      c.add_action("Children", MenuChildrenAction("Children"))
      c.add_action("Label", StringToPaintAction("Paint mid"))
      c.add_action("Label Left", StringToPaintAction("Paint left"))
      c.add_action("Label Right", StringToPaintAction("Paint right"))
      c.add_action("Img Left", ImgToPaintAction("Paint left"))
      c.add_action("Img Right", ImgToPaintAction("Paint right"))
      cs.add(c)

      c := createcmc_converter("CanvasCheckBoxMenuItem")
      c.add_action("Label", StringToPaintAction("Paint mid"))
      cs.add(c)

      c := createcc_converter("CanvasTableColumn")
      c.add_action("Label", StringToPaintAction("Paint"))
      c.add_extra(FixAutoWidthAction())
      cs.add(c)

      c := createcc_converter("CanvasTable")
      c.add_extra(FixSelectionAction())
      cs.add(c)

      c := createcc_converter("CanvasTree")
      c.add_extra(FixSelectionAction())
      cs.add(c)

      c := createcc_converter("CanvasTextList")
      c.add_extra(FixSelectionAction())
      cs.add(c)

      c := createcc_converter("CanvasIcon")
      c.add_action("Img", ImgToPaintAction("Paint"))
      cs.add(c)

      c := createcc_converter("CanvasLabel")
      c.add_action("Label", StringToPaintAction("Paint"))
      cs.add(c)

      cs.add(createcc_converter("CanvasToolBar", "CanvasBorder"))
      cs.add(createcc_converter("CanvasSplit"))
      cs.add(createcc_converter("CanvasSplitContent"))
      cs.add(createcc_converter("CanvasLine"))
      cs.add(createcc_converter("CanvasTextDisplay"))

      return cs
   end

   public new(s)
      version := 2
      code := s
      return
   end
end




#
# An action to apply to a field of a class
#
class Action()
   public optional apply()
end


#
# Rename the key
#
class RenameAction(Action)
   public new_key

   public apply(decode, proxy, key, val)
      proxy.add(new_key, val)
   end

   public new(new_key)
      self.new_key := new_key
      return
   end
end


#
# Change the entry
#
class ChangeAction(Action)
   public new_val

   public apply(decode, proxy, key, val)
      proxy.add(key, new_val)
   end

   public new(new_val)
      self.new_val := new_val
      return
   end
end


#
# Remove the entry
#
class RemoveAction(Action)
   public apply()
   end
end


#
# Add a new entry
#
class AddAction(Action)
   public key, val

   public apply(decode, proxy)
      proxy.add(key, val)
   end

   public new(key, val)
      self.key := key
      self.val := val
      return
   end
end


#
# This class holds the details of how to convert one class, as a table
# of actions to apply to the encoded fields.
#
class Converter()
   public orig_name, new_name, actions, extras

   public add_action(key, a)
      insert(actions, key, a)
   end

   public add_extra(a)
      put(extras, a)
   end

   public get_new_name()
      return new_name
   end

   public get_orig_name()
      return orig_name
   end

   public apply_extras(decode, proxy)
      every (!extras).apply(decode, proxy)
   end

   public apply(decode, proxy, key, val)
      if member(actions, key) then
         actions[key].apply(decode, proxy, key, val)
      else
         proxy.add(key, val)
   end

   public new(s, t)
      orig_name := s
      new_name := \t | s
      actions := table()
      extras := []
      return
   end
end


#
# Simple holder for several Converters stored in a map.
#
class ConverterSet()
   public map

   public add(c)
      insert(map, c.get_orig_name(), c)
   end

   public get_converter(s)
      if member(map, s) then
         return map[s]
   end

   public new()
      map := table()
      return
   end
end


#
# Simple class to hold the values of the fields representing a class as a
# table of field names.
#
class ClassProxy()
   public name, l, t

   public add(name, val)
      local i
      #
      # Check if we need to convert an object into its proxy (eg
      # a CanvasTextPaint).
      #
      if type(val) == "object" & not is(val, ClassProxy) then
         val := (encode(val) ? CustomDecode().decode())
      if i := \t[name] then
         l[i][2] := val
      else {
         put(l, [name, val])
         t[name] := *l
      }
   end

   public get_name()
      return name
   end

   public get_map()
      return l
   end

   public get(name)
      return l[\t[name]][2]
   end

   public new(s)
      name := s
      l := []
      t := table()
      return
   end
end

#
# An amended Decoder with decode_class changed to output to ClassProxy instances.
#
class LegacyCustomDecode()
   private readable
      tag_count, 
      cs,
      seen

   protected decode_string(s)
      local r
      static esc
      initial
         esc := ~'\\'
      r := ""
      s ? {
         repeat {
            r ||:= tab(many(esc))
            if pos(0) then
               return r
            move(1)
            r ||:= char(move(3))
         }
      }
   end

   protected line_in()
      local s
      if s := tab(upto('|')) then {
         move(1)
         return s
      }
   end

   protected note_seen(o)
      seen[tag_count +:= 1] := o
   end

   protected decode_record()
      local rname, n, res, i
      (rname := line_in() &
       n := integer(line_in())) | fail
      res := proc(rname)()
      note_seen(res)
      every i := 1 to n do
         res[i] := decode() | fail
      
      return res
   end

   public decode()
      local i, n, t, res, def, key, val

      t := line_in() | fail

      if i := integer(t) then
         return \seen[i]

      case t of {
         "null":
            return

         "procedure" :
            return proc(line_in())

         "record" :
            return decode_record()

         "class" :
            return decode_object()

         "string" :
            return decode_string(line_in())

         "integer" :
            return integer(line_in())

         "real" :
            return real(line_in())

         "cset" :
            return cset(decode_string(line_in()))

         "list" : {
            n := integer(line_in()) | fail
            res := []
            note_seen(res)
            every 1 to n do 
               put(res, decode()) | fail
            return res
         }

         "set" : {
            n := integer(line_in()) | fail
            res := set()
            note_seen(res)
            every 1 to n do 
               insert(res, decode()) | fail
            return res
         }

         "table" : {
            def := decode() | fail
            res := table(def)
            n := integer(line_in()) | fail
            note_seen(res)
            every 1 to n do {
               (key := decode() &
               val := decode()) | fail
               res[key] := val
            }
            return res
         }            

         default :
            return
      }            
   end

   private decode_object()
      local cname, res, n, i, key, val, c
      cname := line_in() | fail
      if c := cs.get_converter(cname) then {
         res := ClassProxy(c.get_new_name())
         note_seen(res)
         n := integer(line_in()) | fail
         every i := 1 to n do {
            key := line_in() | fail
            val := decode() | fail
            c.apply(self, res, key, val)
         }
         c.apply_extras(self, res)
      } else {
         res := ClassProxy(cname)
         note_seen(res)
         n := integer(line_in()) | fail
         every i := 1 to n do {
            key := line_in() | fail
            val := decode() | fail
            res.add(key, val)
         }
      }
      return res
   end

   public new(cs)
      tag_count := 0
      seen := table()
      self.cs := cs
      return
   end
end

class CustomDecode()
   private cs,
      tag_count,
      seen

   private decode_string(s)
      local r
      static esc
      initial
         esc := ~'\\'
      r := ""
      s ? {
         repeat {
            r ||:= tab(many(esc))
            if pos(0) then
               return r
            move(1)
            r ||:= char(move(3))
         }
      }
   end

   private line_in()
      local s
      if s := tab(upto(Codec.CODE_SEPARATOR)) then {
         move(1)
         return s
      }
   end

   private int_in()
      local s
      if s := tab(many(&digits ++ '\-')) then
         return integer(s)
   end

   private char_in()
      return move(1)
   end

   private note_seen(o)
      seen[tag_count +:= 1] := o
   end

   public decode()
      local i, n, rname, t, res, def, con, l

      t := char_in() | fail

      case t of {
         Codec.REF_CODE : {
            i := int_in() | fail
            return \seen[i]
         }

         Codec.NULL_CODE :
            return

         Codec.YES_CODE :
            return &yes

         Codec.CLASS_CODE :
            return Class.for_name(line_in())

         Codec.CONSTRUCTOR_CODE : {
            rname := line_in() | fail
            con := Prog.get_global(rname) | fail
            type(con) == "constructor" | fail
            return con
         }

         Codec.PROCEDURE_CODE :
            return proc(line_in())

         Codec.OBJECT_CODE :
            return decode_object()

         Codec.STRING_CODE :
            return decode_string(line_in())

         Codec.INTEGER_CODE :
            return int_in()

         Codec.REAL_CODE :
            return real(line_in())

         Codec.CSET_CODE : {
             n := int_in() | fail
             l := []
             every 1 to n do 
                put(l, decode()) | fail
             return Text.create_cset(l)
          }

         Codec.UCS_CODE :
            return ucs(decode_string(line_in()))

         Codec.RECORD_CODE : {
            (rname := line_in() &
             n := int_in()) | fail
            con := Prog.get_global(rname) | fail
            type(con) == "constructor" | fail
            res := con()
            note_seen(res)
            every i := 1 to n do
               res[i] := decode() | fail
            return res
         }

         Codec.LIST_CODE : {
            n := int_in() | fail
            res := []
            note_seen(res)
            every 1 to n do 
               put(res, decode()) | fail
            return res
         }

         Codec.SET_CODE : {
            n := int_in() | fail
            res := set()
            note_seen(res)
            every 1 to n do 
               insert(res, decode()) | fail
            return res
         }

         Codec.TABLE_CODE : {
            n := int_in() | fail
            def := decode() | fail
            res := table(def)
            note_seen(res)
            every 1 to n do
               insert(res, decode(), decode()) | fail
            return res
         }            

         default :
            fail
      }            
   end

   private decode_object()
      local cname, res, n, i, key, val, c

      cname := line_in() | fail
      if c := (\cs).get_converter(cname) then {
         res := ClassProxy(c.get_new_name())
         note_seen(res)
         n := integer(decode()) | fail
         every i := 1 to n do {
            key := decode() | fail
            val := decode() | fail
            c.apply(self, res, key, val)
         }
         c.apply_extras(self, res)
      } else {
         res := ClassProxy(cname)
         note_seen(res)
         n := integer(decode()) | fail
         every i := 1 to n do {
            key := decode() | fail
            val := decode() | fail
            res.add(key, val)
         }
      }

      return res
   end

   public new(cs)
      tag_count := 0
      seen := table()
      self.cs := cs
      return
   end
end


#
# The converse of CustomDecode, this creates the amended string.
#
class CustomEncode()
   private readable
      tag_count,
      seen, 
      string_buff

   public get_string()
      return string_buff.done()
   end

   private note_seen(o)
      seen[o] := (tag_count +:= 1)
   end

   private encode_string(s)
      local r
      static printable
      initial {
         printable := cset(&ascii[33:128]) -- '\\' -- Codec.CODE_SEPARATOR
      }
      r := ""
      s ? {
         repeat {
            r ||:= tab(many(printable))
            if pos(0) then
               return r
            # This MUST be done in two steps to take advantage of
            # catenation optimisation - very slow otherwise!
            r ||:= "\\"
            r ||:= right(ord(move(1)), 3, "0")
         }
      }	 
   end

   public encode(o)
      local ty, i, pairs, l

      if i := \seen[o] then {
         chars_out(Codec.REF_CODE)
         int_out(i)
         return
      }

      ty := type(o)
      case ty of {
         "object" : {
            chars_out(Codec.OBJECT_CODE)
            encode_object(o)
         }

         "class": {
            chars_out(Codec.CLASS_CODE)
            line_out(Class.get_name(o))
         }

         "procedure" : {
            chars_out(Codec.PROCEDURE_CODE)
            line_out(Proc.get_name(o))
         }

         "constructor" : {
            chars_out(Codec.CONSTRUCTOR_CODE)
            line_out(Constructor.get_name(o))
         }

         "null" :
            chars_out(Codec.NULL_CODE)

         "yes" :
            chars_out(Codec.YES_CODE)

         "string" : {
            chars_out(Codec.STRING_CODE)
            line_out(encode_string(o))
         }

         "ucs": {
            chars_out(Codec.UCS_CODE)
            line_out(encode_string(string(o)))
         }

         "cset" : {
            chars_out(Codec.CSET_CODE)
            l := []
            every put(l, Text.get_ord_range(o))
            int_out(*l)
            every encode(!l)
         }

         "integer" : {
            chars_out(Codec.INTEGER_CODE)
            int_out(string(o))
         }

         "real" : {
            chars_out(Codec.REAL_CODE)
            line_out(string(o))
         }

         "record" : {
            chars_out(Codec.RECORD_CODE)
            line_out(Constructor.get_name(o))
            int_out(*o)
            note_seen(o)
            every encode(!o)
         }

         "list" : {
            chars_out(Codec.LIST_CODE)
            int_out(*o)
            note_seen(o)
            every encode(!o)
            }

         "set" : {
            chars_out(Codec.SET_CODE)
            int_out(*o)
            note_seen(o)
            every encode(!o)
         }

         "table" : {
            chars_out(Codec.TABLE_CODE)
            int_out(*o)
            encode(o[[]])
            note_seen(o)
            pairs := sort(o)
            every encode(!!pairs)
         }

         default : 
            chars_out(Codec.NULL_CODE)
      }
   end

   private line_out(s)
      string_buff.out(s || Codec.CODE_SEPARATOR)
   end

   private chars_out(s)
      string_buff.out(s)
   end

   private int_out(i)
      string_buff.out(string(i))
   end

   private encode_object(o)
      local l, e
      is(o, ClassProxy) | stop("Expected ClassProxy, got:", image(o))
      line_out(o.get_name())
      note_seen(o)
      l := o.get_map()
      encode(*l)
      every e := !l do {
         encode(e[1])
         encode(e[2])
      }
   end

   public new()
      tag_count := 0
      seen := table()
      string_buff := RamStream()
      return
   end
end


class StringToPaintAction(Action)
   public new_key

   public apply(decode, proxy, key, val)
      proxy.add(new_key, CanvasTextPaint(\val))
   end

   public new(new_key)
      self.new_key := new_key
      return
   end
end


class ImgToPaintAction(Action)
   public new_key

   public apply(decode, proxy, key, val)
      proxy.add(new_key, CanvasImagePaint().set_image(\val))
   end

   public new(new_key)
      self.new_key := new_key
      return
   end
end


class FixTooltipAction(Action)
   public apply(decode, proxy, key, val)
      if type(\val) == "string" then
         proxy.add(key, CanvasTextPaint(val))
      else
         proxy.add(key, val)
   end
end


#
# Convert event handling
#
class VarCategoryAction(Action)
   public apply(decode, proxy, key, val)
      case val of {
         &null : {
            proxy.add("Class Variable", 1)
            proxy.add("Event Handlers", [["&null", "handle_v2_events"]])
         }
         1 :
            proxy.add("Class Variable", 1)
         2 :
            proxy.add("Class Variable", &null)
      }
   end
end


class FixGroupsAction(Action)
   public apply(decode, proxy, key, val)
      if type(val) == "list" then
         proxy.add(key, val)
      else
         proxy.add(key, val.get("Boxes"))
   end
end


#
# Convert children fields
#
class ChildrenAction(Action)
   public apply(decode, proxy, key, val)
      proxy.add("Children", val)
      every (!val).add("Parent Component", proxy)
   end
end


#
# Convert children fields
#
class MenuChildrenAction(Action)
   public apply(decode, proxy, key, val)
      proxy.add("Children", val)
      every (!val).add("Parent", proxy)
   end
end


class FixAutoWidthAction(Action)
   public apply(decode, proxy)
      if \proxy.get("Auto width flag") then
         proxy.add("Column Width", &null)
   end
end


class FixSelectionAction(Action)
   public apply(decode, proxy)
      if \proxy.get("Select One") then
         proxy.add("Select Mode", Select.ONE)
      if \proxy.get("Select Many") then
         proxy.add("Select Mode", Select.MANY)
   end
end


class FixDefaultDimensionsAction(Action)
   public apply(decode, proxy)
      if \proxy.get("W Default") then
         proxy.add("W Spec", &null)
      if \proxy.get("H Default") then
         proxy.add("H Spec", &null)
   end
end


class FixConstraintsAction(Action)
   public apply(decode, proxy)
      local t, s
      t := proxy.get("Constraints")
      /t := table()
      # Empty strings for these meant "on"
      every s := "eol"|"x_fill"|"y_fill" do
         if t[s] === "" then t[s] := &yes
      if s := member(t, "tab") then {
         delete(t, "tab")
         insert(t, "x_tab", s)
      }
      proxy.add("Constraints", normalize_constraints(t))
   end
end

class FixAttribsAction(Action)
   private fixone(t, oldk, newk)
      local v
      if v := \t[oldk] then {
         delete(t, oldk)
         t[newk] := v
      }
   end

   public apply(decode, proxy)
      local e, k, v, t
      t := proxy.get("WAttribs")
      if /t then {
         t := table()
         every e := !\proxy.get("Attribs") do {
            e ? {
               k := tab(upto('='))
               ="="
               v := tab(0)
               insert(t, k, v)
            }
         }
      }
      fixone(t, "drawop", "draw_op")
      fixone(t, "linestyle", "line_style")
      fixone(t, "linewidth", "line_width")
      fixone(t, "minheight", "min_height")
      fixone(t, "maxheight", "max_height")
      fixone(t, "minwidth", "min_width")
      fixone(t, "maxwidth", "max_width")
      fixone(t, "minsize", "min_size")
      fixone(t, "maxsize", "max_size")
      proxy.add("WAttribs", normalize_wattribs(t))
   end
end


class FixEventsAction(Action)
   public apply(decode, proxy)
      local e
      static t
      initial {
         t := table()
         insert(t, "CLOSEBUTTON", "Event.WINDOW_CLOSE_BUTTON")
         insert(t, "CLOSE_BUTTON_EVENT", "Event.WINDOW_CLOSE_BUTTON")
         insert(t, "MOUSEMOVED", "Event.MOUSE_MOVEMENT")
         insert(t, "MOUSE4UP", "Event.MOUSE_WHEEL_UP")
         insert(t, "MOUSE5UP", "Event.MOUSE_WHEEL_DOWN")
         insert(t, "&lpress", "Event.MOUSE_LEFT_PRESS")
         insert(t, "&mpress", "Event.MOUSE_MIDDLE_PRESS")
         insert(t, "&rpress", "Event.MOUSE_RIGHT_PRESS")
         insert(t, "&ldrag", "Event.MOUSE_LEFT_DRAG")
         insert(t, "&mdrag", "Event.MOUSE_MIDDLE_DRAG")
         insert(t, "&rdrag", "Event.MOUSE_RIGHT_DRAG")
         insert(t, "&lrelease", "Event.MOUSE_LEFT_RELEASE")
         insert(t, "&mrelease", "Event.MOUSE_MIDDLE_RELEASE")
         insert(t, "&rrelease", "Event.MOUSE_RIGHT_RELEASE")
         insert(t, "&windowclose", "Event.WINDOW_CLOSE_BUTTON")
         insert(t, "&mousemove", "Event.MOUSE_MOVEMENT")
         insert(t, "&wheelup", "Event.MOUSE_WHEEL_UP")
         insert(t, "&wheeldown", "Event.MOUSE_WHEEL_DOWN")
         insert(t, "INVOKELATER", "Window.INVOKE_LATER")
         insert(t, "ICON_EVENT", "Event.ICON")
         insert(t, "BUTTON_RELEASE_EVENT", "Event.BUTTON_RELEASE")
         insert(t, "BUTTON_PRESS_EVENT", "Event.BUTTON_PRESS")
         insert(t, "BUTTON_HELD_EVENT", "Event.BUTTON_HELD")
         insert(t, "SELECTION_CHANGED_EVENT", "Event.SELECTION_CHANGED")
         insert(t, "CONTENT_CHANGED_EVENT", "Event.CONTENT_CHANGED")
         insert(t, "CURSOR_MOVED_EVENT", "Event.CURSOR_MOVED")
         insert(t, "VALUE_CHANGED_EVENT", "Event.VALUE_CHANGED")
         insert(t, "ACTION_EVENT", "Event.ACTION")
         insert(t, "TREE_NODE_EXPANSION_EVENT", "Event.TREE_NODE_EXPANSION")
         insert(t, "SIZER_PRESSED_EVENT", "Event.SIZER_PRESS")
         insert(t, "SIZER_DRAGGED_EVENT", "Event.SIZER_DRAG")
         insert(t, "SIZER_RELEASED_EVENT", "Event.SIZER_RELEASE")
         insert(t, "MOUSE_ENTER_EVENT", "Event.MOUSE_ENTER")
         insert(t, "MOUSE_EXIT_EVENT", "Event.MOUSE_EXIT")
         insert(t, "KEY_EVENT", "Event.KEY_PRESS")
         insert(t, "Event.KEY", "Event.KEY_PRESS")
         insert(t, "TICK_EVENT", "999")
         insert(t, "GOT_FOCUS_EVENT", "Event.GOT_FOCUS")
         insert(t, "LOST_FOCUS_EVENT", "Event.LOST_FOCUS")
         insert(t, "DIALOG_CLOSED_EVENT" , "Event.DIALOG_DISPOSED")
      }
      every e := !\proxy.get("Event Handlers") do
         e[1] := \t[e[1]]
   end
end


#
# Add a key if it is absent, or its value is &null
#
class AddMissingKeyAction(Action)
   public key, val

   public apply(decode, proxy)
      \proxy.get(key) | proxy.add(key, val)
   end

   public new(key, val)
      self.key := key
      self.val := val
      return
   end
end


class FixBorderContentAction(Action)
   public apply(decode, proxy)
      local children
      if proxy.get("Content") then
         return
      children := proxy.get("Children")
   end
end


#
# Menu event handling
#
class MenuVarCategoryAction(Action)
   public apply(decode, proxy, key, val)
      case val of {
         &null : {
            proxy.add("Class Variable", 1)
            proxy.add("Event Method", "handle_v2_events")
         }
         1 :
            proxy.add("Class Variable", 1)
         2 :
            proxy.add("Class Variable", &null)
      }
   end
end


procedure old_img_style(s)
   return case s of {
      "box_up" :
"11,g2,_
11111111111_
11111111110_
11~~~~~~~00_
11~~~~~~~00_
11~~~~~~~00_
11~~~~~~~00_
11~~~~~~~00_
11~~~~~~~00_
11~~~~~~~00_
11000000000_
10000000000_
"
         "box_down" :
"11,g2,_
00000000001_
00000000011_
00000000011_
00000000011_
00000000011_
00000000011_
00000000011_
00000000011_
00000000011_
01111111111_
11111111111_
"
      "diamond_up" :
"11,g5,_
~~~~~4~~~~~_
~~~~444~~~~_
~~~44~44~~~_
~~44~~~44~~_
~44~~~~~44~_
44~~~~~~~44_
~00~~~~~00~_
~~00~~~00~~_
~~~00~00~~~_
~~~~000~~~~_
~~~~~0~~~~~_
"
         "diamond_down" :
"11,g5,_
~~~~~0~~~~~_
~~~~000~~~~_
~~~00000~~~_
~~0000000~~_
~000000000~_
00000000000_
~440000044~_
~~4400044~~_
~~~44044~~~_
~~~~444~~~~_
~~~~~4~~~~~_
"
   }
end






