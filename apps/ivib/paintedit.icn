import gui, io, lang

class PaintEdit(Dialog)
   public 
      c, 
      tabset,
      code_tab,
      okay_result

   #
   # Add the tab to the TabSet
   #
   public add_tab(t) 
      tabset.add(t)
   end

   #
   # Add the tab to the TabSet, as the first item
   #
   public add_tab_at_front(t) 
      tabset.add(t, 1)
   end

   public setup()
      local oc, label
      Class.get_name(c) ? {
         ="Canvas"
         label := tab(0) || " Setup"
      }
      self.set_wattrib("label", label)

      set_layout(GridLayout())

      tabset := TabSet("c:eol")
      add(tabset)
      code_tab := CodeTab(c)
      tabset.add(code_tab)

      oc := OkCancel()
      oc.listen(self)
      self.add(oc)
   end

   public on_cancel()
      self.dispose()
   end

   public validate_input()
      return
   end

   public on_okay()
      self.validate_input() | fail
      self.set_vals(c)
      self.okay_result := 1
      self.dispose()
   end

   public set_vals()
      code_tab.set_vals()
   end

   public new(c)
      cast(self,Dialog).new()
      self.c := c
      setup()
      return
   end
end


class ImagePaintEdit(PaintEdit)
   public 
      scale_up,
      max_w, 
      max_h, 
      max_w_field, 
      max_h_field,
      img,
      img_field,
      cache_cb

   public on_max_w()
      max_w_field.toggle_is_shaded()
   end

   public on_max_h()
      max_h_field.toggle_is_shaded()
   end

   public set_vals()
      cast(self,PaintEdit).set_vals()
      c.max_w := if max_w.is_checked() then
         integer(max_w_field.get_contents())
      else
         &null

      c.max_h := if max_h.is_checked() then
         integer(max_h_field.get_contents())
      else
         &null

      c.scale_up_flag := scale_up.get_status()

      if cache_cb.is_checked() then {
         c.image_cache := img_field.get_contents()
         c.image_str := &null
      } else {
         c.image_cache := &null
         c.image_str := img_field.get_contents()
      }
   end

   public on_file_browse()
      local fd, s, l, p
      fd := FileDialog()
      l := []
      s := img_field.get_contents()
      if is_image_file(s) then {
         p := Files.get_path(s).canonical()
         put(l, p.parent().str())                             
         fd.set_file(p.get(-1))                               
      }
      put(l, \Ivib.last_icon_dir)
      fd.set_directories(l)
      fd.set_filters(["*.gif;*.png;*.jpg","*"])
      fd.show_modal(self)
      Ivib.last_icon_dir := fd.get_directory()
      if s := fd.get_result() then {
         self.img.set_image(s)
         img_field.set_contents(s)
      }
   end

   public on_name_browse()
      local d, s
      d := NamedImageDialog(img_field.get_contents())
      d.show_modal(self)
      if s := d.get_result() then {
         self.img.set_image(s)
         img_field.set_contents(s)
      }
   end

   public on_img_field()
      img.set_image(img_field.get_contents())
   end

   public setup()
      local p, q
      cast(self,PaintEdit).setup()

      p := TabItem("label=General")
      p.set_layout(GridLayout("extra=borders"))
      add_tab_at_front(p)

      max_w := CheckBox("label=Max width :")
      max_w.connect(self.on_max_w, Event.ACTION)
      p.add(max_w)
      max_w_field := TextField("size=130", "filter=",&digits, "c:eol")
      p.add(max_w_field)
      if \c.max_w then {
         max_w.set_is_checked(&yes)
         max_w_field.set_contents(c.max_w)
      } else
         max_w_field.set_is_shaded(&yes)

      max_h := CheckBox("label=Max height :")
      max_h.connect(self.on_max_h, Event.ACTION)
      p.add(max_h)
      max_h_field := TextField("size=130", "filter=",&digits, "c:eol")
      p.add(max_h_field)
      if \c.max_h then {
         max_h.set_is_checked(&yes)
         max_h_field.set_contents(c.max_h)
      } else
         max_h_field.set_is_shaded(&yes)

      scale_up := CheckBox("label=Scale up", "c:eol")
      if \c.scale_up_flag then
         scale_up.set_is_checked(&yes)
      p.add(scale_up)

      p := TabItem("label=Images")
      p.set_layout(GridLayout())
      add_tab_at_front(p)

      q := Component("layout=",GridLayout("flush"), "c:eol", "c:x_fill", "c:x_weight=1.0")
      img := Image("size=60,60")
      img.clear_constraints()
      q.add(img)
      q.add(img_field := TextField("size=200",
                                   "e:content_changed=", on_img_field))
      p.add(q)

      q := Component("layout=",GridLayout("flush"), "c:x_align=l")
      q.add(TextButton("label=File...", "e:action=",on_file_browse))
      q.add(TextButton("label=Name...", "e:action=",on_name_browse))
      q.add(cache_cb := CheckBox("label=Cache"))
      p.add(q)

      if \c.image_str then {
         img.set_image(c.image_str)
         img_field.set_contents(c.image_str)
      }
      if \c.image_cache then {
         img.set_image(c.image_cache)
         img_field.set_contents(c.image_cache)
         cache_cb.set_is_checked(&yes)
      }
   end

end



class GridPaintEdit(PaintEdit)
   public 
      res,
      input,
      output, 
      clipboard,
      ucs_cb

   public on_input(ev)
      render()
   end

   public init_dialog()
      render()
   end

   public set_vals()
      cast(self,PaintEdit).set_vals()
      c.fmt := input.get_contents_str()
   end

   public render(ev)
      output.set_spec(input.get_contents_str())
   end

   private on_ucs_cb()
      if ucs_cb.is_checked() then
         input.set_contents([u""])
      else
         input.set_contents([""])
      render()
   end

   public on_input_press(ev)
      local m, i, pm
      m := Menu()
      i := TextMenuItem("label=Save to...")
      i.connect(self.on_save_input, Event.ACTION)
      m.add(i)
      i := TextMenuItem("label=Load from...")
      i.connect(self.on_load_input, Event.ACTION)
      m.add(i)
      i := TextMenuItem("label=Image file...")
      i.connect(self.on_insert_image_file, Event.ACTION)
      m.add(i)
      i := TextMenuItem("label=Image name...")
      i.connect(self.on_insert_image_name, Event.ACTION)
      m.add(i)
      i := TextMenuItem("label=Debug")
      i.connect(self.on_debug, Event.ACTION)
      m.add(i)
      pm := PopupMenu()
      pm.popup(self, m, ev.x, ev.y)
   end

   public on_debug()
      output.d.g.print_layout()
   end

   public on_save_input()
      local d, fn
      d := FileDialog()
      d.show_modal(self)
      fn := d.get_result() | fail
      Files.string_to_file(fn, input.get_contents_str()) | 
         return alert_error(self, ["Couldn't save to " || fn, &why])
   end

   public on_load_input()
      local d, fn, s
      d := FileDialog()
      d.show_modal(self)
      fn := d.get_result() | fail
      s := Files.file_to_string(fn) | return alert_error(self, ["Couldn't load from " || fn, &why])
      input.set_contents_str(s)
   end

   public on_insert_image_file(e)
      local d, s
      d := FileDialog()
      d.set_directory(\Ivib.last_icon_dir)
      d.set_filters(["*.gif;*.png;*.jpg","*"])
      d.show_modal(self)
      Ivib.last_icon_dir := d.get_directory()
      if s := d.get_result() then {
         s := ".img " || s || "\n"
         input.insert_string(s, e)
      }
   end

   public on_insert_image_name(e)
      local d, s
      d := NamedImageDialog()
      d.show_modal(self)
      if s := d.get_result() then {
         s := ".img " || s || "\n"
         input.insert_string(s, e)
      }
   end

   public setup()
      local p
      cast(self,PaintEdit).setup()

      p := TabItem("label=Edit")
      p.set_layout(GridLayout())
      add_tab_at_front(p)

      input := EditableTextList("c:x_weight=0", "size=250,200", "move_on_rpress=no")
      self.set_focus(input.find_focus())
      input.connect(self.on_input, Event.CONTENT_CHANGED)
      input.connect(self.on_input_press, Event.MOUSE_RIGHT_PRESS)
      input.set_contents_str(\c.fmt)

      output := ShowGridPaint("size=150,200", "c:eol")

      p.add(Split("left=",input, "right=",output, "c:eol"))

      ucs_cb := CheckBox("label=Ucs content")
      if type(c.fmt) == "ucs" then
         ucs_cb.set_is_checked(&yes)
      ucs_cb.connect(self.on_ucs_cb, Event.ACTION)
      p.add(ucs_cb)
   end
end


class ShowGridPaint(DrawScrollArea)
   public d

   public get_subject_width()
      return (\d).w | 0
   end

   public get_subject_height()
      return (\d).h | 0
   end

   public get_default_vertical_increment()
      return 10
   end

   public get_default_horizontal_increment()
      return 10
   end

   public draw()
      (\d).draw(view.cbwin, 
                view.x - get_area_x(), 
                view.y - get_area_y())
   end

   public finally()
      cast(self,DrawScrollArea).finally()
      (\d).finally()
   end

   public set_spec(s)
      (\d).finally()
      d := GridPaint(s)
      d.initially(self)
      d.draw_grid()
      compute_and_invalidate()
   end
end

class NamedImageDialog(Dialog)
   public 
      curr,
      lst, 
      result

   private get_names()
      local l, s
      l := []
      every s := key(ImageCache.names) do {
         if (s == "ivib.icon") | not match("ivib.", s) then
            put(l, s)
      }
      return sort(l)
   end

   public setup()
      local oc
      attrib("w:label=Select image")
      set_layout(GridLayout())

      lst := TextList("size=250,300",
                      "select_mode=one",
                      "c:eol",
                      "contents=",get_names())
      lst.object_set_selections([\curr])
      lst.set_cursor(lst.get_selections()[1])
      set_focus(lst)
      self.add(lst)
      oc := OkCancel()
      oc.listen(self)
      self.add(oc)
   end

   public init_dialog()
      lst.center_line(lst.get_selections()[1])
   end

   public on_cancel()
      self.dispose()
   end

   public on_okay()
      result := lst.object_get_selections()[1] | return alert_error(self, "No image selected")
      self.dispose()
   end

   public get_result()
      return \self.result
   end

   public new(curr)
      cast(self,Dialog).new()
      self.curr := curr
      setup()
      return
   end
end
