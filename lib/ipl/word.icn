############################################################################
#
#	File:     word.icn
#
#	Subject:  Procedure to scan UNIX-style command line words
#
#	Author:   Robert J. Alexander
#
#	Date:     August 14, 1996
#
############################################################################
#
#   This file is in the public domain.
#
############################################################################
#
#  word(s) -- Produces the position past a UNIX-style command line
#  word.
#
#  dequote(s) -- Produces the UNIX-style command line word s with any
#  quoting characters removed.
#
############################################################################

package ipl.word

import
   ipl.scan(begin_analysis)

#
#  word(s) -- Produces the position past a UNIX-style command line
#  word.
#
procedure word(s,i1,i2)
   local c,d,p,e,a
   a := begin_analysis(s,i1,i2) | fail
   s := a.s; i1 := a.i; i2 := a.j
   s[i1:i2] ? {
      (while tab(upto(' \t"\'')) do {
	 if (c := move(1)) == ("\"" | "'") then {
	    e := c ++ "\\"
	    while tab(upto(e)) do {
	       d := move(1)
	       if d == c then break
	       move(1)
	       }
	    }
	 else break
	 }) | "" ~== tab(0) | fail
      p := &pos
      }
   return i1 + p - 1
end


#
#  dequote(s) -- Produces the UNIX-style command line word s with any
#  quoting characters removed.
#

procedure word_dequote(s)
   local c,d
   s ? {
      s := ""
      while s ||:= tab(upto('"\'\\')) do {
	 c := move(1)
	 if c == "\\" then s ||:= move(1)
	 else {
	    if \d then (s ||:= d ~== c) | (d := &null)
	    else d := c
	    }
	 }
      return s || tab(0)
      }
end
