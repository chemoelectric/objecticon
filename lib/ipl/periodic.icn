############################################################################
#
#	File:     periodic.icn
#
#	Subject:  Procedures related to periodic sequences
#
#	Author:   Ralph E. Griswold
#
#	Date:     June 10, 2001
#
############################################################################
#
#  Sqrt(i, j) produces a rational approximation to the square root of i
#  with j iterations of the half-way method.  j defaults to 5.
#
############################################################################
#
#  Requires:  Large-integer arithmetic  
#
############################################################################
#
#  Links:  lists, numbers, rational, strings
#
############################################################################

package ipl.periodic

import
   ipl.lists(lequiv, lextend),
   util(Math),
   numbers(Rat)

record perseq(pre, rep)

# rational approximate to square root
procedure Sqrt(i, j)		
   local rat, half

   /j := 5

   half := Rat(1, 2)

   rat := Rat(integer(Math.sqrt(i)))	# initial approximation

   i := Rat(i)

   every 1 to j do
      rat := half.mul(rat.add(i.div(rat)))

   return rat

end

# continued fraction sequence for rational
procedure rat2cf(i, j)		
   local r, result

   result := []

   repeat {
     put(result, integer(i / j))
     r := i % j
     i := j
     j := r
     if j = 0 then break
     }

   return perseq(result, [])

end

# continued-fraction approximation
procedure cfapprox(lst)		
   local prev_n, prev_m, n, m, t

   lst := copy(lst)

   prev_n := [1]
   prev_m := [0, 1]

   put(prev_n, get(lst).d) | fail

   while t := get(lst) do {
      n := t.d * get(prev_n) + t.n * prev_n[1]
      m := t.d * get(prev_m) + t.n * prev_m[1]
      suspend Rat(n, m)
      put(prev_n, n)
      put(prev_m, m)
      if t.d ~= 0 then {		# renormalize
         every !prev_n /:= t.d
         every !prev_m /:= t.d
         }
      }

end

# convert repeating decimal to rational
procedure dec2rat(pre, rep)	
   local s

   s := ""

   every s ||:= (!pre | |!rep) \ (*pre + *rep)

   return Rat(s - left(s, *pre),
      10 ^ (*pre + *rep) - 10 ^ *pre)

end

# decimal expansion of rational
procedure rat2dec(numer, denom)		
   local  result, remainders, count, seq

   result := ""

   remainders := table()

   numer %:= denom
   numer *:= 10

   count := 0

   while numer > 0 do {
      count +:= 1
      if member(remainders, numer) then {	# been here; done that
         seq := perseq()
         result ? {
            seq.pre := move(remainders[numer] - 1)
            seq.rep := tab(0)
            }
         return seq
         }
      else insert(remainders, numer, count)
      result ||:= numer / denom
      numer %:= denom
      numer *:= 10
      }

   return perseq([denom], [])		# WRONG!!!

end

# find repeat in sequence
procedure repeater(seq, ratio, limit)		
   local i, prefix, results, segment, span

   /ratio := 2
   /limit := 0.75

   results := copy(seq)

   prefix := []

   repeat {
      span := *results / ratio
      every i := 1 to span do {
         segment := results[1+:i] | next
         if lequiv(lextend(segment, *results), results) then
            return perseq(prefix, segment)
         }
      put(prefix, get(results)) |		# first term to prefix
         return perseq(prefix, results)
      if *prefix > limit * *seq then return perseq(seq, [])
      }

end

# sequence image
procedure seqimage(seq)		
   local result

   result := ""

   every result ||:= !seq.pre || ","

   result ||:= "["

   if *seq.rep > 0 then {
      every result ||:= !seq.rep || ","
      result[-1] := "]"
      }
   else result ||:= "]"

   return result

end
