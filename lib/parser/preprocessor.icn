package parser

import io, util, ipl.scan

class Preprocessor(Connectable)
   private
      sym_table,if_stack,file_stack,if_state,
      file,filename,line,
      nest_level,include_name,
      include_set,print_filename,
      encoding,print_encoding

   private static const 
      DEFAULT_SYMBOLS

   private static init()
      local s
      DEFAULT_SYMBOLS := table()
      # reverse engineer the predefined symbols list from our predefined symbols
      every s := &features do {
         DEFAULT_SYMBOLS[case s of {
            "Object Icon": "_OBJECT_ICON"
            "V2": "_V2"
            "keyboard functions": "_KEYBOARD_FUNCTIONS"
            "JPEG images": "_JPEG"
            "PNG images": "_PNG"
            "SSL networking": "_SSL"
            "MS Win32":"_MS_WIN32"
            "UNIX":"_UNIX"
            "POSIX":"_POSIX"
            "ASCII":"_ASCII"
            "co-expressions":"_CO_EXPRESSIONS"
            "dynamic loading":"_DYNAMIC_LOADING"
            "event monitoring":"_EVENT_MONITOR"
            "large integers":"_LARGE_INTEGERS"
            "multiple programs":"_MULTITASKING"
            "pipes":"_PIPES"
            "system function":"_SYSTEM_FUNCTION"
            "graphics":"_GRAPHICS"
            "X Windows":"_X_WINDOW_SYSTEM"
            "Linux": "_LINUX"
            "Solaris": "_SOLARIS"
            "AIX": "_AIX"
            "BSD": "_BSD"
            "Darwin": "_DARWIN"
            "Cygwin": "_CYGWIN"
         }] := "1"
      }
   end

   #
   # Preprocess a file to a string.
   #
   public preprocess(fname, predefined_syms)
      local rs
      return use {
         rs := RamStream(),
         {
            every rs.write1(preprocess_gen(fname, predefined_syms))
            rs.str()
         }
      }
   end

   #
   # Preprocesses a file, generating output lines.
   #
   public preprocess_gen(fname, predefined_syms)
      local s
      preproc_new(fname, predefined_syms) | fail
      suspend sync_lines()      
      while s := read_line() do {
         # s will be &null if we have just come back from an include.  If so,
         # produce a #line directive.
         if /s then
            suspend sync_lines()      
         else s ? {
            if match("#line ") then {
               move(1)
               suspend scan_directive(&yes)
            } else {
               scan_space()
               if ="$" & any(&letters ++ &digits ++ ' \t\f\r') then {
                  suspend scan_directive()
               } else {
                  move(1)
                  suspend scan_text()
               }
            }
         }
      }
      close(\file)
      done()
   end

   #
   # a_Initializes the preprocessor "object".
   #
   private preproc_new(fname,predefined_syms)
      local k, v
      sym_table := copy(DEFAULT_SYMBOLS)
      every k := key(\predefined_syms) do {
         v := predefined_syms[k]
         if /v then
            delete(sym_table, k)
         else
            insert(sym_table, k, string(v)) | runerr(103, v)
      }
      if_stack := []
      file_stack := []
      include_set := set(fname)
      if_state := &null
      filename := include_name := fname
      line := 0
      print_filename := &null
      print_encoding := &null
      nest_level := 0
      encoding := "ASCII"
      if not(file := open(fname)) then {
         err("Cannot open " || image(fname) || ": " || &why)
         fail
      }
      return
   end

   #
   # Clears the values of certain space-consuming variables.
   #
   private done()
      sym_table := if_stack := file_stack :=
         filename := print_filename := print_encoding :=
         encoding := file := include_set := &null
      return
   end

   #
   # a_Scans an encoding identifier.
   #
   private get_encoding()
      suspend any(&letters) & tab(many(&letters ++ &digits ++ '-'))
   end

   #
   # a_Scans a preprocessor directive.  This method fails or returns
   # either a blank line or a #line directive.
   #
   private scan_directive(no_err)
      local sym,value,new_file,new_filename,new_line,new_encoding,old,preproc_command,old_if_state,fname,t
      scan_space()
      preproc_command := scan_word()
      if \if_state then {
         if match("if",preproc_command) then {
            preproc_command := "$if"
         }
      }
      scan_space()
      case preproc_command of {
         "define": {
            if /if_state then {
               if sym := scan_word() &
                  value := scan_define_value() then 
               {
                  if (old := member(sym_table, sym)) ~=== value then {
                     err("Redefinition of " || sym || " = " ||
                                   old)
                  } else {
                     sym_table[sym] := value
                  }
               } else {
                  err()
               }
               return ""
            }
         }
         "undef": {
            if /if_state then {
               if sym := scan_word() & at_end() then {
                  delete(sym_table,sym)
               } else {
                  err()
               }
               return ""
            }
         }
         "load": {
            if /if_state then {
               if sym := scan_word() &
                  scan_opt_space() &
                  fname := scan_qword() & *fname > 0 & at_end() then 
               {
                  if t := Files.path_find(fname, 
                                          "OI_INCL",
                                          FilePath(filename).parent().str()) then
                  {
                     if value := image(Files.file_to_string(t)) then {
                        if (old := member(sym_table, sym)) ~=== value then {
                           err("Redefinition of " || sym || " = " ||
                               old)
                        } else {
                           sym_table[sym] := value
                        }
                     } else
                        err("Cannot load " || image(t) || ": " || &why)
                  } else
                     err("Cannot find " || image(fname) || " on path")
               } else
                  err()
               return ""
            }
         }
         "uload": {
            if /if_state then {
               if sym := scan_word() &
                  scan_opt_space() &
                  fname := scan_qword() & *fname > 0 & at_end() then 
               {
                  if t := Files.path_find(fname, 
                                          "OI_INCL",
                                          FilePath(filename).parent().str()) then
                  {
                     if value := "u" || image(Files.file_to_string(t)) then {
                        if (old := member(sym_table, sym)) ~=== value then {
                           err("Redefinition of " || sym || " = " ||
                               old)
                        } else {
                           sym_table[sym] := value
                        }
                     } else
                        err("Cannot load " || image(t) || ": " || &why)
                  } else
                     err("Cannot find " || image(fname) || " on path")
               } else
                  err()
               return ""
            }
         }
         "if": {
            push(if_stack,if_state)
            if value := scan_expr() then {
               if /if_state then
                  if_state := if \value then &null else "false"
               else
                  if_state := "off"
            } else {
               # if_state stays the same
               return ""
            }

            if /if_state then
               return ""
            else
               fail
         }
         "$if": {    # handles if... command in "if'd-out" code
            push(if_stack,if_state)
            if_state := "off"
            fail
         }
         "elsif" : {
            if \if_state == "off" then
               fail
            if *if_stack <= nest_level then {
               err("No corresponding $if...")
               return ""
            }
            if value := scan_expr() then {
               if \if_state then
                  if_state := if \value then &null else "false"
               else
                  if_state := "off"
            } else {
               if_state := if \if_state then &null else "off"
            }
            if /if_state then
               return sync_lines()
            else
               fail
         }
         "else": {
            if \if_state == "off" then
               fail
            if *if_stack <= nest_level then {
               err("No corresponding $if...")
               return ""
            }
            if not at_end() then
               err()      # extraneous stuff is reported, but the else is processed as if it were OK.
            if_state := if \if_state then &null else "false"
            if /if_state then
               return sync_lines()
            else
               fail
         }
         "endif": {
            old_if_state := if_state
            if at_end() then {
               (*if_stack >= nest_level &
                if_state := pop(if_stack)) |
                  err("No corresponding $if...")
            } else {
               *if_stack >= nest_level &
                  if_state := pop(if_stack)
               err()
            }
            if /if_state then {
               if /old_if_state then
                  return ""
               else
                  return sync_lines()
            } else
               fail
         }
         "include": {
            if /if_state then {
               if sym := scan_qword() & *sym > 0 & at_end() then {
                  if member(include_set,sym) then {
                     err("Circular reference to " || image(sym))
                  } else {
                     if new_filename := Files.path_find(sym, 
                                                        "OI_INCL",
                                                        FilePath(filename).parent().str()) then
                     {
                        if new_file := open(new_filename) then {
                           every push(file_stack,
                                      encoding|file|filename|
                                      include_name|line|
                                      nest_level)
                           filename := new_filename
                           include_name := sym
                           file := new_file
                           line := 0
                           encoding := "ASCII"
                           nest_level := *if_stack
                           insert(include_set,sym)
                           return sync_lines()
                        } else
                           err("Cannot open " || image(new_filename) || ": " || &why)
                     } else
                        err("Cannot find " || image(sym) || " on path")
                  }
               } else {
                  err()
               }
               return ""
            }
         }
         "line": {
            if new_line := integer(tab(many(&digits))) &
               scan_opt_space() &
               (new_filename := scan_qword() | &null) \ 1 &
               (/new_filename | *new_filename > 0) \ 1 &
               scan_opt_space() &
               (new_encoding := get_encoding() | &null) \ 1 &
               at_end() then 
            {
               filename := Files.path_find(\new_filename, 
                                           &null,
                                           FilePath(filename).parent().str()) | \new_filename
               encoding := \new_encoding
               line := new_line - 1
               if /if_state then
                  return sync_lines()
            } else {
               # if no_err is set, then we have a "#line" directive, for which errors are ignored.
               if /no_err then
                  err()
               if /if_state then    
                  return ""
            }
         }
         "error": {
            if /if_state then {
               scan_space()
               t := "Explicit $error: " || trim(tab(find("#") | 0),' \t\f\r')
               err(t)
               return ""
            }
         }
         "encoding": {
            if /if_state then {
               if sym := get_encoding() &
                  at_end() then
                  encoding := sym
               else
                  err()
               return sync_lines()
            }
         }
         "ITRACE": {
            if /if_state then {
               if value := integer(tab(many(&digits ++ '-'))) &
                  at_end() then 
               {
                  &trace := value
               } else {
                  err()
               }
               return ""
            }
         }
         default: {
            if /if_state then {
               err("Unknown preprocessor directive:" || preproc_command)
               return ""
            }
         }
      }
   end

   #
   # Scans and make substitutions in a text line.
   #
   private scan_text(done_set)
      local q,result,p,ident,value,t,c
      if /if_state then {
         if *sym_table > 0 then {
            &pos := q := 1
            result := ""
            while tab(upto('#"\'_' ++ &letters ++ &digits)) do {
               case c := move(1) of {
                  "#": {
                     tab(0)
                  }
                  !&digits: {
                     # Reproduces the logic in nskip() in ipp.c
                     tab(many(&digits))
                     if =("r"|"R") then
                        tab(many(&digits ++ &letters))
                     else {
                        if ="." then
                           tab(many(&digits))
                        if =("e"|"E") then {
                           =("+"|"-")
                           tab(many(&digits))
                        }                           
                     }
                  }
                  "\"" | "'": {
                     repeat {
                        # Look for end of literal; if found continue scannning
                        if skip_string(c) then
                           break

                        # Multi-line literal.  Suspend line and read more.
                        suspend result || &subject[q:0]

                        t := read_line() | fail
                        # If we've just come back from a $include, t will be &null, so return
                        # "#line ..." (this is bound to be a nonsense result anyway, since we're in
                        # the middle of a multi-line string).
                        if /t then
                           return sync_lines()
                        &subject := t
                        q := 1
                        result := ""
                     }
                  }
                  default: {
                     move(-1)
                     # Check for ucs literal
                     if ="u" & any('"') then
                        next
                     p := &pos
                     ident := tab(many(&letters ++ &digits ++ '_'))
                     if value := member(sym_table, ident) then {
                        if /done_set |
                        {type(done_set) == "string" &
                            done_set := set(done_set)
                         not member(done_set,ident)} then {
                            value ? value :=
                               scan_text(insert(copy(\done_set),
                                                ident) | ident)
                            result ||:= &subject[q:p] || value
                            q := &pos
                         }
                     }
                  }
               }
            }
            return result || &subject[q:0]
         }
         else {
            return .&subject
         }
      } else {
         # We're skipping an $if section, so skip over any multi-line
         # string literals.
         if c := multi_line_start() then {
            repeat {
               t := read_line() | fail
               if /t then {
                  if /if_state then
                     return sync_lines()
                  else
                     fail
               }
               &subject := t
               if skip_string(c) then
                  c := multi_line_start() | break
            }
         }
      }
   end

   # If this line ends on a multi-line literal, succeed and return the
   # relevant opening quote char.
   # Eg :-
   #    abc 'def' "xyz_
   # would succeed and return "
   #
   private static multi_line_start()
      local c
      while tab(upto('\"\'')) do {
         c := move(1)
         skip_string(c) | return c
      }
   end

   # Skip a string/cset literal. q is " or ', and &pos should be just
   # after the opening quote.  Succeeds and leaves &pos just after the
   # closing quote, or at end of line if not closing quote was found.
   # Fails iff the literal is a multi-line.
   #
   # Examples (q=") :-
   #  one"blah -> succeeds leaving &pos after the "
   #  noquote -> succeeds leaving &pos at the end
   #  noend_ -> fails, also leaving &pos at the end
   #
   private static skip_string(q)
      local c
      q ++:= '\\_'
      while tab(upto(q) | 0) do {
         c := move(1) | return
         case c of {
            "_" : {
               if pos(0) then
                  fail
            }
            "\\" : move(1)
            default: return    # Closing quote found
         }
      }
   end

   #
   # Reads an input line.  Manages end-of-file and reverting to including
   # file if appropriate, failing at end-of-input.
   #
   private read_line()
      local result
      \file | fail
      repeat {
         if result := file.read_line() then {
            if \result then
               break
            # else EOF, fall through
         } else
            err("Error reading: " || &why)
         
         if *if_stack ~= nest_level then {
            err("$if(s) without $endif(s): " ||
                          *if_stack - nest_level)
            until *if_stack <= nest_level do
               if_state := pop(if_stack)
         }
         close(file)
         file := &null
         delete(include_set,include_name)
         (nest_level := pop(file_stack) &
          line := pop(file_stack) &
          include_name := pop(file_stack) &
          filename := pop(file_stack) &
          file := pop(file_stack) &
          encoding := pop(file_stack)) | fail
         return &null
      }
      line +:= 1
      return result
   end

   #
   # Scans an identifier.
   #
   private static scan_word()
      suspend any(&letters ++ '_') & tab(many(&letters ++ &digits ++ '_'))
   end

   #
   # Scans a possibly-quoted word (e.g. a file name).
   #
   private static scan_qword()
      suspend 2(="\"",
                tab(find("\"")),
                move(1)) |
         scan_word()
   end

   #
   # Scans the value of a $define.
   #
   private static scan_define_value()
      local q,interesting_in_quotes

      if ="(" then {
         fail      # "$define name(" is specifically prohibited; needs whitespace
      }
      scan_space()
      q := &pos
      (while tab(upto('#"\'')) do {
         case move(1) of {
            "#": {
               move(-1)
               break
            }
            default: {
               interesting_in_quotes := '\\' ++ &subject[&pos - 1]
               repeat {
                  while tab(upto(interesting_in_quotes)) do {
                     case move(1) of {
                        "\\": move(1)
                        default: {
                           break break
                        }
                     }
                  }
                  fail # end of line inside quotes!!
               }
            }
         }
      }) | tab(0)
      return trim(&subject[q:&pos],' \t\f\r')
   end

   #
   # Scans white space.
   #
   private static scan_space()
      suspend tab(many(' \t\f\r'))
   end

   #
   # Scans optional white space.
   #
   private static scan_opt_space()
      suspend tab(many0(' \t\f\r'))
   end

   #
   # Tests for &pos at the logical end of a preprocessor directive.
   #
   private static at_end()
      scan_space()
      succeed pos(0) | match("#")
   end

   #
   # Scan a boolean expression in a $if or $elsif line.  Returns the
   # resulting flag value, or fails on an error, having output an
   # appropriate message.
   #
   private scan_expr()
      local r
      r := expr1() | fail
      if at_end() then
         return r
      err("$if/$elsif: Extraneous characters")
   end

   private expr1()
      local r, x
      r := expr2() | fail
      repeat {
         scan_space()
         ="|" | break
         x := expr2() | fail
         /r := x
      }
      return r
   end

   private expr2()
      local r, x
      r := expr3() | fail
      repeat {
         scan_space()
         ="&" | break
         x := expr3() | fail
         \r := x
      }
      return r
   end

   private expr3()
      local c, x
      scan_space()
      if x := scan_word() then
         return Yes{ member(sym_table, x) }
      if c := move(1) then {
         case c of {
            "~" : return toggle_flag(expr1())
            "(" : {
               x := expr1() | fail
               scan_space()
               if =")" then
                  return x
               else
                  err("$if/$elsif: ) expected")
            }
            default:
               err("$if/$elsif: Unexpected character")
         } 
      } else
         err("$if/$elsif: Identifier expected")
   end


   #
   # Determines if a "#lines" is called for and generates one if so.
   #
   private sync_lines()
      if (encoding ~=== print_encoding) |
         (filename ~=== print_filename) then 
      {
         print_encoding := encoding
         print_filename := filename
         return "#line " || (line + 1) || " \"" || filename || "\" " || encoding
      }
      return "#line " || (line + 1)
   end

   private err(msg)
      /msg := "Invalid preprocessor line"
      fire("error", ErrorDetail(FilePath(filename).canonical().standard_case().str(), line, msg))
   end

   # Helper function to parse a list of strings in the form
   # "KEY=VALUE" (obtained from command line options), into a table
   # which can be passed to the preprocessor.
   #
   public static parse_defs(l)
      local s, t, k, v
      t := table()
      every s := !l do {
         s ? {
            if k := tab(upto('=')) then {
               move(1)
               v := tab(0)
               insert(t, k, v)
            } else
               # Insert &null, meaning this key is to be deleted (see above).
               insert(t, s)
         }
      }
      return t
   end
end
