import gui, util

class PaintField(Border)
   public 
      paints, 
      os, 
      img, 
      tf1,
      tf2,
      grid_label,
      paint_tbl,
      edit_paint_button

   public on_sel_change()
      edit_paint_button.set_is_shaded(Yes{ paints.get_selection() = 1 })
      os.set_which_one(os.children[paints.get_selection()])
   end

   public on_tf1()
      get_contents().str := tf1.get_contents()
   end

   public on_tf2()
      get_contents().str := tf2.get_contents()
   end

   public on_edit_paint()
      local c, d
      c := get_contents()
      d := c.dialog()
      d.show_modal(self.parent_dialog)
      if \d.okay_result then {
         case paints.get_selection() of {
            4: {
               img.clear_image()
               img.set_cache(\c.image_cache)
               img.set_image(\c.image_str)
            }
            5 : {
               grid_label.set_label(*c.fmt || " chars.")
            }
         }
      }
   end

   public validate_input()
      local t
      t := get_contents()
      case paints.get_selection() of {
         4 : 
            if /t.image_str & /t.image_cache then
               return alert_error(parent_dialog, "No image provided")
         5 :
            if /t.fmt then
               return alert_error(parent_dialog, "No grid format string")
      }
      return
   end

   public set_contents(c)
      paint_tbl := table()
      if /c then
         edit_paint_button.set_is_shaded(&yes)
      else {
         case classof(c) of {
            CanvasTextPaint: {
               if type(c.str) == "string" then {
                  paints.set_selection(2)
                  tf1.set_contents(c.str)
               } else {
                  paints.set_selection(3)
                  tf2.set_contents(c.str)
               }
            }
            CanvasImagePaint: {
               paints.set_selection(4)
               img.clear_image()
               img.set_cache(\c.image_cache)
               img.set_image(\c.image_str)
            }
            CanvasGridPaint: {
               paints.set_selection(5)
               grid_label.set_label(*c.fmt || " chars.")
            }
         }
         paint_tbl[paints.get_selection()] := c
         os.set_which_one(os.children[paints.get_selection()])
      }
      return self
   end

   public get_contents()
      local i, p

      i := paints.get_selection()
      if i = 1 then
         return &null
      p := paint_tbl[i]
      if /p then {
         p := case i of {
            2 : CanvasTextPaint(tf1.get_contents())
            3 : CanvasTextPaint(tf2.get_contents())
            4 : CanvasImagePaint()
            5 : CanvasGridPaint()
         }
         p.change_canvas(parent_dialog.c.parent_Canvas)
         p.rename()
         insert(paint_tbl, i, p)
      }
      return p
   end

   public setup()
      local cc, p
      cc := Component()
      set_content(cc)
      cc.set_layout(GridLayout())
      os := OverlaySet()
      cc.add(os)
      paints := List().set_constraint("x_weight", 0.0).set_selection_list(["&null","Text","Text(Ucs)","Image","Grid"])
      paints.connect(self.on_sel_change, Event.SELECTION_CHANGED)
      cc.add(paints)
      edit_paint_button := TextButton().set_label("Edit...")
      cc.add(edit_paint_button)
      edit_paint_button.connect(self.on_edit_paint, Event.ACTION)

      os.add(Component().set_size(0,0))

      p := Panel().set_layout(GridLayout().set_doi(0))
      tf1 := TextField().set_size(scale(120))
      tf1.connect(self.on_tf1, Event.CONTENT_CHANGED)
      p.add(tf1)
      os.add(p)

      p := Panel().set_layout(GridLayout().set_doi(0))
      tf2 := TextField().set_size(scale(120)).set_contents(u"")
      tf2.connect(self.on_tf2, Event.CONTENT_CHANGED)
      p.add(tf2)
      os.add(p)

      p := Panel().set_layout(GridLayout().set_doi(0))
      img := Image().set_size(scale(40),scale(40)).set_border(PlainBorder())
      p.add(img)
      os.add(p)

      grid_label := Label().set_constraint("x_weight", 1.0).set_constraint("x_fill", &yes).set_label("No format set.")
      os.add(grid_label)
   end

   public new()
      Border.new()
      setup()
      self.clear_constraints()
      self.set_constraint("x_fill", &yes)
      self.set_constraint("x_weight", 1.0)
      return
   end
end


