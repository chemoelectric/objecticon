package gui

#
# This class reserves space for an arbitrary `Component` in a menu.   The `Component` is
# added to the menu when it is shown.
#
class MenuReserve(MenuComponent)
   public
      comp,
      res_w,
      res_h

   public override initially()
      MenuComponent.initially()
      \comp | runerr("No component set")
      # Make comp inherit the parent component's attribs.
      comp.set_wattrib_ancestor(parent_component)
      # Temporarily add comp to the parent dialog in order to
      # calculate the required size.
      parent_component.parent_dialog.add(comp)
      res_w := comp.get_preferred_width()
      res_h := comp.get_preferred_height()
      parent_component.parent_dialog.remove(comp)
   end

   public override prepare_parent_proxy()
      local p
      p := parent.temp_win
      comp.set_pos(label_x - parent.x, label_y - parent.y)
      comp.set_size(label_w, label_h)
      p.add(comp)
   end

   # Set the `Component` to install into the `Menu` when it is shown.
   #
   public set_component(comp)
      self.comp := comp
      link
   end

   public override get_label_mid_width()
      return res_w
   end

   public override get_label_mid_height()
      return res_h
   end

   public override get_label_left_width()
   end

   public override get_label_left_height()
   end

   public override get_label_right_width()
   end

   public override get_label_right_height()
   end

   public override draw_label(W, lw, mw, rw)
   end

   public override new()
      MenuComponent.new()
      self.is_decorative_flag := &yes
      return
   end
end
