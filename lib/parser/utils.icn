package parser

import util

#
# Generate the paths in a path string.
#
procedure path_elements(path)
   local dir, psep

   psep := if has_feature("UNIX" | "PLAN9") then ' :' else ' '

   if has_feature("PLAN9") then
      path := map(path, "\001", " ")

   path ? {
      repeat {
         tab(many(psep))
         if pos(0) then
            break
         if any('\"') then {
            move(1)
            dir := tab(upto('\"') | 0)
            move(1)
         } else
            dir := tab(upto(psep) | 0)
         suspend dir
      }
   }
end

procedure path_find(f, n)
   local p, d, s
   if stat(f) then
      return f
   p := Files.get_path(f)
   every d := path_elements(getenv(n)) do {
      s := Files.get_path(d).append(p).str()
      if stat(s) then
         return s
   }
end

