############################################################################
#
#	File:     ptutils.icn
#
#	Subject:  Procedures relating to objects in 3-space
#
#	Author:   Ralph E. Griswold
#
#	Date:     July 26, 1997
#
############################################################################
#
#  This file is in the public domain.
#
############################################################################
#
#  These procedures provide various operations on 3-dimension objects.
#  in 3-space.
#
############################################################################
#
#  Links:  gobject
#
############################################################################

package ipl.ptutils

import
   io,
   ipl.gobject(Point)

# convert point to coordinate
procedure pt2coord(p)		

   return p.x || " " || p.y || " " || p.z

end

# convert coodinate to path
procedure coord2pt(c)		
   local p

   p := Point()

   c ? {
      p.x := tab(upto(' '))
      move(1)
      p.y := tab(upto(' '))
      move(1)
      p.z := tab(0)
      }

   return p

end

# negative of point
procedure negpt(p)			

    return Point(-p.x, -p.y, -p.z)

end

# test point equality
procedure pteq(p1, p2)			

   if p1.x = p2.x & p1.y = p2.y & p1.z = p2.z then return p2 else fail

end

# make point list from coordinate file
procedure getpts(s)			
   local input, pts

   input := open(s) | stop("*** cannot open ", image(s))

   pts := []

   while put(pts, coord2pt(read(input)))

   return pts

end
