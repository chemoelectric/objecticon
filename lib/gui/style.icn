package gui

import ipl.graphics

class Style()
   public dispose(W)
      BevelDispose(W)
   end

   public dispose_all()
      BevelDisposeAll()
   end

   #
   # Draw a raised rectangle.
   #
   public draw_raised_rectangle(W, x, y, w, h, width)
      BevelRectangle(W, x, y, w, h, width)
   end

   #
   # Draw a sunken rectangle.
   #
   public draw_sunken_rectangle(W, x, y, w, h, width)
      BevelRectangle(W, x, y, w, h, -width)
   end

   public draw_hilite_line(W, x1, y1, x2, y2)
      bev_lookup(W).hilite.draw_line(x1, y1, x2, y2)
   end

   public draw_shadow_line(W, x1, y1, x2, y2)
      bev_lookup(W).shadow.draw_line(x1, y1, x2, y2)
   end

   public get_hilite_color(W)
      return bev_lookup(W).hilite.get_fg()
   end

   public get_shadow_color(W)
      return bev_lookup(W).shadow.get_fg()
   end

   public get_filter_clone(W)
      local t
$ifdef _MS_WIN32
      t := W.clone("fillstyle=masked", "drawop=reverse", "pattern=verydark")
$else
      t := W.clone("fillstyle=masked", "pattern=waves")
      t.toggle_fgbg()
$endif
      return t
   end

   public get_down_clone(W)
      return W.clone("drawop=reverse")
   end

   public get_selection_clone(W)
      return W.clone("drawop=reverse")
   end

   public get_cursor_clone(W)
      return W.clone("bg=red", "fg=white")
   end

   public get_focus_clone(W)
      return W.clone("fg=red", "fillstyle=masked", "pattern=gray")
   end

   public get_dashed_clone(W)
      return bev_lookup(W).shadow.clone("fillstyle=masked", "pattern=gray")
   end

   public get_highlight_clone(W)
      return W.clone("fg=blue", "fillstyle=masked", "pattern=gray")
   end

   public create_tooltip_label()
      return Label("bg=pale yellow", 
                   "font=sans,10", 
                   "fg=black",
                   "border=",CompoundBorder(PlainBorder("line_width=1"),
                                            EmptyBorder("insets=3,3,2,2")))
   end

   public polish(obj)
   end

   public allow_popup(obj)
      return 1
   end

   public distance2ticker_rate(d)
      local r
      r := 200 - 6*d
      r <:= 20
      r >:= 200
      return r
   end
end


