#
# $Id$
#
# This file is in the public domain.
#
# Author: Robert Parlett (parlett@dial.pipex.com)
#

import gui


############################################################################
#
# Class for representing a RangeSpin on the canvas.
#
class CanvasRangeSpin(CanvasComponent,RangeSpin)
   public value

   #
   # Generate code into Code object c.
   #
   public override gen_code(c)
      local s
      CanvasComponent.gen_code(c)
      if \self.lo | \self.hi then {
         s := "set_range(" || (\self.lo|"&null") || ", " ||
            (\self.hi|"&null") || ")" 
         c.chain(s)
      }
      
      c.chain("set_value(" || value || ")")
      c.chain("set_increment_size(" || self.increment_size || ")")
   end

   #
   # Duplicate object.
   #
   public override dup(pc)
      local c
      c := CanvasRangeSpin()
      CanvasComponent.dup_fields(c)
      c.set_range(self.lo, self.hi)
      c.value := self.value
      c.set_value(c.value)
      return c
   end

   public override post_decode()
      CanvasComponent.post_decode()
      set_value(value)
   end

   #
   # Return the I/O template.
   #
   public override get_template()
      suspend CanvasComponent.get_template() |
          ["Hi", "hi"] |
          ["Lo", "lo"] |
          ["Increment Size", "increment_size"] |
          ["Value", "value"]
   end

   #
   # Open the configuration dialog box.
   #
   public override dialog()
      return CanvasRangeSpinDialog(self)
   end

   public override allows_null_height()
      return
   end

   public override allows_null_width()
      return
   end

   public override new()
      RangeSpin.new()
      CanvasComponent.new()
      self.class_name := "RangeSpin"
      self.import_name := "gui"
      self.name := "range_spin"
      return
   end
end

