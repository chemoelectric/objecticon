#
# $Id$
#
# This file is in the public domain.
#
# Author: Robert Parlett (parlett@dial.pipex.com)
#

import parser, ipl.options, io

global opts, arg_packs

procedure warn(s)
   ewrite(s)
end

procedure note(s)
   write("Scanning ", s)
end

procedure usage(opt_help)
   ewrite("Usage: oidoc [OPTIONS] [SRC FILES/PACKAGES...]")
   ewrite("Generate documentation")
   ewrite(opt_help)
end

procedure main(a)
   local s, o, symbol_table

   opts := options(a, [Opt("html",, "Generate HTML output"),
                       Opt("o",string,"OUTPUT FILE/DIR#Output file or directory"),
                       Opt("a",, "Generate documentation for all packages"),
                       Opt("d",, "Generate scanning event debug output"),
                       Opt("r",, "Restrict output to specified packages"),
                       Opt("ci",, "Assume case-insensitive file system")],
                       usage)

   symbol_table := SymbolTable()
   if \opts["d"] then
      symbol_table.connect(note, SymbolTable.SCANNING_EVENT)
   if \opts["a"] then {
      every s := symbol_table.get_all_packages() do
         symbol_table.scan_package(s) | stop(&why)
   }

   arg_packs := set()
   every s := !a do {
      if s[-4:0] == ".icn" then
         symbol_table.scan_file(s) | warn(&why)
      else {
         insert(arg_packs, s)
         symbol_table.scan_package(s) | stop(&why)
      }
   }

   symbol_table.resolve_classes() | stop(&why)

   if \opts["html"] then
      o := HtmlOutputter(opts, symbol_table)
   else
      o := GroffOutputter(opts, symbol_table)

   o.output()
end
