############################################################################
#
#	File:     archives.icn
#
#	Subject:  Procedures for reading files from archives
#
#	Author:   Gregg M. Townsend
#
#	Date:     March 5, 2000
#
############################################################################
#
#   This file is in the public domain.
#
############################################################################

package ipl.archives

import
   io(BufferStream, FileStream, FilterInputStream,
      GeneratorStream, StringStream),
   ipl.strings(endswith),
   util(error, use)

# iszip(zname) succeeds if the named file appears to be a ZIP format
# archive file.
#
procedure iszip(fname)		
   local f
   return use {
      f := FileStream(fname),
      f.reads(4) == "PK\03\04"
   }
end

#
# {Stream} for reading the entries in an archive, one per line of the stream.
#
abstract class ArchiveListerStream(GeneratorStream)
   private
      f, bf

   public succeeded()
      return f.succeeded()
   end

   public close()
      bf.close() | fail
      return self
   end

   protected next_line()
      return bf.read_line()
   end

   protected expect_line()
      local s
      s := bf.read_line() | fail
      return \s | error("Unexpected eof")
   end

   # Generates the lines of the stream.  End-of-file is signalled by
   # returning {&null} and errors are signalled by failing and setting
   # {&why}.
   #
   protected abstract line_gen()

   protected new(prog, args)
      self.bf := BufferStream(f := FilterInputStream(, prog, args))
      return GeneratorStream.new{line_gen()}
   end
end

#
# {Stream} for reading the entries in a zip archive, one per line of the stream.
#
class ZipDirStream(ArchiveListerStream)
   protected line_gen()
      local s, marker
      every 1 to 3 do
         expect_line() | fail
      marker := "---------                     -------"
      repeat {
         s := expect_line() | fail
         if s == marker then
            return
         suspend s[31:0]
      }
   end

   public new(zname)
      return ArchiveListerStream.new("unzip", ["-l", zname])
   end
end

# zipfile(zname, fname) returns a pipe from which the file fname
# within the ZIP archive zname can be read.  It is assumed that zname
# and fname are valid.
#
procedure zipfile(zname, fname)		
   return FilterInputStream(, "unzip", ["-p", zname, fname])
end

#
# {Stream} for reading the entries in a rar archive, one per line of the stream.
#
class RarDirStream(ArchiveListerStream)
   protected line_gen()
      local s, marker
      every 1 to 8 do
         expect_line() | fail
      marker := repl("-", 79)
      repeat {
         s := expect_line() | fail
         if s == marker then
            return
         suspend s[2:0]
         expect_line() | fail
      }
   end

   public new(rname)
      return ArchiveListerStream.new("unrar", ["v", "-c-", rname])
   end
end

# rarfile(rname, fname) returns a pipe from which the file fname
# within the rar archive rname can be read.  It is assumed that rname
# and fname are valid.
#
procedure rarfile(rname, fname)		
   return FilterInputStream(, "unrar", ["p", "-ierr", rname, fname])
end

procedure tartype(s)
   s := map(s)
   if endswith(s, ".tar.gz" | ".tgz") then
      return "z"
   if endswith(s, ".tar.bz2") then
      return "j"
   return ""
end

#
# {Stream} for reading the entries in a tar archive, one per line of the stream.
#
class TarDirStream(ArchiveListerStream)
   protected line_gen()
      suspend |next_line()
   end

   public new(tname)
      return ArchiveListerStream.new("tar", ["tf" || tartype(tname), tname])
   end
end

# tarfile(tname, fname) returns a pipe from which the file fname
# within the tar archive rname can be read.  It is assumed that tname
# and fname are valid.
#
procedure tarfile(tname, fname)		
   return FilterInputStream(, "tar", ["xOf" || tartype(tname), tname, fname])
end

# Perform gzip on a string, returning the gzipped string.
procedure gzip(s)
   local f, r
   if r := use {
      f := FilterInputStream(StringStream(s), "gzip", ["-c"]),
      f.read_all()
   } & f.succeeded() then
      return r
end

# Perform gunzip on a string, returning the unzipped string.
procedure gunzip(s)
   local f, r
   if r := use {
      f := FilterInputStream(StringStream(s), "gunzip", ["-c"]),
      f.read_all()
   } & f.succeeded() then
      return r
end

# Perform gunzip on the given file, returning the unzipped string.
procedure gunzip_file(fn)
   local f, r
   if r := use {
      f := FilterInputStream(, "gunzip", ["-c", fn]),
      f.read_all()
   } & f.succeeded() then
      return r
end
